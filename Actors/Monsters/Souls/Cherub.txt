Class PM_Cherub : PMLustMonsterBase
{
	Default
	{
		PMMonsterBase.CodexLore
		"Demonic progeny cherubs that were served as sentinels from the circle of Lust. These demonic neonate lookalikes were born to swarm around for protection at a later age, and they were starting to master it after some time.
		With their small bodies and bat-like wings, they are capable of flight, and they will snatch onto any prey like a rabid bat.
		
		Usually flies, can lunge at you and spit a tiny fraction of lustrous substance.";
		Health 60;
		Scale 0.865;
		PainChance 255;
		Speed 8;
		Radius 16;
		Height 56;
		Mass 50;
		Monster;
		SeeSound  "Cherub/Sight";
		PainSound "Cherub/Pain";
		DeathSound "Cherub/Death";
		ActiveSound "Cherub/Active";
		AttackSound "Cherub/Attack";
		Renderstyle "Normal";
		DamageFunction 15;
		DeathHeight 4;
		Obituary "%o died a slow death, which is caused by a lucky Cherub.";
		Tag "Cherub";
		Species "Soul";
		Bloodtype "PM_NormalBloodBase";
		//+NOINFIGHTING
		+FLOATBOB
		FloatBobStrength 0.3;
		+NOGRAVITY
		+FORCEXYBILLBOARD
		+DONTHARMCLASS
		+DONTHARMSPECIES
		+ROLLSPRITE
		TeleFogSourceType "PM_SmallDemonicTeleportFog";
		TeleFogDestType "PM_SmallDemonicTeleportFog";
	}
	
	override void PM_Gib()
	{
	  for(int gib=0;gib<random(2,3);gib++)
	 { A_SpawnItemEx("PM_NormalMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY); }
	  for(int a=0;a<3;a++)
	 { A_SpawnItemEx("PM_NormalBloodMulti",0,0,32,random(-1,1),random(-1,1),random(-1,1),frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY); }
	 A_SpawnItemEx("PM_NormalBloodMist",0,0,32,random(-1,1),random(-1,1),random(-1,1),frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
	   A_StartSound("GBloodSplashBig",CHAN_7,CHANF_DEFAULT,0.5);
	}
 
	States
	{
	Spawn:
		CHE_ ABCB 10 PM_Look();
		Loop;
	Look:
		CHE_ AABBCCBB 1 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
		Loop;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","RoarNoAnim","SpotMissile");
		Goto See;
	RoarNoAnim:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	Roar:
		CHE_ A 10 A_FaceTarget();
		CHE_ D 20
		{
			A_FaceTarget();
			PM_AlertSound();
		}
		CHE_ A 5 A_FaceTarget();
		Goto See;
	See:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("Cherub/FlyLoop",35,CHANF_LOOPING,0.4,1.3);
	See2:
		CHE_ A 0 A_ChangeVelocity(random(-1,1), random(-1,1), random(-1,1));
		CHE_ AABB 1 A_Chase("Melee",null);
		CHE_ CCBB 1 A_Chase("Melee",null);
		TNT1 A 0 A_Recoil(-0.5);
		TNT1 A 0 A_Jump(35,"RandomDodge","WanderingFly");
		TNT1 A 0 A_Jump(200,"See2");
		TNT1 A 0 A_JumpIfCloser(500,"StareStart");
		Loop;
	WanderingFly:
		CHE_ A 0 A_ChangeVelocity(random(-1,1), random(-1,1), random(-1,1));
		CHE_ AABB 1 A_Wander();
		CHE_ CCBB 1 A_Wander();
		TNT1 A 0 A_Jump(30,"See2");
		Loop;
	RandomDodge:
		TNT1 A 0;
		TNT1 A 0 ThrustThing(random(0,360),16);
		TNT1 A 0 ThrustThingZ(0,13,random(0,1),1);
		CHE_ AABBCCBB 1
		{
			A_FaceTarget();
		}
		CHE_ A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE);
		Goto See2;
	RandomStareDodge:
		TNT1 A 0;
		TNT1 A 0 ThrustThing(random(0,360),16);
		TNT1 A 0 ThrustThingZ(0,13,random(0,1),1);
		TNT1 A 0 A_FaceTarget();
		CHE_ AABBCCBB 1
		{
			A_FaceTarget();
		}
		CHE_ A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE);
		Goto Stare;
	StareStart:
		CHE_ A 0 A_CheckSight("See2");
		CHE_ A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE);
	Stare:
		CHE_ A 0 A_ChangeVelocity(random(-1,1), random(-1,1), random(-1,1), CVF_REPLACE);
		CHE_ AABB 1 A_FaceTarget();
		CHE_ CCBB 1 A_FaceTarget();
		TNT1 A 0 A_Jump(50,"RandomStareDodge");
		TNT1 A 0 A_Jump(50,"AttackCharge");
		TNT1 A 0 A_Jump(40,"WanderingFly","See2");
		Loop;
	AttackCharge:
		TNT1 A 0 A_JumpIfCloser(500,"Attacking");
		Goto See2;
	Attacking:
		CHE_ ADE 3 A_FaceTarget();
		CHE_ D 2 A_SkullAttack();
		CHE_ DEEEEEEEEEEEED 2
		{
		//A_SpawnProjectile("HorrorLunge",random(1,10),0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,random(40,0));
		}
		CHE_ D 0 A_Stop();
		Goto WanderingFly;
	Melee:
		CHE_ A 0 A_ChangeVelocity(0, 0, 0, CVF_REPLACE);
		CHE_ A 0 A_Stop();
		Goto WanderingFly;
	Pain:
		CHE_ F 2;
		CHE_ F 2 A_Pain();
		CHE_ A 0 A_Jump(70, "RandomDodge", "WanderingFly");
		Goto See2;
	Death:
		TNT1 A 0;
		TNT1 A 0 A_StopSound(35);
		CHE_ F 5;
		CHE_ G 5
		{
			bFLOATBOB = false;
			bFLOAT = false;
			PM_Gib();
			A_ScreamAndUnblock();
		}
		CHE_ HIJK 6;
		CHE_ K -1;
		Stop;
	}
}

Class PM_CherubSpit : DoomImpBall
{
	Default
	{
		Translation "0:255=%[0.00,0.00,0.00]:[1.50,0.80,1.93]";
		+FORCEXYBILLBOARD
		DamageType "Normal";
		DeathSound "";
		SeeSound "";
		Speed 13;
		Scale 0.5;
		DamageFunction (9);
	}
	
	states
  {
  Spawn:
    BAL7 AABB 1 Bright
	{
	  A_SpawnItemEx("PM_InfernalLavaTrail",random(0,-5),random(-5,5),random(-5,5),0,random(-1,1),random(-1,2),0,SXF_CLIENTSIDE|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	  A_SpawnParticleEx("5d1d91",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(1,25),size: frandom(15,35),xoff: random(-1,-10),yoff: frandom(-5,5),zoff: frandom(-5,5),startalphaf: 0.75,sizestep: -.5,startroll: random(-180,180));
	}
    Loop;
  Death:
    TNT1 A 0;
	TNT1 A 0 A_StartSound("Enceinte/CumImpact", volume: 0.7, pitch: 1.45);
	TNT1 AAAAA 0 A_SpawnItemEx("PM_InfernalLavaTrail",xvel:random(-1,1),yvel:random(-1,2),flags:SXF_CLIENTSIDE|SXF_NOCHECKPOSITION|SXF_TRANSFERSCALE|SXF_TRANSFERTRANSLATION);
	TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE);
    Stop;
  }
}