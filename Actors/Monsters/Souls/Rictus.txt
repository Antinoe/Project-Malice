//Rictus, a new monster by Captain Toenail//

Class Rictus : Actor
{
  Default
  {
    obituary "%o was charred by a rictus.";
    health 100;
    radius 16;
    height 56;
    mass 50;
    speed 2;
    painchance 128;
    seesound "rictus/sight";
    deathsound "rictus/death";
    //activesound "rictus/active";
    painsound "rictus/pain";
    MONSTER;
    +FLOAT
    +NOGRAVITY
    +NOICEDEATH
    +NOBLOOD
    +DONTFALL
    +FIRERESIST
    +DONTHARMCLASS
  }

  states
  {
  Spawn:
    RICT A 1 A_Look();
    RICT A 0 RictusFlames();
    RICT A 0 A_StartSound("Rictus/Active",CHAN_7);
    loop;
  See:
    RICT A 1 A_Chase();
    RICT A 0 RictusFlames();
    RICT A 0 A_StartSound("Rictus/Active",CHAN_7);
    loop;
  Missile:
    RICT A 0 A_PlaySound("Rictus/Active",CHAN_7);
    RICT A 0 A_JumpIfHealthLower(25, "Kamikaze");
    RICT A 1 A_FaceTarget();
    RICT A 0 RictusFlames();
    RICT A 1 A_FaceTarget();
    RICT A 0 RictusFlames();
    RICT A 1 A_FaceTarget();
    RICT A 0 RictusFlames();
    RICT A 1 A_FaceTarget();
    RICT A 0 RictusFlames();
    RICT B 1 A_SpawnProjectile("RictusBall", 0);
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    goto See;
  Kamikaze:
    RICT A 0 A_SpawnProjectile("RictusKamakazi",0,0,0,0,0);
    RICT A 1 A_Die("Kamikaze");
    Goto Death.Kamikaze;
  Pain:
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1 A_Pain();
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    RICT B 1;
    RICT B 0 RictusFlames();
    goto See;
  Death:
    TNT1 A 1 A_ScreamAndUnblock();
    TNT1 AAAAA 0 A_SpawnItemEx("RictusFire", random(1,-1),random(1,-1),random(1,-1), 6,0,random(1,-1), random(0,360), SXF_CLIENTSIDE, 0);
    stop;
  Death.Kamikaze:
    TNT1 A 1;
    TNT1 A 1 A_NoBlocking();
    stop;
  }

  void RictusFlames()
  {
    A_SpawnItemEx("RictusFire", random(1,-1),random(1,-1),random(1,-1), 2,0,random(1,-1) , random(90,270), SXF_NOCHECKPOSITION | SXF_CLIENTSIDE, 0);
    A_SpawnItemEx("RictusFire", random(1,-1),random(1,-1),random(1,-1), 2,0,random(1,-1) , random(90,270), SXF_NOCHECKPOSITION | SXF_CLIENTSIDE, 0);
  }
}

Class RictusBall : Actor
{
  Default
  {
    radius 4;
    height 6;
    speed 7;
    damage 4;
    scale 0.75;
    renderstyle "Add";
    DamageType "Fire";
    seesound "rictus/fire";
    deathsound "rictus/fireimpact";
    PROJECTILE;
    +RANDOMIZE
  }

  states
  {
  Spawn:
    DRFX ABCDEF 1 bright A_SpawnItemEx("MiniRictusFire", random(1,-1),random(1,-1),random(1,-1), 2,0,random(1,-1), random(90,270), SXF_NOCHECKPOSITION | SXF_CLIENTSIDE, 0);
    loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode");
    DRFX G 4 bright A_Explode(5,16, XF_HURTSOURCE);
    DRFX GGGGG 0 bright A_SpawnItemEx("MiniRictusFire", random(1,-1),random(1,-1),random(1,-1), 6,0,random(1,-1), random(0,360), SXF_CLIENTSIDE, 0);
    DRFX HIJKL 4 bright;
    stop;
  }
}

Class RictusKamakazi : Actor
{
  Default
  {
    obituary "%o was blown up by a suicidal rictus.";
    radius 16;
    height 56;
    health 25;
    speed 20;
    damage 20;
    seesound "rictus/kamakazi";
    deathsound "rictus/kamakaziimpact";
    decal "Scorch";
    PROJECTILE;
  }

  states
  {
  Spawn:
    RICT B 1 A_SpawnItemEx("RictusFire", random(1,-1),random(1,-1),random(1,-1), 2,0,random(1,-1), random(90,270), SXF_NOCHECKPOSITION | SXF_CLIENTSIDE, 0);
    loop;
  Death:
    DRFX G 0 bright A_SetTranslucent(1, 1);
    DRFX GGGGG 0 bright A_SpawnItemEx("RictusFire", random(1,-1),random(1,-1),random(1,-1), 6,0,random(1,-1), random(0,360), SXF_CLIENTSIDE, 0);
    DRFX G 4 bright A_Explode();
    DRFX HIJKL 4 bright;
    stop;
  }
}

Class RictusFire : Actor
{
  Default
  {
    radius 6;
    height 8;
    speed 1;
    damage 0;
    renderstyle "Add";
    +NOBLOCKMAP
    +NOGRAVITY
    +MISSILE
    +NOTELEPORT
    +DONTSPLASH
  }

  states
  {
  Spawn:
    CFCF ABCDEFGHIJKLMNOP 1 bright;
    stop;
  }
}

Class MiniRictusFire : RictusFire
{
  Default
  {
    scale 0.75;
  }
}
