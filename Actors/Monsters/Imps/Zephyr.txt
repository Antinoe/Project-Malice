Class PM_Zephyr : PMMonsterBase
{
  Default
  {
  Obituary "%o was outpaced by a Zephyr.";
  HitObituary "%o was ripped in half by a Zephyr.";
  Health 50;
  Radius 20;
  Height 56;
  Mass 100;
  speed 16;
  painchance 160;
  Gravity 0.8;
  Species "Imp";
  Tag "Zephyr";
  seesound "Zephyr/Sight";
  painsound "Zephyr/Pain";
  deathsound "Zephyr/Death";
  activesound "Zephyr/Active";
  Bloodtype "PM_NormalBloodBase";
  Monster;
  +FLOORCLIP
  +ROLLSPRITE
  +ROLLCENTER
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +MISSILEevenMORE
  +SLIDESONWALLS
  +AVOIDMELEE
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
  TeleFogDestType "PM_DemonicTeleportFog";
  }
  States
  {
  Spawn:
    BELA A 10 PM_Look();
    loop;
  Look:
	TNT1 A 0 { bMISSILEMORE = false; }
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TNT1 A 0 A_SetSpeed(16);
    BELA EEBB 2 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	TNT1 A 0 A_JumpIfTargetinLOS("Spotted",250,JLOSF_DEADNOJUMP);
	BELA CCDD 2 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Imp/Step",4);
    Loop;
  Spotted:
    BELA AAA 1 A_FaceTarget();
    TNT1 A 0 A_Jump(256,"RoarNoAnim","RoarNoAnim","Jump","CeilingJump","Fight","Roar");
  RoarNoAnim:
    TNT1 A 0 PM_AlertSound();
  See2:
	TNT1 A 0 { bMISSILEMORE = false; }
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TNT1 A 0 A_SetSpeed(16);
    BELA BB 2 A_Chase();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	BELA CC 2 A_Chase();
	BELA DD 2 A_Chase();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	BELA EE 2 A_Chase();
	TNT1 A 0 A_JumpIfCloser(120,"FearCheck");
	TNT1 A 0 A_Jump(25,"Fight");
	TNT1 A 0 A_Jump(20,"Jump");
	TNT1 A 0 A_Jump(30,"CrouchSee");
	TNT1 A 0 A_Jump(40,"CeilingJump");
	Loop;
   CrouchSee:
	TNT1 A 0 A_SetSpeed(26);
	CBEL DDCCBBAACCBBAACCBBAADDCCBBAA 2 A_Stop;
	TNT1 A 0 A_SetSpeed(18);
	TNT1 A 0 A_Jump(50,"Missile");
	Goto See2;
	
	CeilingJump:
	TNT1 A 0 A_JumpIf(CallACS("GetActorZDist") <= 80, 2);
	TNT1 AAA 0;
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "See2");
    CBEL BBAA 4;
	TNT1 A 0 ThrustThingZ(0,25,0,1);
	//TNT1 A 0 A_StartSound("MAMAMIA")	//I will always remember you. F
	CBEL EEEE 2 A_CheckCeiling("CeilingLatch");
	TNT1 A 0 ThrustThingZ(0,45,0,1);
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	CBEL EEE 2 A_CheckCeiling("CeilingLatch");
	TNT1 A 0 ThrustThingZ(0,55,0,1);
	CBEL EEEE 3 A_CheckCeiling("CeilingLatch");
	CBEL EEEEEEE 3 A_CheckFloor("See2");
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	Goto See2;
	
	CeilingLatch:
	TNT1 A 0
	{
	bNOGRAVITY = true;
	bTHRUSPECIES = true;
	bNOVERTICALMELEERANGE = true;
	}
	TNT1 A 0 A_GiveInventory ("IsOnCeiling", 1);
	CBEL DDDDD 3 ThrustThingZ(0,55,0,1); //1
	Goto CeilingCrawl;
	CeilingCrawl:
		TNT1 A 0 { bFRIGHTENED = false; }
		TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
		TNT1 A 0 A_CheckFloor("CeilingDrop");
		TNT1 A 0 ThrustThingZ(0,15,0,1);
		1BEL BBAACCBBAA 3 A_Chase(null,"CeilingMissile");
		//TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 ThrustThingZ(0,15,0,1);
		TNT1 A 0 A_CheckFloor("CeilingDrop");
		TNT1 A 0 ThrustThingZ(0,15,0,1);
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
		TNT1 A 0 A_CheckFloor("CeilingDrop");
		//TNT1 A 0 A_JumpIfCloser(50, "CeilingPounce")
		TNT1 A 0 A_Jump(22,"CeilingDrop");
		Loop;
	CeilingMissile:
	    TNT1 A 0 A_JumpIfCloser(600,"CeilingPounce");
		BELA AAAAFFFFFFF 1 Light("Shadowball2")
		{
		A_FaceTarget();
		A_Spawnitemex("BelphFire",7,random(-12,-14),5,0,0,2);
		A_Spawnitemex("BelphFire",7,random(12,14),5,0,0,2);
		}
		BELA G 6 Light("Shadowball2")
		{
		A_Spawnitemex("BelphFire",7,random(-5,5),random(18,26),5,0,2);
		A_Spawnitemex("BelphFire",7,random(-5,5),random(18,26),5,0,2);
		A_Spawnitemex("BelphFire",7,random(-5,5),random(-18,-26),5,0,2);
		A_Spawnitemex("BelphFire",7,random(-5,5),random(-18,-26),5,0,2);
		A_SpawnProjectile("PM_FastImpBall",5,5,random(-1,1),CMF_OFFSETPITCH,random(-1,1));
		}
		Goto CeilingCrawl;
	CeilingDrop:
		TNT1 A 0;
		TNT1 A 0 A_TakeInventory("IsOnCeiling");
		TNT1 A 0
		{
		bNOGRAVITY = false;
		bCEILINGHUGGER = false;
		}
		1BEL C 2 A_FaceTarget();
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	CeilingDropping:
		CBEL EE 1 A_JumpIf (vel.z == 0, "CeilingLanding");
		TNT1 A 0 ThrustThing(random(0,360)*256/360,2,0,0); //tries to prevent getting stuck
		loop;
	CeilingLanding:
		TNT1 A 0 A_StartSound("Imp/Step",4);
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
		CBEL BB 2 A_FaceTarget();
		Goto See2;
		
	CeilingPounce: 
		TNT1 AAA 0;
		TNT1 A 0 A_TakeInventory ("IsOnCeiling");
		TNT1 A 0
		{
		bNOGRAVITY = false;
		bCEILINGHUGGER = false;
		bTHRUSPECIES = false;
		}
		TNT1 A 0 A_Recoil(-18);
		TNT1 A 0 A_FaceTarget();
		CBEL AAAA 2;
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
		CBEL EEEE 2;
		Goto CeilingDropping;	
      Missile:
	    TNT1 A 0 A_Jump(10,"Jump");
	    TNT1 A 0 A_Jump(30,"Recoil");
	    TNT1 A 0 A_Jump(10,"Roar");
	    TNT1 A 0 A_Jump(180,"StandoffCheck");
	    TNT1 A 0 A_Jump(230,"FireDodge");
	    BELA AAAAFFFFFFF 1 Light("Shadowball2")
	    {
	    A_FaceTarget();
	    A_SpawnitemEx("BelphFire",7,random(12,14),22,0,0,2);
	    A_SpawnitemEx("BelphFire",7,random(-12,-14),22,0,0,2);
	    }
        BELA G 6 Light("Shadowball2")
	    {
	    A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	    A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	    A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	    A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	    A_SpawnProjectile("PM_FastImpBall",35,5);
	    }
        Goto See2;
      StandoffCheck:
	    TNT1 A 0 A_JumpIfCloser(200,"FearCheck");
	    TNT1 A 0 A_JumpIfCloser(300,"StandoffMissile");
	    TNT1 A 0 A_Jump(128,"Standoff2");
      Standoff:
	    TNT1 A 0
		{
		bMISSILEMORE = true;
		bFRIGHTENED = false;
		}
	    TNT1 A 0 A_Jump(12,"See2");
	    TNT1 A 0 A_JumpIfCloser(150,"FearCheck");
	    TNT1 A 0 A_SetSpeed(0);
	    BELA AAAAAAAAAAAA 2
	    {
	    A_Chase("Recoil","StandoffMissile");
	    A_FaceTarget();
	    return A_JumpIfInTargetLOS("AimingAtMe",30,JLOSF_DEADNOJUMP,1000);
	    }
	    TNT1 A 0 A_CheckSight("See2");
	    TNT1 A 0 A_Jump(150,"StandoffRepos");
	    TNT1 A 0 A_Jump(50,"StandoffDodge");
	    Loop;
  Standoff2:
   TNT1 A 0
	{
	bMISSILEMORE = true;
	bFRIGHTENED = false;
	}
	TNT1 A 0 A_Jump(12,"See2");
	TNT1 A 0 A_JumpIfCloser(150,"FearCheck");
	TNT1 A 0 A_SetSpeed(0);
	CBEL DDDDDDDDDDDD 2 
	{
	A_Chase("Recoil","StandoffMissile");
	A_FaceTarget();
	return A_JumpIfInTargetLOS("AimingAtMe",30,JLOSF_DEADNOJUMP,1000);
	}
	TNT1 A 0 A_CheckSight("See2");
	TNT1 A 0 A_Jump(150,"StandoffRepos");
	TNT1 A 0 A_Jump(50,"StandoffDodge");
	Loop;
  AimingAtMe:
	TNT1 A 0;
	TNT1 A 0 A_Jump(35,"Fear");
	TNT1 A 0 A_Jump(40,"StandoffMissile");
	Goto StandoffRepos;
  StandoffRepos:
	TNT1 A 0 A_SetSpeed(26);
	TNT1 A 0 { bFRIGHTENED = true; }
	CBEL DDCCBBAACCBB 2
	{
	A_Chase();
	return A_JumpIfCloser(100,"Recoil");
	}
  StandoffReposLoop:
	TNT1 A 0 A_SetSpeed(22);
	TNT1 A 0 A_StartSound("Imp/Step",4);
    BELA DDEE 2 A_Wander();
	TNT1 A 0 A_StartSound("Imp/Step",4);
	BELA BBCC 2 A_Wander();
	TNT1 A 0 { bFRIGHTENED = true; }
	TNT1 A 0 A_Jump(150,"Standoff","Standoff2");
	TNT1 A 0 A_Jump(120,"ReposJump");
	Loop;
  ReposJump:
	TNT1 A 0 A_Recoil(-20);
	TNT1 A 0 { bNODROPOFF = true; }
	TNT1 A 0 ThrustThingZ(0,40,0,1);
	CBEL E 28 A_FaceTarget();
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	Goto StandoffReposLoop;
  StandoffMissile:
	TNT1 A 0 A_Jump(20,"StandoffRoar");
	TNT1 A 0 A_Jump(140,"StandoffFireDodge");
	TNT1 A 0 { bNODROPOFF = true; }
	TNT1 A 0 ThrustThingZ(0,35,0,1);
	BELA AAAAFFFFFFF 1 Light("Shadowball2")
	{
	A_FaceTarget();
	A_SpawnitemEx("BelphFire",7,random(12,14),22,0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-12,-14),22,0,0,2);
	}
    BELA G 6 Light("Shadowball2")
	{
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnProjectile("PM_FastImpBall",35,5);
	}
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_JumpIfCloser(400,"StandoffBackup");
	TNT1 A 0 A_Jump(128,"Standoff2");
    Goto Standoff;
  StandoffBackup:
	TNT1 A 0 A_SetSpeed(26);
	TNT1 A 0 { bFRIGHTENED = true; }
	CBEL DDCCBBAACCBB 2
	{
	A_Chase();
	A_JumpIfCloser(100,"Recoil");
	}
	TNT1 A 0 A_Jump(110,"Fear");
	TNT1 A 0 A_Jump(128,"Standoff2");
    Goto Standoff;
   StandoffDodge:
	TNT1 A 0
	{
	bNODROPOFF = true;
	ThrustThing(randompick(0,128),random(12,16),0,0);
	ThrustThingZ(0,30,0,1);
	}
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_Jump(50,"StandoffMissile");
	TNT1 A 0 A_Jump(128,"Standoff2");
	Goto Standoff;
  StandoffRoar:
	BELA BBBFFF 3 A_FaceTarget();
	TNT1 A 0 PM_AlertSound();
	BELA AAAAAAAAA 3 A_FaceTarget();
	TNT1 A 0 A_JumpIfCloser(300,"StandoffBackup");
	Goto Standoff;
  StandoffFireDodge:
	TNT1 A 0
	{
	bNODROPOFF = true;
	ThrustThing(randompick(0,128),random(12,16),0,0);
	ThrustThingZ(0,30,0,1);
	}
	BELA AAAAFFFFFFF 1 Light("Shadowball2")
	{
	A_FaceTarget();
	A_Spawnitemex("BelphFire",7,random(12,14),22,0,0,2);
	A_Spawnitemex("BelphFire",7,random(-12,-14),22,0,0,2);
	}
    BELA G 6 Light("Shadowball2")
	{
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnProjectile("PM_FastImpBall",35,5);
	}
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_JumpIfCloser(400,"StandoffBackup");
	TNT1 A 0 A_Jump(128,"Standoff2");
    Goto Standoff;
  FireDodge:
	TNT1 A 0
	{
	bNODROPOFF = true;
	ThrustThing(random(0,128),random(12,16), 0, 0);
	ThrustThingZ(0,30,0,1);
	}
	BELA AAAAFFFFFFF 1 Light("Shadowball2")
	{
	A_FaceTarget();
	A_Spawnitemex("BelphFire",7,random(12,14),22,0,0,2);
	A_Spawnitemex("BelphFire",7,random(-12,-14),22,0,0,2);
	}
    BELA G 6 Light("Shadowball2")
	{
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnitemEx("BelphFire",7,random(-5,5),random(18,26),0,0,2);
	A_SpawnProjectile("PM_FastImpBall",35,5);
	}
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
    Goto See2;
   Melee:
   Recoil:
	TNT1 A 0 { bNODROPOFF = true; }
	TNT1 A 0 A_Jump(160,"Dodge");
	TNT1 A 0 ThrustThingZ(0,35,0,1);
	TNT1 A 0 A_Recoil(25);
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_Jump(50,"Missile");
	Goto See2;
  Dodge:
	TNT1 A 0
	{
	bNODROPOFF = true;
	ThrustThing(randompick(0,128),random(12,16), 0, 0);
	ThrustThingZ(0,30,0,1);
	}
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_Jump(50,"Missile");
	Goto See2;
  Jump:
	TNT1 A 0
	{
	bNODROPOFF = true;
	A_Recoil(-20);
	ThrustThingZ(0,40,0,1);
	}
	CBEL E 28 A_FaceTarget();
	CBEL B 10 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 A_Jump(45,"CrouchSee");
	Goto See2;
  FearCheck:
	TNT1 A 0 A_JumpIfCloser(90,"FearTwo");
	TNT1 A 0 A_Jump(80,"Fear");
	Goto See2;
  FearTwo:
	TNT1 A 0 A_Jump(128,"Recoil");
  Fear:
	TNT1 A 0 { bFRIGHTENED = true; }
	Goto See2;
  Fight:
	TNT1 A 0 { bFRIGHTENED = false; }
	Goto See2;
  Roar:
	BELA BBBFFF 3 A_FaceTarget();
	TNT1 A 0 PM_AlertSound();
	BELA AAAAAAAAA 3 A_FaceTarget();
	BELA BBBB 3 A_FaceTarget();
	Goto See2;
  Pain:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TNT1 A 0 { bNOGRAVITY = false; }
    BELA H 2;
    BELA H 8 A_Pain;
	TNT1 A 0 A_Jump(90,"Recoil");
	TNT1 A 0 { bFRIGHTENED = true; }
    goto See2;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_StopSounds(0,0);
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TNT1 A 0 { bNOGRAVITY = false; }
    BELA I 8;
    BELA J 8 A_Scream;
    BELA K 6;
    BELA L 6 A_NoBlocking;
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
    BELA M -1;
    stop;
  XDeath:
	TNT1 A 0;
	TNT1 A 0 A_StopSounds(0,0);
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TNT1 A 0 { bNOGRAVITY = false; }
    BELA N 5;
    BELA O 5 A_XScream;
    BELA P 5;
    BELA Q 5 A_NoBlocking;
    BELA RS 5;
    BELA T -1;
    stop;
  Raise:
    BELA ML 8;
    BELA KJI 6;
    goto Look;
	}
}

Class RFireBallExplode : Actor
{
    Default
	{
	Scale 0.65;
	Alpha 0.84;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	Speed 1;
	}
	States
{
Spawn:
	FXPR AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06);
	Stop;
	}
}

Class InfernoBallExplode : Actor
{
    Default
	{
	Scale 1.2;
	Alpha 0.84;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	Speed 1;
	}
	States
    {
   Spawn:
	FXPR AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06);
	Stop;
	}
}

Class PM_FastImpBall : PM_CharredBall
{
    Default
	{
	DamageFunction (11);
	Scale 0.915;
	seesound "Zephyr/Ball";
	Decal "PM_RedFireScorch";
	+NOGRAVITY
	-LOWGRAVITY
	Speed 18;
	FastSpeed 21;
	}
	states
  	{
  Spawn:
    BFX5 AABBCC 1 bright Light("Shadowball2") 
	{
	//A_SpawnItemEx("OrangeEmbers",0,0,0,Random(5,-5),Random(5,-5),Random(5,-5),0);
	//A_SpawnItemEx("FastImpBallTrail",0,0,0,0,0,0,0,128,0);
	//A_SpawnItemEx("BelphFastFire",random(-3,3),random(-3,3),random(-3,3),0,0,0,0,128,0);
	A_SpawnItemEx("PM_BelphFlameTrails",0,0,0,0,0,0,0,128,0);
	}
	Loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_SetTranslation("PM_RedFireTint");
	TNT1 A 0 A_SetScale(1);
	TNT1 A 0 A_SpawnItemEx("RFireBallExplode");
    BFX5 D 2 Bright Light("Shadowball2");
    FIR3 CDEFGHIJKLMNOP 2 Bright Light("Shadowball2");
    stop;
  }
}

Class BelphFire : AzazelFire
  {
  Default
  { Scale 0.2; }
  States
  {
  Spawn:
	TNT1 A 0;
	Go:
	FIR1 R 1
	{
	A_FadeOut(0.1);
	A_SetRoll(random(0,360));
	A_SetScale(Scale.X-0.01);
	}
	Loop;
	}
}