Class PM_Wendigo : PMFamineMonsterBase
{ 
    Default
	{
	PMMonsterBase.CodexLore
	"Ravenous hosts that only quench on carrion and flesh. Even though they devour and eat whatever they have seen, their endless hunger is limitless.
	These suffering beasts are known for their relentless endeavour to stop its own famine, yet they have been already doomed to their fate. All of these conditions led them to having a hawkish behavior, making them much more dangerous than they used to be.
	
	Are much more agile compared to the other devils, and WILL slink towards you in a frenzy while you stand.
	Once they find food, they will \crNOT\c- stop.";
    Health 60;
    Radius 20;
    Height 56;
    Speed 14;
    PainChance 100;
    Mass 100;
    MONSTER;
	+MISSILEEVENMORE
    +FLOORCLIP
	+QUICKTORETALIATE
	+HARMFRIENDS
	+DOHARMSPECIES
	-DONTHARMSPECIES
	-DONTHARMCLASS
	XScale 0.95;
	YScale 1.02;
	Species "Imp";
	Tag "Wendigo";
    Obituary "%o is now a host to the endless, ravenous hunger.";
    SeeSound "Wendigo/Sight";
    PainSound "Wendigo/Pain";
	DeathSound "Wendigo/Death";
    ActiveSound "Wendigo/Idle";
	Bloodtype "PM_NormalBloodBase";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	
	override void Tick()
	{
		super.Tick();
		HandleEating(250);
	}
	
	States
	{
	Spawn:
		F_IM F 0  nodelay { bSPRITEFLIP = false; eaten = false; }
	Idle:
		F_IM A 10 PM_Look();
		Loop;
	Eat:
		F_IM AABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAABBCCDDAAB 1;
		F_IM A 0 { eaten = false; bNOPAIN = false; }
		Goto See;
	Look:
		F_IM F 0 { bSPRITEFLIP = false; }
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AABB 6 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCDD 6 PM_LookingForPlayer();
		Goto Look;
	Spotted:
		TNT1 A 0 PM_AlertSound();
		Goto See;
	See:
		F_IM F 0 A_SetSpeed(12);
		F_IM F 0 { bSPRITEFLIP = false; }
		F_IM G 0 A_StopSound(CHAN_7);
		F_IM G 0 A_SetSpeed(14);
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AABB 2 A_Chase();
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCDD 2 A_Chase();
		F_IM D 0 A_Jump(60, "Rush");
		Loop;
	Rush:
		F_IM F 0 { bSPRITEFLIP = false; }
		F_IM F 0 A_CheckProximity("SmallRush", "PM_Wendigo", 3, 420, CPXF_CHECKSIGHT);
		F_IM G 0 A_SetSpeed(17);
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AAABBB 1
		{
			A_Recoil(-.75);
			A_Chase(null, null);
			A_SpawnItemEx("PM_GenericPhaseGhost", zofs: 6, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
			
			if (!random(0, 25)) A_ChangeVelocity(0, 0, frandom(1, 4), CVF_RELATIVE);
			if (target && CheckSight(target) && distance3D(target) <= 80 && !random(0, 4)) SetStateLabel("Retreat");
		}
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCCDDD 1
		{
			A_Recoil(-.75);
			A_Chase("Pounce", null);
			A_SpawnItemEx("PM_GenericPhaseGhost", zofs: 6, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
			
			if (target && CheckSight(target) && distance3D(target) <= 80 && !random(0, 4)) SetStateLabel("Retreat");
		}
		F_IM A 0 A_Jump(40, "See");
		Loop;
	SmallRush:
		F_IM G 0 A_SetSpeed(14);
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AAAABBBB 1
		{
			A_Recoil(-.75);
			A_FaceTarget(4, 4);
			A_Chase(null, null);
			A_SpawnItemEx("PM_GenericPhaseGhost", zofs: 6, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		}
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCCCDDDD 1
		{
			A_Recoil(-.75);
			A_Chase("Pounce", null);
			A_FaceTarget(4, 4);
			A_SpawnItemEx("PM_GenericPhaseGhost", zofs: 6, flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		}
		F_IM A 0 A_Jump(40, "See");
		Loop;
	Retreat:
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AAABBB 1 { A_FaceTarget(); A_Recoil(3); }
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCCDDD 1 { A_FaceTarget(); A_Recoil(2); }
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM AAABBB 1 { A_FaceTarget(); A_Recoil(2); }
		TNT1 A 0 A_StartSound("Revenant/Step",CHAN_6);
		F_IM CCCDDD 1 { A_FaceTarget(); A_Recoil(1); }
		F_IM A random(10, 60) A_FaceTarget();
		Goto See;
	Missile:
		F_IM F 0 A_JumpIfCloser(250, "Melee");
		F_IM F 0 A_Jump(40, "Charge");
		F_IM F 0 A_Jump(128, "Leap");
		Goto See;
	Charge:
		F_IM G 0 A_StartSound("GenericChargeHeavy", pitch: 1.5);
		F_IM GFEEE 4 A_FaceTarget(3, 3);
		F_IM E 4 { vel.z += 4; }
		F_IM F 0 A_StartSound("Wendigo/Sight",CHAN_7);
		F_IM EEEEEEEEEEEEEEEEEEEEEEEEE 1
		{
			A_Recoil(-4);
			A_SpawnItemEx("PM_GenericPhaseGhost", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
			A_SpawnProjectile("PM_WeakMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
		}
		F_IM FG 5;
		Goto See;
	Leap:
		F_IM AFFFF 4 A_FaceTarget();
		F_IM F 0 A_StartSound("Wendigo/Lunge",CHAN_7);
		F_IM F 1 PM_LeapTo(target,800,"See");
		Goto LeapLoop;
	LeapLoop:
		F_IM GGGG 1 
		{
		A_SpawnItemEx("PM_GenericPhaseGhost", flags: SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		A_ScaleVelocity(1.01);
		if(random(1,15) == 1)
		   ThrustThing(random(0,360)*256/360,2,0,0);
		 if(target && Distance3D(target) <= 60)
		 { SetStateLabel("LeapMelee"); }
		}
		F_IM G 0 A_CheckFloor("LeapEnd");
		Loop;
	LeapMelee:
		F_IM G 1 A_Explode(10*random(1,5),50,XF_NOTMISSILE,0,50);
		F_IM GGGG 3 A_CheckFloor("LeapEnd");
		TNT1 A 0 ThrustThing(random(0,360)*256/360,2,0,0);
		Wait;
	LeapEnd:
		F_IM A 0 A_StartSound("fiend/land",CHAN_AUTO,CHANF_DEFAULT,1,1.85,0.7);
		F_IM G 4 A_Stop;
		F_IM G 8;
		Goto See;
	Melee:
		F_IM E 0 A_JumpIfCloser(100, "Close");
		Goto Pounce;
	Pounce:
		F_IM F 0 { bSPRITEFLIP = randompick(true, false); }
		F_IM EF 4 A_FaceTarget();
		TNT1 A 0 ThrustThingZ(0,25,0,1);
		TNT1 A 0 A_FaceTarget();
		TNT1 A 0 A_StartSound("Wendigo/Melee", CHAN_6);
		TNT1 A 0 A_Recoil(random(-10, -25));
		F_IM GGG 2 A_SpawnProjectile("PM_FeralLunge",18,0,0,CMF_AIMDIRECTION);
		F_IM GFE 4;
		F_IM E 0 A_Stop();
		F_IM F 0 { bSPRITEFLIP = false; }
		F_IM F 0 A_Jump(70, "Backup");
		Goto See;
	Close:
		F_IM E 0 A_Jump(135, "Pounce");
		F_IM E 0 { A_Recoil(-5); ThrustThingZ(0,15,0,1); }
		F_IM EF 3 A_FaceTarget();
		F_IM G 3 A_CustomMeleeAttack(7 * random(1, 2), "imp/melee");
		F_IM F 0 { bSPRITEFLIP = true; A_Recoil(-5); ThrustThingZ(0,15,0,1); }
		F_IM EF 3 A_FaceTarget();
		F_IM G 3 A_CustomMeleeAttack(7 * random(1, 2), "imp/melee");
		F_IM F 0 { bSPRITEFLIP = false; }
		F_IM F 0 { if (target && Distance3D(target) > 100) SetStateLabel("See"); }
		F_IM F 0 A_Jump(90, "Backup");
		Loop;
	BackUp:
		F_IM I 0
		{
			vel.z += 5;
			A_Recoil(16);
			A_FaceTarget();
		}
		Goto Charge;
	Pain:
		F_IM F 0 { bSPRITEFLIP = false; }
		F_IM H 2;
		F_IM H 2 A_Pain();
		F_IM H 0 A_Jump(60, "Rush");
		Goto See;
	Death:
		F_IM I 6;
		F_IM J 6 A_Scream();
		F_IM K 6 A_NoBlocking();
		F_IM L 6;
		F_IM M -1;
		Stop;
	Raise:
		F_IM MLKJI 6;
		Goto See;
	}
}

Class PM_WendigoGroup : Actor
{

  Default
	{
	 +ISMONSTER
	}

	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		for (int i = random(2, 3); i > 0; i--) A_SpawnProjectile("PM_WendigoSummon", 1, 0, frandom(0, 360), CMF_AIMDIRECTION);
		Destroy();
		return;
	}
}

Class PM_WendigoSummon : PM_ExaltedCultSummon
{
  States
  {
  Spawn:
	TNT1 A random(1,15) NoDelay;
	TNT1 A 0 A_SpawnItemEx("PM_Wendigo",zofs:1,flags:SXF_NOCHECKPOSITION);
	Stop;
  }
}