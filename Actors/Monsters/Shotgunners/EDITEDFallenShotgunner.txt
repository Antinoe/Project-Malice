Actor PM_FallenShotgunner
{
   Health 120
   Height 56
   Radius 20
   Speed 8
   PainChance 96
   SeeSound "FallenSoldiers/Sight"
   ActiveSound "FallenSoldiers/Active"
   PainSound "FallenSoldiers/Pain"
   DeathSound "FallenSoldiers/Death"
   Obituary "%o's innards were revealed by a Fallen Shotgunner."
   DropItem "Shotgun"
   Species "Shotgunner"
   Tag "Fallen Shotgunner"
   Monster
    +FloorClip
    +NoTarget
	+MissileMore
    +NoInfighting
  States
  {
  Spawn:
    ZSP1 AAAAAAAAAABBBBBBBBBB 1 A_Look()
    Loop
  See:
	TNT1 A 0 A_SetSpeed(7)
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AAA 2 A_Chase
	ZSP1 BBB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCC 2 A_Chase
	ZSP1 DDD 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_CheckRange(500,"SmokeShieldCheck")
  RealMissile:
	ZSP1 EEE 4 A_FaceTarget
    ZSP1 F 2 Bright
    {
	  A_PlaySound("ShotgunnerGunDistant", CHAN_7);
      A_PlaySound("FSGunner/Fire", CHAN_WEAPON);
      A_FaceTarget;
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
    }
    ZSP1 EEE 5 A_FaceTarget
	ZSP1 A 0 A_PlaySound("GenericShotgunPump")
	ZSP1 AA 5 A_FaceTarget
	ZSP1 BB 5 A_FaceTarget
    Goto See
  SmokeShieldCheck:
    TNT1 A 0 A_JumpIfInventory("FallenSGunnerSmokeToken",2,"See")
    Goto SmokeShield
  SmokeShield:
    TNT1 A 0 A_GiveInventory("FallenSGunnerSmokeToken",1)
    ZSP1 AB 5 A_FaceTarget
    TNT1 A 0 A_PlaySound("ZTrooper/FragThrow",CHAN_6)
    ZSP1 AA 5 A_FaceTarget
    ZSP1 B 5 
     {
      A_CustomMissile("PM_FallenSGunnerSmokeGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,8);
      A_FaceTarget;
     }
    Goto Retreat
  Retreat:
	TNT1 A 0 A_SetSpeed(14)
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AABB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCDD 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AABB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCDD 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AABB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCDD 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AABB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCDD 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AABB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCDD 2 A_Chase
    TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
    TNT1 A 0 A_SetSpeed(7)
	Goto See
  Pain:
    ZSP1 G 3 A_Pain
    ZSP1 G 5
    Goto See
  Death:
	TNT1 A 0 A_Jump(35,"PossDeath")
    ZSP1 H 5
    ZSP1 I 5 A_Scream
    ZSP1 J 5
    ZSP1 K 5 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
    ZSP1 L -1
	Stop
  PossDeath:
    ZSP1 H 5
    ZSP1 I 5 A_Scream
    ZSP1 J 5
    ZSP1 K 5 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
	ZSP1 L 90
	TNT1 A 0 A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION)
    ZSP1 L -1
	Stop
  XDeath:
	TNT1 A 0 A_Stop
	TNT1 A 0 A_FaceTarget
	TNT1 A 0 A_XScream
	TNT1 A 0 A_NoBlocking
	SGUY M 4
	SGUY N 4
	SGUY O 4
	SGUY PQRS 4
	SGUY T 4 A_Stop
	SGUY U -1
	Stop
  Raise:
    ZSP1 LKJIH 5
    Goto See
  }
}

Actor PM_FallenSGunnerSmokeGrenade : Rocket
{
  var int user_grenadetime;
  var int user_smoketime;
  Speed 30
  Scale 0.66
  Radius 4
  Height 10
  Gravity 0.8
  Bouncetype Hexen
  BounceFactor 0.44
  WallBounceFactor 0.66
  BounceCount 5
  SeeSound ""
  DeathSound ""
  BounceSound "ZTrooper/FragBounce"
  Decal ""
  -NOGRAVITY
  -ROCKETTRAIL
  +THRUACTORS
  +ROLLSPRITE
  +CANBOUNCEWATER
  +BOUNCEONCEILINGS
  States
  {
  Spawn:
	ZGRN A 1 
    {
     A_SpawnItemEx("PM_DarkExpSmoke",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
     A_SetRoll(roll+15);
    }
	Loop
  Death:
	ZGRN AA 1
		{
		if (user_grenadetime >= 35)
			{
			return state("Smoke");
			}
		A_SpawnItemEx("PM_DarkExpSmoke",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
		A_SetUserVar(user_grenadetime,user_grenadetime+1);
		return state(0);
		}
	Loop
  Smoke:
    TNT1 A 0 A_SetRoll(0)
    ZGRN BB 1
	{
		if (user_smoketime >= 5)
			{
			return state("XDeath");
			}
		A_SpawnItemEx("PM_HuskSmokeCloud",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
		A_SetUserVar(user_smoketime,user_smoketime+1);
		return state(0);
    }
	Loop
   XDeath:
    ZGRN B -1
    Stop
	}
}

Actor FallenSGunnerSmokeToken : Inventory 
{ 
+Inventory.undroppable
 Inventory.maxamount 2
}