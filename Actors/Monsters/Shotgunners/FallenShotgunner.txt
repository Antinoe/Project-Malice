Actor PM_FallenShotgunner
{
   Health 120
   Height 56
   Radius 20
   Speed 8
   PainChance 96
   SeeSound ""
   ActiveSound "FallenSoldiers/Active"
   PainSound "FallenSoldiers/Pain"
   DeathSound "FallenSoldiers/Death"
   Obituary "%o's innards were revealed by a Fallen Shotgunner."
   DropItem "Shotgun"
   Species "Shotgunner"
   Tag "Fallen Shotgunner"
   Monster
    +FloorClip
    +NoTarget
	+MissileMore
    +NoInfighting
  States
  {
  Spawn:
    ZSP1 AAAAAAAAAABBBBBBBBBB 1 A_Look()
    Loop
  See:
    TNT1 A 0 A_CheckSight("See")
	TNT1 A 0 A_Jump(35,"Backup")
	TNT1 A 0 A_SetSpeed(7)
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AAA 2 A_Chase("","",CHF_NOPLAYACTIVE)
	ZSP1 BBB 2 A_Chase("","",CHF_NOPLAYACTIVE)
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCC 2 A_Chase("","",CHF_NOPLAYACTIVE)
	ZSP1 DDD 2 A_Chase("","",CHF_NOPLAYACTIVE)
    Goto Spotted
  Spotted:
    TNT1 A 0 A_Jump(256,"Roar","SpotMissile","See2")
	Goto See2
  Roar:
    TNT1 A 0 A_PlaySound("FallenSoldiers/Sight",CHAN_5)
	Goto See2
  SpotMissile:
    ZSP1 EEE 5 A_FaceTarget
	Goto Missile
  See2:
	TNT1 A 0 A_Jump(35,"Backup")
	TNT1 A 0 A_SetSpeed(7)
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
    ZSP1 AAA 2 A_Chase
	ZSP1 BBB 2 A_Chase
	TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
	ZSP1 CCC 2 A_Chase
	ZSP1 DDD 2 A_Chase
    Loop
  Missile:
    TNT1 A 0 A_CheckRange(300,"SmokeShieldCheck")
    TNT1 A 0 A_Jump(55,"RealMissile2")
  RealMissile:
	ZSP1 EEE 4 A_FaceTarget
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  A_PlaySound("ShotgunnerGunDistant", CHAN_7);
      A_PlaySound("FSGunner/Fire", CHAN_WEAPON);
      A_FaceTarget;
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
    }
    ZSP1 EEE 5 A_FaceTarget
	ZSP1 A 0 A_PlaySound("GenericShotgunPump")
	ZSP1 AA 5 A_FaceTarget
	ZSP1 BB 5 A_FaceTarget
    Goto See2
  RealMissile2:
    TNT1 A 0 A_PlaySound("FallenSoldiers/Sight",CHAN_5)
	ZSP1 EEE 3 A_FaceTarget
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  A_PlaySound("ShotgunnerGunDistant", CHAN_7);
      A_PlaySound("FSGunner/Fire", CHAN_WEAPON);
      A_FaceTarget;
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
	  A_CustomMissile("PM_BulletTracer",35,7,random(-2, 2),CMF_OFFSETPITCH,random(-3, 3));
    }
    ZSP1 EEE 3 A_FaceTarget
	ZSP1 A 0 A_PlaySound("GenericShotgunPump")
	ZSP1 AA 4 A_FaceTarget
	ZSP1 BB 4 A_FaceTarget
	TNT1 A 0 A_Jump(30,"See2")
	TNT1 A 0 A_MonsterRefire(60,"See2")
    Goto RealMissile2+1
  SmokeShieldCheck:
    TNT1 A 0 A_JumpIfInventory("FallenSGunnerSmokeToken",1,"RealMissile")
    Goto SmokeShield
  SmokeShield:
	TNT1 A 0
	TNT1 A 0 A_Jump(90,"RealMissile")
    ZSP1 EEE 4 A_FaceTarget
	TNT1 A 0 A_GiveInventory("FallenSGunnerSmokeToken",1)
    ZSP1 F 2 Bright Light("YellowMediumFlicker2")
    {
	  A_PlaySound("ShotgunnerGunDistant", CHAN_7);
      A_PlaySound("GrenadeLaunch", CHAN_WEAPON);
      A_FaceTarget;
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_CustomMissile("BulletFlare", 35, 15, 15, 0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_SpawnItemEx("PM_BulletHitDust",20,7,34,random(-1,1),random(-1,1),random(-1,1),0,0,0,0);
	  A_CustomMissile("PM_FallenSGunnerSmokeGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,10);
    }
    ZSP1 EE 5 A_FaceTarget
	ZSP1 A 0 A_PlaySound("GenericShotgunPump")
	ZSP1 AA 4 A_FaceTarget
	ZSP1 BB 4 A_FaceTarget
    Goto Backup
  Backup:
		TNT1 A 0 A_CheckSight("See2")
		ZSP1 DCBADCBADCBA 3
		{
		A_FaceTarget;
		A_Recoil(2);
		A_FaceTarget;
		}
	Goto See2
  Pain:
    ZSP1 G 3 A_Pain
    ZSP1 G 5
    Goto See2
  Death:
	TNT1 A 0 A_Jump(35,"PossDeath")
    ZSP1 H 5
    ZSP1 I 5 A_Scream
    ZSP1 J 5
    ZSP1 K 5 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
    ZSP1 L -1
	Stop
  PossDeath:
    ZSP1 H 5
    ZSP1 I 5 A_Scream
    ZSP1 J 5
    ZSP1 K 5 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
	ZSP1 L 90
	TNT1 A 0 A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION)
    ZSP1 L -1
	Stop
  XDeath:
	ZSP1 H 5
    ZSP1 I 5 A_Scream
    ZSP1 J 5
    ZSP1 K 5 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
    ZSP1 L -1
	Stop
  Raise:
    ZSP1 LKJIH 5
    Goto See
  }
}

Actor PM_FallenSGunnerSmokeGrenade : Rocket
{
  var int user_grenadetime;
  var int user_smoketime;
  Speed 16
  Scale 0.66
  Radius 4
  Height 10
  Gravity 0.8
  Bouncetype Hexen
  BounceFactor 0.44
  WallBounceFactor 0.66
  BounceCount 5
  SeeSound ""
  DeathSound ""
  BounceSound "ZTrooper/FragBounce"
  Decal ""
  -NOGRAVITY
  -ROCKETTRAIL
  +THRUACTORS
  +ROLLSPRITE
  +CANBOUNCEWATER
  +BOUNCEONCEILINGS
  States
  {
  Spawn:
	GRNP A 1 
    {
     A_SpawnItemEx("DevilBallSmoke",random(-1,1),random(-1,1),0,0,0,0,0,128,50);
    }
	Loop
  Death:
	GRNP AA 1
		{
		if (user_grenadetime >= 10)
			{
			return state("Smoke");
			}
		//A_SpawnItemEx("PM_FallenShotgunSmoke",0,frandom(-2,2),frandom(-2,2),random(-5,5),random(-5,5),random(0,3));
		A_SetUserVar(user_grenadetime,user_grenadetime+1);
		return state(0);
		}
	Loop
  Smoke:
	TNT1 A 0 A_PlaySound("SmokeGrenadeExp",CHAN_7)
  Smoke1:
    GRNP AA 1
	{
		if (user_smoketime >= 16)
			{
			return state("XDeath");
			}
		A_SpawnItemEx("PM_FallenShotgunSmoke",0,frandom(-2,2),frandom(-2,2),random(-6,6),random(-6,6),random(0,2));
		A_SetUserVar(user_smoketime,user_smoketime+1);
		return state(0);
    }
	Loop
   XDeath:
    GRNP A -1
    Stop
	}
}

Actor FallenSGunnerSmokeToken : Inventory 
{ 
+Inventory.undroppable
 Inventory.maxamount 2
}

Actor PM_FallenShotgunSmoke
{
  Radius 2
  Height 2
  Mass 0x7fffffff
  +NOBLOCKMAP
  +NOGRAVITY
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +NOTELEPORT
  +SKYEXPLODE
  +FORCEXYBILLBOARD
  +FOILINVUL
  +CANBLAST
  +RIPPER
  +THRUACTORS
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  +FORCERADIUSDMG
  +DOOMBOUNCE
  +ROLLSPRITE
  BounceFactor 0.3
  WallBounceFactor 0.3
  Alpha 0.8
  Scale 2.5
  DeathSound "RevGhoul/cloud"
  states
  {
  Spawn:
	TNT1 A 1
	TNT1 A 0 A_SetRoll(random(0,360))
	TNT1 A 0 A_SetScale(0.1)
	GNSM KKKKKKKKKKKKKKK 2 A_SetScale(scalex+0.07)
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
	GNSM K 600
	GNSM KKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKKK 8
	{
	A_FadeOut(0.04);
	}
  Death:
	TNT1 A 1
	Stop
  }
}