Class PM_EnragedBreacher : PMWrathMonsterBase
{ 
    int user_numbullets;
    int user_numbreaths;
	Default
	{
	PMMonsterBase.CodexLore 
	"Former humans doomed to a grizzly fate after being possessed by a Wrath sprite, their blood has completely turned into viscous material similar to ash and their skin into molten rock. Any and all attempts at resisting the influence of the sprite is impossible, as their minds have reverted to its base instincts, with only one solution to every problem:
	
	 Kill.
	 
	 Uses a heavy shotgun to blast you apart. Can also load in a dragon's breath round to blow you and her away.";
    Health 50;
    Radius 20;
    Height 56;
    Speed 12;
    PainChance 200;
    Mass 100;
    MONSTER;
    +FloorClip
	+SLIDESONWALLS
	+HARMFRIENDS
	+DOHARMSPECIES
	Bloodtype "PM_BlackBloodBase";
	BloodColor "Black";
	Species "Shotgunner";
	Tag "Enraged Breacher";
    Obituary "%o was a victim to an Enraged Breacher's wrathful assault.";
    SeeSound "Breacher/Sight";
	PainSound "Breacher/Pain";
	DeathSound "Breacher/Death";
	ActiveSound "FAfflicted/Idle";
	//DropItem "Lith_Shell";
    DropItem "Shotgun";
    Decal "Bulletchip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
	
	override void PM_Gib()
	{
	  for(int i=0; i<2; i++)
	   {
	     A_SpawnItemEx("PM_BlackBloodMist",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
	    for(int gib=0; gib<5; gib++){A_SpawnItemEx("PM_NormalMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);}
	     A_SpawnItemEx("PM_BlackMeatChunkLauncher",0,0,32,random(-10,10),random(-10,10),random(-10,10),frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY);
	   }
	  A_StartSound("ArachExpGore",CHAN_6,CHANF_DEFAULT,0.65);
	  A_StartSound("GBloodSplashBig",CHAN_7,CHANF_DEFAULT,0.75);
	}
	
    States
    {
    Spawn:
        FOMZ A 0 NoDelay
        {
         user_numbullets = random(4,8);
         user_numbreaths = random(1,3);
        }
    Idle:
		FOMZ A 10 PM_Look();
        Loop;
	Look:
        TNT1 A 0 A_Jump(15,"Spotted");
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        FOMZ AA 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		FOMZ BB 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
        TNT1 A 0 A_JumpifTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		FOMZ CC 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		FOMZ DD 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
        Goto Look+1;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","SpotMissile");
		Goto See;
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	SpotMissile:
        TNT1 A 0 PM_AlertSound();
	    FOMZ EEE 3 A_FaceTarget();
	    Goto Missile;
    See:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        FOMZ AA 4 A_Chase();
		TNT1 AA 0 A_Recoil(-0.75);
		FOMZ BB 4 A_Chase();
		TNT1 AA 0 A_Recoil(-0.75);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		FOMZ CC 4 A_Chase();
		TNT1 AA 0 A_Recoil(-0.75);
		FOMZ DD 4 A_Chase();
		TNT1 AA 0 A_Recoil(-0.75);
		TNT1 A 0 A_Jump(20,"Sprint");
        Loop;
	Sprint:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        FOMZ AA 2 A_Chase(null,"RageMissile");
		TNT1 AA 0 A_Recoil(-0.75);
		FOMZ BB 2 A_Chase(null,"RageMissile");
		TNT1 AA 0 A_Recoil(-0.75);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		FOMZ CC 2 A_Chase(null,"RageMissile");
		TNT1 AA 0 A_Recoil(-0.75);
		FOMZ DD 2 A_Chase(null,"RageMissile");
		TNT1 AA 0 A_Recoil(-0.75);
		TNT1 A 0 A_Jump(45,"See");
		Loop;
    Missile:
		TNT1 A 0;
        TNT1 A 0
		{ 
		}
		TNT1 A 0 
		{ 
		  if(!user_numbullets)
		   { SetStateLabel("NoAmmo"); }
		  if(target && CheckSight(target) && Distance3D(target) < 800 && random(1,5) == 1)
		   { SetStateLabel("Breath"); }
		}
		TNT1 A 0 A_Jump(56,"RageMissile");
		FOMZ EE 7
		{
		A_FaceTarget(360,180);
		A_SetAngle(angle+random(-2,2));
		A_SetPitch(pitch+random(-2,2));
		}
		TNT1 A 0 A_StartSound("Breacher/Shotgun",10);
		TNT1 A 0 A_StartSound("ShotgunnerGunDistant",CHAN_7,CHANF_OVERLAP);
		FOMZ F 5 Light("YellowMediumFlicker2")
			{
			 for(int i=6;i>0;i--)
			  A_SpawnProjectile("PM_HellionFlare",35,7,frandom(-8,8),CMF_AIMDIRECTION,pitch+frandom(-4,4));
              user_numbullets--;
			}
		FOMZ EEE 5 A_FaceTarget();
		TNT1 A 0 A_StartSound("GenericShotgunPump");
		FOMZ AA 5 A_FaceTarget();
        TNT1 A 0 A_SpawnItemEx("PM_ShellCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
		FOMZ BB 5 A_FaceTarget();
		Goto See;
	RageMissile:
	    TNT1 A 0 { pm_attackloop = random(3,9); }
		TNT1 A 0 A_StartSound("Breacher/Rage",11);
		FOMZ EE 7
		{
		 A_FaceTarget(360,180);
		 A_SetAngle(angle+random(-2,2));
		 A_SetPitch(pitch+random(-2,2));
		}
	RageLoop:
        TNT1 A 0
		{
		 if(target)
		 {
		  if(!CheckSight(target) && random(1,50) == 1)
		   { SetStateLabel("See"); }
		  if(!user_numbullets)
		   { SetStateLabel("NoAmmo"); }
		 }
		}
		TNT1 A 0 A_StartSound("Breacher/Shotgun",10);
		TNT1 A 0 A_StartSound("ShotgunnerGunDistant",CHAN_7,CHANF_OVERLAP);
		FOMZ F 5 Light("YellowMediumFlicker2")
			{
			 for(int i=6;i>0;i--)
			  A_SpawnProjectile("PM_HellionFlare",35,7,frandom(-8,8),CMF_AIMDIRECTION,pitch+frandom(-4,4));
              user_numbullets--;
			}
		FOMZ EEE 3 A_FaceTarget(10,10);
		TNT1 A 0 A_StartSound("GenericShotgunPump");
		FOMZ AA 5 A_FaceTarget(10,10);
        TNT1 A 0 A_SpawnItemEx("PM_ShellCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
		FOMZ BB 5 A_FaceTarget(10,10);
		Loop;
	Breath:
        TNT1 A 0;
        TNT1 A 0 { if(!user_numbreaths) SetStateLabel("ZReloadBreath"); }
		TNT1 A 0 A_StartSound("Breacher/Sight",CHAN_5);
		FOMZ AAA 3
		{
		  A_FaceTarget(360,180);
		  A_SetAngle(angle+random(-5,5));
		  A_SetPitch(pitch+random(-5,5));
		}
		FOMZ A 10 { A_StartSound("PM/ShellInsert",CHAN_6); A_FaceTarget(5,5); }
		TNT1 A 0 A_StartSound("GenericShotgunPump");
		FOMZ AA 5 A_FaceTarget(5,5);
		FOMZ BB 5 A_FaceTarget(5,5);
		FOMZ EEE 5 A_FaceTarget(5,5);
		TNT1 A 0 A_StartSound("Bulltubus/Shotgun",CHAN_5);
		TNT1 A 0 A_StartSound("ShotgunnerGunDistant", CHAN_7,CHANF_OVERLAP);
		TNT1 A 0 A_Recoil(6);
		FOMZ F 5 Light("YellowMediumFlicker2")
		{
	       for(int i=8;i>0;i--)
	         { A_SpawnProjectile("PM_BulltubusShotgunTracer",32,-34,frandom(-12,12),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-5,5)); }
            user_numbreaths--;
		}
		FOMZ EEEEEEEEEEEEEEEEE 2
		{
		  A_SpawnItemEx("PM_VentSmoke",20,7,34,random(2,-2),random(2,-2),random(1,3),0,0,0,0);
		  A_FaceTarget();
		}
		TNT1 A 0 A_StartSound("GenericShotgunPump");
		FOMZ AA 5 A_FaceTarget();
        TNT1 A 0 A_SpawnItemEx("PM_ShellCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
		FOMZ BB 5 A_FaceTarget();
		Goto See;
	NoAmmo:
		FOMZ E 7 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		FOMZ EE 10 A_FaceTarget();
		TNT1 A 0 A_Jump(100,"ZReload");
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		FOMZ E 8 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		FOMZ E 8 A_FaceTarget();
		TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		FOMZ AAAA 5 A_FaceTarget();
		Goto ZReload;
    ZReload:
        TNT1 A 0;
		TNT1 A 0 A_FaceTarget();
        TNT1 A 0
		{
		if(user_numbullets == 8)
		SetStateLabel("ZReload2");
		}
        FOMZ AAA random(1,4);
        TNT1 A 0 A_StartSound("PM/ShellInsert",CHAN_5);
        TNT1 A 0 { user_numbullets += 1; }
        FOMZ BB 3;
        FOMZ AAA 3;
		TNT1 A 0
	    {
	     if(target && CheckSight(Target) && Distance3D(target) < 250)
	     SetStateLabel("ZReload2");
	    }
        Loop;
    ZReloadBreath:
        TNT1 A 0;
        TNT1 A 0
		{
		if(user_numbreaths == 3)
		SetStateLabel("ZReload2");
		}
        FOMZ AAA random(1,4);
        TNT1 A 0 A_StartSound("PM/ShellInsert",CHAN_5);
        TNT1 A 0 { user_numbreaths += 1; }
        FOMZ BB 3;
        FOMZ AAA 3;
		TNT1 A 0
	    {
	     if(target && CheckSight(Target) && Distance3D(target) < 250)
	     SetStateLabel("ZReload2");
	    }
        Loop;
    ZReload2:
	    TNT1 A 0;
	    TNT1 A 0 A_StartSound("GenericShotgunPump");
	    FOMZ AA 5 A_FaceTarget();
	    FOMZ BB 5 A_FaceTarget();
        Goto See;
    Pain:
        FOMZ G 5;
        FOMZ G 3 A_Pain;
        Goto See;
    Death:
	    TNT1 A 0 A_StopSound(11);
        FOMD A 5 A_NoBlocking;
        FOMD B 5 PM_Scream();
        FOMD C 5;
        FOMD DE 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		FOMD F 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        FOMD G -1;
        Stop;
    XDeath:
        TNT1 A 0 { A_StopSound(11); A_StopSound(555); }
        FOMD H 5 PM_Gib();
        FOMD I 5 A_XScream;
        FOMD J 5 A_NoBlocking;
        FOMD KLMNO 5;
        FOMD P -1;
        Stop;
    Raise:
        FOMD NMLKJIH 5;
        Goto Look;
    }
}