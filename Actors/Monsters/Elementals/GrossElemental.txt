ACTOR PM_GrossElemental : PMMonsterBase
{
  Health 500
  Radius 31
  Height 56
  Mass 400
  Speed 10
  PainChance 160
  BloodColor "Green"
  DamageType "Poison"
  Obituary "$o was melted by the acid vomit of the Gross Elemental, Eugh!"
  SeeSound "GrossElem/Sight"
  DeathSound "GrossElem/Die"
  ActiveSound "GrossElem/Idle"
  PainSound "GrossElem/Pain"
  AttackSound "GrossElem/Fire"
  Tag "Gross Elemental"
  Species "Elemental"
  Bloodtype "PM_PestilenceBloodBase"
  +DONTHARMSPECIES
  +FLOATBOB
   FloatBobStrength 0.4
   +NOGRAVITY
    +FLOAT 
  states
   {
  Spawn:
   DFLY A 10 PM_Look
   Loop
  Look:
   DFLY AA 3 PM_LookingForPlayer()
   DFLY BB 3 PM_LookingForPlayer()
   DFLY CC 3 PM_LookingForPlayer()
   Loop
  Spotted:
   TNT1 A 0 PM_AlertSound()
  See2:
   TNT1 A 0 A_ChangeFlag("MISSILEEVENMORE",0)
   TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
   TNT1 A 0 A_SetSpeed(10)
   DFLY AA 3 A_Chase
   DFLY BB 3 A_FastChase
   DFLY CC 3 A_Chase
   Loop
  Missile:
   DFLY D 0 A_Jump(56,"Vomit")
   TNT1 A 0 A_Jump(115,"Standoff")
   TNT1 A 0 A_Jump(120,"MaggotPuke")
   DFLY D 4 A_FaceTarget
   DFLY E 4 A_FaceTarget
   TNT1 A 0 A_PlaySound("Afflicted/Puke")
   TNT1 AAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
   DFLY F 0 A_PainAttack("PM_GagRotfly",0,PAF_NOSKULLATTACK,8)
   DFLY F 2 A_FaceTarget
   Goto See2
  MaggotPuke:
   DFLY D 4 A_FaceTarget
   DFLY E 4 A_FaceTarget
   TNT1 A 0 A_PlaySound("Afflicted/Puke")
   TNT1 AAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
   DFLY F 0 A_PainAttack("PM_VenomMaggot",0,PAF_NOSKULLATTACK,6)
   DFLY F 2 A_FaceTarget
   Goto See2
  Vomit:
   TNT1 A 0 A_PlaySound("Afflicted/Puke")
   DFLY DDDDDDDDD 1 
   {
   A_FaceTarget;
   A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4));
   }
   DFLY EEEEEEFFFFF 1
   {
   A_FaceTarget;
   A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4));
   }
   TNT1 AAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
   TNT1 A 0 A_PlaySound("GrossElem/Spit",CHAN_5)
   DFLY FFFF 0 A_CustomMissile("PM_GElemVenomBurst", 23, 0, random(-6,6),CMF_OFFSETPITCH,random(-2,2))
   DFLY F 9 A_CustomMissile("PM_GElemVenomBurst", 23, 0, random(-6,6),CMF_OFFSETPITCH,random(-2,2))
   DFLY F 2 A_FaceTarget
   Goto See2
 Standoff:
	TNT1 A 0
	TNT1 A 0 ThrustThing(random(0,360),10)
	TNT1 A 0 ThrustThingZ(0,7,random(0,1),1)
	DFLY ABABAB 2
	{
	A_Chase("","");
	A_FaceTarget;
	}
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
	DFLY AB 4 A_FaceTarget
  StandoffStart:
    TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE)
    TNT1 A 0 A_ChangeFlag("MissileevenMore",1)
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
	TNT1 A 0 A_Jump(7,"See2")
	TNT1 A 0 A_SetSpeed(0)
	DFLY AAAAAA 2 {
		A_Chase("StandoffRepos","StandoffMissile");
		A_FaceTarget;
		}
	DFLY BBBBBB 2 {
		A_Chase("StandoffRepos","StandoffMissile");
		A_FaceTarget;
		}
	DFLY CCCCCCC 2 {
		A_Chase("StandoffRepos","StandoffMissile");
		A_FaceTarget;
		}
	TNT1 A 0 A_CheckSight("See2")
	TNT1 A 0 A_Jump(120,"StandoffRepos")
	Loop
  StandoffRepos:
	TNT1 A 0
	TNT1 A 0 ThrustThing(random(0,360),14)
	TNT1 A 0 ThrustThingZ(0,7,random(0,1),2)
	DFLY ABABAB 2
	{
	A_Chase("","");
	A_FaceTarget;
	}
	Goto StandoffStart
  StandoffMissile:
   TNT1 A 0 A_Jump(67,"StandoffFlySpawn")
   TNT1 A 0 A_Jump(76,"StandoffMaggotSpawn")
	DFLY ABCE 4
		{
		A_Chase;
		A_FaceTarget;
		}
    TNT1 A 0 A_PlaySound("GrossElem/Spit",CHAN_5)
    DFLY DD 3 A_FaceTarget
    TNT1 AAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
	DFLY E 6
		{
		A_Chase;
		A_FaceTarget;
		A_CustomMissile("PM_GElemVenomBurst", 23, 0, random(-3,3),CMF_OFFSETPITCH,random(-1,1));
        A_CustomMissile("PM_GElemVenomBurst", 23, 0, random(-3,3),CMF_OFFSETPITCH,random(-1,1));
        A_CustomMissile("PM_GElemVenomBurst", 23, 0, random(-3,3),CMF_OFFSETPITCH,random(-1,1));
		}
	DFLY ED 5
		{
		A_Chase;
		A_FaceTarget;
		}
    TNT1 A 0 A_Jump(45,"StandoffRepos")
	Goto StandoffStart
  StandoffFlySpawn:
   DFLY D 4 A_FaceTarget
   DFLY E 4 A_FaceTarget
   TNT1 A 0 A_PlaySound("Afflicted/Puke")
   TNT1 AAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
   DFLY F 0 A_PainAttack("PM_GagRotfly",0,PAF_NOSKULLATTACK,8)
   DFLY F 2 A_FaceTarget
   Goto StandoffStart
  StandoffMaggotSpawn:
   DFLY D 4 A_FaceTarget
   DFLY E 4 A_FaceTarget
   TNT1 A 0 A_PlaySound("Afflicted/Puke")
   TNT1 AAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
   DFLY F 0 A_PainAttack("PM_VenomMaggot",0,PAF_NOSKULLATTACK,6)
   DFLY F 2 A_FaceTarget
   Goto StandoffStart
  Pain:
   TNT1 A 0 A_ChangeFlag("MISSILEEVENMORE",0)
   TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
   TNT1 A 0 A_SetSpeed(10)
   DFLY G 4 Fast
   DFLY G 0 A_Wander
   DFLY G 0 A_Wander
   DFLY G 4 Fast A_Pain
   Goto See2
  Death:
   DFLY HI 4 A_PlaySound("GrossElem/Die",CHAN_6)
   DFLY IJ 3 Bright
   TNT1 A 0 A_PlaySound("Blob/Explode",CHAN_6)
   TNT1 A 0 A_PlaySound("GBloodSplashBig",CHAN_7)
   TNT1 AAAAAA 0 A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION)
   TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_PestilenceBloodMist",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION)
   TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodyCloudBig",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION)
   TNT1 AAAAA 0 A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",0,0,random(10,30),0,0,0,0,SXF_NOCHECKPOSITION)
   TNT1 A 5
   TNT1 AAA 0 A_SpawnItemEx("PM_VenomMaggot",random(-25,25),random(-25,25),20,random(-3,3),random(-3,3),random(5,14),0,SXF_NOCHECKPOSITION)
   Stop
  }
}

ACTOR PM_VenomMaggot : PMMonsterBase
{
    Health 50
    Scale 0.40
    Radius 12
    Height 20
    Mass 75
    PainChance 150
    Speed 7
	Tag "Venom Maggot"
    BloodType "PM_PestilenceBloodBase"
    BloodColor "Green"
    Obituary "$o's flesh was infected by the Venom Maggot's deadly bite, Gross!"
    SeeSound "VenMaggot/Sight"
    DeathSound "VenMaggot/Die"
    ActiveSound "VenMaggot/Idle"
    PainSound "VenMaggot/Pain"
    AttackSound "VenMaggot/Melee"
    Species "Elemental"
    +FLOORCLIP
    +DONTHURTSPECIES
    +DONTHARMSPECIES
    -THRUSPECIES
    -COUNTKILL
    States
    {
   Spawn:
     MA1G AB 5 PM_Look()
     Loop
   Look:
     MA1G AABBCCDD 2 Fast PM_LookingForPlayer()
     Loop
   Spotted:
     TNT1 A 0 PM_AlertSound()
     Goto See
   See:
     MA1G AABBCCDD 2 Fast A_Chase
    Loop
   Missile:
    TNT1 A 0
    TNT1 A 0 A_JumpIfCloser(600,"RealMissile")
    Goto See
   RealMissile:
    TNT1 A 0 A_PlaySound("Rotfly/Attack")
    MA1G EEEEEEEEEEEFFFFFFFF 1
    {
    A_FaceTarget;
    A_SpawnItemEx("PM_RotflySmallSplash",0,0,15,0,0,random(-2,0),0,128,125);
    }
   MA1G G 8 A_CustomMissile("PM_VenomMaggotSpit",15,0,random(-5,5),CMF_OFFSETPITCH,random(10,20))
   Goto See
   Melee:
     MA1G EF 6 Fast A_FaceTarget
     MA1G G 6 Fast A_CustomMeleeAttack(random(2, 6) * 4, "VenMaggot/Melee", "")
    Goto See
   Pain:
     MA1G H 2 Fast
     MA1G H 2 Fast A_Pain
    Goto See
   Death:
     MA1G I 4 A_Scream
     TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodMist",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION)
	 TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",random(-2,2),random(-2,2),random(2,5),random(-2,2),random(-2,2),random(2,5),0,SXF_NOCHECKPOSITION)
	 TNT1 AAA 0 A_SpawnItemEx("PM_PestilenceMeatChunkLauncher",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION,130)
     MA1G J 4
     MA1G K 1 A_NoBlocking
     MA1G L 1
     MA1G M 2
     MA1G N -1
    Stop

  }
}

actor PM_VenomMaggotSpit : DoomImpBall
{
	Damage (8)
	seesound ""
	deathsound ""
	Speed 19
	FastSpeed 25
	Scale 0.93
    -NOGRAVITY
    Gravity 0.5
	states
  	{
  Spawn:
    TNT1 A 2
	{
	//A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
	A_SpawnItemEx("PM_RotflySmallSplash",0,0,0,0,0,0,0,128,0);
	}
	loop
  Death:
	TNT1 A 0
	TNT1 AAAAAA 0 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-2,2),random(-2,2),random(0,3))
    stop
  }
}