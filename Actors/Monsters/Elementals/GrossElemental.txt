ACTOR PM_GrossElemental : PainElemental
{
Health 500
Speed 8
PainChance 160
BloodColor "Green"
DamageType "Poison"
Obituary "$o was melted by the acid vomit of the Gross Elemental, Eugh!"
SeeSound ""
DeathSound "GrossElem/Die"
ActiveSound "GrossElem/Idle"
PainSound "GrossElem/Pain"
AttackSound "GrossElem/Fire"
Tag "Gross Elemental"
Species "Elemental"
Bloodtype "PM_PestilenceBloodBase"
+DONTHARMSPECIES
+FLOATBOB
FloatBobStrength 0.4
+MissileMore
states
{
Spawn:
DFLY A 10 A_Look
Loop
See:
DFLY AA 3 A_Chase("","",CHF_NOPLAYACTIVE)
DFLY BB 3 A_Chase("","",CHF_NOPLAYACTIVE)
DFLY CC 3 A_Chase("","",CHF_NOPLAYACTIVE)
TNT1 A 0 A_CheckSight("See")
Goto Spotted
Spotted:
TNT1 A 0 A_PlaySound("GrossElem/Sight",CHAN_5)
See2:
DFLY AA 3 A_Chase
DFLY BB 3 A_FastChase
DFLY CC 3 A_Chase
Loop
Missile:
DFLY D 0 A_Jump(80,"Vomit")
DFLY D 4 A_FaceTarget
DFLY E 4 A_FaceTarget
TNT1 A 0 A_PlaySound("Afflicted/Puke")
TNT1 AAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(5,10),random(-2,2),random(-3,0),random(5,10),random(-3,3),frandom(-4,4))
DFLY F 0 A_PainAttack("PM_Rotfly",0,PAF_NOSKULLATTACK)
DFLY F 2 A_FaceTarget
Goto See2
Vomit:
DFLY D 4 Fast A_FaceTarget
DFLY E 4 Fast A_FaceTarget
DFLY F 9 A_CustomMissile("PukeVenom", 23, random(-6,6), random(-6,6))
DFLY F 9 A_CustomMissile("PukeVenom", 23, random(-6,6), random(-6,6))
DFLY F 9 A_CustomMissile("PukeVenom", 23, random(-6,6), random(-6,6))
DFLY F 9 A_CustomMissile("PukeVenom", 23, random(-6,6), random(-6,6))
DFLY F 2 Fast A_FaceTarget
Goto See2
Pain:
DFLY G 4 Fast
DFLY G 0 A_Wander
DFLY G 0 A_Wander
DFLY G 4 Fast A_Pain
Goto See2
Death:
DFLY H 8 BRIGHT A_Scream
DFLY IJK 8 BRIGHT
DFLY L 8 BRIGHT A_PainDie("VenomMaggot")
DFLY M 8 BRIGHT
Stop
}
}

ACTOR VenomMaggot : Demon 6157
{
Health 90
Scale 0.5
Radius 15
Height 28
PainChance 70
Speed 7
BloodColor "Green"
Obituary "$o's flesh was infected by the Venom Maggot's deadly bite, Gross!"
SeeSound "VenMaggot/Sight"
DeathSound "VenMaggot/Die"
ActiveSound "VenMaggot/Idle"
PainSound "VenMaggot/Pain"
AttackSound "VenMaggot/Melee"
-COUNTKILL
states
{
Spawn:
MA1G AB 5 A_Look
Loop
See:
MA1G AABBCCDD 2 Fast A_Chase
Loop
Melee:
MA1G EF 6 Fast A_FaceTarget
MA1G G 6 Fast A_CustomMeleeAttack(random(2, 6) * 4, "VenMaggot/Melee", "", "Poison")
Goto See
Pain:
MA1G H 2 Fast
MA1G H 2 Fast A_Pain
Goto See
Death:
MA1G I 4 A_Scream
MA1G J 4
MA1G K 2 A_NoBlocking
MA1G L 2
MA1G M 2
MA1G N -1
Stop
Raise:
MA1G N 4
MA1G MLKJI 4
Goto See
}
}

ACTOR PukeVenom : CacodemonBall
{
Speed 15
Damage 5
RenderStyle Add
DamageType "Poison"
SeeSound "TaintedBall"
DeathSound "TaintedBloodExp"
PoisonDamage 1, 8
Gravity 0.4
States
{
Spawn:
VBAL A 2 
{
A_SpawnItemEx("PukeTrail", 0, 0, 0);
A_SpawnItemEx("HostPoisonSplash",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),frandom(-1,1));
}
VBAL B 2
{
A_SpawnItemEx("PukeTrail", 0, 0, 0);
A_SpawnItemEx("HostPoisonSplash",random(-1,1),random(-1,1),random(-1,1),random(-1,1),random(-1,1),frandom(-1,1));
}
Loop
Death:
TNT1 AAAAAA 0 A_SpawnItemEx("HostPoisonSplash",random(-1,1),random(-1,1),random(-1,1),random(-5,5),random(-5,5),frandom(1,6))
VBAL CDE 4
Stop
}
}

ACTOR PukeTrail
{
RenderStyle Add
Gravity 0.2
+FLOATBOB
States
{
Spawn:
Death:
VBAL A 2 A_SetScale(1.0)
VBAL B 2 A_SetScale(0.9)
VBAL A 2 A_SetScale(0.8)
VBAL B 2 A_SetScale(0.7)
VBAL A 2 A_SetScale(0.6)
VBAL B 2 A_SetScale(0.5)
VBAL A 2 A_SetScale(0.4)
VBAL B 2 A_SetScale(0.3)
VBAL A 2 A_SetScale(0.2)
VBAL B 2 A_SetScale(0.1)
Stop
}
}
