Class PM_Wicked : Actor
{
  Default
  {
    Health 350;
    Radius 20;
    Height 80;
    Mass 200;
    Speed 10;
	Scale 1;
    PainChance 112;
	//BloodColor "Black";
    Obituary "%o got blasted by a Wicked.";
    SeeSound "WickSight";
    PainSound "WickPain";
    DeathSound "WickDeath";
    ActiveSound "WickActive";
	+DONTHARMSPECIES;
	Species "Cacodemon";
	Tag "Wicked";
    Monster;
    +NoGravity
	//+NOINFIGHTING
    +Float
  }

  States
  {
  Spawn:
    WIC2 ABCD 8 A_Look();
    Loop;
  See:
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	SeeLoop:
	//TNT1 A 0  A_SetShootable();
	TNT1 A 0 {bNOPAIN = false;}//A_ChangeFlag("NOPAIN",0);
	TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
	TNT1 A 0 A_SetSpeed(10);
    WIC2 AABBCCDD 4 A_Chase();
	TNT1 A 0 A_Recoil(-0.5);
    WIC2 A 0 A_Jump(64, "See2","See3");
    Loop;
  See2:
	//TNT1 A 0  A_UnSetShootable();
	TNT1 A 0 {bNOPAIN = true;}//A_ChangeFlag("NOPAIN",1);
	TNT1 A 0 A_SetRenderStyle(1,STYLE_Stencil);
	TNT1 A 0 A_Playsound("WickRush");
	Go:
	TNT1 A 0 A_SetSpeed(14);
    WIC2 EEFFGGHH 1 WickedPhase();
    WIC2 E 0 A_Jump(55, "See");
    Loop;
  See3:
    WIC2 EEFFGGHH 2 A_Chase();
	TNT1 A 0 A_Recoil(-0.5);
    //WIC2 E 0 A_Jump(2, "See");
    Loop;
  Missile:
	TNT1 A 0  A_SetShootable();
	TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
    WIC2 I 1 A_PlaySound("WickAttack");
	TNT1 A 0  A_SetShootable();
    WIC2 I 4 A_FaceTarget();
    WIC2 J 5 BRIGHT A_FaceTarget();
	TNT1 AAAAAAA 0 A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
	TNT1 A 0 A_SpawnProjectile("BulletFlare",60,10);
    WIC2 K 8 BRIGHT A_SpawnProjectile("WickedShot",60,0,0);
    WIC2 L 5;
    Goto See;
  Pain:
	TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
	TNT1 A 0  A_SetShootable();
    WIC2 M 3;
    WIC2 M 3 A_Pain();
    WIC2 M 3;
    Goto See;
  Death:
	TNT1 A 0  A_SetShootable();
	TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
    WIC2 N 5 A_Scream();
    WIC2 OP 5;
    WIC2 P 0 A_SpawnItemEX("WickedTorso", 0,0,48, 0,0,0, 0, SXF_SETMASTER);
    WICT A 5 A_NoBlocking();
    WICT BCDEF 5;
    WICT G -1 A_SetFloorClip();
    Stop;
  Raise:
    WICT G 0 A_RemoveChildren(true);
    WIC2 G 0 A_SpawnItemEx("WickedTorsoRes", 0,0,0, 0,0,1, 0, 0);
    WICT G 5 A_UnSetFloorClip();
    WICT FEDBC 5;
    WIC2 PON 5;
    Goto See;
  }
   void WickedPhase()
  {
    A_SpawnItem("WickedFX2",0,0);
    A_Wander();
	A_Recoil(-0.7);
  }
}

Class WickedShot : Actor
{
  Default
  {
    Radius 8;
    Height 8;
    Speed 10;
    Damage 5;
    RenderStyle "Add";
    DamageType "Fire";
    Alpha 0.67;
    SeeSound "WickFire";
    DeathSound "WickFireExp";
    Decal "DoomImpScorch";
    Projectile;
    +ThruGhost
    +SeekerMissile
    +ForceXYBillboard
  }

  States
  {
  Spawn:
    WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright A_SpawnItemEx("WickedFX", 0,0,0, random(-1,1),random(-1,1),random(-1,1), 0, SXF_CLIENTSIDE, 0);
    WIBL ABCD 0 Bright A_SeekerMissile(90,90);
    Loop;
  Death:
    WIBL JKLMN 3 Bright;
    Stop;
  }
}

Class WickedFX2 : Actor
{
  Default
  {
    Radius 2;
    Height 2;
    Speed 0;
    Scale 0.95;
    RenderStyle "Subtract";
    Alpha 0.80;
    Projectile;
    +ClientSideOnly
  }

  States
  {
  Spawn:
    TNT1 A 1;
    WIC2 F 5 A_FadeOut(0.25);
    Goto Spawn+1;
  }
}

Class WickedFX : Actor
{
  Default
  {
    Radius 2;
    Height 2;
    RenderStyle "Add";
    Alpha 0.67;
    +NoBlockMap
    +NoGravity
    +DontSplash
    +ForceXYBillboard
  }

  States
  {
  Spawn:
    TNT1 A 3 Bright;
    WIBL EFGHI 3 BRIGHT;
    Stop;
  }
}

Class WickedTorso : Actor
{
  Default
  {
    Mass 1000000;
    Radius 1;
    Height 1;
	Scale 1;
    +IsMonster
    +Corpse
  }

  States
  {
  Spawn:
    WIC2 Q 5 NoDelay A_PlaySound("monster/tenpn1");
    WIC2 R 5;
    Wait;
  Crash:
    WIC2 S 1 A_SetFloorClip();
    WIC2 S 4 A_PlaySound("monster/tenpn2");
    WIC2 TUV 5;
    WIC2 W -1;
    Stop;
  }
}

Class WickedTorsoRes : Actor
{
  Default
  {
    Radius 24;
    Height 24;
	Scale 1;
    +NoGravity
    +NoBlockMap
    +NoClip
  }

  States
  {
  Spawn:
    WICK WVUTSRQ 5;
    Stop;
  }
}

