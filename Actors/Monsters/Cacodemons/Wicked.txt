Class PM_Wicked : PMMonsterBase
{
	Default
	{
		Health 350;
		Radius 31;
		Height 56;
		Mass 200;
		Speed 10;
		Scale 1;
		PainChance 112;
		//BloodColor "Black";
		Obituary "%o was scragged by a Wicked.";
		SeeSound "WickSight";
		PainSound "WickPain";
		DeathSound "WickDeath";
		ActiveSound "WickActive";
		+DONTHARMSPECIES;
		Species "Cacodemon";
		Tag "Wicked";
		FloatBobStrength 0.5;
		Monster;
		Bloodtype "PM_NormalBloodBase";
		+NoGravity
		//+NOINFIGHTING
		+Float
		+FloatBob
		TeleFogSourceType "PM_SmallDemonicTeleportFog";
		TeleFogDestType "PM_DemonicTeleportFog";
		PMMonsterBase.RealHeight 86;
	}

	States
	{
		Spawn:
			WIC2 ABCD 8 PM_Look();
			Loop;
		Look:
			TNT1 A 0 {bNOPAIN = False;}//A_ChangeFlag("NOPAIN",0);
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_SetSpeed(10);
			WIC2 AABB 4 PM_LookingForPlayer();
			TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
			WIC2 CCDD 4 PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
			Loop;
		Spotted:
			TNT1 A 0 PM_AlertSound();
		See1:
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			SeeLoop:
			//TNT1 A 0  A_SetShootable();
			TNT1 A 0 {bNOPAIN = False;}//A_ChangeFlag("NOPAIN",0);
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_SetSpeed(10);
			WIC2 AABBCCDD 4
			{
				A_Chase();
				A_FaceTarget();
			}
			TNT1 A 0 A_Recoil(-0.5);
			WIC2 A 0 A_Jump(64, "See2","See3");
			Loop;
		See2:
			//TNT1 A 0  A_UnSetShootable();
			TNT1 A 0 {bNOPAIN = True;}//A_ChangeFlag("NOPAIN",1);
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Stencil);
			TNT1 A 0 A_StartSound("WickRush");
		Go:
			TNT1 A 0 A_SetSpeed(14);
			WIC2 EEFFGGHH 1 WickedPhase();
			WIC2 E 0 A_Jump(55, "See1");
			Loop;
		See3:
			WIC2 EEFFGGHH 2 A_Chase();
			TNT1 A 0 A_Recoil(-0.5);
			//WIC2 E 0 A_Jump(2, "See");
			Loop;
		Missile:
			TNT1 A 0 A_Jump(128,"Missile12","Missile2","Charge");
			TNT1 A 0  A_SetShootable();
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			WIC2 I 1 A_StartSound("WickAttack");
			TNT1 A 0  A_SetShootable();
			WIC2 I 4 A_FaceTarget();
			WIC2 J 5 BRIGHT A_FaceTarget();
			TNT1 AAAAAAA 0 A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
			TNT1 A 0 A_SpawnProjectile("BulletFlare",60,10);
			WIC2 K 8 BRIGHT A_SpawnProjectile("WickedShot",60,0,0);
			WIC2 K 0 BRIGHT A_SpawnProjectile("WickedShot",60,0,15);
			WIC2 K 0 BRIGHT A_SpawnProjectile("WickedShot",60,0,-15);
			WIC2 L 5;
			Goto See1;
		Missile12:
			TNT1 A 0  A_SetShootable();
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			WIC2 I 1 A_StartSound("WickAttack");
			TNT1 A 0  A_SetShootable();
			WIC2 I 4 A_FaceTarget();
			WIC2 J 5 BRIGHT A_FaceTarget();
			TNT1 AAAAAAA 0 A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
			TNT1 A 0 A_SpawnProjectile("BulletFlare",60,10);
			WIC2 K 8 BRIGHT A_SpawnProjectile("WickedShot",60,0,15);
			WIC2 K 8 BRIGHT A_SpawnProjectile("WickedShot",60,0,0);
			WIC2 K 0 BRIGHT A_SpawnProjectile("WickedShot",60,0,-15);
			WIC2 L 5;
			Goto See1;
		Missile2:
			TNT1 A 0 A_SetShootable();
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			WIC2 I 1 A_StartSound("GenericChargeSmall");
			TNT1 A 0  A_SetShootable();
			WIC2 IIIIIIIIIIIIIIJJJJ 1 Light("AvatarFireball2")
			{
				A_FaceTarget();
				A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
				A_SpawnProjectile("BulletFlare",60,0);
			}
			TNT1 A 0 A_StartSound("WickAttack");
			WIC2 J 5 Light("AvatarFireball3") A_FaceTarget();
			TNT1 AAAAAAA 0 A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
			TNT1 A 0 A_SpawnProjectile("BulletFlare",60,0);
			WIC2 K 1 Light("AvatarFireball4");
			WIC2 K 7 Light("AvatarFireball4") 
			{
				PM_LeadTarget(18);
				A_SpawnProjectile("WickedRevolveShot1",60,0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
				A_SpawnProjectile("WickedRevolveShot2",60,0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
			}
			WIC2 L 5;
			Goto See1;
		Charge:
			TNT1 A 0  A_SetShootable();
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			WIC2 I 1 A_StartSound("GenericChargeHeavy");
			WIC2 IIIIIIIIIIIIIIIIIIIIIIIJJJJJJJ 1 Light("AvatarFireball2")
			{
				A_FaceTarget();
				A_SpawnProjectile("BulletFlare",60,0);
				A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
			}
			WIC2 JJJJJJJJJJJJJJJJJJJJJJ 1 Light("AvatarFireball3")
			{
				A_FaceTarget();
				A_SpawnProjectile("BulletFlare",60,0);
				A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
				A_SpawnItemEx("OrangeEmbersFloat",0,random(-1,1),60,random(1,4),random(-1,1),random(-1,1));
			}
			WIC2 KKKKKK 5 
			{
				A_StartSound("WickAttack");
				A_SpawnProjectile("WickedShot",60,0,random(-10,10),CMF_OFFSETPITCH,random(-5,5));
				A_FaceTarget();
			}
			TNT1 A 0  A_SetShootable();
			WIC2 K 4 A_FaceTarget();
			WIC2 K 5 BRIGHT A_FaceTarget();
			WIC2 K 8 BRIGHT A_FaceTarget();
			WIC2 L 5;
			Goto See1;
		Pain:
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			TNT1 A 0  A_SetShootable();
			WIC2 M 3;
			WIC2 M 3 A_Pain();
			WIC2 M 3;
			Goto See1;
		Death:
			TNT1 A 0 A_StopSounds(0,0);
			TNT1 A 0 {bFLOATBOB = False;}
			TNT1 A 0;
			TNT1 A 0 A_SetRenderStyle(1,STYLE_Normal);
			WIC2 N 5 A_Scream();
			WIC2 OP 5;
			WIC2 P 0 A_SpawnItemEX("WickedTorso", 0,0,48, 0,0,0, 0, SXF_SETMASTER);
			WICT A 5 A_NoBlocking();
			WICT BCDEF 5;
			WICT G -1 A_SetFloorClip();
			Stop;
		Raise:
			WICT G 0 A_RemoveChildren(True);
			WIC2 G 0 A_SpawnItemEx("WickedTorsoRes", 0,0,0, 0,0,1, 0, 0);
			WICT G 5 A_UnSetFloorClip();
			WICT FEDBC 5;
			WIC2 PON 5;
			TNT1 A 0 {bFLOATBOB = True;}
			Goto Look;
	}

	void WickedPhase()
	{
		A_SpawnItem("WickedFX2",0,0);
		A_Wander();
		A_Recoil(-0.7);
	}
}

Class WickedShot : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 10;
		Damage 5;
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		SeeSound "WickFire";
		DeathSound "WickFireExp";
		Decal "DoomImpScorch";
		Projectile;
		+ThruGhost
		+SeekerMissile
		+ForceXYBillboard
	}

	States
	{
		Spawn:
			TNT1 A 0 A_ScaleVelocity(0.25);
			WIBL ABCDABCDABCDABCD 1 A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
			TNT1 A 0 A_ScaleVelocity(0.20);
			WIBL ABCDABCDABCD 1 A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
			TNT1 A 0 A_ScaleVelocity(0.15);
			WIBL ABCDABCD 1 A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
			TNT1 A 0 A_ScaleVelocity(0.1);
			WIBL ABCD 1 A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
			TNT1 A 0 A_ScaleVelocity(0.1);
			WIBL ABCDABCDABCDABCDABCD 1 
			{
				A_SpawnItemEx("WickedFX", 0,0,0, random(-2,2),random(-2,2),random(-2,2), 0, SXF_CLIENTSIDE, 0);
			}
			TNT1 A 0 A_StartSound("WickChargeThrow");
			TNT1 A 0 A_SeekerMissile(90,90,SMF_PRECISE);
			WIBL A 5 A_ScaleVelocity(2);
			WIBL ABCD 1 Bright 
			{
				A_SpawnParticle("FF9838",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
			}
			Wait;
		Death:
			WIBL JKLMN 3 Bright;
			Stop;
	}
}

Class WickedRevolveShot1 : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 18;
		Damage (5);
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		SeeSound "WickFire";
		DeathSound "WickFireExp";
		Decal "DoomImpScorch";
		Projectile;
		+ThruGhost
		+ForceXYBillboard
	}

	States
	{
		Spawn:
			WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright 
			{
				A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
				A_SpawnParticle("FF9838",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
				A_SpawnParticle("FF9838",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
				A_Weave(2,0,2.7,0);
			}
			Loop;
		Death:
			WIBL JKLMN 3 Bright;
			Stop;
	}
}

Class WickedRevolveShot2 : Actor
{
	Default
	{
		Radius 8;
		Height 8;
		Speed 18;
		Damage (5);
		RenderStyle "Add";
		DamageType "Fire";
		Alpha 0.67;
		SeeSound "WickFire";
		DeathSound "WickFireExp";
		Decal "DoomImpScorch";
		Projectile;
		+ThruGhost
		+ForceXYBillboard
	}

	States
	{
		Spawn:
			WIBL ABCDABCDABCDABCDABCDABCDABCD 1 Bright 
			{
				A_SpawnItemEx("PM_YellowProjectileFlare",0,0,0);
				A_SpawnParticle("FF9838",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
				A_SpawnParticle("FF9838",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
				A_Weave(-2,0,2.7,0);
			}
			Loop;
		Death:
			WIBL JKLMN 3 Bright;
			Stop;
	}
}

Class WickedFX2 : Actor
{
	Default
	{
		Radius 2;
		Height 2;
		Speed 0;
		Scale 0.95;
		RenderStyle "Subtract";
		Alpha 0.80;
		Projectile;
		+ClientSideOnly
	}

	States
	{
		Spawn:
			TNT1 A 1;
			WIC2 F 5 A_FadeOut(0.25);
			Goto Spawn+1;
	}
}

Class WickedFX : Actor
{
	Default
	{
		Radius 2;
		Height 2;
		RenderStyle "Add";
		Alpha 0.67;
		+NoBlockMap
		+NoGravity
		+DontSplash
		+ForceXYBillboard
	}

	States
	{
		Spawn:
			TNT1 A 3 Bright;
			WIBL EFGHI 3 BRIGHT;
			Stop;
	}
}

Class WickedTorso : Actor
{
	Default
	{
		Mass 1000000;
		Radius 1;
		Height 1;
		Scale 1;
		-SOLID
		-SHOOTABLE
		+Corpse
	}

	States
	{
		Spawn:
			WIC2 Q 5 NoDelay A_StartSound("monster/tenpn1");
			WIC2 R 5;
			Wait;
		Crash:
			WIC2 S 1 A_SetFloorClip();
			WIC2 S 4 A_StartSound("monster/tenpn2");
			WIC2 TUV 5;
			WIC2 W -1;
			Stop;
	}
}

Class WickedTorsoRes : Actor
{
	Default
	{
		Radius 24;
		Height 24;
		Scale 1;
		+NoGravity
		+NoBlockMap
		+NoClip
	}

	States
	{
		Spawn:
			WICK WVUTSRQ 5;
			Stop;
	}
}

