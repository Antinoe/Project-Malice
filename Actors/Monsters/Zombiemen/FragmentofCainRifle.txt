Class PM_FOCRifle : PMMonsterBase
{ 
    int cainphase;
	Default
	{
    Health 250;
    Radius 20;
    Height 56;
    Speed 14;
    Mass 100;
    PainChance 35;
    Damage 1;
    MONSTER;
    +FloorClip
	+SLIDESONWALLS
	+DONTHARMCLASS
	+DONTFALL
	+NOICEDEATH
	+NOCLIP
	+NOBLOOD
	+NOBLOCKMONST
	Species "Zombie";
	Tag "FR?GM?NT ?F CA?N";
    Obituary "%o lost against a fragment of a betrayer.";
	RenderStyle "Stencil";
	StencilColor "Black";
    SeeSound "FOC/Sight";
	PainSound "";
	DeathSound "FOC/Pain";
	ActiveSound "FOC/Active";
	BloodType "PM_BlackBloodBase";
    DropItem "Clip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
	
    States
    {
    Spawn:
		TNT1 A 10 PM_Look();
        Loop;
	Look:
        TNT1 A 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",50,JLOSF_DEADNOJUMP);
		TNT1 A 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
        Loop;
	Spotted:
	    TNT1 A 0 PM_AlertSound();
	    TNT1 A 0 A_Jump(256,"PhaseStart","See2");
		Goto See2;
    See2:
        UDM2 AA 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		UDM2 BB 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		UDM2 CC 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		UDM2 DD 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_Jump(20,"PhaseStart");
        Loop;
	 PhaseStart:
		TNT1 A 0 A_StartSound("HellPriestSee",0,volume:0.15,pitch:frandom(0.3,0.5));
		TNT1 A 0 { cainphase += 80; }
		TNT1 AAAAAAAAA 0 A_FadeOut(0.1,0);
		Goto PhaseGo;
	 PhaseGo:
        UDM2 AABBCCDD 1
		{
		if(cainphase < 0)
		{ SetStateLabel("Dephase"); }
		if(random(1,60) == 1)
		{ cainphase += 20; }
		A_Wander();
		A_SpawnItemEx("PM_FOCPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSPRITEFRAME);
		cainphase--;
		}
		Loop;
	  Dephase:
	    UDM2 AAAAAAAAA 1 A_FadeIn(0.1,0);
    Missile:
	    UDM2 E 12 A_StartSound("FOC/Attack");
        UDM2 E 7 { pm_attackloop = random(5,13); }
		UDM2 E 7
		{
		A_FaceTarget(360,180);
		A_SetAngle(angle+random(-10,10));
		A_SetPitch(pitch+random(-15,15));
		}
	MissileLoop:
        UDM2 F 2
		{
		if(pm_attackloop <= 0)
		{ SetStateLabel("MissileEnd"); }
		if(random(1,5) == 1)
		{ ThrustThing(randompick(0,128),random(5,9),0,0); }
		A_SpawnProjectile("PM_FOCTracer",33,7,random(-1,1),CMF_AIMDIRECTION,pitch+frandom(-1,1));
		pm_attackloop--;
		}
		UDM2 E random(2,10) A_FaceTarget(30,60,0,0);
        Loop;
	MissileEnd:
	    UDM2 E random(10,15) A_FaceTarget(30,60,0,0);
		Goto See2;
    Pain:
	    TNT1 AAAAAAAAAA 0 A_FadeIn(0.1,0);
        UDM2 G 5;
        UDM2 G 3 A_Pain;
		TNT1 A 0 A_Jump(25,"PhaseStart");
        Goto See2;
	XDeath:
    Death:
	    TNT1 A 0 A_StopSounds(0,0);
		TNT1 A 0 A_Recoil(-10);
        UDM2 G 1 A_NoBlocking;
	    TNT1 A 0 A_Scream;
		TNT1 A -1;
        Stop;
    }
}


Class PM_FOCTracer : PM_BulletTracer
{
 Default
 {
 RenderStyle "Normal";
 Decal "None";
 Alpha 1.5;
 }
 States
 {
 Spawn:
 TNT1 A 0;
 TNT1 A 0
        {
        A_SpawnParticleEx
            (
            "000000",
            TexMan.CheckForTexture("GNSMK0"),
            style: STYLE_Add,
            flags: SPF_RELATIVE|SPF_ROLL,
            lifetime: random(10,250),
            size: frandom(10,50),
            xoff: -25,
            velx: frandom(0,2),
            vely: frandom(-0.4,0.4),
            velz: frandom(-0.4,0.7),
            startalphaf: frandom(0.3,0.5),
            fadestepf: -0.5,
            sizestep: frandom(0.0,1.4),
            startroll: random(-180,180),
            rollvel: frandom(-1,1)
            );
            A_SpawnParticleEx
            (
            "00000",
            TexMan.CheckForTexture("DTPRR0"),
            style: STYLE_Add,
            flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
            lifetime: random(1,4),
            size: frandom(40,190),
            xoff: -22,
            startalphaf: 1.0,
            sizestep: frandom(0.0,3),
            startroll: random(-180,180)
            );
            A_SpawnParticleEx
            (
            "00000",
            TexMan.CheckForTexture("DTPRR0"),
            style: STYLE_Add,
            flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
            lifetime: random(1,4),
            size: frandom(40,190),
            xoff: -22,
            startalphaf: 1.0,
            fadestepf: 0,
            sizestep: frandom(0.0,3),
            startroll: random(-180,180)
            );
        }
	Go:
		TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        Loop;
	Death:
	  Stop;
  }
}

Class PM_FOCPhaseGhost : Actor
{
 Default
 {
  +NOINTERACTION
  +BRIGHT
  RenderStyle "Stencil";
  Alpha 0.5;
 }
States
{
Spawn:
    TNT1 A 3;
    "----" A 1 A_FadeOut(0.06);
    Wait;
    }
}

Class PM_FOCRifleFlee : Actor
{
  Default
  {
  +NOINTERACTION
  +THRUACTORS
  +FLOORHUGGER
  +MISSILE
  +BRIGHT
  Speed 20;
  Alpha 0;
  RenderStyle "Stencil";
  StencilColor "Black";
  }
  States
  {
  Spawn:
	UDM2 ABCDABCDABCDABCDABCDABCDABCDABCDABCDABCDABCDABCDABCDABCDABCD 1 
	{
	ThrustThing(random(-360,360),random(1,3),0,0);
	A_SpawnItemEx("PM_FOCPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSPRITEFRAME);
	}
	Stop;
	}
}