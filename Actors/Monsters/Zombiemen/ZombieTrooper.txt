ACTOR PM_ZombieTrooper : Zombieman
{ 
    Health 40
    Radius 20
    Height 56
    Speed 8
    PainChance 200
    Damage 1
    MONSTER
	+MissileMore
	+MISSILEevenMORE
    +FloorClip
	//+NOINFIGHTING
	+SLIDESONWALLS
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	Species "Zombie"
	Tag "Zombie Trooper"
    OBITUARY "%o was taken down by a Zombie Trooper."
    SeeSound "HelmetZombie/Sight"
	PainSound "HelmetZombie/Pain"
	DeathSound "HelmetZombie/Death"
	ActiveSound "HelmetZombie/Active"
    DropItem "Clip"
    Decal Bulletchip
    states
    {
    Spawn:
		UTM2 A 10 A_Look
        Loop
    See:
		TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
        UTM2 AA 4 A_Chase
		TNT1 AA 0 A_Recoil(-1)
		UTM2 BB 4 A_Chase
		TNT1 AA 0 A_Recoil(-1)
		TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
		UTM2 CC 4 A_Chase
		TNT1 AA 0 A_Recoil(-1)
		UTM2 DD 4 A_Chase
		TNT1 AA 0 A_Recoil(-1)
        Loop
     Missile:
        TNT1 A 0 A_Jump(43,"GrenadeCheck")
		TNT1 A 0 A_Jump(60, "MissileLeft", "MissileRight")
		TNT1 A 0 A_Jump(40,"Missile2")
        UTM2 EEEEE 1 A_FaceTarget
		TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
		TNT1 A 0 A_PlaySound("ZTrooper/Fire")
		UTM2 F 1 bright Light("YellowMediumFlicker2")
		{
		A_CustomMissile("PM_BulletTracer",33,7,random(-1, 1), CMF_OFFSETPITCH, random(-3, 0));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
		}
		UTM2 E 1 A_FaceTarget
		TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
		TNT1 A 0 A_PlaySound("ZTrooper/Fire")
		UTM2 F 1 bright Light("YellowMediumFlicker2")
		{
		A_CustomMissile("PM_BulletTracer",33,7,random(-1, 1), CMF_OFFSETPITCH, random(-2, 2));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
		}
		UTM2 E 1 A_FaceTarget
		TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
		TNT1 A 0 A_PlaySound("ZTrooper/Fire")
		UTM2 F 1 bright Light("YellowMediumFlicker2")
		{
		A_CustomMissile("PM_BulletTracer",33,7,random(-1, 1), CMF_OFFSETPITCH, random(0, 3));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
		}
		UTM2 EEE 5 A_FaceTarget
      goto see 
	Missile2:
		UTM2 E 4 A_FaceTarget
		TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
		TNT1 A 0 A_PlaySound("ZTrooper/Fire")
        UTM2 F 2 BRIGHT Light("YellowMediumFlicker2")
		{
		A_CustomMissile("PM_BulletTracer",33,7,random(-2, 2), CMF_OFFSETPITCH, random(-2, 2));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
		}
		UTM2 E 4 A_FaceTarget
		TNT1 A 0 A_Jump(30,"See")
		UTM2 E 0 A_MonsterRefire(60,"See")
		Goto Missile2+1
	MissileLeft:
	  UTM2 E 2 A_FaceTarget
	  TNT1 A 0 ThrustThing(angle*256/360+192, 4, 0, 0)
	  UTM2 E 2 A_FaceTarget
	  TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
      TNT1 A 0 A_PlaySound("ZTrooper/Fire")
      UTM2 F 4 BRIGHT Light("YellowMediumFlicker2")
	  {
		A_CustomMissile("PM_BulletTracer",33,7,random(-5, 5), CMF_OFFSETPITCH, random(-4, 4));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
	  }
	  TNT1 A 0 A_Jump(150, "MissileLeft")
	  Goto See
   MissileRight:
	  UTM2 E 2 A_FaceTarget
      TNT1 A 0 ThrustThing(angle*256/360+64, 4, 0, 0)
	  UTM2 E 2 A_FaceTarget
	  TNT1 A 0 A_PlaySound("ZombieRifleDistant",CHAN_7)
      TNT1 A 0 A_PlaySound("ZTrooper/Fire")
	  UTM2 F 4 BRIGHT Light("YellowMediumFlicker2")
	  {
		A_CustomMissile("PM_BulletTracer",33,7,random(-5, 5), CMF_OFFSETPITCH, random(-4, 4));
		A_CustomMissile("BulletFlare", 33, 7, 0, 0);
	  }
	  TNT1 A 0 A_Jump(150, "MissileRight")
	  Goto See
    GrenadeCheck:
      TNT1 A 0 A_JumpIfInventory("ZTrooperFragToken",3,"Missile2")
      TNT1 A 0 A_CheckRange(600,"GrenadeFar")
      TNT1 A 0 A_CheckRange(450,"RealGrenade")
      TNT1 A 0 A_CheckRange(150,"GrenadeNear")
      TNT1 A 0 A_JumpIfInventory("ZTrooperFragToken",3,"Missile2")
      Goto Missile
    RealGrenade:
      TNT1 A 0 A_GiveInventory("ZTrooperFragToken",1)
      TNT1 A 0 A_PlaySound("HelmetZombie/Sight")
      UTM2 AA 5 A_FaceTarget
      TNT1 A 0 A_PlaySound("ZTrooper/FragThrow",CHAN_6)
      UTM2 AA 5 A_FaceTarget
      UTM2 A 5 
      {
       A_CustomMissile("PM_ZTrooperFragGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,8);
       A_FaceTarget;
      }
      Goto See
    GrenadeFar:
      TNT1 A 0 A_GiveInventory("ZTrooperFragToken",1)
      TNT1 A 0 A_PlaySound("HelmetZombie/Sight")
      UTM2 AA 5 A_FaceTarget
      TNT1 A 0 A_PlaySound("ZTrooper/FragThrow",CHAN_6)
      UTM2 AA 5 A_FaceTarget
      UTM2 A 5 
      {
       A_CustomMissile("PM_ZTrooperFragGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,19);
       A_FaceTarget;
      }
      Goto See
    GrenadeNear:
      TNT1 A 0 A_GiveInventory("ZTrooperFragToken",1)
      TNT1 A 0 A_PlaySound("HelmetZombie/Sight")
      UTM2 AA 5 A_FaceTarget
      TNT1 A 0 A_PlaySound("ZTrooper/FragThrow",CHAN_6)
      UTM2 AA 5 A_FaceTarget
      UTM2 A 5 
      {
       A_CustomMissile("PM_ZTrooperFragGrenade",33,0,random(-1,1),CMF_OFFSETPITCH,-5);
       A_FaceTarget;
      }
      Goto See
    Pain:
        UTM2 G 5
        UTM2 G 3 A_Pain
        Goto See
    Death:
		TNT1 A 0 A_Jump(35,"PossDeath")
        UTM2 J 5 A_NoBlocking
        UTM2 K 5 A_Scream
        UTM2 L 5 
        UTM2 M 5
		TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
        UTM2 N -1
        Stop
	PossDeath:
        UTM2 J 5 A_NoBlocking
        UTM2 K 5 A_Scream
        UTM2 L 5 
        UTM2 M 5
		TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
		UTM2 N 90
		TNT1 A 0 A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION)
        UTM2 N -1
        Stop
    XDeath:
        UTM2 O 5 A_NoBlocking
        UTM2 P 5 A_XScream
        UTM2 Q 5 
        UTM2 RSTU 5
        UTM2 V -1
        Stop
    Raise:
        UTM2 NMLKJIH 5
        Goto See
    }
}

Actor PM_ZTrooperFragGrenade : Rocket
{
  var int user_grenadetime;
  Speed 30
  Scale 0.66
  Radius 4
  Height 10
  Gravity 0.8
  Bouncetype Hexen
  BounceFactor 0.44
  WallBounceFactor 0.66
  BounceCount 5
  SeeSound ""
  DeathSound ""
  BounceSound "ZTrooper/FragBounce"
  Decal ""
  -NOGRAVITY
  -ROCKETTRAIL
  +THRUACTORS
  +ROLLSPRITE
  +CANBOUNCEWATER
  +BOUNCEONCEILINGS
  States
  {
  Spawn:
	ZGRN A 1 
    {
     A_SpawnItemEx("DevilBallSmoke",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
     A_SetRoll(roll+15);
    }
	Loop
  Death:
    TNT1 A 0 A_PlaySound("ZTrooper/FragArm")
  Death2:
	ZGRN AA 1 Light("SatyrLordRedMissile5")
		{
		if (user_grenadetime >= 35)
			{
			return state("XDeath");
			}
		A_SpawnItemEx("DevilBallSmoke",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
		A_SetUserVar(user_grenadetime,user_grenadetime+1);
		return state(0);
		}
	Loop
  XDeath:
	TNT1 A 0 A_PlaySound("ZTrooper/FragExp",CHAN_6)
	TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
    TNT1 AAAA 0 A_SpawnItemEx("PM_GenericExplosionFlames",0,0,0,random(-10,10),random(-10,10),random(-10,10),0,SXF_NOCHECKPOSITION)
	TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-10,10),random(-10,10),random(-10,10),0,SXF_NOCHECKPOSITION)
	TNT1 AAAAA 0 A_CustomMissile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360))
	TNT1 A 0 A_QuakeEx(3,3,3,12,0,400,0,0,0,0,0,150)
	TNT1 A 8 Bright Light("YellowMediumFlicker2") A_Explode(30,200)
	TNT1 A 6 Bright Light("YellowMediumFlicker2")
    TNT1 A 5 Bright Light("YellowMediumFlicker2")
	Stop
	}
}

Actor ZTrooperFragToken : Inventory 
{ 
+Inventory.undroppable
 Inventory.maxamount 3 
}
