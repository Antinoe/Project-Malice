Class PM_ZombieTrooper : PMUnaffMonsterBase
{ 
	int direction;
	Default
	{
	PMMonsterBase.CodexLore
	"Humanity was outnumbered and outsmarted. Hell had proved to have been a threat that they absolutely could have never conquered. And knowing that, Hell took its sweet time, reveling in the suffering they created.
	They allowed pockets of resistance to form. They allowed them to enter gateways into other universes, knowing that humanity, out of sheer, primal desperation, chose to expand the underworld's reach into other universes.
	They allowed them to preserve the image that they still had a chance at victory, thus only few know of the true insanity and hopelessness of their situation.
	Hell's pure resentment against humanity could not have been made any more clearer.
	
	Attacks using battle rifles, firing in bursts.
	Comes packing with a handful of frag grenades to toss at the player.";
    Health 40;
    Radius 20;
    Height 56;
    Mass 100;
    Speed 8;
    PainChance 200;
    Damage 1;
    MONSTER;
	+MissileMore
	+MISSILEevenMORE
    +FloorClip
	+SLIDESONWALLS
	+HARMFRIENDS
	+DOHARMSPECIES
	Species "Zombie";
	Tag "Zombie Trooper";
    OBITUARY "%o was taken down by a Zombie Trooper.";
    SeeSound "HelmetZombie/Sight";
	PainSound "HelmetZombie/Pain";
	DeathSound "HelmetZombie/Death";
	ActiveSound "HelmetZombie/Active";
	Bloodtype "PM_NormalBloodBase";
    DropItem "Clip";
    Decal "Bulletchip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
	void throwGrenade()
	{
		int arc;
		let p = A_SpawnProjectile("PM_ZTrooperFragGrenade",33,0,random(-1,1));
		if(distance2d(target) <= 600) arc = 90;
		if(distance2d(target) <= 300) arc = 20;
		if(p) p.vel = CalcThrowVector(pos, target.pos, p.gravity, arc);
		A_FaceTarget();
	}
    states
    {
    Spawn:
        UTM2 A 0 NoDelay { user_numbullets = random(5,31); }
    Idle:
		UTM2 A 10 PM_Look();
        Loop;
	Look:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        UTM2 AA 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		UTM2 BB 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		UTM2 CC 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		UTM2 DD 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
        Loop;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","SpotMissile","See");
		Goto See;
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	SpotMissile:
        TNT1 A 0 PM_AlertSound();
	    UTM2 EEE 2 A_FaceTarget();
	    Goto Missile;
	See:
	    TNT1 A 0;
	    TNT1 A 0 A_CheckSight("See3");
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        UTM2 AA 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		UTM2 BB 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		UTM2 CC 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
		UTM2 DD 4 A_Chase();
		TNT1 AA 0 A_Recoil(-1);
        Loop;
	See3:
	    TNT1 A 0 A_Jump(9,"WaitGrenadeCheck");
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        UTM2 AA 4 A_Chase(null,"Missile",CHF_NOPLAYACTIVE);
		TNT1 AA 0 A_Recoil(-1);
		UTM2 BB 4 A_Chase(null,"Missile",CHF_NOPLAYACTIVE);
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		UTM2 CC 4 A_Chase(null,"Missile",CHF_NOPLAYACTIVE);
		TNT1 AA 0 A_Recoil(-1);
		UTM2 DD 4 A_Chase(null,"Missile",CHF_NOPLAYACTIVE);
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_CheckSight("See3");
        Goto See;
     Missile:
	    TNT1 A 0;
		TNT1 A 0 A_JumpIfInventory("ZTrooperToken",1,"WaitGrenade2");
        TNT1 A 0 A_Jump(63,"GrenadeCheck");
		TNT1 A 0 A_Jump(60, "MissileEvade");
		TNT1 A 0 A_Jump(40,"Missile2");
        UTM2 E 1 
		{
		  A_FaceTarget(360,180,0,0);
		  A_SetAngle(angle+random(-10,10));
		  A_SetPitch(pitch+random(-15,15));
		}
		UTM2 EEEEEEE 1 A_FaceTarget(10,10);
		UTM2 FFF 1 bright Light("YellowMediumFlicker2")
		{
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		  A_StartSound("ZTrooper/Fire");
		  A_SpawnProjectile("PM_BulletTracer",33,7,frandom(-1, 1),CMF_AIMDIRECTION,pitch+frandom(-2,2));
		  A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
          user_numbullets--;
	    }
		UTM2 E 1 A_FaceTarget(10,10);
		UTM2 EEE 5 A_FaceTarget(10,10);
      Goto See;
	Missile2:
		UTM2 E 4
		{
		  A_FaceTarget(360,180,0,0);
		  A_SetAngle(angle+random(-10,10));
		  A_SetPitch(pitch+random(-15,15));
		}
        UTM2 F 2 BRIGHT Light("YellowMediumFlicker2")
		{
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		  A_StartSound("ZTrooper/Fire");
		  A_SpawnProjectile("PM_BulletTracer",33,7,frandom(-1, 1),CMF_AIMDIRECTION,pitch+frandom(-2,2));
		  A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
          user_numbullets--;
	    }
		UTM2 E 4 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_Jump(30,"See");
		UTM2 E 0 A_MonsterRefire(60,"See");
		Goto Missile2+1;
	MissileEvade:
      TNT1 A 0;
	  UTM2 E 2
	  {
	    direction = random(0,1);
	    A_FaceTarget(360,180,0,0);
	    A_SetAngle(angle+random(-10,10));
	    A_SetPitch(pitch+random(-15,15));
	  }
	  TNT1 A 0 
	  {
	   if(direction)
	      A_ChangeVelocity(0,5.5,0,CVF_RELATIVE);
	   else
	      A_ChangeVelocity(0,-5.5,0,CVF_RELATIVE);
	  }
	  UTM2 E 2 A_FaceTarget(10,20,0,0);
      UTM2 F 4 BRIGHT Light("YellowMediumFlicker2")
	  {
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		  A_StartSound("ZTrooper/Fire");
		  A_SpawnProjectile("PM_BulletTracer",33,7,frandom(-1, 1),CMF_AIMDIRECTION,pitch+frandom(-2,2));
		  A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
          user_numbullets--;
	  }
	  TNT1 A 0 A_Jump(64,"See");
	  Goto MissileEvade+2;
	NoAmmo:
      UDM2 E 7 A_FaceTarget(10,20,0,0);
      TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	  UDM2 E 7 A_FaceTarget(10,20,0,0);
	  Goto ZReload;
    ZReload:
      TNT1 A 0 A_StartSound("PM/RifleMagOut",CHAN_5);
      UTM2 EE 5 A_FaceTarget(10,20,0,0);
      TNT1 A 0 A_SpawnItemEx("PM_EmptyRifleMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-2,0));
      UTM2 CCCBB 5 A_FaceTarget(10,20,0,0);
      TNT1 A 0 A_StartSound("PM/RifleMagIn",CHAN_5);
      TNT1 A 0 { user_numbullets = (user_numbullets + random(10,31)); }
      UTM2 EEDBBB 4 A_FaceTarget(10,20,0,0);
      TNT1 A 0 A_StartSound("PM/RifleBoltSlide",CHAN_5);
      UTM2 ABB 5 A_FaceTarget(10,20,0,0);
      Goto See;
    GrenadeCheck:
	  TNT1 A 0;
	  TNT1 A 0 A_JumpIfHigherOrLower("Missile2",null);
	  TNT1 A 0 A_CheckLOF("GrenadeCheck2");
      Goto Backup;
	  GrenadeCheck2:
      TNT1 A 0 A_JumpIfInventory("ZTrooperFragToken",3,"Missile2");
      TNT1 A 0
	  {
		if(distance2d(target) <= 600) SetStateLabel("RealGrenade");
	  }
      TNT1 A 0 A_JumpIfInventory("ZTrooperFragToken",3,"Missile2");
      Goto Missile;
    RealGrenade:
      TNT1 A 0 A_GiveInventory("ZTrooperFragToken",1);
      UTM2 AA 5 A_FaceTarget();
      TNT1 A 0 A_StartSound("ZTrooper/FragArm",CHAN_6);
      UTM2 AAAA 5 A_FaceTarget();
      UTM2 A 5 
      {
       throwGrenade();
      }
      Goto Backup;
	WaitGrenadeCheck:
	  TNT1 A 0;
	  TNT1 A 0 A_JumpIfInventory("ZTrooperToken",1,"See");
	  TNT1 A 0
	  {
		if(distance2d(target) <= 600) SetStateLabel("RealGrenade");
	  }
	  TNT1 A 0 A_JumpIfInventory("ZTrooperFragToken",3,"See");
	  Goto WaitGrenade1;
	WaitGrenade1:
	  TNT1 A 0;
	  TNT1 A 0 A_GiveInventory("ZTrooperToken",1);
	  TNT1 A 0 A_StartSound("ZTrooper/FragArm",CHAN_7);
	  Goto See;
	WaitGrenade2:
	  TNT1 A 0;
	  TNT1 A 0 A_JumpIfHigherOrLower("Missile2",null);
	  TNT1 A 0 A_CheckRange(450,"WaitGrenade");
	  Goto See;
	WaitGrenade:
	  TNT1 A 0;
	  TNT1 A 0 A_TakeInventory("ZTrooperToken",1);
	  TNT1 A 0 A_GiveInventory("ZTrooperFragToken",1);
      UTM2 AAA 5 A_FaceTarget();
      UTM2 A 5 
      {
       throwGrenade();
       A_FaceTarget();
      }
      Goto Backup;
	Backup:
		TNT1 A 0 A_CheckSight("See");
		UTM2 DCBADCBADCBA 3
		{
		A_FaceTarget();
		A_Recoil(2);
		A_FaceTarget();
		}
	Goto See;
    Pain:
        UTM2 G 5;
        UTM2 G 3 A_Pain;
        Goto See;
    Death:
        TNT1 A 0;
        TNT1 A 0 A_JumpIfInventory("ZTrooperToken",1,"GrenadeDeath");
        UTM2 J 5 A_NoBlocking;
        UTM2 K 5 PM_Scream();
        UTM2 L 5;
        UTM2 M 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		UTM2 N 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        UTM2 N -1;
        Stop;
    GrenadeDeath:
        UTM2 J 5 A_NoBlocking;
        UTM2 K 5 PM_Scream();
        UTM2 L 5 A_SpawnItemEx("PM_ZTrooperFragGrenade",0,0,5,0,0,0,0,SXF_NOCHECKPOSITION);
        UTM2 M 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		UTM2 N 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        UTM2 N -1;
        Stop;
    XDeath:
        TNT1 A 0 A_StopSound(555);
        UTM2 O 5 PM_Gib();
        UTM2 P 5 A_XScream;
        UTM2 Q 5 A_NoBlocking;
        UTM2 RSTU 5;
        UTM2 V -1;
        Stop;
    Raise:
        UTM2 NMLKJIH 5;
        Goto Look;
    }
}

Class PM_ZTrooperFragGrenade : Rocket
{
  Default
  {
  Bouncetype "Doom";
  Speed 24;
  Scale 0.52;
  Radius 4;
  Height 10;
  Gravity 1.25;
//   BounceFactor 0.65;
  WallBounceFactor 0.25;
  BounceCount 5;
  Alpha 1.0;
  DamageFunction (3);
  SeeSound "ZTrooper/FragThrow";
  DeathSound "";
  BounceSound "ZTrooper/FragBounce";
  Decal "";
  -NOGRAVITY
  -ROCKETTRAIL
  -THRUACTORS
  +ROLLSPRITE
  +BOUNCEONFLOORS
  +BOUNCEONWALLS
  +BOUNCEONACTORS
  +BOUNCEONCEILINGS
  +CANBOUNCEWATER
  }
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 0 A_SpawnItemEx("PM_ZTrooperFragTick",0,0,0,0,0,0,0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
  Spawn2:
	ZGRN A 1 Light("SatyrLordRedMissile9")
    {
	 A_SpawnParticle("FF2A2A",SPF_FULLBRIGHT|SPF_RELATIVE,95,13,0,0,0,0,0,0,0,0,0,0,0.85,-0.2,-1);
     A_SpawnItemEx("PM_DevilBallSmoke",0,frandom(-2,2),frandom(-2,2),0,0,frandom(0,2));
     A_SetRoll(roll+15);
    }
	Loop;
  Bounce:
	Goto Spawn2;
  Death:
	TNT1 A 0 A_SetRoll(0);
    TNT1 A 0 A_StartSound("ZTrooper/FragBounce");
  Death2:
	ZGRN BB 1 Light("SatyrLordRedMissile9");
	Loop;
  XDeath:
	TNT1 A 0 A_SetRoll(0);
	TNT1 A 0 A_StartSound("ZTrooper/FragBounce");
  XDeath2:
    ZGRN BB 1 Light("SatyrLordRedMissile9");
	Loop;
	}
}

Class PM_ZTrooperFragTick : Actor
{
    Default
	{
	Radius 3;
	Height 3;
	Health 1000;
	Scale 0.75;
	-NOBLOCKMAP
	+NOBLOOD
	+NOICEDEATH
	+NOGRAVITY
	+NEVERTARGET
	-SHOOTABLE
	+NOTONAUTOMAP
	-SOLID
	-COUNTKILL
	+NOTARGETSWITCH
	+THRUSPECIES
	+THRUACTORS
	+DONTTHRUST
	OBITUARY "%o was grenaded by a Zombie Trooper.";
	}
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 AAAAAAAAAA 1;
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 A 0 A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 Light("SatyrLordRedMissile4")
			{
			A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
			}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 A 0 A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 Light("SatyrLordRedMissile4")
			{
			A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
			}
		TNT1 AAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 A 0 A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 Light("SatyrLordRedMissile4")
			{
			A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
			}
		TNT1 AAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 A 0 A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 Light("SatyrLordRedMissile4")
			{
			A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
			}
		TNT1 AAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0 A_StartSound("ZTrooper/FragTick",CHAN_5);
		TNT1 A 0 A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 Light("SatyrLordRedMissile4")
			{
			A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
			}
		TNT1 AAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,5,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	Death:
		TNT1 A 0 A_StartSound("ZTrooper/FragExp",CHAN_6);
		TNT1 A 0 A_StartSound("ZTrooper/FragDebris",CHAN_7);
		TNT1 A 0 {bNoGravity = true;}
		TNT1 AAAA 0 A_SpawnItemEx("PM_GrenadeGenericExplosionFlames",0,0,0,random(-2,2),random(-2,2),random(-1,2),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-6,6),random(-6,6),random(-1,4),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
		TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0);
		TNT1 A 1;
		TNT1 A 0 A_SpawnItemEx("PM_FragShrapnelSparks",0,0,1);
		TNT1 AAAAAAA 0 A_SpawnItemEx("PM_ExplosionLongerSmoke",0,0,1,random(-3,3),random(-3,3),random(0,2));
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnProjectile("PM_FragGrenadeShrapnel",random(2,20),random(-4,4),random(-180,180),CMF_OFFSETPITCH,random(5,80));
		TNT1 A 0 A_RemoveMaster(RMVF_EVERYTHING);
		TNT1 A 8 Bright Light("YellowMediumFlicker2") A_Explode(100,400);
		TNT1 A 6 Bright Light("YellowMediumFlicker2");
		TNT1 A 5 Bright Light("YellowMediumFlicker2");
		Stop;
	}
}


Class ZTrooperFragToken : Inventory 
{ 
Default
 {
+Inventory.undroppable;
 Inventory.maxamount 3;
 }
}

Class ZTrooperToken : Inventory 
{ 
 Default
 {
 Inventory.maxamount 1;
 }
}
