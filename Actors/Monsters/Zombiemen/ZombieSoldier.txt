Class PM_ZombieSoldier : PMUnaffMonsterBase
{ 
	int user_numbullets;
	Default
	{
	PMMonsterBase.CodexLore
	"Most of the zombies you fight are former members of various armies that were either alerted to Hell's incursion or ambushed by Hell's forces, both outcomes leaving a massive dent in their manpower and adding onto Hell's already endless supply of horrors.
	 Some of them who had realized they were being possessed had attempted to make one last effort to defy the face of evil through committing suicide, hoping in doing so the sprite would forcibly abandon the body.
	 Most of the attempts didn't work.
	 
	 Uses its standard issue assault rifle. 
	 Has terrible aim and lacks the ability to properly reload due to missing one half of its brain.";
    Health 20;
    Radius 20;
    Height 56;
    Mass 100;
    Speed 8;
    PainChance 200;
    Damage 1;
    MONSTER;
    MeleeRange 60;
    +FloorClip
	+SLIDESONWALLS
	+HARMFRIENDS
	+DOHARMSPECIES
	+DOHARMSPECIES
	Species "Zombie";
	Tag "Zombie Soldier";
    OBITUARY "%o was finished off by a Zombie Soldier.";
    SEESOUND "ZombieSoldier/See";
    PainSound "ZombieSoldier/Pain";
    DeathSound "ZombieSoldier/Death";
    ACTIVESOUND "ZombieSoldier/Active";
	Bloodtype "PM_NormalBloodBase";
    DropItem "Clip";
    Decal "Bulletchip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
    states
    {
    Spawn:
        POS1 A 0 NoDelay
        {
         user_numbullets = random(0,31);
        }
    Idle:
        POS1 A 10 PM_Look();
        Loop;
	Look:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        POS1 AA 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		POS1 BB 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POS1 CC 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
		POS1 DD 4 PM_LookingForPlayer();
		TNT1 AA 0 A_Recoil(-1);
        Loop;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","SpotMissile","See");
		Goto See;
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
	SpotMissile:
        TNT1 A 0 PM_AlertSound();
	    POS1 EEE 3 A_FaceTarget();
	    Goto Missile;
	See:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        POS1 AA 4 A_Chase();
		TNT1 AAA 0 A_Recoil(-1);
		POS1 BB 4 A_Chase();
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POS1 CC 4 A_Chase();
		TNT1 A 0 A_Recoil(-0.5);
		POS1 DD 4 A_Chase();
        Loop;
     NoAmmo:
        POS1 E 7 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POS1 EE 10 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_Jump(80,"SeeNoAmmo");
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POS1 E 8 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		POS1 E 8 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POS1 EEEEE 5 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("ZombieSoldier/Attack");
      SeeNoAmmo:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        POS1 AA 3 A_Chase("Lunge","Spit");
		TNT1 AAA 0 A_Recoil(-2.0);
		POS1 BB 3 A_Chase("Lunge","Spit");
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POS1 CC 3 A_Chase("Lunge","Spit");
		TNT1 A 0 A_Recoil(1.5);
		POS1 DD 3 A_Chase("Lunge","Spit");
        Loop;
    Lunge:
	    POS1 A 10 A_FaceTarget();
	    TNT1 A 0 A_StartSound("ZombieSoldier/Attack");
	    TNT1 A 0 A_Recoil(-5);
	    TNT1 A 0 ThrustThingZ(0,15,0,1);
        POS1 B 8 A_SpawnProjectile("HorrorLunge",18,0,0,CMF_AIMDIRECTION);
	    POS1 B 20;
	    POS1 D 10;
	    Goto SeeNoAmmo;
    Spit:
		TNT1 A 0;
		TNT1 A 0 A_Jump(90,"NoAmmo");
		TNT1 A 0 A_Jump(20,"ZReload");
		TNT1 A 0 A_Recoil(-3);
	    POS1 CC 5 A_FaceTarget();
	    TNT1 A 0 A_StartSound("GenericBloodPuke",2);
	    TNT1 A 0 A_StartSound("Afflicted/Puke");
	    POS1 BBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
	    POS1 B 1 A_SpawnProjectile("PM_HSpawnSpitterBlood",42,0,random(-10,10),CMF_AIMDIRECTION,random(-4,6));
		TNT1 A 0 A_Recoil(-3);
	    POS1 BBBBBBBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(-20,10));
		TNT1 A 0 A_Recoil(-3);
	    POS1 BBDDC 4;
	    Goto SeeNoAmmo;
	ZReload:
		TNT1 A 0 A_StartSound("PM/RifleMagOut",CHAN_5);
		POS1 EEEEEEEE 5 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_SpawnItemEx("PM_EmptyRifleMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-2,0));
		POS1 EEEBBBBBBB 5 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_StartSound("PM/RifleMagIn",CHAN_5);
		TNT1 A 0 { user_numbullets = (user_numbullets + random(8,31)); }
		POS1 EEEBBBBBBBB 4 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_StartSound("PM/RifleBoltSlide",CHAN_5);
		POS1 AAABBBBBB 4 A_FaceTarget(10,20,0,0);
		Goto See;
    Missile:
        TNT1 A 0;
		TNT1 A 0 A_Jump(30,"Puke");
		TNT1 A 0 A_Jump(50,"Missile2");
	RealMissile:
        POS1 EEE 6 
		{
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  pm_attackloop = random(1,5);
		  A_FaceTarget(360,180,0,0);
		  A_SetAngle(angle+random(-10,10));
		  A_SetPitch(pitch+random(-15,15));
		}
	RMissileLoop:
        POS1 F 2 BRIGHT Light("YellowMediumFlicker2")
		{
		  if(!pm_attackloop)
		   { SetStateLabel("RMissileEnd"); }
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		  A_StartSound("ZombieSoldier/Rifle");
		  A_SpawnProjectile("PM_BulletTracer",33,9,frandom(-7, 7),CMF_AIMDIRECTION,pitch+frandom(-4, 4));
          A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
          user_numbullets--;
		  pm_attackloop--;
		}
		POS1 E 3 A_FaceTarget(10,15);
		Loop;
     RMissileEnd:
		POS1 E 6 A_FaceTarget(10,15);
        Goto See;
	Missile2:
		TNT1 A 0 A_StartSound("ZombieSoldier/Attack",CHAN_6);
		POS1 E 6
		{
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_FaceTarget(360,180,0,0);
		  A_SetAngle(angle+random(-10,10));
		  A_SetPitch(pitch+random(-15,15));
		}
		POS1 E 6 A_FaceTarget(5,5);
        POS1 F 2 BRIGHT Light("YellowMediumFlicker2")
		{
		  if(user_numbullets <= 0)
		   { SetStateLabel("NoAmmo"); }
		  A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		  A_StartSound("ZombieSoldier/Rifle");
		  A_SpawnProjectile("PM_BulletTracer",33,9,frandom(-7, 7),CMF_AIMDIRECTION,pitch+frandom(-4, 4));
          A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
          user_numbullets--;
		}
		POS1 E 6 A_FaceTarget(8,8);
		TNT1 A 0 A_Jump(30,"See");
		D3CG E 0 A_MonsterRefire(60,"See");
		Goto Missile2+1;
	Puke:
		TNT1 A 0 A_StartSound("GenericBloodPuke");
		POS1 GGGGGGGGG 1 A_SpawnProjectile("HungerBloodSpray",46,0,random(20,-20),CMF_AIMDIRECTION);
		POS1 G 1 A_SpawnProjectile("HungerMaggotMissile",46,0,random(6,-6),CMF_AIMDIRECTION,random(4,-4));
		POS1 GGGGGGGGG 1 A_SpawnProjectile("HungerBloodSpray",46,0,random(20,-20),CMF_AIMDIRECTION);
		POS1 G 3;
		Goto See;
    Pain:
        POS1 G 5;
        POS1 G 3 A_Pain;
        Goto SeeCheck;
    SeeCheck:
	    TNT1 A 0;
        TNT1 A 0
		{
		if(!user_numbullets)
		  { SetStateLabel("SeeNoAmmo"); }
		}
        Goto See;
    Death:
        POS1 H 5 A_NoBlocking;
        POS1 I 5 PM_Scream();
        POS1 J 5;
        POS1 K 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
        POS1 L 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        POS1 L -1;
        Stop;
    XDeath:
        TNT1 A 0 A_StopSound(555);
		POS1 M 5 PM_Gib();
		POS1 N 5 A_XScream;
        POS1 O 5 A_NoBlocking;
        POS1 P 5;
        POS1 Q 5;
        POS1 RST 5;
        POS1 U -1;
        Stop;
    Raise:
        POS1 LKJIH 5;
        Goto Look;
    }
}

