Class PM_DecayedSoldier : PMPestilenceMonsterBase
{ 
    int bloattimer;
	Default
	{
	//$Category "PM_Monsters/Zombiemen"
    Health 34;
    Radius 20;
    Height 56;
    Mass 100;
    Speed 9;
    PainChance 200;
    Damage 1;
    MONSTER;
	+MISSILEMORE
    +FloorClip
	+SLIDESONWALLS
	+HARMFRIENDS
	+DOHARMSPECIES
	Species "Zombie";
	Tag "Decayed Soldier";
    OBITUARY "%o was punctured by a Decayed Soldier.";
    SEESOUND "Decayed/Sight";
    PainSound "Decayed/Pain";
    DeathSound "Decayed/Death";
    ACTIVESOUND "Decayed/Active";
    DropItem "Clip";
	BloodColor "DarkRed";
	Bloodtype "PM_PestilenceZombieBloodBase";
    Decal "Bulletchip";
	TeleFogSourceType "PM_PossessedTeleportFog";
	TeleFogDestType "PM_PossessedTeleportFog";
	}
    states
    {
    Spawn:
        TNT1 A 0 NoDelay
        {
         user_numbullets = random(0,31);
        }
    Idle:
        POSK AA 10 PM_Look();
        Loop;
    Look:
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POSK AABBCC 3 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POSK DD 3 PM_LookingForPlayer();
        Loop;
	Spotted:
	    TNT1 A 0 A_Jump(256,"Roar","Roar","SpotMissile");
		Goto See;
	Roar:
	    TNT1 A 0 PM_AlertSound();
		Goto See;
    SpotMissile:
        TNT1 A 0 PM_AlertSound();
        POSK EE 6 A_FaceTarget();
        Goto Missile;
    See:
		TNT1 A 0 A_SetSpeed(9);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POSK AA 3 A_Chase();
		POSK BB 3 A_Chase();
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POSK CC 3 A_Chase();
		POSK DD 3 A_Chase();
		TNT1 A 0 A_Jump(15,"Backup");
		TNT1 A 0 A_Jump(30,"MaggotDrop");
        TNT1 A 0 A_Jump(30,"Sprint");
        Loop;
	Sprint:
		TNT1 A 0 A_SetSpeed(7);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
        POSK AA 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.6);
		POSK BB 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.6);
		TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
		POSK CC 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.6);
		POSK DD 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.6);
		TNT1 A 0 A_Jump(45,"See");
		Loop;
    Missile:
        TNT1 A 0;
		TNT1 A 0 A_Jump(13,"Puke");
		TNT1 A 0 A_Jump(30,"Missile2");
		TNT1 A 0 A_Jump(125,"RollRight","RollLeft");
		POSK E 6
		{
		A_FaceTarget(360,180,0,0);
		A_SetAngle(angle+random(-10,10));
		A_SetPitch(pitch+random(-15,15));
		pm_attackloop = 1;
		}
	Missile1:
        POSK F 2 Light("YellowMediumFlicker2") 
		{
		if(user_numbullets <= 0)
		SetStateLabel("NoAmmo");
		if(pm_attackloop < 0)
		SetStateLabel("MissileEnd");
		A_StartSound("Decayed/Fire");
		A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		A_SpawnProjectile("PM_BulletTracer",30,7,frandom(-6, 6),CMF_AIMDIRECTION,pitch+frandom(-3, 3));
		A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(-5,-15),frandom(5,8),0,SXF_NOCHECKPOSITION);
        user_numbullets--;
		pm_attackloop--;
	    }
		POSK E 2 A_FaceTarget(40,40,0,0);
		Goto Missile1;
    MissileEnd:
		POSK E 8 A_FaceTarget();
		TNT1 A 0 A_Jump(50,"See");
        Goto See;
	Missile2:
	    TNT1 A 0 { pm_attackloop = random(3,8); }
		POSK E 5
		{
		A_FaceTarget(360,180,0,0);
		A_SetAngle(angle+random(-10,10));
		A_SetPitch(pitch+random(-15,15));
		}
        POSK F 2 Light("YellowMediumFlicker2")
		{
		if(user_numbullets <= 0)
		SetStateLabel("NoAmmo");
		if(pm_attackloop < 0)
		SetStateLabel("MissileEnd");
		A_StartSound("Decayed/Fire");
		A_StartSound("ZombieRifleDistant",CHAN_7,CHANF_OVERLAP);
		A_SpawnProjectile("PM_BulletTracer",30,7,frandom(-6, 6),CMF_AIMDIRECTION,pitch+frandom(-3, 3));
		A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(-5,-15),frandom(5,8),0,SXF_NOCHECKPOSITION);
        user_numbullets--;
		pm_attackloop--;
	    }
		POSK E 2 A_FaceTarget(30,35,0,0);
		TNT1 A 0 A_Jump(30,"See");
		Goto Missile2+2;
	MaggotDrop:
		TNT1 A 0 A_SpawnProjectile("WMaggotSpawn", 45, random(-4,4), 0, 0, 0);
		Goto See;
	RollRight:
		TNT1 A 0 A_StartSound("Decayed/Roll");
		ZRO1 A 2 A_FaceTarget();
		TNT1 A 0 ThrustThing(0,16,0,0);
		ZRO1 ABCDE 3 A_FaceTarget();
		TNT1 A 0 { pm_attackloop = 1; }
		TNT1 A 0 A_CheckSight("See3");
		Goto Missile1;
	RollLeft:
		TNT1 A 0 A_StartSound("Decayed/Roll");
		ZRO1 A 2 A_FaceTarget();
		TNT1 A 0 ThrustThing(128,16,0,0);
		ZRO1 EDCBA 3 A_FaceTarget();
		TNT1 A 0 { pm_attackloop = 1; }
		TNT1 A 0 A_CheckSight("See3");
		Goto Missile1;
	Backup:
	POSK DCBADCBADCBA 2
	{
	A_FaceTarget();
	A_Recoil(2);
	A_FaceTarget();
	}
	Goto See;
	Puke:
		TNT1 A 0 A_StartSound("GenericBloodPuke");
		POSK GGGGGGGGGGGGGGGGGG 1
		{
		A_FaceTarget();
		A_SpawnProjectile("MaggotPuke",46,-6,random(6,-6),CMF_OFFSETPITCH,random(-6,6));
		}
		POSK G 3;
		Goto See;
	NoAmmo:
        POSK E 7 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POSK EE 10 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_Jump(100,"ZReload");
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POSK E 8 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
		POSK E 8 A_FaceTarget(10,20,0,0);
        TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
        POSK AAAA 5 A_FaceTarget(10,20,0,0);
		Goto ZReload;
    ZReload:
		TNT1 A 0 A_StartSound("PM/RifleMagOut",CHAN_5);
		POSK EEE 5 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_SpawnItemEx("PM_EmptyRifleMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-2,0));
		POSK CCCCCBBB 5 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_StartSound("PM/RifleMagIn",CHAN_5);
		TNT1 A 0
		{
		user_numbullets = (user_numbullets + random(25,31));
		}
		POSK DDDDBBB 4 A_FaceTarget(10,20,0,0);
		TNT1 A 0 A_StartSound("PM/RifleBoltSlide",CHAN_5);
		POSK AAABB 4 A_FaceTarget(10,20,0,0);
		Goto See;
    Pain:
        POSK G 5 A_Pain;
		TNT1 A 0 A_Jump(105,"RollRight","RollLeft");
        Goto See;
    Death:
        POSK H 5 A_NoBlocking;
        POSK I 5 PM_Scream();
        POSK J 5 A_Jump(55,"BloatDeath");
        POSK K 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		POSK L 90;
		TNT1 A 0 { if(random(1,3) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
        POSK L -1;
        Stop;
	 BloatDeath:
	    POSK K 5;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		POSK L random(90,140);
		POSK L 0 { bloattimer = random(90,220); }
		POSK L 1 
		     {
			  if(bloattimer < 0)
			   SetStateLabel("BloatDeathGo");
			  if(random(1,3) == 1)
	           A_SpawnParticleEx("252D00",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(54,80),size: frandom(15,45), xoff:random(-15,15), yoff:random(-15,15), zoff:random(0,5), velx: frandom(-0.1,0.1),vely: frandom(-0.1,0.1),velz: frandom(1,2),accelx: frandom(-0.02,0.02),accely: frandom(-0.02,0.02),accelz: frandom(-0.03,0.01),startalphaf: frandom(0.3,0.6),fadestepf: -0.001,sizestep: frandom(0.2,1),startroll: random(-180,180),rollvel: random(-2,2));
			  bloattimer--;
			 }
	    Goto BloatDeath+4;
	 BloatDeathGo:
		TNT1 A 0 A_StartSound("CivZombie/MutationRip",15);
        POSK LLLLLLLLLLLLLLLLLL 2 
		     {
			  A_SetScale(scale.x+0.02,scale.y+0.02);
			  if(random(1,3) == 1)
			  A_ChangeVelocity(frandom(-1.5,1.5),frandom(-1.5,1.5),flags:CVF_RELATIVE);
			  A_SpawnItemEx("PM_DarkRedBlood",random(-5,5),random(-5,5),random(5,10),random(0,2),random(-2,2),random(0,2),random(1,4),SXF_NOCHECKPOSITION);
			 }
		TNT1 A 0 
		     {
			  A_SetScale(1.0);
			  PM_Gib();
			  A_SpawnItemEx("PM_ArachExpMist",0,0,20,0,0,0,0,SXF_NOCHECKPOSITION);
			  for(int i=random(6,10);i>0;i--)
			  A_SpawnItemEx("PM_PestilenceMeatChunk",random(-10,10),random(-10,10),random(10,30),random(-15,15),random(-15,15),random(0,20),0,SXF_NOCHECKPOSITION);
			  A_Explode(40,158);
			 }
		POSK MMNNOOPPQQRRSSTTUU 3 
		     {
			  A_SpawnItemEx("PM_LargeWhiteMaggot",0,0,0,random(-3,3),random(-3,3),random(0,6),0,SXF_NOCHECKPOSITION,54);
			  A_SpawnItemEx("PM_Fly",random(-5,5),random(-5,5),random(0,5),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(0,3.5),random(-180,180),flags:SXF_NOCHECKPOSITION,failchance:54);
			 }
	    POSK U -1;
        Stop;
    XDeath:
        TNT1 A 0 A_StopSound(555);
        POSK M 5 PM_Gib();
        POSK N 5 A_XScream;
        POSK O 5 A_NoBlocking;
        POSK PQRST 5;
        POSK U -1;
        Stop;
    Raise:
        POSK NMLKJIH 5;
        Goto Look;
    }
}

