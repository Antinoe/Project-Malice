Class PM_BloodStalker : PMUmbralMonsterBase
{
    Default
	{
	//$Category "PM_Monsters/Pinkies"
	Health 300;
	Radius 30;
	Height 56;
	Speed 13;
	PainChance 56;
	Mass 400;
	Scale 1.15;
	+FLOORCLIP
	+DONTHARMSPECIES
	+DONTHARMCLASS
	Species "Brute";
	Monster;
	MeleeRange 100;
	MeleeDamage 4;
	BloodColor "Black";
	Bloodtype "PM_BlackBloodBase";
	SeeSound "BStalker/Sight";
	DeathSound "BStalker/Death";
	ActiveSound "BStalker/Active";
	PainSound "BStalker/Pain";
	Tag "Blood Stalker";
    StencilColor "Red";
	Obituary "%o was torn apart before %g could react by a Blood Stalker.";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	
	override void PostBeginPlay()
	{
	  Super.PostBeginPlay();
	  tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	  tex[1] = TexMan.CheckForTexture("REDLA0",TexMan.Type_Sprite);
	  tex[2] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	  tex[3] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[4];
	
	void PM_BStalkerWarpParticles()
	{
	   A_SpawnParticleEx("bc0707",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_ROLL|SPF_RELATIVE,lifetime: random(3,85),size: frandom(25,75),xoff: frandom(-3,3),yoff: frandom(-3,3),zoff: frandom(0,6),velx: frandom(-1.4,1.4),vely: frandom(-1.4,1.4),velz: frandom(-1.4,1.4),startalphaf: frandom(0.1,0.65)
	   ,fadestepf: -1,sizestep: frandom(3,6.4),startroll: random(-180,180));
	   A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,25),size: random(1,100),xoff: random(-20,20),yoff: random(-20,20),zoff: random(10,45),velx: random(-3,3),vely: random(-3,3),velz: random(-3,3),startalphaf: frandom(0,1),startroll: random(-180,180));
	}
	
	void PM_BStalkerWarp()
	{
	    if(target && CheckSight(target) && Distance3D(target) < 600)
          A_Chase(null,null);
		else
		  A_Wander();
		  A_SpawnItemEx("PM_GenericPhaseGhost",flags:SXF_TRANSFERSCALE|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERSPRITEFRAME);
		for(int i=random(1,2);i>0;i--)
		  A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,70),size: random(1,100),xoff: random(-20,20),yoff: random(-20,20),zoff: random(10,45),velx: frandom(-0.1,0.1),vely: frandom(-0.1,0.1),velz: frandom(-0.1,0.1),startalphaf: 1,startroll: random(-180,180));
	}
	
	States
	{
		Spawn:
			SAR2 A 4 PM_Look();
			Loop;
		Look:
			TNT1 A 0 A_SetSpeed(15);
			SAR2 AA 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 BB 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
            TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
			SAR2 CC 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 DD 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
			Loop;
		Spotted:
		    TNT1 A 0 A_Jump(256,"RoarNoAnim","RoarCheck");
			Goto RoarNoAnim;
		RoarNoAnim:
		    TNT1 A 0 PM_AlertSound();
		See:
			TNT1 A 0 A_SetSpeed(15);
			SAR2 AA 2 Fast A_Chase;
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 BB 2 Fast A_Chase;
			TNT1 A 0 A_Recoil(-0.5);
			SAR2 CC 2 Fast A_Chase;
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 DD 2 Fast A_Chase;
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_Jump(5,"RoarCheck");
			TNT1 A 0 A_Jump(20,"Walk");
			TNT1 A 0 
			{
			 if(random(1,8) == 1 && target && CheckSight(target))
			  SetStateLabel("TPCheck");
			}
			Loop;
		Walk:
			TNT1 A 0 A_SetSpeed(10);
			SAR2 AAA 2 A_Chase;
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 BBB 2 A_Chase;
			SAR2 CCC 2 A_Chase;
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 DDD 2 A_Chase;
			TNT1 A 0 A_Jump(10,"See");
			Loop;
		TPCheck:
		    TNT1 A 0 A_Jump(66,"TPRush");
			Goto TP;
		TPRush:
		    TNT1 A 0 { A_StartSound("BStalker/Sight");
			pm_attackloop = random(15,25); }
		TPRushGo:
			SAR2 AA 1 Fast { PM_BStalkerWarpParticles(); A_Chase(); }
			TNT1 A 0 A_Recoil(-0.8);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 BB 1 Fast { PM_BStalkerWarpParticles(); A_Chase(); }
			TNT1 A 0 A_Recoil(-0.8);
			SAR2 CC 1 Fast { PM_BStalkerWarpParticles(); A_Chase(); }
			TNT1 A 0 A_Recoil(-0.8);
			TNT1 A 0 A_StartSound("demonstep", 3);
			SAR2 DD 1 Fast { PM_BStalkerWarpParticles(); A_Chase(); }
			TNT1 A 0 A_Recoil(-0.8); 
			TNT1 A 0 { if(pm_attackloop < 0) SetStateLabel("Walk"); pm_attackloop--; }
			Loop;
		  TP:
		    TNT1 A 0;
		    TNT1 A 0 
		    {
		      bNOPAIN = true;
		      A_SetSpeed(3);
		    }
		    SAR2 H 0 A_StartSound("PM/BigElectricCharge",2);
		    SAR2 AAAAAA 1 Bright Light("Shadowball3")
		    {
		      PM_BStalkerWarpParticles();
		      A_Chase(null,null);
		      A_SpawnParticleEx("",tex[2],style: STYLE_Add,lifetime: 30,size: random(20,35),zoff: 54,startalphaf: frandom(0.15,0.35));
            }
		    TNT1 A 0 A_StartSound("demonstep",10);
            SAR2 BBBBBBB 1 Bright Light("Shadowball3")
		    {
		      PM_BStalkerWarpParticles();
		      A_Chase(null,null);
		      A_SpawnParticleEx("",tex[2],style: STYLE_Add,lifetime: 30,size: random(30,65),zoff: 54,startalphaf: frandom(0.15,0.35));
            }
		    SAR2 CCCCCCCC 1 
		    {
		      PM_BStalkerWarpParticles();
		      A_Chase(null,null);
		      A_SpawnParticleEx("",tex[2],style: STYLE_Add,lifetime: 30,size: random(35,95),zoff: 54,startalphaf: frandom(0.15,0.35));
            }
		    TNT1 A 0 A_StartSound("demonstep",10);
		    SAR2 DDDDDDDDD 1 Bright Light("Shadowball3")
		    {
		      PM_BStalkerWarpParticles();
		      A_Chase(null,null);
		      A_SpawnParticleEx("",tex[2],style: STYLE_Add,lifetime: 30,size: random(90,135),zoff: 54,startalphaf: frandom(0.15,0.35));
            }
		    SAR2 DDD 1 Bright Light("Shadowball3") 
		    {
		      PM_BStalkerWarpParticles();
		      A_SetSpeed(20);
		    }
		    SAR2 B 20
		    { 
		     for(int i=random(35,65);i>0;i--)
		      PM_BStalkerWarp();
		      A_StartSound("BStalker/Warp",20);
		      A_StartSound("NetherConduit/HomingExp",21,pitch:frandom(0.75,1));
		      A_StartSound("Depraved/SuperShock1",22);
		      A_StartSound("Broken/Railgun",23,volume:0.25,pitch:0.55);
		    }
		    TNT1 A 0 
		    {
		     bNOPAIN = false;
		     A_SetSpeed(13);
	        }
            SAR2 AABBCCDD 2 A_Chase(null,null);
		     Goto See;
		RoarCheck:
			TNT1 A 0 A_JumpIfInTargetLOS("Roar",360,JLOSF_DEADNOJUMP,2000);
			Goto See;
		Roar:
			SAR2 A 3 A_FaceTarget;
			SAR2 E 1 A_StartSound("BloodStalker/Roar");
			SAR2 EFFFFFFFFEEEFFFFE 4
			{
				A_QuakeEx(1, 0, 0, 30, 0, 300, "", QF_SCALEDOWN|QF_RELATIVE|QF_3D, rollintensity: 0.5, rollwave: 0.5);
				A_FaceTarget();
			}
			Goto See;
		Melee:
		    TNT1 A 0 A_Jump(45,"FrostBreath");
			TNT1 A 0 A_StartSound("GenericPinkyBite");
			SAR2 E 7 A_FaceTarget;
			SAR2 F 5
			{
			A_Recoil(-15);
			ThrustThingZ(0,18,0,1);
			}
			SAR2 G 8 A_SpawnProjectile("PM_FastMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
			Goto See;
		FrostBreath:
			TNT1 A 0 A_StartSound("BStalker/Sight");
			SAR2 EEEEEEEEEEEFFFFFFFFFFFF 1 
			{
			A_FaceTarget(0.75,3);
	 	    A_SpawnParticleEx("C4C5FF",tex[0],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(5,35),size: frandom(3,75),xoff: frandom(-2,2),yoff: frandom(-10,10),zoff: frandom(20,40),velx: frandom(0.1,0.5),vely: frandom(-0.5,0.5),velz: frandom(0.1,0.5),startalphaf: frandom(0.1,0.9)
			,fadestepf: -1,sizestep: frandom(0.3,1),startroll: random(-180,180),rollvel: frandom(-5,5));
			}
			SAR2 FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1
			{
			 A_SpawnProjectile("PM_BStalkerFrostBreath",35,0,frandom(-2,2),CMF_OFFSETPITCH|CMF_AIMDIRECTION,frandom(-1,1));
			 A_FaceTarget(0.65,3);
		     for(int i=random(1,2);i>0;i--)
		     A_SpawnParticleEx("9fc5e8",tex[3],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,100),size: frandom(1,12),xoff: frandom(-2,2),yoff: frandom(-10,10),zoff: frandom(20,40),velx: frandom(1,12),vely: frandom(-0.5,0.5),velz: frandom(-0.6,2),accelx: frandom(-0.005,0.005),accely: frandom(-0.005,0.005),accelz: frandom(-0.0,-0.8)
			 ,fadestepf: -1,sizestep: -0.2,startroll: random(-180,180));
			}
			SAR2 EE 4 A_FaceTarget;
			Goto See;
		Pain:
			SAR2 H 5 A_Pain;
			Goto See;
		Death:
			SAR2 I 6 PM_Scream();
			SAR2 IJKLM 6 A_NoBlocking;
			TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
			SAR2 N -1;
			Stop;
		Raise:
		    SAR2 NMLKJI 6;
			Goto Look;
	}
}

Class PM_BStalkerFrostBreath : Actor
{
	Default
	{
		Radius 4;
		Height 8;
		Speed 15;
		DamageFunction (0);
		DamageType "Ice";
		Alpha 0.65;
		Scale 0.1;
		Projectile;
		+THRUGHOST
		+FORCEXYBILLBOARD
		+ROLLSPRITE
		+ROLLCENTER
		+RIPPER
	}
	
	override void Tick()
	{
		if (level.isFrozen()) return;
		super.Tick();
		
		if ((level.maptime % 2) == 0)
		  { A_SpawnParticleEx("C4C5FF",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(5,35),size: frandom(3,65),xoff: random(-2,2),yoff: random(-2,2),zoff: random(-2,2),velx: frandom(0.5,3),vely: frandom(-2,2),velz: frandom(0.5,1),accelx: frandom(-0.05,0.05),accely: frandom(-0.1,0.1),accelz: frandom(0.01,0.08),startalphaf: frandom(0.1,0.7)
		  ,fadestepf: -1,sizestep: frandom(0.3,1),startroll: random(-180,180),rollvel: frandom(-5,5)); }
	}

	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_StartSound("Nightmare/WindPull",pitch:frandom(0.75,1));
			TNT1 A 0 A_SetScale(frandom(0.05,0.15));
			TNT1 A 0 A_SetRoll(frandom(-180,180));
		Looper:
			TNT1 A 8
			{
				A_FadeOut(0.225);
				A_Explode(3,45,0,false,1,0,0,"","Ice");
				A_SetScale(Scale.X+0.015);
			}
		    Loop;
		Death:
			TNT1 A 0;
			Stop;
	 }
}