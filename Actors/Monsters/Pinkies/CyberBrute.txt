ACTOR PM_CyberBrute : PMMonsterBase
{
	Health 200
	Radius 30
	Height 56
	Speed 12
	PainChance 100
	Mass 400
	+FLOORCLIP
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+SLIDESONWALLS
	Species "Brute"
	Monster
	AttackSound "GenericPinkyBite"
	MeleeDamage 4
	MeleeThreshold 230
	//MaxStepHeight 32
	//MaxDropOffHeight 32
	MeleeRange 80
	Speed 13
	Scale 1.1
	Bloodtype "PM_NormalBloodBase"
	SeeSound "CBrute/Sight"
	DeathSound "Brute/Death"
	ActiveSound "CBrute/Active"
	PainSound "Brute/Pain"
	Obituary "%o was torn to pieces by a starving Cyber Brute."
	Tag "Cyber Brute"
	States
	{
		Spawn:
			CBRU A 10 PM_Look()
			loop
		Look:
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
			CBRU AA 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CBRU BB 2 Fast PM_LookingForPlayer()
            TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP)
			TNT1 A 0 A_Recoil(-0.5)
			CBRU CC 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CBRU DD 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_Recoil(-0.5)
            Loop
		Spotted:
		    TNT1 A 0 A_Jump(256,"RoarCheck","RoarCheck","See2")
			Goto See2
		See2:
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
			CBRU AA 2 Fast A_Chase
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CBRU BB 2 Fast A_Chase
			TNT1 A 0 A_Recoil(-0.5)
			CBRU CC 2 Fast A_Chase
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CBRU DD 2 Fast A_Chase
			TNT1 A 0 A_Recoil(-0.5)
			TNT1 A 0 A_Jump(8,"RoarCheck")
			TNT1 A 0 A_JumpIfCloser(230,"See")
			TNT1 A 0 A_JumpIfCloser(340,"Lunge")
			loop
		RoarCheck:
			TNT1 A 0 A_JumpIfInTargetLOS("Roar",360,JLOSF_DEADNOJUMP,2000)
			Goto See2
		Roar:
			CBRU A 3 A_FaceTarget
			CBRU E 1 A_PlaySound("CBrute/Sight")
			CBRU EFFFFFFFFE 4 A_FaceTarget
			Goto See2
		Lunge:
			TNT1 A 0 A_CheckSight("See")
			TNT1 A 0 A_JumpIfCloser(340,1)
			Goto See2
			CBRU H 8
			{
			A_PlaySound("CBrute/Hyd",2);
			A_FaceTarget;
			}
			CBRU E 3 A_FaceTarget
			TNT1 A 0 A_ChangeFlag("NODROPOFF",True)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
			TNT1 A 0 ThrustThingZ(0,35,0,1)
			CBRU F 5
			{
			A_ChangeFlag("NOPAIN", true);
			A_Recoil(-20);
			A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION);
			A_PlaySound("CBrute/Jump");
			}
			Goto JumpLoop
		JumpLoop:
			TNT1 A 0 A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION)
			CBRU F 2 A_CheckFloor("JumpLand")
			Loop
		JumpLand:
			TNT1 A 0 A_ChangeFlag("NODROPOFF",False)
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			CBRU G 2
			TNT1 A 0 A_Quake(1,8,0,500,0)
			CBRU H 10 A_PlaySound("CBrute/Land",3)
			Goto See2
		Melee:
			CBRU E 2 A_FaceTarget
			CBRU E 3
			TNT1 A 0 //A_PlaySound("GenericPinkyBite")
			CBRU E 3
			TNT1 A 0 A_Recoil(-10)
			TNT1 A 0 ThrustThingZ(0,25,0,1)
			CBRU F 6
			CBRU G 6 A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION)
			CBRU G 8
			Goto See2
		Pain:
			CBRU H 2 A_Pain
			Goto See2
		Death:
			CBRU I 6 A_Scream
			CBRU J 6 A_NoBlocking
			CBRU KLM 6
			TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
			CBRU N -1
			Stop
		Raise:
		    CBRU NMLKJI 6
			Goto Look
	}
}

ACTOR CBruteJump
{
Height 15
Radius 15
Speed 22
Damage 2
+RIPPER
+THRUGHOST
+NODAMAGETHRUST
DeathSound ""
SeeSound ""  
PROJECTILE
States
{
Spawn:
	TNT1 A 2
    Stop
    }
}