Class PM_CyberBrute : PMUnaffMonsterBase
{
    Default
	{
	PMMonsterBase.CodexLore
	"A blind subspecies of Brute modified through mechanical means, Cyber Brutes hunt purely by smell, but their one-track mind makes them just as predictable as a regular Brute.
	 Most of the Cyber Brutes that aid Hell's forces in the field are modified by an unknown party to gain a significant movement advantage over regular Brutes.
	 This, combined with their excellent sense of smell, allows them to reach their prey in a single, near-precise leap, often catching them in their jaws to be devoured.
	 Some Cyber Brutes also come with a special cloaking field similar to the Phase Brute's, often recognized by their pale skin that seems to be a side-effect of the field somehow.
	 
	 Despite that ability and added health, they still don't have much. Their leap can be interrupted.";
	Health 200;
	Radius 30;
	Height 56;
	Speed 12;
	FastSpeed 20;
	PainChance 100;
	Mass 400;
	+FLOORCLIP
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+SLIDESONWALLS
	Species "Brute";
	Monster;
	AttackSound "GenericPinkyBite";
	MeleeDamage 4;
	DamageType "Bite";
	MeleeThreshold 230;
	//MaxStepHeight 32
	//MaxDropOffHeight 32
	MeleeRange 80;
	Speed 13;
	Scale 1.1;
	Bloodtype "PM_NormalBloodBase";
	SeeSound "CBrute/Sight";
	DeathSound "Brute/Death";
	ActiveSound "CBrute/Active";
	PainSound "Brute/Pain";
	Obituary "%o was torn to pieces by a starving Cyber Brute.";
	Tag "Cyber Brute";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	States
	{
		Spawn:
			CBRU AAAAAAAAAA 10 PM_Look();
			CBRU A 0
			{
			if (!random(0, 30))
				{
				A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
				}
			}
			CBRU A 0 A_Jump(45,"Explore");
			Loop;
		Explore:
			TNT1 A 0
			{
			if (!random(0, 12))
				{
				A_StartSound(activesound,554,volume:frandom(0.05,0.3),pitch:frandom(0.60,0.78));
				}
			}
			TNT1 A 0 A_SetSpeed(4);
			TNT1 A 0 A_StartSound("DTechWalk",4,volume:0.25);
			CBRU AAAABBBB 4
			{
			PM_Look();
			A_Wander();
			}
			TNT1 A 0 A_StartSound("DTechWalk",4,volume:0.25);
			CBRU CCCCDDDD 4
			{
			A_Wander();
			PM_Look();
			}
			CBRU A 0 A_Jump(40,"Spawn");
			Loop;
		Look:
			TNT1 A 0 { bNOPAIN = false; }
			TNT1 A 0 { bSLIDESONWALLS = false; }
			TNT1 A 0 A_SetSpeed(13);
			CBRU AA 2 PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("DTechWalk", 3);
			TNT1 A 0 A_Recoil(-0.5);
			CBRU BB 2 PM_LookingForPlayer();
            TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
			TNT1 A 0 A_Recoil(-0.5);
			CBRU CC 2 PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("DTechWalk", 3);
			TNT1 A 0 A_Recoil(-0.5);
			CBRU DD 2 PM_LookingForPlayer();
			TNT1 A 0 A_Recoil(-0.5);
            Loop;
		Spotted:
		    TNT1 A 0 A_Jump(65,"RoarCheck");
			Goto See;
		See:
			TNT1 A 0 { bNOPAIN = false; }
			TNT1 A 0 { bSLIDESONWALLS = false; }
			TNT1 A 0 A_SetSpeed(13);
			CBRU AA 2 A_Chase();
			TNT1 A 0 A_StartSound("DTechWalk", 3);
			TNT1 A 0 A_Recoil(-0.5);
			CBRU BB 2 A_Chase;
			TNT1 A 0 A_Recoil(-0.5);
			CBRU CC 2 A_Chase();
			TNT1 A 0 A_StartSound("DTechWalk", 3);
			TNT1 A 0 A_Recoil(-0.5);
			CBRU DD 2 A_Chase();
			TNT1 A 0 A_Recoil(-0.5);
			TNT1 A 0 A_Jump(8,"RoarCheck");
			TNT1 A 0 A_JumpIfCloser(340,"LungeCheck");
			Loop;
		RoarCheck:
			TNT1 A 0 A_JumpIfInTargetLOS("Roar",360,JLOSF_DEADNOJUMP,2000);
			Goto See;
		Roar:
			CBRU A 3 A_FaceTarget;
			CBRU E 1 A_StartSound("CBrute/Sight");
			CBRU EFFFFFFFFE 4 A_FaceTarget;
			Goto See;
		LungeCheck:
		    TNT1 A 0;
			TNT1 A 0 A_CheckSight("See");
			TNT1 A 0 A_JumpIfHigherOrLower("See","Lunge");
		Lunge:
		    TNT1 A 0;
			TNT1 A 0 A_CheckSight("See");
			TNT1 A 0 A_JumpIfCloser(340,1);
			Goto See;
			CBRU H 8
			{
			A_StartSound("CBrute/Hyd",2);
			A_FaceTarget();
			}
			CBRU E 3 A_FaceTarget;
			TNT1 A 0 { bNODROPOFF = true; }
			TNT1 A 0 { bSLIDESONWALLS = true; }
			TNT1 A 0 ThrustThingZ(0,35,0,1);
			CBRU F 5
			{
			bNOPAIN = true; 
			A_Recoil(-20);
			A_SpawnProjectile("PM_FastMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
			A_StartSound("CBrute/Jump");
			}
			Goto JumpLoop;
		JumpLoop:
			CBRU FFF 2 A_CheckFloor("JumpLand");
			TNT1 A 0 ThrustThing(random(0,360)*256/360,2,0,0);
			TNT1 A 0 A_JumpIfCloser(200,"JumpBite");
			Loop;
		JumpBite:
		    CBRU GG 2 A_SpawnProjectile("PM_FastMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
		JumpLoop2:
		    CBRU GGG 2 A_CheckFloor("JumpLand");
			TNT1 A 0 ThrustThing(random(0,360)*256/360,2,0,0);
			Loop;
		JumpLand:
			TNT1 A 0 { bNODROPOFF = false; }
			TNT1 A 0 { bNOPAIN = false; }
			CBRU G 2;
			TNT1 A 0 A_Quake(1,8,0,500,0);
			CBRU H 10 A_StartSound("CBrute/Land",3);
			Goto See;
		Melee:
			CBRU E 2 A_FaceTarget;
			CBRU E 3;
			TNT1 A 0; //A_StartSound("GenericPinkyBite")
			CBRU E 3;
			TNT1 A 0 A_Recoil(-10);
			TNT1 A 0 ThrustThingZ(0,25,0,1);
			CBRU F 6;
			CBRU G 6 A_SpawnProjectile("PM_FastMeleeHitbox",18,0,0,CMF_AIMDIRECTION);
			CBRU G 8;
			Goto See;
		Pain:
			CBRU H 2 A_Pain;
			Goto See;
		Death:
			CBRU I 6 PM_Scream();
			CBRU J 6 A_NoBlocking;
			CBRU KLM 6;
			TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
			CBRU N -1;
			Stop;
		Raise:
		    CBRU NMLKJI 6;
			Goto Look;
	}
}

/*Class CBruteJump : Actor
{
  Default
  {
  Height 15;
  Radius 15;
  Speed 22;
  DamageFunction 4;
  +RIPPER
  +THRUGHOST
  +NODAMAGETHRUST
  DeathSound "";
  SeeSound "";
  DamageType "Melee";
  PROJECTILE;
  }
  States
  {
  Spawn:
	TNT1 A 2;
    Stop;
    }
}*/