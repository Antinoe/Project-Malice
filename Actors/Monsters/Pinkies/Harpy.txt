Class PM_Harpy : PMLustMonsterBase
{
    Default
	{
	PMMonsterBase.CodexLore
	"Harpies are lesser demons residing within Lust, seemingly a fusion between human and bird. They are rapacious demons, able to close in on prey in a matter of seconds and tear into their body with sharp talons.
	 They are relentless in the field, and are just as smart as your average Brute in terms of intelligence.
	 
	 Has the ability to quickly lunge at you and can fly, but otherwise has the same aggressiveness as a Brute.";
	Health 90;
	Radius 20;
	Height 56;
	Speed 14;
	PainChance 120;
	Mass 400;
	+FLOAT
	+NOGRAVITY
	+FLOORCLIP
	//+NOINFIGHTING
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+SLIDESONWALLS
	Species "Brute";
	Monster;
	MeleeDamage 4;
	MeleeRange 300;
	Scale 1;
	Bloodtype "PM_NormalBloodBase";
	SeeSound "Harpy/Sight";
	DeathSound "Harpy/Death";
	ActiveSound "";
	PainSound "Harpy/Pain";
	Obituary "%o had %p innards ripped out by a Harpy.";
	Tag "Harpy";
	}
	
	States
	{
		Spawn:
			HRP_ A 10 PM_Look();
			Loop;
        Look:
			HRP_ AAABBB 2 PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
            TNT1 A 0 A_JumpIfTargetinLOS("Spotted",150,JLOSF_DEADNOJUMP);
			HRP_ CCCDDDCCCBBB 2 PM_LookingForPlayer();
			Loop;
		Spotted:
		    TNT1 A 0 A_Jump(256,"RoarCheck","RoarNoAnim");
			Goto RoarNoAnim;
		RoarNoAnim:
		    TNT1 A 0 PM_AlertSound();
			Goto See;
		See:
			HRP_ AAABBB 2 A_Chase();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCCDDD 2 A_Chase();
			HRP_ CCCBBB 2 A_Chase();
			TNT1 A 0 A_Jump(7,"RoarCheck");
			Loop;
		SeeFast:
			HRP_ AAABBB 1 A_Chase(null,null);
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCCDDD 1 A_Chase(null,null);
			HRP_ CCCBBB 1 A_Chase(null,null);
			HRP_ AAABBB 1 A_Chase(null,null);
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCCDDD 1 A_Chase(null,null);
			HRP_ CCCBBB 1 A_Chase();
			Goto See;
		RoarCheck:
			TNT1 A 0 A_JumpIfInTargetLOS("Roar",360,JLOSF_DEADNOJUMP,2000);
			Goto See;
		Roar:
			HRP_ A 3 A_FaceTarget;
			HRP_ B 1 A_StartSound("Harpy/Roar",7);
			HRP_ GGGGGB 4 A_FaceTarget;
			Goto See;
		Dodge:
		    TNT1 A 0 {
			A_ChangeVelocity(0,randompick(-10,10),0,CVF_RELATIVE);
			A_FaceTarget();
			}
			HRP_ AAABB 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCDD 1 A_FaceTarget();
			HRP_ CCBB 1 A_FaceTarget();
			HRP_ AAABB 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCDD 1 A_FaceTarget();
			HRP_ CCBB 1 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			TNT1 A 0 { if(target && CheckSight(target) && Distance3D(target) < 350) SetStateLabel("RealMelee"); }
			Goto See;
		Missile:
		    TNT1 A 0 A_Jump(65,"Dodge");
			TNT1 A 0 A_JumpIfCloser(350,"RealMelee");
			Goto See;
		RealMelee:
			HRP_ E 7 A_FaceTarget;
			TNT1 A 0 A_StartSound("Harpy/Sight",7);
			HRP_ E 4 A_FaceTarget;
			TNT1 A 0 A_Recoil(-20);
			HRP_ FFFFF 1;
			HRP_ FFF 4 A_SpawnProjectile("PM_MeleeHitbox",20,0,0);
			TNT1 A 0 A_SpawnProjectile("PM_MeleeHitbox",20,0,0);
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			Goto See;
		Pain:
		    TNT1 A 0 A_StopSound(7);
			HRP_ H 2 A_Pain;
	        HRP_ H 2 {
			if(random(1,3) == 1)
			A_ChangeVelocity(0,randompick(-7,7),random(-5,5),CVF_RELATIVE|CVF_REPLACE);
			else
			A_ChangeVelocity(random(-4,-7),0,0,CVF_RELATIVE|CVF_REPLACE); }
			HRP_ AAABB 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCDD 1 A_FaceTarget();
			HRP_ CCBB 1 A_FaceTarget();
			HRP_ AAABB 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Harpy/WingFlap",12);
			HRP_ CCDD 1 A_FaceTarget();
			HRP_ CCBB 1 A_FaceTarget();
			TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
			TNT1 A 0 A_Jump(56,"SeeFast");
			Goto See;
		Death:
		    TNT1 A 0 A_StopSound(7);
			HR__ A 6 PM_Scream();
			HR__ B 6 A_Fall();
			HR__ CD 6;
			Wait;
		Crash:
			TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
			HR__ EEF 6 A_Fall();
			HR__ G -1;
			Stop;
		Raise:
		    HR__ GFEDCBA 6;
			Goto Look;
	}
}