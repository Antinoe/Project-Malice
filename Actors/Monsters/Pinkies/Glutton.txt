Class PM_Glutton : PMGluttonyMonsterBase
{
    Default
	{
	PMMonsterBase.CodexLore
	"Gluttons are fleshy bloated demons cursed to be ravenously hungry yet somehow perpetually full on the verge of bursting, forcing them into a disturbing cycle of endlessly regurgitating viscera in-between periods of desperately eating anything in sight.
	In Hell, most of them exist en masse in waist-high swamps of gory slurry, where blood and guts are endlessly eaten, regurgitated, and eaten again.
	
	They are much more slower and tankier than their Brute counterparts, but can vomit corrosive gore at you.";
	Health 300;
	PainChance 64;
	Speed 35;
	Radius 30;
	Height 57;
	Mass 400;
	XScale 1.4;
	YScale 1.27;
	Monster;
	+FLOORCLIP
	+FORCERADIUSDMG
	+DONTHARMCLASS
	+NOTARGET
	+LOOKALLAROUND
	Species "Brute";
	BloodColor "DarkRed";
	Bloodtype "PM_NormalBloodBase";
	SeeSound 	"Glutton/Sight";
	PainSound 	"Glutton/Pain";
	DeathSound 	"Glutton/Death";
	ActiveSound "Glutton/Active";
	AttackSound "Glutton/Attack";
	Obituary "%o was digested alive by a Glutton's acid slurry.";
	HitObituary "%o became part of the Glutton's feeding cycle.";
	Tag "Glutton";
	}
	
	States
	{
	Spawn:
		BPWA A 10 PM_Look();
		Loop;
	Look:
			TNT1 A 0 A_SetSpeed(35);
			BPWA A 8 Fast
			{
			PM_LookingForPlayer();
		    A_StartSound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA BB 6 Fast PM_LookingForPlayer();
            TNT1 A 0 A_JumpifTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
			TNT1 A 0 A_SetSpeed(35);
			BPWA C 8 Fast
			{
			PM_LookingForPlayer();
			A_StartSound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA DD 6 Fast PM_LookingForPlayer();
            Loop;
	Spotted:
	        TNT1 A 0 A_Jump(256,"Roar","RoarNoAnim");
	RoarNoAnim:
	        TNT1 A 0 A_StartSound("Glutton/Sight",CHAN_5);
	See:
			TNT1 A 0 A_SetSpeed(35);
			TNT1 A 0 A_Jump(26,"Roar");
			BPWA A 8 Fast
			{
			A_Chase();
		    A_StartSound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA BB 6 Fast A_Chase();
			TNT1 A 0 A_SetSpeed(35);
			BPWA C 8 Fast
			{
			A_Chase();
			A_StartSound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA DD 6 Fast A_Chase();
			TNT1 A 0 A_Jump(10,"Sprint");
			Loop;
	Sprint:
			TNT1 A 0 A_SetSpeed(35);
			BPWA A 4 Fast
			{
			A_Recoil(-1.5);
			A_Chase("Melee",null);
		    A_StartSound("hoofstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA BB 3 Fast
			{
			A_Chase("Melee",null);
			A_Recoil(-1.5);
			}
			TNT1 A 0 A_SetSpeed(35);
			BPWA C 4 Fast
			{
			A_Recoil(-1.5);
			A_Chase("Melee",null);
			A_StartSound("hoofstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5);
			BPWA DD 3 Fast
			{
			A_Chase("Melee",null);
			A_Recoil(-1.5);
			}
			TNT1 A 0 A_Jump(20,"See");
			Loop;
	Roar:
		TNT1 A 0;
		TNT1 A 0 A_CheckSight("See");
		TNT1 A 0 A_StartSound("GluttonSight2",CHAN_7);
		BPBI A 7 A_FaceTarget;
		BPBI C 14 A_FaceTarget;
		BPBI BBBBABBAAA 5 A_FaceTarget;
		Goto Sprint;
	Melee:
		BPBI AB 8 A_FaceTarget;
		BPBI C 20 A_SargAttack;
		Goto See;
	Missile:
			TNT1 A 0 A_StartSound("Afflicted/Puke",CHAN_6);
			BPBI CCAABB 3
			{
			A_FaceTarget();
			A_SpawnProjectile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
			}
			BPBI B 0 A_StartSound("Glutton/Spit",CHAN_5);
			BPBI BBB 1 A_SpawnProjectile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
			TNT1 A 0 A_FaceTarget;
			BPBI BBBBB 0 A_SpawnProjectile("PM_GluttonBloodshot",38,0,random(-5,5),CMF_OFFSETPITCH,random(5,-17));
			TNT1 A 0 A_FaceTarget;
			BPBI BBBBBBBBB 1 A_SpawnProjectile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
			BPBI BA 5 A_FaceTarget;
			Goto See;
	Pain:
		BPPA A 2;
		TNT1 A 0 A_StopSound(CHAN_7);
		BPPA A 14 A_Pain;
		Goto See;
		
	Death:
		BPDE A 1;
		BPDE A 8 PM_Scream();
		BPDE B 8; 
		BPDE C 6;
		BPDE D 5 A_NoBlocking;
		BPDE E 4;
		TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
		BPDE F -1;
		Stop;
	Raise:
		BPDE F 5;
		BPDE EDCBA 5;
		Goto Look;
	}
}

Class PM_GluttonBloodshot : Actor
{
  Default
  {
  Radius 3;
  Height 3;
  Speed 17;
  DamageFunction 8;
  Alpha 1;
  +ThruGhost
  -NOGRAVITY
  -NOLIFTDROP
  +BOUNCEONWALLS
  +BOUNCEONFLOORS
  +ROLLSPRITE
  +NOTELEPORT
  +DROPOFF
  +NOBLOCKMAP
  +ACTIVATEIMPACT
  +ACTIVATEMCROSS
  +MISSILE
  Gravity 0.35;
  BounceFactor 0.5;
  DeathSound "";
  }
  States
  {
  Spawn:
		TNT1 A 0 A_SetScale(frandom(0.7,4.5));
		TNT1 A 0 A_SetRoll(random(0,180));
		TNT1 A 0 A_SpawnItemEx("PM_NormalBloodMulti");
		TNT1 A 0 A_Jump(256,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20);
		MEAT ABCDEFGHIJLKMNOPQRST 0 A_JumpIf(1 == 1,"Play");
	Play:
		"####" "#" 2 A_SpawnItemEx("PM_NormalBloodEx",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION,100);
		"####" "#" 0 A_CheckFloor("Death");
		"####" "#" 0 A_SetRoll(roll+15);
		Loop;
	Death:
		TNT1 A 0 { bMISSILE = false; }
		"####" "#" 0
		{
		A_SetRoll(0);
		for(int a=0;a<3;a++)
		A_SpawnProjectile("RedGroundOoze",1,random(-6,6),random(-90,90));
		A_SpawnItemEx("BloodPukeSplatter",random(-3,3),random(-3,3));
		A_SpawnProjectile("RedGroundStain",1,random(-6,6),random(-90,90));
		}
		"####" "#" 0 A_StartSound("TaintedBloodExp");
		"####" "#" 0 A_SpawnItemEx("PM_NormalBloodEx",random(3,-3),random(3,-3),random(3,-3),0,0,0,0,SXF_NOCHECKPOSITION);
		"####" "#" 0 A_QueueCorpse;
		"####" "#" 100;
		"####" "#" 1 A_FadeOut;
		Wait;
	}
}

Class PM_GluttonBloodtrail : Actor
{
  Default
  {
  Radius 1;
  Height 1;
  Projectile;
  RenderStyle "Translucent";
  Alpha 0.67;
  }
  States
  {
  Spawn:
    TNT1 A 3;
    FBL1 FGH 3;
    Stop;
  }
}