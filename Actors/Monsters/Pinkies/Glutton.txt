ACTOR PM_Glutton : Demon
{
	Health 300
	PainChance 64
	Speed 35
	Radius 30
	Height 57
	Mass 400
	XScale 1.4
	YScale 1.27
	Monster
	+FLOORCLIP
	+FORCERADIUSDMG
	//+NOINFIGHTING
	+DONTHARMSPECIES
	+NOTARGET
	+LOOKALLAROUND
	Species "Brute"
	//DropItem "RagePoint1"
	BloodColor "DarkRed"
	SeeSound 	""
	PainSound 	"Glutton/Pain"
	DeathSound 	"Glutton/Death"
	ActiveSound "Glutton/Active"
	AttackSound "Glutton/Attack"
	Obituary "%o was digested alive by a Glutton's acid slurry."
	HitObituary "%o became part of the Glutton's feeding cycle."
	Tag "Glutton"
	States
	{
	Spawn:
		BPWA A 10 A_Look
		Loop
	See:
			TNT1 A 0 A_SetSpeed(35)
			BPWA A 8 Fast
			{
			A_Chase("","",CHF_NOPLAYACTIVE);
		    A_PlaySound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA BB 6 Fast A_Chase("","",CHF_NOPLAYACTIVE)
			TNT1 A 0 A_SetSpeed(35)
			BPWA C 8 Fast
			{
			A_Chase("","",CHF_NOPLAYACTIVE);
			A_PlaySound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA DD 6 Fast A_Chase("","",CHF_NOPLAYACTIVE)
			Goto Spotted
	Spotted:
	        TNT1 A 0 A_Jump(256,"Roar","See2","RoarNoAnim")
			Goto See2
	RoarNoAnim:
	        TNT1 A 0 A_PlaySound("Glutton/Sight",CHAN_5)
	See2:
			TNT1 A 0 A_SetSpeed(35)
			TNT1 A 0 A_Jump(26,"Roar")
			BPWA A 8 Fast
			{
			A_Chase;
		    A_PlaySound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA BB 6 Fast A_Chase
			TNT1 A 0 A_SetSpeed(35)
			BPWA C 8 Fast
			{
			A_Chase;
			A_PlaySound("demonstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA DD 6 Fast A_Chase
			TNT1 A 0 A_Jump(10,"Sprint")
			Loop
	Sprint:
			TNT1 A 0 A_SetSpeed(35)
			BPWA A 4 Fast
			{
			A_Recoil(-1.5);
			A_Chase("Melee","");
		    A_PlaySound("hoofstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA BB 3 Fast
			{
			A_Chase("Melee","");
			A_Recoil(-1.5);
			}
			TNT1 A 0 A_SetSpeed(35)
			BPWA C 4 Fast
			{
			A_Recoil(-1.5);
			A_Chase("Melee","");
			A_PlaySound("hoofstep", 3);
			}
			TNT1 A 0 A_SetSpeed(5)
			BPWA DD 3 Fast
			{
			A_Chase("Melee","");
			A_Recoil(-1.5);
			}
			TNT1 A 0 A_Jump(20,"See2")
			Loop
	Roar:
		TNT1 A 0
		TNT1 A 0 A_CheckSight("See2")
		TNT1 A 0 A_PlaySound("GluttonSight2",CHAN_7)
		BPBI A 7 A_FaceTarget
		BPBI C 14 A_FaceTarget
		BPBI BBBBABBAAA 5 A_FaceTarget
		Goto See2
	Melee:
		BPBI AB 8 A_FaceTarget
		BPBI C 20 A_SargAttack
		Goto See2
	Missile:
			TNT1 A 0 A_PlaySound("Afflicted/Puke",CHAN_6)
			BPBI CCAABB 3
			{
			A_FaceTarget;
			A_CustomMissile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20));
			}
			BPBI B 0 A_PlaySound("Glutton/Spit",CHAN_5)
			BPBI BBB 1 A_CustomMissile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20))
			TNT1 A 0 A_FaceTarget
			BPBI BBBBB 0 A_CustomMissile("PM_GluttonBloodshot",38,0,random(-5,5),CMF_OFFSETPITCH,random(-5,17))
			TNT1 A 0 A_FaceTarget
			BPBI BBBBBBBBB 1 A_CustomMissile("PM_TumorBurstBlood",38,0,random(-25,25),CMF_AIMDIRECTION,random(-10,20))
			BPBI BA 5 A_FaceTarget
			Goto See2
	Pain:
		BPPA A 2
		TNT1 A 0 A_StopSound(CHAN_7)
		BPPA A 14  A_Pain
		Goto See2
		
	Death:
		BPDE A 1
		TNT1 A 0 A_StopSound(CHAN_7)
		BPDE A 8 A_Scream
		BPDE B 8 
		BPDE C 6
		BPDE D 5 A_NoBlocking
		BPDE E 4
		TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
		BPDE F -1 
		Stop
	XDeath:
		"####" "#" 0 A_BossDeath
		Goto Death
		
	Raise:
		BPDE F 5
		BPDE EDCBA 5
		Goto See
	}
}

Actor PM_GluttonBloodshot
{
  Radius 3
  Height 3
  Speed 17
  Damage (random(5,5))
  Projectile
  RenderStyle Translucent
  Alpha 0.80
  +ThruGhost
  -NOGRAVITY
  Gravity 0.35
  DeathSound "TaintedBloodExp"
  States
  {
  Spawn:
    FBL1 AB 3 A_SpawnItemEx("PM_BloodSkullTrail",0,0,0,0,0,0,128)
    Loop
  Death:
	 TNT1 A 0 A_CustomMissile("RedGroundOoze",1,random(-6,6),random(-90,90))
	TNT1 A 0 A_CustomMissile("RedGroundOoze",1,random(-6,6),random(-90,90))
	TNT1 A 0 A_CustomMissile("RedGroundOoze",1,random(-6,6),random(-90,90))
	TNT1 A 0 A_SpawnItemEx("BloodPukeSplatter",random(-3,3),random(-3,3))
	TNT1 A 0 A_CustomMissile("RedGroundStain",1,random(-6,6),random(-90,90))
    FBL1 CDE 3 Bright
    Stop
  }
}

Actor PM_GluttonBloodtrail
{
  Radius 1
  Height 1
  Projectile
  RenderStyle Translucent
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 3
    FBL1 FGH 3
    Stop
  }
}