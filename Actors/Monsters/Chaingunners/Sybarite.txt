Class PM_Sybarite : PMLustMonsterBase
{
  Default
  {
  PMMonsterBase.CodexLore 
  "Sybarites act as guards and shock troops for the many pawns of Lust, ensuring these cabals can continue doing unspeakable things uninterrupted. When not acting as bouncers, Sybarites mercilessly hunt down and capture new victims. They revel in their work, and the arrival of Hell has only enhanced their cruelty. Unlike many possessed humans, Sybarites are still in control of their bodies, and are aware enough to know exactly what they're doing. They enjoy it.
   
   Equipped with a micro-missile launcher. When close, they'll fire away with grenades or a net launcher.";
  Health 80;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 10;
  Scale 1;
  PainChance 100;
  MeleeRange 250;
  Monster;
  +FLOORCLIP
  +HARMFRIENDS
  +DOHARMSPECIES
  +MISSILEMORE
  SeeSound "Sybarite/Sight";
  PainSound "Sybarite/Pain";
  DeathSound "Sybarite/Death";
  ActiveSound "Sybarite/Active";
  Tag "Sybarite";
  Obituary "%o was blown apart to satisfy a Sybarite's self-indulgence.";
  Dropitem "Chaingun";
  Species "Chaingunner";
  Bloodtype "PM_NormalTyrantBloodBase";
  TeleFogSourceType "PM_PossessedTeleportFog";
  TeleFogDestType "PM_PossessedTeleportFog";
  }

	void PM_SybariteGrenade()
	{
		if(!target) {
			SetStateLabel("Spawn");
			return;
		}
		int arc;
		let p = A_SpawnProjectile("PM_SybGrenade",28,15,frandom(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH);
		if(distance2d(target) <= 400) arc = 20;
		else arc = 60;
		if(p) p.vel = CalcThrowVector(pos, target.pos, p.gravity, arc);
	}
	
	void PM_SybariteNetFired()
	{
		if(!target) {
			SetStateLabel("Spawn");
			return;
		}
		int arc;
		let p = A_SpawnProjectile("PM_SybariteNet",30,15);
		if(distance2d(target) <= 400) arc = 15;
		else arc = 40;
		if(p) p.vel = CalcThrowVector(pos, target.pos, p.gravity, arc);
	}

  States
  {
  Spawn:
    SY_A A 0 NoDelay
    {
     user_numbullets = random(15,25);
    }
  Idle:
    SY_A AA 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    SY_A AABB 3 PM_LookingForPlayer();
    TNT1 A 0 A_JumpIfTargetInLOS("Spotted",150,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	SY_A CCDD 3 PM_LookingForPlayer();
    Goto Look+1;
  Roar:
	TNT1 A 0;
	TNT1 A 0 A_StartSound("Sybarite/Roar",CHAN_VOICE);
	Goto See;
  Spotted:
	TNT1 A 0 A_Jump(128,"Roar");
    TNT1 A 0 A_StartSound("Sybarite/Sight",CHAN_VOICE);
  See:
	TNT1 A 0 { bMissileEvenMore = False; }
	TNT1 A 0 { bNOPAIN = False; }
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6,attenuation:0.7);
    SY_A AABB 3
	{
	A_Recoil(-0.1);
	A_Chase(null,"Missile");
	}
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6,attenuation:0.7);
	SY_A CCDD 3
	{
	A_Recoil(-0.1);
	A_Chase(null,"Missile");
	}
	TNT1 A 0 A_CheckSight("See");
	TNT1 A 0 A_Jump(128,"SeeWander");
	Loop;
  SeeWander:
	TNT1 A 0 { bMissileEvenMore = True; }
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6,attenuation:0.7);
    SY_A AABB 3
	{
	A_Recoil(-0.1);
	A_Chase(null,"Missile",CHF_DONTMOVE|CHF_DONTTURN);
	A_Wander();
	}
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6,attenuation:0.7);
	SY_A CCDD 3
	{
	A_Recoil(-0.1);
	A_Chase(null,"Missile",CHF_DONTMOVE|CHF_DONTTURN);
	A_Wander();
	}
	TNT1 A 0 A_JumpIfTargetinLOS("SeeWander",150,JLOSF_DEADNOJUMP);
    Goto See;
  AltMelee:
	SY_A E 0 A_StartSound("Sybarite/Roar",CHAN_VOICE);
	Goto Capturing;
  Melee:
	SY_A E 1 A_FaceTarget();
	TNT1 A 0 A_Jump(100,"AltMelee");
	SY_A E 0 A_StartSound("Sybarite/Capture",CHAN_VOICE);
  Capturing:
	SY_A EEEEE 1 A_FaceTarget();
	SY_A E 0 A_StartSound("Sybarite/NetCharge",32);
	SY_A EEEEEEEE 4
		{
		A_FaceTarget();
		A_SpawnParticleEx( "", TexMan.CheckForTexture("SPKRA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 5
		, size: 70, xoff: 20,yoff:10,zoff:32, startalphaf: 1, fadestepf: 0, sizestep: -8,startroll: random(-180,180));
		}
	SY_A F 5
		{
		A_FaceTarget();
		PM_SybariteNetFired();
		}
	SY_A EEEEEEEEEEEEEEE 1
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_VentSmoke",20,10,32,random(2,-2),random(2,-2),random(1,3),0,0,0,0);
	}
	Goto See;
  Missile:
	TNT1 A 0;
	TNT1 A 0 A_JumpIfCloser(700,"ChargeChoice");
  MissileProceed:
	SY_A E 1 A_FaceTarget();
    SY_A E 20 
	{
	 if(random(1,3) == 1)
	  A_StartSound("Sybarite/Roar",CHAN_VOICE);
	  A_StartSound("PM/ArachnoReload",13,pitch:0.85);
	 A_FaceTarget(360,180);
	 A_SetAngle(angle+random(-5,5));
	 A_SetPitch(pitch+random(-5,5));
	}
	SY_A E 5 A_Jump(115,"GrenadeLaunch");
  AutoCannon:
    TNT1 A 0 { pm_attackloop = random(1,4); }
  ACLoop:
    TNT1 A 0 {
	if(pm_attackloop <= 0)
	 SetStateLabel("MissileEnd");
	if(user_numbullets <= 0)
	 SetStateLabel("NoAmmo");
	}
    SY_A FFFFFF 5 Bright 
	{
	  A_FaceTarget(15,15);
	  A_SpawnProjectile("PM_SybariteMicroMissile",32,15,frandom(-1,1),CMF_AIMDIRECTION,pitch+frandom(-1,1));
	}
	SY_A E 0 { user_numbullets--; pm_attackloop--; }
	Loop;
  ChargeChoice:
	TNT1 A 0;
	TNT1 A 0 A_Jump(128,"MissileProceed");
	TNT1 A 0 A_Jump(128,"Charge");
	TNT1 A 0 A_StartSound("Sybarite/Roar",CHAN_VOICE);
	Goto Charging;
  Charge:
	TNT1 A 0;
	TNT1 A 0 A_JumpIfCloser(300,"Charging");
	TNT1 A 0 A_StartSound("Sybarite/Charge",CHAN_VOICE);
  Charging:
	TNT1 A 0 { bNOPAIN = True; }
	TNT1 A 0 A_SetSpeed(15);
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
    SY_A AABB random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
	SY_A CCDD random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
    SY_A AABB random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
	SY_A CCDD random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
    SY_A AABB random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
	SY_A CCDD random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee",null);
	}
	TNT1 A 0 A_Jump(120,"MeleeCharge");
	TNT1 A 0 A_Jump(30,"See");
	Loop;
  MeleeCharge:
	TNT1 A 0 { bNOPAIN = True; }
	TNT1 A 0 A_SetSpeed(15);
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
    SY_A AABB random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee","Melee");
	}
	TNT1 A 0 A_StartSound("Sybarite/Step",CHAN_6,CHANF_OVERLAP,0.5);
	SY_A CCDD random(2,3)
	{
	A_Recoil(-0.5);
	A_Chase("Melee","Melee");
	}
	TNT1 A 0 A_Jump(30,"See");
	Loop;
  GrenadeLaunch:
    TNT1 A 0;
    TNT1 A 0 { pm_attackloop = random(1,3); }
  GLLoop:
    TNT1 A 0 {
	if(pm_attackloop <= 0)
	 SetStateLabel("MissileEnd");
	if(user_numbullets <= 0)
	 SetStateLabel("NoAmmo");
	}
	TNT1 A 0;
   GLPreFire:
	TNT1 A 0 A_Jump(128,"NoArcGL");
    SY_A F 5 Bright 
	{
	 A_FaceTarget();
	 A_StartSound("Sybarite/Thump",13);
	 PM_SybariteGrenade();
	}
    GLPostFire:
	SY_A E 20 { A_StartSound("Sybarite/SlotIn",12); user_numbullets--; pm_attackloop--; }
	Goto GLLoop;
  NoArcGL:
	SY_A F 5 Bright 
	{
	 A_FaceTarget();
	 A_StartSound("Sybarite/Thump",13);
	 A_SpawnProjectile("PM_SybGrenadeNoArc",28,15,frandom(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,frandom(-35,-6));
	}
	Goto GLPostFire;
  MissileEnd:
    SY_A E 10 A_FaceTarget(5,5);
	Goto See;
  NoAmmo:
	SY_A E 7 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A EE 10 A_FaceTarget();
	TNT1 A 0 A_Jump(56,"ZReload");
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A E 8 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A E 8 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A AAAA 5 A_FaceTarget();
	Goto ZReload;
  ZReload:
     TNT1 A 0 A_StartSound("Sybarite/Reload1",31);
     SY_A EEEEEEE 5;
     TNT1 A 0 A_SpawnItemEx("PM_EmptyHeavyMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-4,0));
     SY_A CCCBBBB 6;
     TNT1 A 0 A_StartSound("CivZombie/IEDTakeOut",32);
     SY_A DDDBBB 3;
     TNT1 A 0 A_StartSound("Sybarite/Reload2",33);
	 TNT1 A 0 { user_numbullets = (user_numbullets + random(5,15)); }
     SY_A AAAABB 4;
     TNT1 A 0 A_StartSound("Sybarite/SlotIn",34);
	 SY_A CC 4;
	 TNT1 A 0 A_StartSound("Sybarite/Reload3",35);
     SY_A CCAA 4;
     Goto See;
  Pain:
	TNT1 A 0 A_StopSound(CHAN_VOICE);
    SY_A G 3 A_StopSound(10);
    SY_A G 8 A_Pain;
    Goto See;
  Death:
    TNT1 A 0 A_StopSound(10);
	TNT1 A 0 A_StopSound(CHAN_VOICE);
    SY_D A 5;
    SY_D B 5 PM_Scream();
    SY_D C 5 A_NoBlocking;
    SY_D D 5;
	  TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
	  SY_D EFG 5;
	  SY_D G 90;
	  TNT1 A 0 { if(random(1,4) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
    SY_D G -1;
    Stop;
  Raise:
    SY_A L 5;
    SY_A KJIH 5;
    Goto Look;
  }
}

Class PM_SybGrenadeNoArc : PM_SybGrenade
{
Default
{
Gravity 1;
Speed 28;
BounceFactor 0.6;
WallBounceFactor 0.6;
}
}

Class PM_SybGrenade : PM_ZTrooperFragGrenade
{
 int timer;
 Default
 {
  Radius 10;
  Height 8;
  Speed 24;
  Gravity 1.35;
  SeeSound "";
  BounceSound "Sybarite/GrenadeBounce";
  +ROLLSPRITE
  Scale 0.75;
  BounceFactor 0.3;
  WallBounceFactor 0.3;
 }
 States
 {
 Spawn:
  TNT1 A 0;
  TNT1 A 0
	{
	A_SpawnParticleEx( "", TexMan.CheckForTexture("LEYSO0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 5
	, size: frandom(300,350), xoff: -5, startalphaf: 1.0, sizestep: frandom(0.0,3),startroll: random(-180,180));
	}
  TNT1 A 0 A_StartSound("Sybarite/GrenadeLaunch",35);
  TNT1 A 0 A_SpawnItemEx("PM_SybGrenadeTick",flags:SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
 Go:
  GREN AAABBBCCCDDDEEEFFF 1 BRIGHT
  {
	A_SpawnParticleEx("",TexMan.CheckForTexture("LENRA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -5,zoff:8,startalphaf: 0.45);
	A_SpawnParticle("FF2A2A",SPF_FULLBRIGHT|SPF_RELATIVE,95,13,0,-5,0,8,0,0,0,0,0,0,0.85,-0.2,-1);
	A_SpawnParticleEx( "#d2d2d2", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(20,75),
	size: frandom(15,35), xoff: -5, velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),
	startalphaf: frandom(0.6,0.85), fadestepf: -1, sizestep: frandom(0.0,2.5), startroll: random(-180,180), rollvel: frandom(-1,1));
  }
 Loop;
 Bounce:
  TNT1 A 0;
  TNT1 A 0 A_SetRoll(random(0,350),SPF_INTERPOLATE);
  Goto Go;
 Death:
 XDeath:
 Crash:
	TNT1 A 0;
    TNT1 A 0 A_StartSound("Sybarite/GrenadeBounce");
  Land:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	GREN GG 1 Light("SatyrLordRedMissile9")
	{
	if(timer >= 500)
	SetStateLabel("Fade");
	timer++;
	A_SpawnParticleEx("",TexMan.CheckForTexture("LENRA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -1,zoff:8,startalphaf: 0.45);
	}
	Loop;
  Fade:
    GREN G 2 A_FadeOut(0.05);
	Loop;
 }
}

Class PM_SybGrenadeTick : Actor
{
    int wait;
    Default
	{
	Radius 3;
	Height 3;
	Scale 0.75;
	+NOINTERACTION
	+FORCERADIUSDMG
	+DONTTHRUST
	+NOBLOOD
	DamageType "Fire";
	Species "Chaingunner";
	OBITUARY "%o was caught in the blast zone of a Sybarite's grenade.";
	}
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 wait = 4;
	 tex[0] = TexMan.CheckForTexture("SPKOB0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[3];
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	Go:
	    TNT1 A 0 { if(wait <= 0) SetStateLabel("Explode"); } 
		TNT1 AAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0  {
		A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		A_StartSound("CivZombie/IEDBeep",CHAN_5,pitch:1.15);
		A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		wait--; }
		TNT1 AAAA 1 Light("SatyrLordRedMissile3") A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
        Loop;
	Explode:
	    TNT1 A 0;
		TNT1 A 1 Light("SatyrLordRedMissile4") A_StartSound("Draugr/RocketLock",10,pitch:1.25);
		TNT1 AAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	Death:
	    TNT1 A 0 A_StopSound(10);
		TNT1 A 0 A_StartSound("Noble/BombExp",6);
		TNT1 A 0 A_StartSound("Noble/BombExpFar",7);
		TNT1 A 0 {bNoGravity = true;}
		TNT1 A 0 
		{
		A_SpawnParticleEx(
		"ff9d1c", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: 10, size: 800, zoff: 45, startalphaf: 1, fadestepf: -1, sizestep: -20,
		startroll: random(-180,180), rollvel: frandom(-5,5));
		for(int i=2;i>0;i--)
			A_SpawnParticleEx(
			"ff9d1c", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: 10, size: 700, zoff: 10, startalphaf: 1, fadestepf: -1, sizestep: 20,
			startroll: random(-180,180), rollvel: frandom(-5,5));
		for(int i=random(4,8);i>0;i--)
		A_SpawnParticleEx(
			"", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(80,120), size: frandom(1,15), zoff: random(10,60), velx: random(-6,6), vely: random(-6,6),
			velz: random(0,8), accelz: frandom(-0.2,-0.8), startalphaf: 1, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-2,2)
			);
		for(int i=random(5,10);i>0;i--)
		A_SpawnParticleEx
			( "727272", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(40,70), size: frandom(1,22), zoff: random(10,35),
			velx: frandom(-7,7), vely: frandom(-7,7), velz: frandom(1,12),
			accelz: frandom(-0.1,-0.9), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-4,4));
		}
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-6,6),random(-6,6),random(-1,4),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
		TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0);
		TNT1 A 1;
		TNT1 A 0 A_RemoveMaster(RMVF_EVERYTHING);
		TNT1 A 8 Bright Light("YellowMediumFlicker2") A_Explode(30,244);
		TNT1 A 6 Bright Light("YellowMediumFlicker2");
		TNT1 A 5 Bright Light("YellowMediumFlicker2");
		Stop;
	}
}

Class PM_SybariteMicroMissile : PMProjectileBase
{
    Default
    {
	Radius 2;
	Height 2;
	DamageFunction 15;
	Speed 15;
	FastSpeed 15;
	Projectile;
	RenderStyle "Normal";
	Decal "PM_OrangeFireScorch";
	SeeSound "Sybarite/MissileFire";
	DeathSound "SmallMissileExplosion";
	PMProjectileBase.WhizSound "FastMissileFlyby";
	PMProjectileBase.WhizRangeCheck 300;
	Scale 0.8;
	+RANDOMIZE
	+THRUGHOST
	+FORCEXYBILLBOARD
    }
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("LENYA0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("LEYSO0",TexMan.Type_Sprite);
	}
	
	TextureID tex[4];
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0
		{
		 A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(2,4)
		  , size: frandom(150,250), xoff: -8, startalphaf: 1.0, sizestep: frandom(0.0,3),startroll: random(-180,180));
		 A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(2,4)
		 , size: frandom(100,200), xoff: -5, startalphaf: 1.0, sizestep: frandom(0.0,3),startroll: random(-180,180));
		}
		TNT1 A 0 A_StartSound("Fiend/HomingFire",32,volume:0.8,pitch:frandom(1.3,1.4));
		TNT1 A 0 A_ScaleVelocity(0.6);
		MICR AAAAA 1 BRIGHT;
		TNT1 A 0 A_ScaleVelocity(0.6);
		MICR AAAAA 1 BRIGHT;
		TNT1 A 0 A_StartSound("FastMissileFlyby",24,volume:1,pitch:frandom(1.1,1.35),0.5);
		TNT1 A 0 A_StartSound("Sybarite/MissileLoop",CHAN_6,CHANF_LOOPING);
		TNT1 A 0 A_ScaleVelocity(4);
	GoStart:
		MICR AAAAAA 1 BRIGHT
		{
		A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
		A_SpawnParticleEx("",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,50),size: 10.0,xoff: frandom(-2,-15),startalphaf: 0.8
		,fadestepf: -1,sizestep: 0.8,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	    A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size:frandom(15,60),xoff: -10,startalphaf: 0.9);
		}
		TNT1 A 0 A_Jump(256,"Go1","Go2","Go3");
	Go1:
		MICR A 1 BRIGHT
		{
		A_Weave(2, 2, 2.0, 1.0);
		A_SpawnParticleEx("",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,50),size: 10.0,xoff: frandom(-2,-15),startalphaf: 0.8
		,fadestepf: -1,sizestep: 0.8,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	    A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size:frandom(15,60),xoff: -10,startalphaf: 0.9);
		}
		Loop;
	Go2:
		MICR A 1 BRIGHT
		{
		A_Weave(-2, -2, -2.0, -1.0);
		A_SpawnParticleEx("",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,50),size: 10.0,xoff: frandom(-2,-15),startalphaf: 0.8
		,fadestepf: -1,sizestep: 0.8,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	    A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size:frandom(15,60),xoff: -10,startalphaf: 0.9);
		}
		Loop;
	Go3:
		MICR A 1 BRIGHT
		{
		A_Weave(3, 3, 2.0, 1.0);
		A_SpawnParticleEx("",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,50),size: 10.0,xoff: frandom(-2,-15),startalphaf: 0.8
		,fadestepf: -1,sizestep: 0.8,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	    A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size:frandom(15,60),xoff: -10,startalphaf: 0.9);
		}
		Loop;
	Death:
		TNT1 A 0 A_StopSound(CHAN_6);
		TNT1 A 0 A_StartSound("SmallMissileExplosion",volume:0.7);
		TNT1 A 0 A_StartSound("Sybarite/MissileExp",35);
		TNT1 A 0 
		{
	     A_SpawnParticleEx
		("",TexMan.CheckForTexture("EXN1B0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 30, size: 80.0, startalphaf: 1, fadestepf: -1, sizestep: frandom(0.0,2.5), startroll: random(-180,180));
		}
		TNT1 AA 0 A_SpawnItemEx("PM_SmallerGenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
		TNT1 AAAA 0 A_SpawnProjectile("PM_FiendBallExplode",0,0,random(0,360),2,random(0,360));
		FSHT BBCCDDEE 1 BRIGHT Light("AvatarFireball4");
		Stop;
	}
}

Class PM_SybariteNet : PMProjectileBase
{
  int user_grenadetime;
  Default
  {
  Speed 20;
  Scale 2;
  Radius 15;
  Height 10;
  Gravity 0.7;
  Bouncetype "Doom";
  BounceFactor 0.1;
  WallBounceFactor 0.1;
  DamageFunction (10);
  RenderStyle "Normal";
  DamageType "Electric";
  SeeSound "Sybarite/NetFire";
  BounceSound "";
  DeathSound "";
  Decal " ";
  -NOGRAVITY
  +CANBOUNCEWATER
  }
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 0
	{
	A_SpawnParticleEx( "", TexMan.CheckForTexture("LEYSO0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 5
	, size: frandom(300,350), xoff: -5, startalphaf: 1.0, sizestep: frandom(0.0,3),startroll: random(-180,180));
	}
	TNT1 A 0 A_StartSound("FastMissileFlyby",24,volume:1,pitch:0.7,0.8);
  Go:
	_NET AAAABBBBBBCCCCCCCCC 1
	{
	A_SpawnParticleEx("",TexMan.CheckForTexture("GNSMK0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(70,200),size: 160.0,xoff: frandom(-10,-25),startalphaf: 0.25
	,fadestepf: -1,sizestep: -2,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
	A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,3),size: random(5,90),xoff: random(-40,40),yoff: random(-40,40), zoff: random(-40,40), velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
	}
  Go1:
	_NET D 1
	{
	A_SpawnParticleEx("",TexMan.CheckForTexture("GNSMK0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(70,200),size: 200.0,xoff: frandom(-10,-25),startalphaf: 0.25
	,fadestepf: -1,sizestep: -2,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
	A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,3),size: random(5,90),xoff: random(-40,40),yoff: random(-40,40), zoff: random(-40,40), velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
	}
	Loop;
  Death:
	TNT1 A 0 A_StartSound("Corpse/FallMetal",CHAN_6,pitch:1.4);
	_NET EEEEEEEE 1;
	_NET FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 5
	{
	A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,3),size: random(5,90),xoff: random(-40,40),yoff: random(-40,40), zoff: random(0,10), velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
			if (!random(0, 7))
			{
				A_SpawnProjectile("PM_ConduitBeamWarning",70,random(-50,50),random(-180,180),CMF_OFFSETPITCH,random(90,70));
				A_StartSound("Sybarite/NetShock",38,CHANF_OVERLAP,0.8,pitch:frandom(0.8,1.2));
			}
	}
  DeathContinue:
	_NET F 1
	{
	A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,4),size: random(5,90),xoff: random(-40,40),yoff: random(-40,40), zoff: random(0,10), velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
	A_Fadeout(0.01);
	}
	Loop;
  XDeath:
	TNT1 A 1 A_RadiusThrust(2000,90,RTF_NOIMPACTDAMAGE);
	TNT1 A 0 A_StartSound("Corpse/FallMetal",CHAN_6,pitch:1.4);
	TNT1 A 0
	{
	A_SpawnProjectile("PM_ConduitBeamWarning",70,random(-50,50),random(-180,180),CMF_OFFSETPITCH,random(90,70));
	A_StartSound("Sybarite/NetShock",38,CHANF_OVERLAP,0.8,pitch:frandom(0.8,1.2));
	A_RadiusGive("PM_SybariteNetted",60,RGF_PLAYERS|RGF_MONSTERS,1);
	//A_GiveToTarget("PM_SybariteNetted",1,AAPTR_TARGET);
	}
	Stop;
	}
}

Class PM_SybariteNetted : PowerSpeed
{
	Default
	{
		Powerup.Duration -9;
		Speed 0.30;
		Inventory.Icon "";
		Powerup.Color "00 00 00", .3;
		+POWERSPEED.NOTRAIL;
	}
	Override void DoEffect()
	{
		Super.DoEffect();
		if(owner.health > 0 && !IsFrozen())
		{
			owner.A_SpawnParticleEx("",TexMan.CheckForTexture("BLULA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(2,4),size: random(5,90),xoff: random(-30,30),yoff: random(-30,30), zoff: random(0,60), velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.0,0.9),startroll: random(-180,180),rollvel: frandom(-30,30));
			if(!random(0, 30))
			{
			owner.A_Explode(5,10,damagetype:"Electric");
			if(owner && owner.health > 0 && !IsFrozen())
			{
			owner.A_SpawnProjectile("PM_ConduitBeamWarning",70,random(-50,50),random(-180,180),CMF_OFFSETPITCH,random(90,70));
			owner.A_StartSound("Sybarite/NetShock",100,CHANF_OVERLAP,0.8,pitch:frandom(0.8,1.2));
			}
			}
		}
	}
}