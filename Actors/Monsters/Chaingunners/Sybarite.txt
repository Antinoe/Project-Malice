Class PM_Sybarite : PMLustMonsterBase
{
  Default
  {
  PMMonsterBase.CodexLore 
  "Sybarites are zombies whose possessors have an openly self-indulgent approach towards war, arming themselves with heavy weaponry in order to shamble into the field with explosions galore. They care not for friendly fire, only concerning themselves with who dies first and so on in the hands of their mania.
   
   Comes in with a repeating autocannon. If you're sufficiently close enough, it'll fire away with bouncy grenades.";
  Health 80;
  Radius 20;
  Height 56;
  Mass 100;
  Speed 8;
  Scale 1.1;
  PainChance 100;
  Monster;
  +FLOORCLIP
  +HARMFRIENDS
  +DOHARMSPECIES
  SeeSound "Sybarite/Sight";
  PainSound "Sybarite/Pain";
  DeathSound "Sybarite/Death";
  ActiveSound "HelmetZombie/Active";
  Tag "Sybarite";
  Obituary "%o was blown apart to satisfy a Sybarite's self-indulgence.";
  Dropitem "Chaingun";
  Species "Chaingunner";
  Bloodtype "PM_NormalTyrantBloodBase";
  TeleFogSourceType "PM_PossessedTeleportFog";
  TeleFogDestType "PM_PossessedTeleportFog";
  }
  States
  {
  Spawn:
    SY_A A 0 NoDelay
    {
     user_numbullets = random(15,25);
    }
  Idle:
    SY_A AA 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    SY_A AABB 3 PM_LookingForPlayer();
    TNT1 A 0 A_JumpIfTargetInLOS("Spotted",150,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	SY_A CCDD 3 PM_LookingForPlayer();
    Goto Look+1;
  Spotted:
    TNT1 A 0 PM_AlertSound();
  See:
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
    SY_A AABB 3 A_Chase();
	TNT1 A 0 A_StartSound("ZombieSoldier/Step",CHAN_6);
	SY_A CCDD 3 A_Chase();
	Loop;
  Missile:
    SY_A E 20 
	{
	 if(random(1,3) == 1)
	  A_StartSound("Sybarite/Sight",10);
	  A_StartSound("PM/ArachnoReload",13,pitch:0.85);
	 A_FaceTarget(360,180);
	 A_SetAngle(angle+random(-5,5));
	 A_SetPitch(pitch+random(-5,5));
	}
	SY_A E 5 A_Jump(90,"GrenadeLaunch");
  AutoCannon:
    TNT1 A 0 { pm_attackloop = random(1,4); }
  ACLoop:
    TNT1 A 0 {
	if(pm_attackloop <= 0)
	 SetStateLabel("MissileEnd");
	if(user_numbullets <= 0)
	 SetStateLabel("NoAmmo");
	}
    SY_A FFFFFF 5 Bright 
	{
	  A_FaceTarget(15,15);
	  A_StartSound("Sybarite/Autocannon",11);
	  A_StartSound("GunnerRifleDistant",12,CHANF_OVERLAP);
	  A_SpawnProjectile("PM_HellionFlare",32,15,frandom(-4.5,4.5),CMF_AIMDIRECTION,pitch+frandom(-3.5,3.5));
	}
	SY_A E 5 { A_StartSound("Sybarite/SlotIn",12); user_numbullets--; pm_attackloop--; }
	Loop;
  GrenadeLaunch:
    TNT1 A 0;
    TNT1 A 0 { pm_attackloop = random(1,3); }
  GLLoop:
    TNT1 A 0 {
	if(pm_attackloop <= 0)
	 SetStateLabel("MissileEnd");
	if(user_numbullets <= 0)
	 SetStateLabel("NoAmmo");
	}
    SY_A F 5 Bright 
	{
	 A_FaceTarget(13,13);
	 A_StartSound("Sybarite/GrenadeLaunch",11);
	 let p = A_SpawnProjectile("PM_SybGrenade",32,15,frandom(-2.5,2.5),CMF_AIMDIRECTION|CMF_OFFSETPITCH);
	 if(p) p.vel = CalcThrowVector(pos, target.pos, 1.35, frandom(65.0,90.0));
	}
	SY_A E 20 { A_StartSound("Sybarite/SlotIn",12); user_numbullets--; pm_attackloop--; }
	Loop;
  MissileEnd:
    SY_A E 10 A_FaceTarget(5,5);
	Goto See;
  NoAmmo:
	SY_A E 7 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A EE 10 A_FaceTarget();
	TNT1 A 0 A_Jump(56,"ZReload");
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A E 8 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A E 8 A_FaceTarget();
	TNT1 A 0 A_StartSound("PM/GunClick",CHAN_5);
	SY_A AAAA 5 A_FaceTarget();
	Goto ZReload;
  ZReload:
     TNT1 A 0 A_StartSound("PM/NailgunMagOut",5);
     SY_A EEEEEEE 5;
     TNT1 A 0 A_SpawnItemEx("PM_EmptyHeavyMag",0,0,20,frandom(-1,1),frandom(-1,-5),random(-4,0));
     SY_A CCCBBBB 6;
     TNT1 A 0 A_StartSound("CivZombie/IEDTakeOut",5);
     SY_A DDDBBB 3;
     TNT1 A 0 A_StartSound("PM/NailgunMagIn",5);
	 TNT1 A 0 { user_numbullets = (user_numbullets + random(5,15)); }
     SY_A AAAABB 4;
     TNT1 A 0 A_StartSound("Sybarite/SlotIn",CHAN_5);
     SY_A CCAA 4;
     Goto See;
  Pain:
    SY_A G 3 A_StopSound(10);
    SY_A G 8 A_Pain;
    Goto See;
  Death:
    TNT1 A 0 A_StopSound(10);
    SY_D A 5;
    SY_D B 5 PM_Scream();
    SY_D C 5 A_NoBlocking;
    SY_D D 5;
	  TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
	  SY_D EFG 5;
	  SY_D G 90;
	  TNT1 A 0 { if(random(1,4) == 1) A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION); }
    SY_D G -1;
    Stop;
  Raise:
    SY_A L 5;
    SY_A KJIH 5;
    Goto Look;
  }
}

Class PM_SybGrenade : PM_ZTrooperFragGrenade
{
 int timer;
 Default
 {
  Radius 10;
  Height 8;
  Speed 24;
  Gravity 1.35;
  SeeSound "";
  BounceSound "Sybarite/GrenadeBounce";
  Scale 1.10;
  BounceFactor 0.15;
  WallBounceFactor 0.35;
 }
 States
 {
 Spawn:
  TNT1 A 0;
  TNT1 A 0 A_SpawnItemEx("PM_SybGrenadeTick",flags:SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
 Go:
  GRNP A 1
  {
	A_SpawnParticleEx( "#d2d2d2", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(20,75),
	size: frandom(5,35), xoff: -5, velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),
	startalphaf: frandom(0.3,0.5), fadestepf: -1, sizestep: frandom(0.0,2.5), startroll: random(-180,180), rollvel: frandom(-1,1));
  }
 Loop;
 Bounce:
  Goto Go;
 Death:
 XDeath:
 Crash:
	TNT1 A 0;
    TNT1 A 0 A_StartSound("Sybarite/GrenadeBounce");
  Land:
	GRNP AA 1 Light("SatyrLordRedMissile9")
	{
	if(timer >= 500)
	SetStateLabel("Fade");
	timer++;
	A_SpawnParticleEx( "#d2d2d2", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(20,75),
	size: frandom(5,35), xoff: -5, velx: frandom(-0.2,0.2), vely: frandom(-0.4,0.4), velz: frandom(0.5,2),
	startalphaf: frandom(0.3,0.5), fadestepf: -1, sizestep: frandom(0.0,2.5), startroll: random(-180,180), rollvel: frandom(-1,1));
	}
	Loop;
  Fade:
    GRNP A 2 A_FadeOut(0.05);
	Loop;
 }
}

Class PM_SybGrenadeTick : Actor
{
    int wait;
    Default
	{
	Radius 3;
	Height 3;
	Scale 0.75;
	+NOINTERACTION
	+FORCERADIUSDMG
	+DONTTHRUST
	+NOBLOOD
	DamageType "Fire";
	Species "Chaingunner";
	OBITUARY "%o was caught in the blast zone of a Sybarite's grenade.";
	}
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 wait = 4;
	 tex[0] = TexMan.CheckForTexture("SPKOB0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[3];
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	Go:
	    TNT1 A 0 { if(wait <= 0) SetStateLabel("Explode"); } 
		TNT1 AAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		TNT1 A 0  {
		A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
		A_StartSound("CivZombie/IEDBeep",CHAN_5,pitch:1.15);
		A_SpawnItemEx("PM_FragGrenadePing",0,0,0, 0,0,0, 0,SXF_SETMASTER|SXF_ORIGINATOR|SXF_NOCHECKPOSITION);
		wait--; }
		TNT1 AAAA 1 Light("SatyrLordRedMissile3") A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
        Loop;
	Explode:
	    TNT1 A 0;
		TNT1 A 1 Light("SatyrLordRedMissile4") A_StartSound("Draugr/RocketLock",10,pitch:1.25);
		TNT1 AAAAAAAAAAAAAAA 1 A_Warp(AAPTR_Target,0,0,2,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	Death:
	    TNT1 A 0 A_StopSound(10);
		TNT1 A 0 A_StartSound("Noble/BombExp",6);
		TNT1 A 0 A_StartSound("Noble/BombExpFar",7);
		TNT1 A 0 {bNoGravity = true;}
		TNT1 A 0 
		{
		A_SpawnParticleEx(
		"ff9d1c", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: 10, size: 800, zoff: 45, startalphaf: 1, fadestepf: -1, sizestep: -20,
		startroll: random(-180,180), rollvel: frandom(-5,5));
		for(int i=2;i>0;i--)
			A_SpawnParticleEx(
			"ff9d1c", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: 10, size: 700, zoff: 10, startalphaf: 1, fadestepf: -1, sizestep: 20,
			startroll: random(-180,180), rollvel: frandom(-5,5));
		for(int i=random(4,8);i>0;i--)
		A_SpawnParticleEx(
			"", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(80,120), size: frandom(1,15), zoff: random(10,60), velx: random(-6,6), vely: random(-6,6),
			velz: random(0,8), accelz: frandom(-0.2,-0.8), startalphaf: 1, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-2,2)
			);
		for(int i=random(5,10);i>0;i--)
		A_SpawnParticleEx
			( "727272", TexMan.CheckForTexture("DUSTC0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(40,70), size: frandom(1,22), zoff: random(10,35),
			velx: frandom(-7,7), vely: frandom(-7,7), velz: frandom(1,12),
			accelz: frandom(-0.1,-0.9), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-4,4));
		}
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-6,6),random(-6,6),random(-1,4),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
		TNT1 A 0 Radius_Quake (2, 24, 0, 15, 0);
		TNT1 A 1;
		TNT1 A 0 A_RemoveMaster(RMVF_EVERYTHING);
		TNT1 A 8 Bright Light("YellowMediumFlicker2") A_Explode(30,244);
		TNT1 A 6 Bright Light("YellowMediumFlicker2");
		TNT1 A 5 Bright Light("YellowMediumFlicker2");
		Stop;
	}
}
