ACTOR PM_MutatedGunner : PMMonsterBase
{
    var int user_numbullets;
	Health 80
	Radius 20
	Height 56
	Speed 8
	PainChance 100
	Mass 400
	+FLOORCLIP
	//+SEEINVISIBLE
	+HARMFRIENDS
    +DOHARMSPECIES
	//+NOINFIGHTING
	Monster
	SeeSound "MutatedGunner/Sight"
	PainSound "MutatedGunner/Pain"
	DeathSound "MutatedGunner/Death"
	ActiveSound "MutatedGunner/Active"
	Tag "Mutated Gunner"
	Dropitem "Chaingun"
	DropItem "Lith_ClipBox"
	Species "Chaingunner"
	Bloodtype "PM_NormalBloodBase"
	Obituary "%o was riddled with holes by a Mutated Gunner."
	States
	{
		Spawn:
            TNT1 A 0 NoDelay
            {
             user_numbullets = random(5,150);
            }
        Idle:
			D3C2 A 10 PM_Look()
			loop
		Look:
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 AABB 3 PM_LookingForPlayer()
            TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP)
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 CCDD 3 PM_LookingForPlayer()
            Goto Look+1
		Spotted:
		    TNT1 A 0 PM_AlertSound()
			Goto See2
		See2:
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 AABB 3 A_Chase
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 CCDD 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump(50,"Spit")
			TNT1 A 0 A_PlaySound("MutatedGunner/WindUp")
			D3C2 E 20 A_FaceTarget
            TNT1 A 0 A_PlaySound("MutatedGunner/PreFire",CHAN_6)
            D3C2 E 1 A_FaceTarget
			Goto Missile1
		Missile1:
			D3C2 EE 1 Light("YellowMediumFlicker1") A_Facetarget
			TNT1 A 0 A_PlaySound("MutatedGunner/Fire")
			TNT1 A 0 A_SpawnItemEx("PM_VentSmoke",20,10,32,1,0,1,0,0,0,0)
			D3C2 F 1 BRIGHT Light("YellowMediumFlicker2") 
			{
            if(user_numbullets < 0)
              { 
               return state("NoAmmo");
              }
			A_PlaySound("GunnerRifleDistant", CHAN_7);
			A_CustomMissile("PM_BulletTracer",32,10, random(8,-8),CMF_OFFSETPITCH,random(-2,2));
			A_CustomMissile("BulletFlare", 32, 10, 0, 0);
            A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
            user_numbullets--;
            return state(0);
			}
			D3C2 E 1 A_MonsterRefire(130,"CoolDown")
			//TNT1 A 0 A_CheckSight("SuppressionCheck")
			TNT1 A 0 A_Jump(2,"CoolDown")
			Goto Missile1+1

		CoolDown:
			D3C2 E 10
			TNT1 A 0 A_PlaySound("MutatedGunner/WindDown")
			D3C2 E 10
			TNT1 A 0 A_PlaySound("GenericSteam")
			D3C2 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_SpawnItemEx("PM_VentSmoke",20,10,32,random(2,-2),random(2,-2),random(1,3),0,0,0,0)
			Goto See2
		SuppressionCheck:
			TNT1 A 0 A_Jump(200,"Suppression")
			Goto See2
		Suppression:
			TNT1 A 0 A_PlaySound("Tentaclecommando/sight",CHAN_6)
		SuppressionStart:
			D3C2 EE 1 Light("YellowMediumFlicker1") A_FaceTarget(0.0000001,3,0,0)
			TNT1 A 0 A_PlaySound("MutatedGunner/GunLoop")
			TNT1 A 0 A_SpawnItemEx("PM_VentSmoke",20,10,32,1,0,1,0,0,0,0)
			D3C2 F 1 BRIGHT Light("YellowMediumFlicker2") 
			{
            if(user_numbullets < 0)
              { 
               return state("NoAmmo");
              }
			A_PlaySound("GunnerRifleDistant", CHAN_7);
			A_CustomMissile("PM_BulletTracer",32,10,random(-6, 6),CMF_OFFSETPITCH|CMF_AIMDIRECTION,random(pitch*-1-2,pitch*-1+2));
			A_CustomMissile("BulletFlare", 32, 10, 0, 0);
            A_SpawnItemEx("PM_BulletCasing",-10,0,32,0,frandom(5,15),frandom(5,8),0,SXF_NOCHECKPOSITION);
            user_numbullets--;
            return state(0);
			}
			TNT1 A 0 A_Jump(3,"CoolDown")
			TNT1 A 0 A_CheckSight("SuppressionStart")
			Goto Missile+3
		Spit:
			TNT1 A 0 A_PlaySound("Afflicted/Puke")
			D3C2 G 20 A_FaceTarget
			D3C2 GGGGGGGGGGGGGGGGG 1
			{
			A_FaceTarget;
			A_CustomMissile("HungerBloodSpray",48,0,random(14,-14),CMF_AIMDIRECTION|CMF_OFFSETPITCH,random(13,-4));
			}
			D3C2 E 10
			{
			A_FaceTarget;
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			}
			D3C2 A 10 A_FaceTarget
			Goto See2
       NoAmmo:
         TNT1 A 0 A_PlaySound("MutatedGunner/WindDown",CHAN_6)
		 D3C2 E 10
	     TNT1 A 0 A_PlaySound("GenericSteam")
		 D3C2 EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 1 A_SpawnItemEx("PM_VentSmoke",20,10,32,random(2,-2),random(2,-2),random(1,3),0,0,0,0)
	     D3C2 E 7 A_FaceTarget
	     TNT1 A 0 A_PlaySound("PM/GunClick",CHAN_5)
	     D3C2 EE 10 A_FaceTarget
	     TNT1 A 0 A_Jump(56,"ZReload")
	     TNT1 A 0 A_PlaySound("PM/GunClick",CHAN_5)
	     D3C2 E 8 A_FaceTarget
	     TNT1 A 0 A_PlaySound("PM/GunClick",CHAN_5)
	     D3C2 E 8 A_FaceTarget
	     TNT1 A 0 A_PlaySound("PM/GunClick",CHAN_5)
	     D3C2 AAAA 5 A_FaceTarget
	     Goto ZReload
       ZReload:
         TNT1 A 0 A_PlaySound("PM/HeavyRifleOpen",CHAN_5)
         D3C2 AAAABBBB 5 A_FaceTarget
         TNT1 A 0 A_PlaySound("PM/GatlingBeltCram",CHAN_5)
         D3C2 EEEE 5 A_FaceTarget
         TNT1 A 0 A_PlaySound("PM/GatlingBeltAlign",CHAN_5)
         D3C2 CCCBBBBB 6 A_FaceTarget
         TNT1 A 0 A_PlaySound("PM/HeavyRifleClose",CHAN_5)
         TNT1 A 0 A_SetUserVar(user_numbullets,150)
         D3C2 DDDDDBBBBB 3 A_FaceTarget
         TNT1 A 0 A_PlaySound("PM/GatlingReady",CHAN_5)
         D3C2 AAAABB 4 A_FaceTarget
         Goto See2
		Melee:
			D3C2 A 1 A_FaceTarget
			D3C2 A 6 A_PlaySound("Doom4/Other/MeleeSwing",CHAN_5)
			D3C2 E 6 A_FaceTarget
			D3C2 V 6 A_CustomMeleeAttack(random(1, 5) * 10, "Doom4/Other/MeleeHit")
			Goto See2
		Pain:
			D3C2 G 3
			D3C2 G 3 A_Pain
		Goto See2
		Death:
		    TNT1 A 0 A_StopSounds(0,0)
			TNT1 A 0 A_Jump(35,"PossDeath")
			D3C2 H 5 
			D3C2 I 5 A_Scream
			D3C2 J 5 A_NoBlocking
            TNT1 A 0 A_PlaySound("Corpse/FallMetal",CHAN_6)
			D3C2 KLM 5
			D3C2 N -1
			Stop
		PossDeath:
		    TNT1 A 0 A_StopSounds(0,0)
			D3C2 H 5
			D3C2 I 5 A_Scream
			D3C2 J 5 A_NoBlocking
            TNT1 A 0 A_PlaySound("Corpse/FallMetal",CHAN_6)
			D3C2 KLM 5
			D3C2 N 90
			TNT1 A 0 A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION)
			D3C2 N -1
			Stop
		XDeath:
			D3C2 O 5 
			D3C2 P 5 A_XScream
			D3C2 Q 5 A_NoBlocking
			D3C2 RST 5
			D3C2 U -1
			Stop
		Raise:
			D3C2 N 5
			D3C2 MLKJIH 5
			Goto Look
	}
}

Actor BulletFlare
{
+MISSILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
Renderstyle Add
Radius 1
Height 1
Speed 12
Alpha 0.9
Scale 0.4
states
	{
	Spawn:
		LEYS O 3 bright
		stop
	}
}

Actor BulletSpark
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
Renderstyle Add
Radius 1
Height 1
Alpha 1
Scale 0.6
states
	{
	Spawn:
		TNT1 A 0 A_SetRoll(random(0,360))
		SPKO O 5 bright
		stop
	}
}

Actor SparkOne
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
RenderStyle Add
Radius 1
Height 1
Scale 0.7
states
{
	Spawn:
		FX57 ABCDEFGHIJ 1 bright
		stop
	}
}

Actor SparkTwo
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
RenderStyle Add
Radius 1
Height 1
Scale 0.7
states
{
	Spawn:
		FX57 KLMNOPQRST 1 bright
		stop
	}
}

Actor PM_VentSmoke
{ 
    Scale 0.80
    Alpha 0.50
	Speed 5
    RenderStyle Add
    +NOCLIP
    +NOGRAVITY
    States
    {
    Spawn:
      X111 ABCDEFGHIJKLMNOPQ 1
      Stop
    }
}