ACTOR PM_MutatedGunner
{
	Health 80
	Radius 20
	Height 56
	Speed 8
	PainChance 100
	Mass 400
	+FLOORCLIP
	//+SEEINVISIBLE
	+DONTHARMSPECIES
	//+NOINFIGHTING
	Monster
	SeeSound ""
	PainSound "MutatedGunner/Pain"
	DeathSound "MutatedGunner/Death"
	ActiveSound "MutatedGunner/Active"
	Tag "Mutated Gunner"
	Dropitem "Chaingun"
	DropItem "Lith_ClipBox"
	Species "Chaingunner"
	Obituary "%o was riddled with holes by a Mutated Gunner."
	States
	{
		Spawn:
			D3C2 A 10 A_Look
			loop
		See:
		    TNT1 A 0 A_CheckSight("See")
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 AABB 3 A_Chase("","",CHF_NOPLAYACTIVE)
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 CCDD 3 A_Chase("","",CHF_NOPLAYACTIVE)
			Goto Spotted
		Spotted:
		    TNT1 A 0 A_Jump(256,"Roar","See2")
		Roar:
		    TNT1 A 0 A_PlaySound("MutatedGunner/Sight",CHAN_5)
			Goto See2
		See2:
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 AABB 3 A_Chase
			TNT1 A 0 A_PlaySound("ZombieSoldier/Step",CHAN_6)
			D3C2 CCDD 3 A_Chase
			Loop
		Missile:
			TNT1 A 0 A_Jump(50,"Spit")
			TNT1 A 0 A_PlaySound("MutatedGunner/WindUp")
			D3C2 E 20 A_FaceTarget
			Goto Missile1
		Missile1:
			D3C2 EE 1 Light("YellowMediumFlicker1") A_Facetarget
			TNT1 A 0 A_PlaySound("MutatedGunner/GunLoop")
			TNT1 A 0 A_SpawnItemEx("PM_VentSmoke",20,10,32,1,0,1,0,0,0,0)
			D3C2 F 1 BRIGHT Light("YellowMediumFlicker2") 
			{
			A_PlaySound("GunnerRifleDistant", CHAN_7);
			A_CustomMissile("PM_BulletTracer",32,10, random(8,-8),CMF_OFFSETPITCH,random(-2,2));
			A_CustomMissile("BulletFlare", 32, 10, 0, 0);
			}
			D3C2 E 1 A_MonsterRefire(130,"CoolDown")
			//TNT1 A 0 A_CheckSight("SuppressionCheck")
			TNT1 A 0 A_Jump(2,"CoolDown")
			Goto Missile1+1
		CoolDown:
			D3C2 E 10
			TNT1 A 0 A_PlaySound("MutatedGunner/WindDown")
			D3C2 E 10
			TNT1 A 0 A_PlaySound("GenericSteam")
			D3C2 GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_SpawnItemEx("PM_VentSmoke",20,10,32,random(2,-2),random(2,-2),random(1,3),0,0,0,0)
			Goto See2
		SuppressionCheck:
			TNT1 A 0 A_Jump(200,"Suppression")
			Goto See2
		Suppression:
			TNT1 A 0 A_PlaySound("Tentaclecommando/sight",CHAN_6)
		SuppressionStart:
			D3C2 EE 1 Light("YellowMediumFlicker1") A_FaceTarget(0.0000001,3,0,0)
			TNT1 A 0 A_PlaySound("MutatedGunner/GunLoop")
			TNT1 A 0 A_SpawnItemEx("PM_VentSmoke",20,10,32,1,0,1,0,0,0,0)
			D3C2 F 1 BRIGHT Light("YellowMediumFlicker2") 
			{
			A_PlaySound("GunnerRifleDistant", CHAN_7);
			A_CustomMissile("PM_BulletTracer",32,10,random(-6, 6),CMF_OFFSETPITCH|CMF_AIMDIRECTION,random(pitch*-1-2,pitch*-1+2));
			A_CustomMissile("BulletFlare", 32, 10, 0, 0);
			}
			TNT1 A 0 A_Jump(3,"CoolDown")
			TNT1 A 0 A_CheckSight("SuppressionStart")
			Goto Missile+3
		Spit:
			TNT1 A 0 A_PlaySound("Afflicted/Puke")
			D3C2 G 20 A_FaceTarget
			D3C2 GGGGGGGGGGGGGGGGG 1
			{
			A_FaceTarget;
			A_CustomMissile("HungerBloodSpray",48,0,random(14,-14),CMF_AIMDIRECTION|CMF_OFFSETPITCH,random(13,-4));
			}
			D3C2 E 10
			{
			A_FaceTarget;
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			A_CustomMissile("PM_BloodSpit",48,0,random(7,-7),CMF_OFFSETPITCH,random(7,-7));
			}
			D3C2 A 10 A_FaceTarget
			Goto See2
		Melee:
			D3C2 A 1 A_FaceTarget
			D3C2 A 6 A_PlaySound("Doom4/Other/MeleeSwing",CHAN_5)
			D3C2 E 6 A_FaceTarget
			D3C2 V 6 A_CustomMeleeAttack(random(1, 5) * 10, "Doom4/Other/MeleeHit")
			Goto See2
		Pain:
			D3C2 G 3
			D3C2 G 3 A_Pain
		Goto See2
		Death:
			TNT1 A 0 A_Jump(35,"PossDeath")
			D3C2 H 5
			D3C2 I 5 A_Scream
			D3C2 J 5 A_NoBlocking
			D3C2 KLM 5
			TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
			D3C2 N -1
			Stop
		PossDeath:
			D3C2 H 5
			D3C2 I 5 A_Scream
			D3C2 J 5 A_NoBlocking
			D3C2 KLM 5
			TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
			D3C2 N 90
			TNT1 A 0 A_SpawnItemEx("PM_PossessingDemon",0,0,5,0,0,0,random(0,360),SXF_NOCHECKPOSITION)
			D3C2 N -1
			Stop
		XDeath:
			D3C2 O 5 
			D3C2 P 5 A_XScream
			D3C2 Q 5 A_NoBlocking
			D3C2 RST 5
			D3C2 U -1
			Stop
		Raise:
			D3C2 N 5
			D3C2 MLKJIH 5
			Goto See
	}
}

Actor BulletFlare
{
+MISSILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
Renderstyle Add
Radius 1
Height 1
Speed 12
Alpha 0.9
Scale 0.4
states
	{
	Spawn:
		LEYS O 3 bright
		stop
	}
}

Actor BulletSpark
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
Renderstyle Add
Radius 1
Height 1
Alpha 1
Scale 0.6
states
	{
	Spawn:
		TNT1 A 0 A_SetRoll(random(0,360))
		SPKO O 5 bright
		stop
	}
}


Actor PM_BulletTracer
{
Projectile
+BLOODSPLATTER 
+THRUGHOST
height 2
radius 2
speed 36
damage (random(5,15))
scale 0.85
RenderStyle Add
Alpha 1.0
States
{
	Spawn:
		//TNT1 A 0 A_SpawnItemEX("BulletFlare",0,0,0)
	//Go:
		TRCR A 1 BRIGHT A_Quake(1,4,0,100,0)
        Loop
	Death:
		TNT1 A 0 A_Jump(256,"Death1","Death2")
  Death1:
	TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TRCR A 0 A_PlaySound("PM/BulletHit",2)
	TNT1 A 0 A_SpawnItemEx("PM_BulletHitDust",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,170)
    stop
  Death2:
	TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
	TRCR A 0 A_PlaySound("PM/BulletHit",2)
    TNT1 AAAAAA 0 A_SpawnItemEx("PM_BulletShrapnel",0,0,0,random(-10,10),random(-10,10),random(-10,10),random(-180,180),SXF_NOCHECKPOSITION,170)
	TNT1 A 0 A_SpawnItemEx("PM_BulletHitFog",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,150)
    stop
  XDeath:
     TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
     TRCR A 0 A_PlaySound("PM/BulletHit",2)
     stop
	}
}

Actor SparkOne
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
RenderStyle Add
Radius 1
Height 1
Scale 0.7
states
{
	Spawn:
		FX57 ABCDEFGHIJ 1 bright
		stop
	}
}

Actor SparkTwo
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
RenderStyle Add
Radius 1
Height 1
Scale 0.7
states
{
	Spawn:
		FX57 KLMNOPQRST 1 bright
		stop
	}
}

Actor PM_VentSmoke
{ 
    Scale 0.80
    Alpha 0.50
	Speed 5
    RenderStyle Add
    +NOCLIP
    +NOGRAVITY
    States
    {
    Spawn:
      SMOK ABCDEFGHIJKLMNOPQ 1
      Stop
    }
}