ACTOR PM_CyberSpectre : PMMonsterBase
{
	Health 160
	Radius 30
	Height 56
	Speed 12
	PainChance 100
	Mass 400
	+FLOORCLIP
	+DONTHURTSPECIES
	+DONTHARMSPECIES
	+SLIDESONWALLS
	Species "Brute"
	Monster
	MeleeDamage 5
	MeleeThreshold 240
	MeleeRange 80
	MaxStepHeight 32
	MaxDropOffHeight 32
	Speed 13
	Bloodtype "PM_NormalBloodBase"
	SeeSound "CBrute/Sight"
	DeathSound "PBrute/Death"
	ActiveSound "CBrute/Active"
	PainSound "Brute/Pain"
	Tag "Cyber Spectre"
	Obituary "%o was torn to pieces by a starving Cyber Spectre."
	TeleFogSourceType "PM_SmallDemonicTeleportFog"
	TeleFogDestType "PM_DemonicTeleportFog"
	States
	{
		Spawn:
			CYSP A 4 PM_Look()
			loop
		Look:
			TNT1 A 0 A_SetTranslucent(0.1)
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
			CYSP AA 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CYSP BB 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_JumpIfTargetInLOS("Spotted",180,JLOSF_DEADNOJUMP)
			TNT1 A 0 A_Recoil(-0.5)
			CYSP CC 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CYSP DD 2 Fast PM_LookingForPlayer()
			TNT1 A 0 A_Recoil(-0.5)
			Loop
		Spotted:
		    TNT1 A 0 A_Jump(53,"RoarNoAnim")
			Goto See2
		RoarNoAnim:
			TNT1 A 0 PM_AlertSound()
		See2:
			TNT1 A 0 A_SetTranslucent(0.1)
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
			CYSP AA 2 Fast A_Chase
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CYSP BB 2 Fast A_Chase
			TNT1 A 0 A_Recoil(-0.5)
			CYSP CC 2 Fast A_Chase
			TNT1 A 0 A_PlaySound("DTechWalk", 3)
			TNT1 A 0 A_Recoil(-0.5)
			CYSP DD 2 Fast A_Chase
			TNT1 A 0 A_Recoil(-0.5)
			TNT1 A 0 A_JumpIfCloser(230,"See2")
			TNT1 A 0 A_JumpIfCloser(340,"Lunge")
			loop
		Lunge:
		    TNT1 A 0
			TNT1 A 0 A_CheckSight("See2")
			TNT1 A 0 A_JumpIfCloser(350,1)
			Goto See2
			CYSP H 14
			{
			A_SetTranslucent(1);
			A_PlaySound("CBrute/Hyd",2);
			A_FaceTarget;
			}
			CYSP E 3 A_FaceTarget
			TNT1 A 0 A_CheckSight("See2")
			TNT1 A 0 A_ChangeFlag("NODROPOFF",True)
			TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
			TNT1 A 0 ThrustThingZ(0,35,0,1)
			CYSP F 5
			{
			A_ChangeFlag("NOPAIN", true);
			A_Recoil(-20);
			A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION);
			A_PlaySound("CBrute/Jump");
			}
			Goto JumpLoop
		JumpLoop:
			TNT1 A 0 A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION)
			CYSP F 2 A_CheckFloor("JumpLand")
			Loop
		JumpLand:
			TNT1 A 0 A_ChangeFlag("NOPAIN", false)
			CYSP G 2
			TNT1 A 0 A_SetTranslucent(0.5)
			TNT1 A 0 A_Quake(1,8,0,500,0)
			CYSP H 10 A_PlaySound("CBrute/Land",3)
			TNT1 A 0 A_ChangeFlag("NODROPOFF",False)
			TNT1 A 0 A_SetTranslucent(0.1)
			Goto See2
		Melee:
			TNT1 A 0 A_SetTranslucent(0.5)
			CYSP E 2 A_FaceTarget
			CYSP E 3
			TNT1 A 0 A_SetTranslucent(1)
			TNT1 A 0 A_PlaySound("GenericPinkyBite")
			CYSP E 3
			TNT1 A 0 A_Recoil(-10)
			TNT1 A 0 ThrustThingZ(0,16,0,1)
			CYSP F 6
			CYSP G 6 A_CustomMissile("CBruteJump",18,0,0,CMF_AIMDIRECTION)
			TNT1 A 0 A_SetTranslucent(0.5)
			CYSP G 8
			TNT1 A 0 A_SetTranslucent(0.1)
			Goto See2
		Pain:
			TNT1 A 0 A_SetTranslucent(0.5)
			CYSP H 4 A_Pain
			TNT1 A 0 A_ChangeFlag("NODROPOFF",False)
			TNT1 A 0 A_SetTranslucent(0.1)
			Goto See2
		Death:
		    TNT1 A 0 A_StopSounds(0,0)
			TNT1 A 0 A_SetTranslucent(1)
			CYSP I 6 A_Scream
			CYSP J 6 A_NoBlocking
			CYSP KLM 6
			TNT1 A 0 A_PlaySound("Corpse/Fall",CHAN_6)
			CYSP N -1
			Stop
		Raise:
			CYSP NMLKJIA 3
		Goto Look
	}
}