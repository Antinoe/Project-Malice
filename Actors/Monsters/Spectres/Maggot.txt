Class PM_Maggot : PMLustMonsterBase
{
    bool spooked;
	Default
	{
	PMMonsterBase.CodexLore
	"Maggots are demons belonging to the same line as Zephyrs, having adapted within the circle of Lust. Just like Zephyrs, they like to confuse and distract their victims before lunging in for a kill, or worse.
	 Their lack of fire magic is made up for the fact that they have the ability to cloak, however how they're able to is still unknown to mortals. It seems the extent of humanity's sin continues to influence the demons invading.
	 
	 Employs hit and run tactics, but is a bit more predictable than a Zephyr.";
	Health 110;
	Radius 20;
	Height 56;
	Speed 22;
	PainChance 145;
	Mass 100;
	MeleeRange 60;
	MaxTargetRange 1400;
	Scale 1.1;
	Gravity 1;
	+FLOORCLIP
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+ROLLSPRITE
	Species "Brute";
	Monster;
	Bloodcolor "Red";
	Bloodtype "PM_NormalBloodBase";
	MeleeDamage 4;
	SeeSound "Maggot/Sight";
	DeathSound "Maggot/Death";
	ActiveSound "Maggot/Active";
	PainSound "Maggot/Pain";
	Tag "Maggot";
	Obituary "%o was struck down by a Maggot %p didn't see coming.";
	}
	
	void PM_MaggotChase()
	{
	 if(bFRIGHTENED)
	  bFRIGHTENED = false;
	 if(alpha <= 1)
	  A_FadeIn(0.1,0);
	 if(target && CheckSight(target) && CheckIfInTargetLOS(10,JLOSF_DEADNOJUMP,765,0) && random(1,30) == 1)
	  SetStateLabel("Evade");
	 A_Chase();
	}
	
	void PM_MaggotRetreat() //[NERU] THE JOESTAR'S SECRET TECHNIQUE
	{
	 if(alpha <= 1)
	  A_FadeIn(0.1,0);
	 if(target && CheckSight(target) && CheckIfInTargetLOS(30,JLOSF_DEADNOJUMP,1200,0) && random(1,20) == 1)
	  A_ChangeVelocity(0,randompick(-18,18),3,CVF_RELATIVE|CVF_REPLACE);
	 A_Chase(null,null);
	}
	
	void PM_MaggotPhase()
	{
	 if(target && Distance3D(target) > 1000)
	  A_Chase(null,null,CHF_NOPLAYACTIVE);
	 else
	  {
	   if(random(1,50) == 1 && target && Distance3D(target) < 700)
	   SetStateLabel("Missile");
	   A_Wander();
	  }
	}
	
	States
	{
		Spawn:
			MAG_ A 4 PM_Look();
			Loop;
		Look:
			MAG_ AA 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ BB 2 Fast PM_LookingForPlayer();
            TNT1 A 0 A_JumpifTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
			MAG_ DD 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ CC 2 Fast PM_LookingForPlayer();
			Loop;
		Spotted:
		    TNT1 A 0 A_Jump(65,"See");
		Roar:
			TNT1 A 0 A_Jump(100,"Howl","Phase");
		    TNT1 A 0 PM_AlertSound();
			Goto See;
		Howl:
			TNT1 A 0 A_JumpIfCloser(400,"Dodge");
			MAG_ EEEEE 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Maggot/Howl",34,volume:0.7,attenuation:0.5,pitch:frandom(0.9,1.1));
			MAG_ FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEEEEEEEEEE 1 A_FaceTarget();
			TNT1 A 0 A_Jump(90,"Phase");
			Goto See;
		See:
			MAG_ AA 2 Fast PM_MaggotChase();
			//TNT1 A 0 A_Jump(50,"Scamper1");
			MAG_ BB 2 Fast PM_MaggotChase();
			TNT1 A 0 A_StartSound("demonstep", 10,volume:0.65,pitch:1.4);
		//SeeBetween:
			MAG_ DD 2 Fast PM_MaggotChase();
			//TNT1 A 0 A_Jump(70,"Scamper2");
			MAG_ CC 2 Fast PM_MaggotChase();
			TNT1 A 0 A_StartSound("demonstep", 10,volume:0.65,pitch:1.4);
			TNT1 A 0 A_SetSpeed(22);
		SeeChoices:
			TNT1 A 0 A_Jump(15,"Dodge","Jump","CrouchSee","CrouchRepos");
			TNT1 A 0 A_Jump(20,"Phase");
			TNT1 A 0 A_Jump(8,"Howl");
			Goto See;
		Scamper1:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0 A_Recoil(-9);
			MAG_ KKLL 2 PM_MaggotChase();
			Goto See;
		Scamper2:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0 A_Recoil(-9);
			MAG_ MMJJ 2 PM_MaggotChase();
			//Goto SeeBetween;
		CrouchRepos:
			TNT1 A 0 A_SetSpeed(26);
			MAG_ JJKKLLMMJJKKLLMM 2 A_Wander();
			TNT1 A 0 A_SetSpeed(22);
			Goto See;
		CrouchSee:
			TNT1 A 0 A_SetSpeed(26);
			MAG_ JJKKLLMMJJKKLLMMJJKKLLMMJJKKLLMM 2 A_Chase();
			TNT1 A 0 A_SetSpeed(22);
			Goto See;
		Dodge:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0
			{
			bNODROPOFF = true;
			ThrustThing(randompick(0,128),random(12,26), 0, 0);
			ThrustThingZ(0,60,0,1);
			}
			MAG_ I 10 A_FaceTarget();
			TNT1 A 0 { bNODROPOFF = false; }
			Goto See;
		Jump:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0
			{
			bNODROPOFF = true;
			A_Recoil(-28);
			ThrustThingZ(0,50,0,1);
			}
			MAG_ I 28 A_FaceTarget();
			MAG_ J 10 A_FaceTarget();
			TNT1 A 0 { bNODROPOFF = false; }
			TNT1 A 0 A_Jump(45,"CrouchSee");
			Goto See;
        Evade:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
		    MAG_ J 15 
			{
			 A_ChangeVelocity(0,randompick(-24,24,-46,46),4,CVF_RELATIVE|CVF_REPLACE);
			 A_FaceTarget();
			}
			Goto See;
		Phase:
			TNT1 A 0 A_StartSound("Maggot/Hiss",32,volume:0.6,attenuation:0.6,pitch:frandom(0.6,1.3));
		    MAG_ J 1 
			{
			 if(alpha < 0)
			 SetStateLabel("PhaseHunt");
			 A_FadeOut(0.1,0);
			}
			Loop;
		PhaseHunt:
		    MAG_ JJKK 1 PM_MaggotPhase();
			TNT1 A 0 A_StartSound("Imp/Step",10,volume:0.35);
			MAG_ LLMM 1 Fast PM_MaggotPhase();
			TNT1 A 0 A_StartSound("Imp/Step",10,volume:0.35);
			TNT1 A 0 A_Jump(10,"See");
			Loop;
		Retreat:
		    TNT1 A 0 { bFRIGHTENED = true; }
			MAG_ MMLL 1 Fast PM_MaggotRetreat();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ KKJJ 1 Fast PM_MaggotRetreat();
			TNT1 A 0 
			{
			  A_StartSound("Imp/Step",10);
			  if(target && !CheckSight(target))
			   { SetStateLabel("See"); }
			  return A_Jump(13,"See");
			}
			Goto Retreat+1;
		Missile:
		    TNT1 A 0
			{
			 if(target && CheckSight(target) && Distance3D(target) < 289)
			  SetStateLabel("Melee");
			}
	    Leap:
			TNT1 A 0 A_StartSound("Maggot/Attack",11);
			MAG_ AAAA 2 { if(alpha < 0)
			A_FadeIn(0.8,0);
			A_FaceTarget(); }
			MAG_ J 10 A_FaceTarget();
			TNT1 A 0 PM_LeapTo(target,1568,"Retreat");
			TNT1 A 0 A_Recoil(-3);
			TNT1 A 0 A_JumpIfCloser(500,"LeapStraight");
			TNT1 A 0 ThrustThingZ(0,12,1,1);
			TNT1 A 0 A_Recoil(-12);
			Goto LeapLoop;
		LeapStraight:
			TNT1 A 0 ThrustThingZ(0,8,1,1);
			TNT1 A 0 A_Recoil(-15);
		LeapLoop:
			MAG_ IIIIIIFFGGGGG 1 
			{ 
			 if(random(1,15) == 1)
		       { ThrustThing(random(0,360)*256/360,2,0,0); }
			 A_SpawnProjectile("PM_FastMeleeHitbox",20,0,0,2);
			 A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE);
			}
			TNT1 A 0 A_CheckFloor("LeapEnd");
			Goto LeapLoop+9;
		LeapEnd:
			MAG_ G 5;
			TNT1 A 0 A_Jump(56,"Retreat");
			TNT1 A 0 A_Jump(35,"Phase");
			Goto See;
		Melee:
		    TNT1 A 0 
			{
			 if(alpha < 0)
			  A_FadeIn(0.5,0);
			}
			MAG_ EEEEE 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("FeralImp/Swing",12,CHANF_OVERLAP);
			MAG_ F 2 { ThrustThingZ(0,13,0,0); A_Recoil(-15); A_FaceTarget(); }
			MAG_ GGG 1 A_SpawnProjectile("PM_MeleeHitbox",5,0,0,2);
			MAG_ G 12 A_FaceTarget();
			TNT1 A 0 A_Jump(67,"Retreat");
		    Goto See;
		Pain:
			MAG_ H 9 A_Pain;
			TNT1 A 0 A_Jump(45,"Retreat");
			Goto See;
		Death:
		    TNT1 A 0 { if(alpha <= 0) A_FadeIn(1.0,0); }
			MAD_ A 8 A_Scream;
			MAD_ B 6 A_NoBlocking;
			MAD_ CD 6;
			TNT1 A 0 A_StartSound("Corpse/Fall",6);
			MAD_ E 6;
			MAD_ F -1;
			Stop;
		Raise:
			MAD_ FECDBA 3;
			Goto Look;
	}
}