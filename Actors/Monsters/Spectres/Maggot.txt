Class PM_Maggot : PMLustMonsterBase
{
    bool spooked;
	Default
	{
	PMMonsterBase.CodexLore
	"Maggots are demons belonging to the same line as Zephyrs, having adapted within the circle of Lust. Just like Zephyrs, they like to confuse and distract their victims before lunging in for a kill, or worse.
	 Their lack of fire magic is made up for the fact that they have the ability to cloak, however how they're able to is still unknown to mortals. It seems the extent of humanity's sin continues to influence the demons invading.
	 
	 Employs hit and run tactics, but is a bit more predictable than a Zephyr.";
	Health 100;
	Radius 20;
	Height 56;
	Speed 19;
	PainChance 145;
	Mass 100;
	MeleeRange 60;
	MaxTargetRange 700;
	Scale 1.1;
	Gravity 1;
	+FLOORCLIP
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+ROLLSPRITE
	+SLIDESONWALLS
	+ROLLCENTER
	Species "Brute";
	Monster;
	Bloodcolor "Red";
	Bloodtype "PM_NormalBloodBase";
	MeleeDamage 4;
	DamageType "Bite";
	SeeSound "Maggot/Sight";
	DeathSound "Maggot/Death";
	ActiveSound "Maggot/Active";
	PainSound "Maggot/Pain";
	Tag "Maggot";
	Obituary "%o was struck down by a Maggot %p didn't see coming.";
	}
	
	void PM_MaggotChase()
	{
	 if(bFRIGHTENED)
	  bFRIGHTENED = false;
	 if(alpha <= 1)
	  A_FadeIn(0.1,0);
	 if(target && CheckSight(target) && CheckIfInTargetLOS(10,JLOSF_DEADNOJUMP,765,0) && random(1,30) == 1)
	  SetStateLabel("Evade");
	 A_Chase();
	}
	
	void PM_MaggotRetreat() //[NERU] THE JOESTAR'S SECRET TECHNIQUE
	{
	 if(alpha <= 1)
	  A_FadeIn(0.1,0);
	 if(target && CheckSight(target) && CheckIfInTargetLOS(30,JLOSF_DEADNOJUMP,1200,0) && random(1,20) == 1)
	  A_ChangeVelocity(0,randompick(-18,18),3,CVF_RELATIVE|CVF_REPLACE);
	 A_Chase(null,null);
	}
	
	void PM_MaggotPhase()
	{
	 if(target && Distance3D(target) > 1000)
	  A_Chase(null,null,CHF_NOPLAYACTIVE);
	 else
	  {
	   if(random(1,50) == 1 && target && Distance3D(target) < 700)
	   SetStateLabel("PhaseAttack");
	   A_Wander();
	  }
	}
	
	States
	{
		Spawn:
			MAG_ A 4 PM_Look();
			Loop;
		Look:
			MAG_ AA 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ BB 2 Fast PM_LookingForPlayer();
            TNT1 A 0 A_JumpifTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
			MAG_ DD 2 Fast PM_LookingForPlayer();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ CC 2 Fast PM_LookingForPlayer();
			Loop;
		Spotted:
		    TNT1 A 0 A_Jump(65,"See");
		Roar:
			TNT1 A 0 A_Jump(100,"Howl","Phase");
		    TNT1 A 0 PM_AlertSound();
			Goto See;
		Howl:
			TNT1 A 0 A_JumpIfCloser(400,"Dodge");
			MAG_ EEEEE 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("Maggot/Howl",34,volume:0.7,attenuation:0.5,pitch:frandom(0.9,1.1));
			MAG_ FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEEEEEEEEEE 1 A_FaceTarget();
			TNT1 A 0 A_Jump(90,"Phase");
			Goto See;
		See:
			TNT1 A 0 { bNOGRAVITY = false; }
			MAG_ AAA 2 Fast PM_MaggotChase();
			//TNT1 A 0 A_Jump(50,"Scamper1");
			MAG_ BBB 2 Fast PM_MaggotChase();
			TNT1 A 0 A_StartSound("demonstep", 10,volume:0.65,pitch:1.4);
		//SeeBetween:
			MAG_ DDD 2 Fast PM_MaggotChase();
			//TNT1 A 0 A_Jump(70,"Scamper2");
			MAG_ CCC 2 Fast PM_MaggotChase();
			TNT1 A 0 A_StartSound("demonstep", 10,volume:0.65,pitch:1.4);
			TNT1 A 0 A_SetSpeed(19);
		SeeChoices:
			TNT1 A 0 A_Jump(40,"Dodge","Jump","CrouchSee","CrouchRepos");
			TNT1 A 0 A_Jump(30,"Phase");
			TNT1 A 0 A_Jump(7,"Howl");
			TNT1 A 0 A_Jump(30,"StartFly");
			Goto See;
		StartFly:
			TNT1 A 0 PM_AlertSound();
			MAG_ J 10 A_FaceTarget();
			TNT1 A 0 { bNOGRAVITY = true; }
			TNT1 A 0 ThrustThingZ(0,70,0,1);
			TNT1 A 0 A_StartSound("Harvester/PhaseStart",30,attenuation:0.6);
			MAG_ EEEEEFFFFFGGGGG 1
			{
			A_SpawnParticleEx("320044",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(2,10),size: frandom(20,70),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 0.7
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE);
			}
		FlyUp:
			TNT1 A 0 ThrustThingZ(0,random(0,45),1,1);
			TNT1 A 0 A_StartSound("Harvester/PhaseLoop",31,CHANF_LOOPING,volume:0.8,attenuation: 0.6,pitch:0.9);
		Flying:
			TNT1 A 1
			{
			 A_SpawnParticleEx("320044",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(2,10),size: frandom(55,110),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			 A_SpawnParticleEx("000000",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,30),size: frandom(50,100),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			}
			TNT1 A 0 A_Jump(3,"FlyingChaseLoop");
			TNT1 A 0 A_Jump(10,"FlyingVelocityChange");
			TNT1 A 0 A_Jump(1,"FlyingLand");
			TNT1 A 0 A_CheckFloor("FlyingLand");
			Loop;
		FlyingChaseLoop:
			TNT1 A 0 A_CheckFloor("FlyingBounce");
			TNT1 A 1
			{
		     A_Recoil(-1);
			 A_FaceTarget();
			 A_SpawnParticleEx("320044",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(2,10),size: frandom(55,110),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			 A_SpawnParticleEx("000000",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,30),size: frandom(50,100),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			}
			TNT1 A 0 A_JumpIfCloser(300,"FlyingLand");
			TNT1 A 0 A_Jump(1,"Flying");
			TNT1 A 0 A_Jump(1,"FlyingLand");
			TNT1 A 0 A_JumpIfHigherOrLower("FlyingRaiseAlt","FlyingLowerAlt");
			Loop;
		FlyingBounce:
			TNT1 A 0 A_Jump(120,"FlyingLand");
			TNT1 A 0 ThrustThingZ(0,25,0,1);
			Goto Flying;
		FlyingLowerAlt:
			TNT1 A 0 ThrustThingZ(0,1,1,1);
			Goto FlyingChaseLoop;
		FlyingRaiseAlt:
			TNT1 A 0 ThrustThingZ(0,1,0,1);
			Goto FlyingChaseLoop;
		FlyingVelocityChange:
			TNT1 A 0 A_ChangeVelocity(random(-15,15),random(-15,15),random(-2,2));
			TNT1 A 0 ThrustThingZ(0,random(0,15),random(0,1),1);
			Goto Flying;
		FlyingLand:
			TNT1 A 0 A_ChangeVelocity(random(-5,5),random(-5,5),0);
			TNT1 A 0 ThrustThingZ(0,70,1,1);
		FlyingLandLoop:
			TNT1 A 0 { bNOGRAVITY = false; }
			TNT1 A 0 A_CheckFloor("FlyingLanded");
			TNT1 A 1
			{
			A_SpawnParticleEx("320044",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(2,10),size: frandom(55,110),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			 A_SpawnParticleEx("000000",TexMan.CheckForTexture("GNSMK0"),style: STYLE_Normal,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(10,30),size: frandom(50,100),xoff: frandom(-8,8),yoff: frandom(-8,8),zoff: frandom(0,16),velx: frandom(-1,1),vely: frandom(-1,1),velz: frandom(-1,1),startalphaf: 1
			,fadestepf: -1,sizestep: frandom(1,4.4),startroll: random(-180,180));
			}
			TNT1 A 0 A_Jump(1,"FlyingLanded");
			Loop;
		FlyingLanded:
			TNT1 A 0 { bNOGRAVITY = false; }
			TNT1 A 0 A_StopSound(31);
			TNT1 A 0 A_StartSound("Harvester/PhaseEnd",30,attenuation:0.6);
			MAG_ J 30 A_FaceTarget();
			TNT1 A 0 A_Jump(130,"Phase","CrouchSee","CrouchRepos");
			Goto See;
		Scamper1:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0 A_Recoil(-9);
			MAG_ KKLL 2 PM_MaggotChase();
			Goto See;
		Scamper2:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0 A_Recoil(-9);
			MAG_ MMJJ 2 PM_MaggotChase();
			//Goto SeeBetween;
		CrouchRepos:
			TNT1 A 0 A_SetSpeed(26);
			MAG_ JJKKLLMMJJKKLLMM random(1,2) A_Wander();
			TNT1 A 0 A_SetSpeed(19);
			Goto See;
		CrouchSee:
			TNT1 A 0 A_SetSpeed(26);
			MAG_ JJKKLLMMJJKKLLMMJJKKLLMMJJKKLLMM random(1,2) A_Chase();
			TNT1 A 0 A_SetSpeed(19);
			Goto See;
		Dodge:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0
			{
			bNODROPOFF = true;
			ThrustThing(randompick(0,128),random(12,26), 0, 0);
			ThrustThingZ(0,60,0,1);
			}
			MAG_ I 10 A_FaceTarget();
			TNT1 A 0 { bNODROPOFF = false; }
			Goto See;
		Jump:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
			TNT1 A 0
			{
			bNODROPOFF = true;
			A_Recoil(-28);
			ThrustThingZ(0,50,0,1);
			}
			MAG_ I 28 A_FaceTarget();
			MAG_ J 10 A_FaceTarget();
			TNT1 A 0 { bNODROPOFF = false; }
			TNT1 A 0 A_Jump(45,"CrouchSee");
			Goto See;
        Evade:
			TNT1 A 0 A_StartSound("Maggot/Scamper",33);
		    MAG_ J 15 
			{
			 A_ChangeVelocity(0,randompick(-24,24,-46,46),4,CVF_RELATIVE|CVF_REPLACE);
			 A_FaceTarget();
			}
			Goto See;
		Phase:
			TNT1 A 0 A_StartSound("Maggot/Hiss",32,volume:0.6,attenuation:0.6,pitch:frandom(0.6,1.3));
		    MAG_ J 1 
			{
			 if(alpha < 0)
			 SetStateLabel("PhaseHunt");
			 A_FadeOut(0.1,0);
			}
			Loop;
		PhaseHunt:
		    MAG_ JJKK 2 Fast PM_MaggotPhase();
			TNT1 A 0 A_StartSound("Imp/Step",10,volume:0.35);
			MAG_ LLMM 2 Fast PM_MaggotPhase();
			TNT1 A 0 A_StartSound("Imp/Step",10,volume:0.35);
			TNT1 A 0 A_Jump(10,"See");
			Loop;
		Retreat:
		    TNT1 A 0 { bFRIGHTENED = true; }
			MAG_ MMLL 1 Fast PM_MaggotRetreat();
			TNT1 A 0 A_StartSound("Imp/Step",10);
			MAG_ KKJJ 1 Fast PM_MaggotRetreat();
			TNT1 A 0 
			{
			  A_StartSound("Imp/Step",10);
			  if(target && !CheckSight(target))
			   { SetStateLabel("See"); }
			  return A_Jump(13,"See");
			}
			Goto Retreat+1;
		Missile:
		    TNT1 A 0
			{
			 if(target && CheckSight(target) && Distance3D(target) < 289)
			  SetStateLabel("Melee");
			}
	    Leap:
			TNT1 A 0 A_Jump(128,"See","CrouchRepos","Phase");
			TNT1 A 0 A_StartSound("Maggot/Attack",11);
			MAG_ AAAA 2 { if(alpha < 0)
			A_FadeIn(0.8,0);
			A_FaceTarget(); }
			MAG_ J 10 A_FaceTarget();
			TNT1 A 0 PM_LeapTo(target,1568,"Retreat");
			TNT1 A 0 A_Recoil(-3);
			TNT1 A 0 A_JumpIfCloser(500,"LeapStraight");
			TNT1 A 0 ThrustThingZ(0,25,1,1);
			TNT1 A 0 A_Recoil(-12);
			Goto LeapLoop;
		LeapStraight:
			TNT1 A 0 ThrustThingZ(0,8,1,1);
			TNT1 A 0 A_Recoil(-15);
		LeapLoop:
			MAG_ IIIIIIFFGGGGG 1 
			{ 
			 if(random(1,15) == 1)
		       { ThrustThing(random(0,360)*256/360,2,0,0); }
			 A_SpawnProjectile("PM_FastMeleeHitbox",20,0,0,2);
			 A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE);
			}
			TNT1 A 0 A_CheckFloor("LeapEnd");
			Goto LeapLoop+9;
		LeapEnd:
			MAG_ G 5;
			TNT1 A 0 A_Jump(30,"Retreat");
			TNT1 A 0 A_Jump(20,"Phase");
			Goto See;
		PhaseAttack:
			 TNT1 A 0
			{
			 if(target && CheckSight(target) && Distance3D(target) < 289)
			  SetStateLabel("PhaseMelee");
			}
			//TNT1 A 0 A_StartSound("Maggot/Hiss",32,volume:0.8,pitch:frandom(0.6,1.3));
			MAG_ AAAAAAA 2 { if(alpha < 0)
			A_FadeIn(0.8,0);
			A_FaceTarget(); }
			Goto Leap;
		PhaseMelee:
			//TNT1 A 0 A_StartSound("Maggot/Hiss",32,volume:0.8,pitch:frandom(0.6,1.3));
			MAG_ AAAAAAA 2 { if(alpha < 0)
			A_FadeIn(0.8,0);
			A_FaceTarget(); }
		Melee:
			TNT1 A 0 A_Jump(100,"MeleeSucc");
		    TNT1 A 0 
			{
			 if(alpha < 0)
			  A_FadeIn(0.5,0);
			}
			MAG_ EEEEE 1 A_FaceTarget();
			TNT1 A 0 A_StartSound("FeralImp/Swing",12,CHANF_OVERLAP);
			MAG_ F 2 { ThrustThingZ(0,13,0,0); A_Recoil(-15); A_FaceTarget(); }
			MAG_ GGG 1 A_SpawnProjectile("PM_MeleeHitbox",5,0,0,2);
			MAG_ G 12 A_FaceTarget();
			TNT1 A 0 A_Jump(67,"Retreat");
		    Goto See;
		MeleeSucc:
			TNT1 A 0 A_StartSound("Maggot/Pain",34);
			TNT1 A 0 
			{
			 if(alpha < 0)
			  A_FadeIn(0.5,0);
			}
			MAG_ EEEEE 1 A_FaceTarget();
			TNT1 A 0 A_JumpIfCloser(90,"BloodSucking");
			MAG_ F 10 { ThrustThingZ(0,13,0,0); A_Recoil(-30); A_FaceTarget(); }
			TNT1 A 0 A_JumpIfCloser(90,"BloodSucking");
			MAG_ GGG 1 A_SpawnProjectile("PM_MeleeHitbox",5,0,0,2);
			TNT1 A 0 A_JumpIfCloser(90,"BloodSucking");
			MAG_ G 6 A_FaceTarget();
			TNT1 A 0 A_JumpIfCloser(90,"BloodSucking");
			Goto See;
		BloodSucking:
			MAG_ FFFF 1
			{
			A_FaceTarget();
			A_Recoil(-2);
			}
			TNT1 A 0 A_CustomMeleeAttack(random(2,16)*1,"ZombieFodder/Melee");
			TNT1 A 0 A_StartSound("Maggot/Scamper",35);
			TNT1 A 0 A_StartSound("Maggot/BloodSuck",33,attenuation:0.6);
			TNT1 A 0 A_RadiusGive("PM_MaggotBloodSucked",130,RGF_PLAYERS|RGF_MONSTERS,1);
			MAG_ FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF 1
			{
			A_FaceTarget();
			A_Recoil(-2);
			}
			Goto Retreat;
		Pain:
			TNT1 A 0 { bNOGRAVITY = false; }
			TNT1 A 0 A_StopSound(31);
			MAG_ H 9 A_Pain;
			TNT1 A 0 A_Jump(105,"Retreat");
			Goto See;
		Death:
		    TNT1 A 0 { if(alpha <= 0) A_FadeIn(1.0,0); }
			TNT1 A 0 { bNOGRAVITY = false; }
			TNT1 A 0 A_StopSound(31);
			MAD_ A 8 A_Scream;
			MAD_ B 6 A_NoBlocking;
			MAD_ CD 6;
			TNT1 A 0 A_StartSound("Corpse/Fall",6);
			MAD_ E 6;
			MAD_ F -1;
			Stop;
		Raise:
			MAD_ FECDBA 3;
			Goto Look;
	}
}

Class PM_MaggotBloodSucked : PowerSpeed
{
	Default
	{
		Powerup.Duration -2;
		Speed 0.1;
		Inventory.Icon "";
		Powerup.Color "00 00 00", 0;
		+POWERSPEED.NOTRAIL;
	}
	Override void DoEffect()
	{
		Super.DoEffect();
		if(owner.health > 0 && !IsFrozen())
		{
			if(!random(0, 12))
			{
			owner.A_Explode(5,10);
			}
		}
	}
}