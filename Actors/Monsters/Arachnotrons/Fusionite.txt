Actor PM_Fusionite : Arachnotron
{
	Health 500
	PainChance 100
	Speed 7
	Radius 48
	Scale 1.1
	MaxStepHeight 24
	MaxDropOffHeight 24
	Tag "Fusionite"
	Obituary "%o was liquefied by a Fusionite."
	//DropItem "RagePoint1"
	Species "Arachnotron"
	+DONTHARMSPECIES
	SEESOUND ""
	PAINSOUND "Fusionite/Pain"
	DEATHSOUND "Fusionite/Death"
	ACTIVESOUND "Fusionite/Active"
	//+NOINFIGHTING
	States
{
Spawn:
	FSPI A 0
	Goto Idle
Idle:
    FSPI A 10 A_Look
    Loop
See:
    FSPI A 2 A_PlaySound("GenericArachWalk")
    FSPI ABBCC 2 A_Chase("","Missile",CHF_NOPLAYACTIVE)
    TNT1 A 0 A_JumpIfTargetinLOS("Spotted",270,JLOSF_DEADNOJUMP)
    FSPI D 2 A_PlaySound("GenericArachWalk")
    FSPI DEEFF 2 A_Chase("","",CHF_NOPLAYACTIVE)
    TNT1 A 0 A_CheckSight("See")
    Goto Spotted
Spotted:
    TNT1 A 0 A_PlaySound("Fusionite/Sight",CHAN_5)
See2:
    FSPI A 2 A_PlaySound("GenericArachWalk")
    FSPI ABBCC 2 A_Chase
    FSPI D 2 A_PlaySound("GenericArachWalk")
    FSPI DEEFF 2 A_Chase
    Loop
Missile:
	TNT1 A 0 A_Jump(100,"Shotgun","Rapid")
    FSPI G 12 A_FaceTarget
	FSPI G 3 A_FaceTarget
	TNT1 A 0 A_PlaySound("GenericPlasmaFire")
    FSPI H 3 Light("GreenBall3")
	{
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_FusionShot",22,0);
	}
    FSPI G 1 A_MonsterRefire(10,"See2") //A_SpidRefire
	TNT1 A 0 A_Jump(10,"See2")
    Goto Missile+2
Shotgun:
	FSPI A 9 A_FaceTarget
	TNT1 A 0 A_PlaySound("Fusionite/ScatterCharge")
	FSPI GGGGGG 5 Light("GreenBall2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Fusionite/Scattershot")
    FSPI H 3 Light("GreenBall3")
	{
	A_FaceTarget;
	//A_SpawnItemEx("CSmoke",0,2,20,1,0,1,0,0,0,0);
	//A_SpawnItemEx("CSmoke",0,2,20,1,0,1,0,0,0,0);
	//A_SpawnItemEx("CSmoke",0,2,20,1,0,1,0,0,0,0);
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_GreenFusioniteFlare",22,0);
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-4,4));
	A_CustomMissile("PM_ScatterFusionShot",22,0,random(-7,7),CMF_OFFSETPITCH,random(-6,4));
	}
	FSPI G 3 A_FaceTarget
	TNT1 A 0 A_PlaySound("GenericSteam")
	FSPI GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 //A_SpawnItemEx("CSmoke",19,0,32,random(2,-2),random(2,-2),random(1,3),0,0,0,0)
    Goto See2
Rapid:
	FSPI A 8 
	{
	A_FaceTarget;
	A_PlaySound("Fusionite/SmallCharge");
	}
	FSPI GGG 5 A_FaceTarget
	FSPI G 1 A_FaceTarget
	FSPI H 1 Light("GreenBall4")
	{
	A_CustomMissile("PM_GreenFusioniteFireFlare",22,0);
	A_CustomMissile("PM_SmallFusionShot",22,0,random(-4,4),CMF_OFFSETPITCH,random(-3,3));
	}
	FSPI G 1 A_MonsterRefire(10,"See2") //A_SpidRefire
	Goto Rapid+4
Pain:
	TNT1 A 0
    FSPI I 12 A_Pain
    Goto See2
Death:
	FSPI J 15 A_Scream
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode",random(-10,10),0,random(-10,10))
    FSPI K 4 Light("AvatarFireball4") A_SpawnItemEx("OrangeEmbersSpawn",0,0,30)
	TNT1 A 0 A_PlaySound("ArachChassisExplode",CHAN_5)
	TNT1 A 0 A_SpawnItemEx("OrangeEmbersSpawn",0,0,25)
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode",random(-10,10),0,30)
	TNT1 AAAAA 0 A_SpawnItemEx("PM_ArachnotronGenericExplosionFlames",0,0,35,random(-7,7),random(-7,7),random(0,7))
	//{
	//A_SpawnItemEx("FusioniteExplode",0,0,35);
	//A_SpawnItemEx("FusioniteExplode",random(-10,10),random(-10,10),35);
	//A_CustomMissile("FusioniteExplode",25,0,random(0,360),2,random(0,360));
	//A_CustomMissile("FusioniteExplode",25,0,random(0,360),2,random(0,360));
	//A_NoBlocking;
	//}
	TNT1 A 0 A_NoBlocking
	TNT1 FFFFFFFFFFFFFFFFFFFF 0 A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 32, random (4, 10), random(4,10), random (4, 15), random(0, 360))
	TNT1 A 0 A_SpawnItemEx("PM_ArachExpMist",0,0,20)
	TNT1 A 0 A_PlaySound("ArachExpGore",CHAN_6)
	FSPI L 6 Light("TyrantMExp")
	TNT1 AAAAAA 0 A_CustomMissile("SmokePuff",random(24,40),0,random(0,360),2,random(0,360))
    FSPI MN 6 Light("TyrantMExp") A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 15, random (-3, 3), random(-3,3), random (4, 15))
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode",random(-10,10),0,random(-10,10))
	TNT1 A 0 A_PlaySound("ArachCrash",CHAN_7)
    FSPI O 6 Light("AvatarFireball4") A_SpawnItemEx("HuskSmoke",random(-30,30),random(-30,30),random(0,5),0,0,2)
	FSPI PPPPPPP 3 
	{
	A_SpawnItemEx("HuskSmoke",random(-30,30),random(-30,30),random(0,5),0,0,2);
	A_SpawnItemEX("PM_ArachExpGoreSplash", 0, 0, 15, random (-3, 3), random(-3,3), random (4, 15));
	}
	FSPI P 1 A_KillMaster
    FSPI P -1
    Stop
Raise:
	FSPI ONMLKJ 6
	Goto See
    }
}

Actor FusioniteExplode
{
	Scale 1
	Damage 0
	Missile
	Projectile
	Renderstyle Add
	+CLIENTSIDEONLY
	+NOINTERACTION
	+ROLLSPRITE
	+Bright
	Speed 3
	States
{
Spawn:
	TNT1 A 0 NoDelay A_SetRoll(random(0,360))
	FXPO AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.04)
	Stop
	}
}

ACTOR SmokePuff
{
	Scale 0.7
	Speed 2
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Missile
	Damage 0
	PROJECTILE
	Renderstyle Translucent
	Alpha 0.7
	States
	
{
Spawn:
	SMK3 ABCDEFGHIJKLM 5 A_FadeOut(0.03)
	Stop
	}
}

ACTOR PM_FusionShot
{
  //Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]"
  Radius 13
  Height 8
  Speed 24
  FastSpeed 26
  Damage (random(15,25))
  Scale 1.65
  RENDERSTYLE ADD
  +ROLLSPRITE
  +ROLLCENTER
  SeeSound "GenericPlasmaFire" 
  DeathSound "GenericPlasmaExp"
  PROJECTILE
  States
  {
  Spawn:
    APPL AAABBB 1 Bright Light("GreenBall4")
	{
	A_SetRoll(roll+15);
	A_SpawnItemEx("PM_FusionTrail",16,0,0,0,0,0,0,128,0);
	}
    Loop
  Death:
	TNT1 A 0
	TNT1 A 0 A_SetTranslation("PM_GreenFusionTint")
	TNT1 A 0 A_SetScale(2.5)
	TNT1 A 0 A_SpawnItem("PM_GreenFusioniteFlare")
    FS03 HIJ 6 Bright Light("GreenBall4")
    Stop
  }
}

ACTOR PM_ScatterFusionShot
{
  //Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]"
  Radius 13
  Height 8
  Speed 26
  FastSpeed 28
  Damage (22)
  Scale 2.8
  RENDERSTYLE ADD
  SeeSound "Fusionite/Scattershot" 
  DeathSound "GenericPlasmaExp"
  PROJECTILE
  States
  {
  Spawn:
    APPL AAABBBB 1 Bright Light("GreenBall3")
	{
	A_SpawnItemEx("PM_ScatterFusionTrail",-20,0,0,0,0,0,0,128,0);
	}
    Loop
  Death:
	TNT1 A 0
	TNT1 A 0 A_SetTranslation("PM_GreenFusionTint")
	TNT1 A 0 A_RadiusThrust(6000,90)
	TNT1 A 0 A_SetScale(3.5)
	TNT1 AAA 0 A_CustomMissile("GreenShock",2,0,random(0,360),CMF_OFFSETPITCH,random(0,360))
	TNT1 A 0 A_SpawnItem("PM_GreenFusioniteFlare")
    FS03 HIJ 8 Bright Light("GreenBall3")
    Stop
  }
}

ACTOR PM_SmallFusionShot
{
  Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]"
  Radius 13
  Height 8
  Speed 27
  FastSpeed 26
  Damage (15)
  Scale 0.6  
  RENDERSTYLE ADD
  SeeSound "Fusionite/SmallShot" 
  DeathSound "GenericPlasmaExp"
  PROJECTILE
  States
  {
  Spawn:
    FS03 AB 1 Bright Light("GreenBall5") A_SpawnParticle("80FF50",SPF_FULLBRIGHT|SPF_RELATIVE,14,16,0,random(-22,-5),random(-5,5),random(-5,5),0,0,0,0,0,0,1,-1,-2)
	Loop
  Death:
	TNT1 A 0
	TNT1 A 0 A_SetScale(1.2)
    FS03 HIJ 3 Bright Light("GreenBall5")
    Stop
  }
}

ACTOR PM_FusionTrail
{
Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]"
+Missile
 +NoGravity
 +NoBlockmap
 +NoInteraction
 -Solid
 +Bright
Scale 1.5
States
{
Spawn:
	TNT1 A 0
	TNT1 A 2
	Go:
    FS03 AB 1
	{
	A_FadeOut(0.25);
	A_SetScale(scalex-0.4);
	}
    Loop
    }
}

ACTOR PM_ScatterFusionTrail
{
Translation "0:255=%[0,0,0]:[0.8,2.0,0.28]"
+Missile
 +NoGravity
 +NoBlockmap
 +NoInteraction
 -Solid
 +Bright
 RENDERSTYLE ADD
Scale 0.7
States
{
Spawn:
	TNT1 A 1
    FS03 AB 1 A_FadeOut(0.27)
    Goto Spawn+1
    }
}