
Class PM_HeresyLord : PMUnaffMonsterBase
{
    Default
	{
	PMMonsterBase.CodexLore
	"Heresy Lords are intelligent, aeonic archdemons, their deepest but taciturn level of cruelty seen in their actions. They serve as the commanders of lesser and greater demons, the source of countless plights during the invasion as they revel in sowing the seeds of terror in the field.
	 Their wings have grown vestigial since they aged, but they serve as extremely telling indicators of their authority; both to the demons and the humans.

	 Can fire a triple salvo of red fireballs or a precise white soul orb to strike down a target on the move, and can strengthen both energies for a powerful barrage.";
    Health 1000;
    Radius 22;
    Height 64;
    Speed 9;
	Scale 1.2;
    PainChance 20;
    Mass 1500;
    MONSTER;
	Bloodtype "PM_NormalBloodBase";
    +FLOORCLIP
	//+NOINFIGHTING
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+MISSILEMORE
	Species "Nobles";
	//DropItem "RagePoint1"
    SeeSound "HeresyLord/Sight";
    PainSound "HeresyLord/Pain";
    DeathSound "HeresyLord/Death";
    ActiveSound "HeresyLord/Active";
    MeleeSound "baron/melee";
	Tag "Heresy Lord";
    Obituary "%o fell before the unholy might of a Heresy Lord.";
    MeleeDamage 8;
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	PMMonsterBase.RealHeight 78;
	}
    States
    {
    Spawn:
        LOHS A 10 PM_Look();
        Loop;
    Look:
        LOHS AABB 4 PM_LookingForPlayer();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 A_StartSound("hoofstep",3);
        TNT1 A 0 A_JumpIfTargetinLOS("Spotted",250,JLOSF_DEADNOJUMP);
		LOHS CCDD 4 PM_LookingForPlayer();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 A_StartSound("hoofstep",3);
        Loop;
    Spotted:
        TNT1 A 0 PM_AlertSound();
    See:
	    TNT1 A 0 { bSPRITEFLIP = false; }
        LOHS AABB 4 A_Chase();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 A_StartSound("hoofstep",3);
		LOHS CCDD 4 A_Chase();
		TNT1 A 0 A_Quake(1, 4, 0, 312);
		TNT1 A 0 A_StartSound("hoofstep",3);
        Loop;
    Missile:
	  TNT1 A 0 A_Jump(64,"MDoubleSerie");
	  TNT1 A 0 A_Jump(128,"MRightHand");
    MLeftHand:
	  TNT1 A 0 A_StartSound("GenericChargeHeavy",CHAN_6);
	  LOHS D 7 Light("AzazelSoul") A_FaceTarget();
	  LOHS EEEEEEEEEEEEEE 1 Light("AzazelSoul")
	  {
       A_SpawnItemEX("PM_WhiteFireEffect",7,random(40,35),65,0,0,2);
	   PM_LeadTarget(22);
	  }
	  LOHS F 7 Light("AzazelSoul") PM_LeadTarget(22);
	  TNT1 A 0 A_StartSound("baron/attack");
	  LOHS G 1 Light("AzazelSoul");
	  LOHS G 7 Light("AzazelSoul")
	  {
	  PM_LeadTarget(22);
	  A_SpawnProjectile("PM_HeresyLordWhiteSoul",40,0,0,CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch*1);
	  }
	  TNT1 A 0 A_StopSound(CHAN_6);
	  Goto See;
	MRightHand:
	TNT1 A 0 { bSPRITEFLIP = true; }
	TNT1 A 0 A_StartSound("GenericChargeHeavy",CHAN_6);
    LOHS EEEEEEEEEE 1 Light("ShadowBall2")
	{
	A_SpawnItemEX("PM_HeresyLordRFEffect",7,random(-40,-35),65,0,0,2);
	A_FaceTarget();
	}
	LOHS EEEE 1 A_SpawnItemEX("PM_HeresyLordRFEffect",7,random(-40,-35),65,0,0,2);
	LOHS F 8 Light("ShadowBall2") A_FaceTarget();
    LOHS G 6
		{
      A_SpawnProjectile("PM_HeresyLordRedBall", 38, 0,  8);
      A_SpawnProjectile("PM_HeresyLordRedBall", 38, 0,  0);
      A_SpawnProjectile("PM_HeresyLordRedBall", 38, 0, -8);
		}
	TNT1 A 0 A_StopSound(CHAN_6);
    Goto See;
    MDoubleSerie:
	  TNT1 A 0 { bSPRITEFLIP = true; }
	  TNT1 A 0 A_StartSound("HeresyLord/BarrageRCharge",CHAN_5);
	  TNT1 A 0 A_FaceTarget();
	  LOHS F 5 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual1Fake",10,-20,60,0,0,0,32);
	  LOHS E 5 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual1Fake",10,-9,46,0,0,0,32);
	  LOHS E 6 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual1Fake",10,4,28,0,0,0,32);
	  TNT1 A 0 A_StartSound("HeresyLord/BarrageWCharge",CHAN_6);
	  TNT1 A 0 A_FaceTarget();
	  LOHS E 5 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual2Fake",10,20,60,0,0,0,32);
	  LOHS E 5 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual2Fake",10,9,46,0,0,0,32);
	  LOHS E 6 Light("ShadowBall2") A_SpawnItemEx("PM_HeresyLordMissileDual2Fake",10,-4,28,0,0,0,32);
	  TNT1 A 0 A_StartSound("baron/attack");
	  TNT1 A 0 A_FaceTarget();
	  LOHS EE 4 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual1Real",60,-20,random(-2,3),CMF_OFFSETPITCH,random(-2,2));
	  LOHS EF 4 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual1Real",46,-9,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
	  LOHS F 6 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual1Real",28,4,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
	  TNT1 A 0 A_StartSound("baron/attack");
	  TNT1 A 0 A_FaceTarget();
	  TNT1 A 0 { bSPRITEFLIP = false; }
	  LOHS EE 4 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual2Real",60,20,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
	  LOHS FF 4 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual2Real",46,9,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
	  LOHS G 6 Light("ShadowBall2") A_SpawnProjectile("PM_HeresyLordMissileDual2Real",28,-4,random(-2,2),CMF_OFFSETPITCH,random(-2,2));
	  Goto See;
    Melee:
	  TNT1 A 0 A_Jump(128,"Melee2");
	  TNT1 A 0 A_StartSound("baron/attack");
      LOHS EF 7 A_FaceTarget();
      LOHS G 7 A_CustomMeleeAttack(30,"Doom4/Other/MeleeHit");
	  Goto See;
    Melee2:
	  TNT1 A 0 { bSPRITEFLIP = true; }
	  TNT1 A 0 A_StartSound("baron/attack");
        LOHS EF 7 A_FaceTarget();
        LOHS G 7 A_CustomMeleeAttack(30,"Doom4/Other/MeleeHit");
        Goto See;
    Pain:
	    TNT1 A 0 { bSPRITEFLIP = false; }
		TNT1 A 0 A_StopSound(CHAN_6);
        LOHS H 5;
		LOHS H 5 A_Pain;
        Goto See;
    Death:
	    TNT1 A 0 { bSPRITEFLIP = false; }
        LOHS I 8
		{
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(20,60),0,0,0,0,SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(20,60),0,0,0,0,SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(20,60),0,0,0,0,SXF_NOCHECKPOSITION);
		}
		LOHS J 8
		{
		PM_Scream();
		A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,50),random(-3,3),random(-3,3),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,50),random(-3,3),random(-3,3),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(20,50),random(-3,3),random(-3,3),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(20,40),0,0,0,0,SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(20,40),0,0,0,0,SXF_NOCHECKPOSITION);
		}
		LOHS K 8
		{
		A_SpawnItemEx("PM_NormalInstestinGib",random(-4,4),random(-4,4),random(15,30),random(-3,3),random(-3,3),random(0,5),random(-180,180),SXF_NOCHECKPOSITION);
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(10,30),0,0,0,0,SXF_NOCHECKPOSITION);
		A_Fall();
		}
		LOHS LMN 8
		{
		A_SpawnItemEx("PM_NormalMeatChunkLauncher",random(-2,2),random(-2,2),random(10,20),0,0,0,0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
		LOHS O -1;
        Stop;
	Raise:
	    LOHS ONMLKJI 6;
		Goto Look;
    }
}

Class PM_HeresyLordRedBall : DoomImpBall
{
    Default
    {
	DamageFunction (random(30,35));
	Scale 1.2;
	SeeSound "HeresyLord/RedFire";
	DeathSound "HeresyLord/RedDeath";
	DamageType "Fire";
	+NOGRAVITY
	-LOWGRAVITY
	Speed 14;
	Decal "PM_RedFireScorch";
    }
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("SPKRA0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("GNSML0",TexMan.Type_Sprite);
	}
	
	TextureID tex[3];

	States
  	{
  Spawn:
    BFX5 AABBCC 1 bright Light("Shadowball2") 
	{
	   A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
	   A_SpawnItemEx("PM_HeresyLordFlameTrails");
	   A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 1,size: frandom(80,120.0),xoff: -5,startalphaf: frandom(0.3,0.6));
	   A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,90),size: frandom(0.2,10),xoff: random(-10,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0,fadestepf: -1,sizestep: -0.2,startroll: random(-180,180));
	   A_SpawnParticleEx("",tex[2],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 56,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	}
	loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_SetTranslation("PM_RedFireTint");
	TNT1 A 0 A_SetScale(1.4);
	TNT1 A 0 A_SpawnItemEx("PM_RFireBallExplode");
    BFX5 D 2 bright Light("Shadowball2");
    FIR3 CDEFGHIJKLMNOP 2 Light("Shadowball2") bright;
    stop;
  }
}

Class PM_HeresyLordRFEffect : PM_RedFireEffect
  {
  Default { Scale 0.3; }
  States
  {
  Spawn:
	FIR1 R 1
	{
	  A_FadeOut(0.05);
	  A_SetRoll(random(0,360));
	  A_SetScale(Scale.X-0.015);
	}
	Loop;
	}
}

Class PM_WhiteFireEffect : PM_RedFireEffect
  {
  Default { Scale 0.4; }
  States
  {
  Spawn:
	FIR1 W 1
	{
	A_FadeOut(0.05);
	A_SetRoll(random(0,360));
	A_SetScale(Scale.X-0.015);
	}
	Loop;
	}
}

Class PM_WhiteFireTrail : PM_RedFireEffect
  {
  Default { Scale 0.4; }
  States
  {
  Spawn:
  TNT1 A 2;
  Go:
	FIR1 W 1
	{
	A_FadeOut(0.05);
	A_SetRoll(random(0,360));
	A_SetScale(Scale.X-0.015);
	}
	Loop;
	}
}

//*************************************************
//*************************************************


Class PM_HLM1Trail : Actor
{
    Default
	{
	Projectile;
	-NOGRAVITY
	Gravity 0.4;
	+NOCLIP
	Damage 0;
	RenderStyle "Add";
	Alpha 0.8;
	Scale 0.5;
	}
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 ThrustThingZ(0,random(-3,8),0,1);
		SHER ABCDDE 3 Bright;
		SHER F 1 Bright A_FadeOut(0.1);
		Wait;
	}
}




//*************************************************
//*************************************************
Class PM_HeresyLordWhiteSoul : Actor
{
    Default
	{
	Speed 22;
	DamageFunction 32;
	scale 1.3;
	radius 6;
	height 12;
	Renderstyle "Add";
	Alpha 0.75;
	Projectile;
	seesound "HeresyLord/WhiteFire";
	deathsound "HeresyLord/WhiteDeath";
	Decal "BigScorch";
	}
	states
	{
	Spawn:
		PSMM DDEEFF 1 Bright Light("AzazelSoul")
		{
		  A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
		  A_SpawnProjectile("PM_HeresyLordSoulTrails",0,0,0);
		}
		Loop;
	Death:
		PSMM GGHHIIJJKKLLMM 3 Bright A_FadeOut(0.05);
		stop;
	}
}

Class PM_HLM2Trail : Actor
{
    Default
	{
	+NOCLIP
	Damage 0;
	RenderStyle "Add";
	Projectile;
	Alpha 0.45;
	Scale 0.9;
	}
	States
	{
	Spawn:
		TNT1 A 2;
		PUF2 BDFHJLNPRTVXZ 1;
		PUF3 B 1 A_FadeOut(0.2);
		Wait;
	}
}

//*************************************************
//*************************************************
Class PM_HeresyLordMissileDual1Fake : Actor
{
    Default
	{
	Projectile;
	Speed 0;
	Scale 0.7;
	RenderStyle "Add";
	SeeSound "HeresyLord/BarrageSummon";
	}
	States
	{
	Spawn:
		MSP2 ABCD 2;
		MSP2 ABCD 2;
		MSP2 ABCD 2;
		MSP2 ABCD 2;
		Stop;
	Death:
		TNT1 A 1;
		Stop;
	}
}
Class PM_HeresyLordMissileDual1Real : Actor
{
    Default
	{
	Projectile;
	Speed 16;
	Scale 0.8;
	RenderStyle "Add";
	DamageFunction (28);
	Radius 6;
	height 12;
	SeeSound "HeresyLord/BarrageRedFire";
	DeathSound "HeresyLord/BarrageRedDeath";
	Decal "PM_RedFireScorch";
	}
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("DTPRR0",TexMan.Type_Sprite);
	}
	
	TextureID tex[4];

	States
	{
	Spawn:
		MSP2 AABBCCDD 1
		{
		  A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: 1,size: frandom(55,85.5),xoff: -6,startalphaf: frandom(.25,0.65),fadestepf: -2);
		  A_SpawnParticleEx("#890000",tex[1],style: STYLE_Normal,flags: SPF_RELATIVE,lifetime: random(60,70),size: 25.0,xoff: -5,startalphaf: 0.25,fadestepf: -1,sizestep: -0.08,startroll: random(-180,180));
		  A_SpawnParticleEx("ff2424",tex[2],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: random(1,50),size: random(10,15),xoff: frandom(-12.5,12.5),yoff: frandom(-10.5,10.5),zoff: frandom(-12.5,12.5),velx: frandom(-0.5,-1.3),accelx: frandom(-0.5,-1.0),accelz: frandom(-0.5,-1.0),startalphaf: 1.20,fadestepf: -1,sizestep: -0.25);
		}
		Loop;
	Death:
		TNT1 A 0
		{
		   for(int i=random(6,12);i>0;i--)
		     A_SpawnParticleEx("#fc2121",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(25,70),size: frandom(45,75),xoff: -5,velx: frandom(-2,2),vely: frandom(-2,2),velz: frandom(-2,2),startalphaf: frandom(0.25,0.55),fadestepf: -1,sizestep: -0.08,startroll: random(-180,180));
		   for(int i=2;i>0;i--)
		     A_SpawnParticleEx("",tex[3],style: STYLE_Add,flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: 35,size: 120.0,startalphaf: frandom(0.65,0.95),startroll: random(-180,180));
		 for(int i=random(7,15);i>0;i--)
		     A_SpawnParticleEx("ff2424",tex[2],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE,lifetime: random(1,50),size: random(10,15),xoff: frandom(-2.5,2.5),yoff: frandom(-2.5,2.5),zoff: frandom(-2.5,2.5),velx: frandom(-3,3),vely: frandom(-3,3),velz: frandom(-0.5,0.5),accelz: frandom(-0.3,-0.7),startalphaf: 1.20,fadestepf: -1,sizestep: -0.65);
			 	 
		 A_SpawnItemEx("PM_RFireballExplode",xvel: frandom(-1,1),yvel: frandom(-1,1),zvel: frandom(-1,1),flags: SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY);
		}
		Stop;
	}
}

Class PM_HLMD1RTrail : Actor
{
    Default
	{
	Projectile;
	-NOGRAVITY
	Gravity 0.4;
	+NOCLIP
	Damage 0;
	RenderStyle "Add";
	Alpha 0.8;
	Scale 0.3;
	}
	States
	{
	Spawn:
		TNT1 A 1;
		TNT1 A 0 ThrustThingZ(0,random(-3,5),0,1);
		SHER ABCDDE 3 Bright;
		SHER F 1 Bright A_FadeOut(0.1);
		Wait;
	}
}
//*************************************************

Class PM_HeresyLordMissileDual2Fake : Actor
{
    Default
	{
	Projectile;
	Speed 0;
	Scale 1.5;
	RenderStyle "Add";
	Alpha 0.75;
	SeeSound "HeresyLord/BarrageSummon";
	Translation "112:127=80:111";
	}
	States
	{
	Spawn:
		CSSF DEDE 2 Bright;
		CSSF DEDE 2 Bright;
		CSSF DEDE 2 Bright;
		CSSF DEDE 2 Bright;
		Stop;
	Death:
		TNT1 A 1;
		Stop;
	}
}
Class PM_HeresyLordMissileDual2Real : Actor
{
    Default
	{
	Projectile;
	Speed 18;
	Radius 6;
	DamageFunction (28);
	Scale 1;
	RenderStyle "Add";
	Alpha 0.75;
	Translation "112:127=80:111";
	SeeSound "HeresyLord/BarrageWhiteFire";
	DeathSound "HeresyLord/BarrageWhiteDeath";
	Height 12;
	Decal "PlasmaScorchLower";
	}
	
	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("FIR1W0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	}
	
	TextureID tex[2];

	States
	{
	Spawn:
		CSSF DDEE 1 Bright
		{
		  A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: random(10,25),size: random(35,40),xoff: random(-10,-8),yoff: random(3,5),startalphaf: frandom(0.1,0.5),fadestepf: -1,sizestep: frandom(-2,-1.2),startroll: random(-180,180));
	      A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime: random(1,45),size: random(25,35),xoff: random(-12,-10),yoff: random(-6,6),velx: frandom(-0.5,0.5),vely: frandom(-0.5,0.5),velz: frandom(-0.1,0.1),startalphaf: frandom(0.05,0.3),fadestepf: -1,sizestep: frandom(-2,-0.85),startroll: random(-180,180));
		}
		Loop;
	Death:
	    TNT1 A 0
		{
		 for(int i=random(5,8);i>0;i--)
		   A_SpawnParticleEx("555555",tex[1],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(25,55),size: frandom(1,15),xoff: frandom(-3.5,3.5),yoff: frandom(-3.5,3.5),zoff: frandom(-3.5,3.5),velx: frandom(-0.3,0.3),vely: frandom(-0.3,0.3),velz: frandom(-0.3,0.3),startalphaf: 1,fadestepf: -1,sizestep: 2,startroll: random(-180,180));
		 for(int i=random(4,14);i>0;i--)
		   A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(25,55),size: frandom(1,10),xoff: frandom(-3.5,3.5),yoff: frandom(-3.5,3.5),zoff: frandom(-3.5,3.5),velx: frandom(-0.5,0.5),vely: frandom(-0.5,0.5),velz: frandom(-0.5,0.5),startalphaf: 1,fadestepf: -1,sizestep: 2,startroll: random(-180,180));
		}
		CSSF GHI 4 Bright; 
		CSSF I 1 Bright A_FadeOut;
		Wait;
	}
}

Class PM_HLMD2RTrail : Actor
{
    Default
	{
	Projectile;
	Speed 0;
	Damage 0;
	+NOCLIP
	RenderStyle "Add";
	Alpha 0.5;
	Scale 0.25;
	}
	States
	{
	Spawn:
		PUF2 BCDEFGHIJKL 1;
		PUF2 L 1 A_FadeOut(0.2);
		Wait;
	}
}
