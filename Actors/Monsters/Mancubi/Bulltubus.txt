Class PM_Bulltubus : PMWrathMonsterBase
 {
  int casingcount;
  Default
  {
   //$Category "PM_Monsters/Mancubi"
   Health 600;
   Radius 48;
   Height 64;
   painchance 70;
   Speed 7;
   Scale 1.05;
   Mass 1000;
   SeeSound "Bulltubus/Sight";
   PainSound "Bulltubus/Pain";
   DeathSound "Bulltubus/Death";
   ActiveSound "Bulltubus/Active";
   Obituary "%o was utterly obliterated by a Bulltubus.";
   Species "Mancubus";
   MinMissileChance 220;
   Tag "Bulltubus";
   BloodColor "Orange";
   Monster;
   Bloodtype "PM_LavaBloodBase";
   +DONTHARMCLASS
   +DONTHARMSPECIES
   +FLOORCLIP
   TeleFogSourceType "PM_SmallDemonicTeleportFog";
   TeleFogDestType "PM_DemonicTeleportFog";
   }
   
   override void PostBeginPlay()
   {
    Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
   }
   
   TextureID tex[2];
   
   void PM_BulltubusFlamerCharge()
   {
	 A_SpawnParticleEx
		(   
		"",
	    tex[0],
		style: STYLE_Add,
		flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	    lifetime: random(150,250),
		size: frandom(10,150),
		xoff: frandom(20,25),
		yoff: 34,
		zoff: frandom(-3.5,3.5) + 25,
		startalphaf: 0.07,
		fadestepf: -1,
		startroll: random(-180,180)
		);
	 A_SpawnParticleEx
		(   
		"",
		tex[0],
		style: STYLE_Add,
		flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: random(150,250),
		size: frandom(10,150),
		xoff: frandom(20,25),
		yoff: -34,
		zoff: frandom(-3.5,3.5) + 25,
		startalphaf: 0.07,
		fadestepf: -1,
		startroll: random(-180,180)
		);
	 A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,50),
		size: random(3,15),
		xoff: frandom(20,25),
		yoff: 34,
		zoff: frandom(-3.5,3.5) + 25,
		velx: frandom(-0.95,0.95),
		vely: frandom(-0.95,0.95),
		velz: frandom(-0.95,0.95),
		startalphaf: frandom(0.25,0.9),
		fadestepf: 0,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: frandom(-30,30)
		);
	A_SpawnParticleEx
		(
		"",
		tex[1],
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,50),
		size: random(3,15),
		xoff: frandom(20,25),
		yoff: -34,
		zoff: frandom(-3.5,3.5) + 25,
		velx: frandom(-0.95,0.95),
		vely: frandom(-0.95,0.95),
		velz: frandom(-0.95,0.95),
		startalphaf: frandom(0.25,0.9),
		fadestepf: 0,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: frandom(-30,30)
		);
   }
   States
 {
  Spawn:
    FEFE A 0 NoDelay { pm_irecount = random(10,114); }
  Idle:
    FEFE A 10 PM_Look();
    Loop;
  Look:
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAA 3 PM_LookingForPlayer();
	FEFE BBBCCC 3 PM_LookingForPlayer();
	TNT1 A 0 A_JumpIfTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDD 3 PM_LookingForPlayer();
	FEFE EEEFFF 3 PM_LookingForPlayer();
    Loop;
  Spotted:
    TNT1 A 0 A_Jump(85,"SpotMissile");
  Roar:
    TNT1 A 0 PM_AlertSound();
    Goto See2;
  See2:
	TNT1 A 0 
	{
	 if(ire == true)
	 { A_Jump(256,1); }
	 else if(pm_irecount < 0)
	 { SetStateLabel("Ire"); }
	}
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAA random(2,3)
	{
	 if(ire)
	 {
	  A_SetTics(2);
	 }
	A_Chase();
	}
	FEFE BBBCCC random(2,3)
	{
	 if(ire)
	 {
	  A_SetTics(2);
	 }
	A_Chase();
	}
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDD random(2,3)
	{
	 if(ire)
	 {
	  A_SetTics(2);
	 }
	A_Chase();
	}
	FEFE EEEFFF random(2,3)
	{
	 if(ire)
	 {
	  A_SetTics(2);
	 }
	A_Chase();
	}
	TNT1 A 0 { pm_irecount -= 5; }
    Loop;
   SpotMissile:
    FEFE G 5 A_FaceTarget();
   Goto Missile;
  Rush:
	TNT1 A 0 { bNOPAIN = true; }
	TNT1 A 0 PM_AlertSound();
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 2 A_Chase(null,null);
	FEFE EEEEFFFF 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 1 A_Chase(null,null);
	FEFE EEEEFFFF 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
    FEFE AAAA 1 A_Chase(null,null);
	FEFE BBBBCCCC 1 A_Chase(null,null);
	TNT1 A 0 A_StartSound("MancubStep");
	FEFE DDDD 1 A_Chase(null,null);
	FEFE EEEEFFF 1 A_Chase(null,null);
	TNT1 A 0
	{
	if(ire)
	 {
	  bNOPAIN = true;
	 }
	 else
	 {
	  bNOPAIN = false;
	 }
	}
    Goto See2;
  Missile:
    TNT1 A 0;
	TNT1 A 0 { if(ire && random(1,8) == 1)
	SetStateLabel("Flamethrower"); }
  Minigun:
	TNT1 A 0 { if(random(1,3) == 1) A_StartSound("Bulltubus/Attack",10);
	if(ire)
	 {
	  A_StartSound("Bulltubus/WindUp",pitch:1.25);
	  pm_attackloop = random(65,70);
	 }
	else
	 {
	  A_StartSound("Bulltubus/WindUp");
	  pm_attackloop = random(35,40); 
	 }
	casingcount = 0;  
	}
	FEFE GGGGGG 5 A_FaceTarget();
	TNT1 A 0
	{
	A_SetAngle(angle+random(-30,30));
	A_SetPitch(pitch+random(-15,30));
	}
	TNT1 A 0 { 
	if(ire)
	 {
	 bNOPAIN = true;
	 A_StartSound("Bulltubus/WindLoop",11,CHANF_LOOPING,pitch:1.25);
	 A_StartSound("Bulltubus/IreWindLoop1",14,CHANF_LOOPING);
	 A_StartSound("Bulltubus/IreWindLoop2",15,CHANF_LOOPING);
	 A_StartSound("Bulltubus/IreGun",16,CHANF_LOOPING);
	 }
	else
	A_StartSound("Bulltubus/WindLoop",11,CHANF_LOOPING);
	}
  MinigunLoop:
    FEFE G 3 Light("AvatarFireball2")
	{
	 if(pm_attackloop <= 0)
	 SetStateLabel("MinigunEnd");
	 if(ire)
	 {
	  A_SpawnProjectile("PM_BulltubusFastTracer",32,-34,frandom(-2,2),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-2,2));
	  A_SpawnProjectile("PM_BulltubusFastTracer",32,34,frandom(-2,2),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-2,2));
	  A_SetTics(1);
	 }
	 else
	 {
	  A_StartSound("Bulltubus/Gun");
	  A_StartSound("GunnerRifleDistant",22,CHANF_OVERLAP);
	  A_SpawnProjectile("PM_BulltubusSlowTracer",32,-34,frandom(-4,4),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-4,4));
	  A_SpawnProjectile("PM_BulltubusSlowTracer",32,34,frandom(-4,4),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-4,4));
	 }
	 casingcount++;
	 pm_attackloop--;
	}
    FEFE I 3 Light("AvatarFireball3") { if(ire)A_FaceTarget(0.45,3,0,0); else A_FaceTarget(1.5,3,0,0); }
	Loop;
  MinigunEnd:
    TNT1 A 0 { A_StopSound(11); A_StopSound(14); A_StopSound(15); A_StopSound(16); 
	if(ire)
	{
	bNOPAIN = false;
	A_StartSound("Bulltubus/IreGunDown",16);
	A_StartSound("Bulltubus/WindDown",12,pitch:1.25);
	}
	else 
	{ A_StartSound("Bulltubus/WindDown",12); }
	}
    FEFE G 20 A_FaceTarget(0.85,3,0,0);
	TNT1 A 0 A_StartSound("PM/HeavyRifleBoltSlide",13,pitch:0.75);
	FEFE G 15 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagOpen",14);
	FEFE I 10 { 
	for(int i=casingcount;i>0;i--)
	 {
	 A_SpawnItemEx("PM_BigBulletCasing",-20,-34,40,frandom(0,-25),frandom(-5,-15),frandom(5,15),0,SXF_NOCHECKPOSITION);
     A_SpawnItemEx("PM_BigBulletCasing",-20,34,40,frandom(0,-25),frandom(5,15),frandom(5,15),0,SXF_NOCHECKPOSITION);
	 }
	A_FaceTarget();
	casingcount = 0;
	}
	FEFE III 5 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/MagClose",15);
	FEFE G 15 A_FaceTarget();
	Goto See2;
  Flamethrower:
	FEFE G 1;
	TNT1 A 0 { bNOPAIN = true; pm_attackloop = 160; }
    FEFE GGGGGG 3 A_FaceTarget();
	TNT1 A 0 A_StartSound("Bulltubus/FlamerStart",CHAN_6);
	FEFE GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Light("AvatarFireBall2")
	{
	A_FaceTarget();
	PM_BulltubusFlamerCharge();
	A_SpawnItemEx("OrangeFireEffect",18,random(34,42),random(23,27),0,0,2);
	A_SpawnItemEx("OrangeFireEffect",18,random(-34,-42),random(23,27),0,0,2);
	}
	TNT1 A 0 {
	A_StartSound("Bulltubus/Roar");
	A_StartSound("Defiler/Breath",30,CHANF_LOOPING);
	A_StartSound("Bulltubus/FlamerLoop",31,CHANF_LOOPING);
	}
  FTLoop:
	FEFE HHH 1 Light("AvatarFireball2")
	{
	if(pm_attackloop < 0)
	SetStateLabel("FTEnd");
	A_SpawnProjectile("PM_BulltubusFlamer", 30, 34,random(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-0.5,0.5));
	A_SpawnProjectile("PM_BulltubusFlamer", 30, -34,random(-3,3),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-0.5,0.5));
	A_FaceTarget(0.85,3,0,0);
	pm_attackloop--;
	}
	FEFE H 1 A_FaceTarget(0.85,3,0,0);
	Loop;
  FTEnd:
	TNT1 A 0 { 
	ire = false;
	bNOINFIGHTING = false;
	bNOFEAR = false;
	bNOPAIN = false;
    pm_irecount = random(70,164);
	A_RemoveChildren(1,RMVF_EVERYTHING); 
	A_StopSound(30); A_StopSound(31); A_StopSound(20); A_StopSound(19); }
	FEFE GGIIGGIIGGII 4 A_FaceTarget(0.85,3,0,0);
	Goto See2;
  Ire:
    FEFE AA 5 A_FaceTarget();
	TNT1 A 0 PM_AlertSound();
	FEFE GGG 5 Light("AvatarFireball2") A_FaceTarget();
	TNT1 A 0 
	{ 
	 ire = true;
	 bNOINFIGHTING = true;
	 bNOFEAR = true;
	 A_SpawnItemEx("PM_WrathIreActivate",0,0,15);
	 A_SpawnItemEx("PM_WrathIreEffect",0,0,45,flags:SXF_SETMASTER);
    }
	TNT1 A 0 PM_IreStartSound();
	FEFE GGIIGGIIGGIIGGIIGGII 4 Light("AvatarFireball3") 
	{
	A_FaceTarget();
	if (!(level.time % 2)) A_SpawnItemEx("OrangeEmbersFloat",0,random(-10,10),15,random(1,12),random(-3,3),random(-3,3));
	}
	Goto See2;
  Pain:
    FEFE J 1;
	TNT1 A 0 A_StopSound(11);
	TNT1 A 0 { pm_irecount -= 10; }
	TNT1 A 0 A_Jump(120,"Rush");
    FEFE J 9 A_Pain;
    Goto See2;
  Death:
	   TNT1 A 0;
	   TNT1 A 0 A_RemoveChildren(1,RMVF_EVERYTHING);
	   TNT1 A 0 A_StopSounds(0,0);
	   FDFD A 6
	   {
	   A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   }
       FDFD B 6
	   {
	   A_Scream();
	   }
       FDFD C 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       FDFD DEF 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
	   TNT1 A 0 A_StartSound("Corpse/FallMetal",CHAN_6);
	   FDFD GH 6 A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-4,4),random(-4,4),random(20,30),random(-3,3),random(-3,3),random(0,5),random(-50,50),SXF_NOCHECKPOSITION);
       FDFD I 6 A_NoBlocking;
       FDFD J -1;
	   Stop;
  Raise:
	FDFD JIHGFEDCBA 6;
	TNT1 A 0 { bNOPAIN = false; }
    Goto Look;
  }
}

Class PM_BulltubusSlowTracer : Fatshot
{
  Default
  {
   Projectile;
   Speed 22;
   Alpha 0.85;
   Radius 4;
   Height 6;
   Scale 1.0;
   DamageFunction (random(15,20));
   SeeSound "";
   DeathSound "FeralImp/Ireball";
   Decal "BigScorch";
   DamageType "Fire";
   RenderStyle "Add";
  }
  
  override void PostBeginPlay()
  {
   Super.PostBeginPlay();
   tex[0] = TexMan.CheckForTexture("SPKOC0",TexMan.Type_Sprite);
   tex[1] = TexMan.CheckForTexture("DTPRO0",TexMan.Type_Sprite);
   tex[2] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
   tex[3] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
   tex[4] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
  }
  
  TextureID tex[5];

  override void Tick()
  {
   if(level.IsFrozen()) return;
   Super.Tick();
   if(level.maptime % 5)
   {
	A_SpawnParticleEx
	 (
		"",
		tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,15), size: frandom(0.2,10), xoff: random(-1,1), yoff: random(-1,1),
		zoff: random(-1,1), velx: frandom(-0.6,0.6), vely: frandom(-0.6,0.6), velz: frandom(-0.6,0.6),
		startalphaf: 1.0, fadestepf: -0.5, sizestep: -0.2, startroll: random(-180,180)
	 );
	A_SpawnParticleEx
	 (
		"",
		tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 1, size: 80.0, xoff: -5, startalphaf: 0.6
	 );
    }
  }
 
  States
  {
    Spawn:
    TNT1 A 0;
	  TNT1 A 0
	  {
		A_SpawnParticleEx
		( "", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(2,5), size: frandom(100,170), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),
		startroll: random(-180,180));
		
		A_SpawnParticleEx
		( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,7),
		size: frandom(90,155), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
		
		A_SpawnParticleEx
		( "#d2d2d2", tex[4], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,300),
		size: frandom(10,90), xoff: -10, velx: frandom(0,6), vely: frandom(-0.8,0.8), velz: frandom(-0.4,0.7), startalphaf: frandom(0.3,0.7),
		fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		
		for(int i=random(1,3);i>0;i--)
		A_SpawnParticleEx
		( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,100), size: frandom(1,12), xoff: frandom(-2,2), yoff: frandom(-2,2), zoff: frandom(-2,2),
		velx: frandom(1,10), vely: frandom(-3,3), velz: frandom(-0.6,3),
		accelx: frandom(-0.005,0.005), accely: frandom(-0.005,0.005), accelz: frandom(-0.0,-0.8), startalphaf: 1.0, fadestepf: -0.5,
		sizestep: -0.2, startroll: random(-180,180));
	  }
    Go:
      TRCR AA 1 Bright;
      Loop;
    Death:
	  TNT1 A 0 A_StartSound("HellionProjExp",volume:0.75,pitch:frandom(1.0,1.85));
	  TNT1 A 0 Radius_Quake (1, 2, 0, 5, 0);
	  TNT1 A 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
	  TNT1 A 0
	  {
	  A_SpawnParticleEx
       ( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,20),
	   size: frandom(100,120), xoff: -5, startalphaf: frandom(0.75,0.95), sizestep: frandom(0.0,0.65), 
	   startroll: random(-180,180) );
	  A_SpawnParticleEx
	   ( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT,
	   lifetime: random(1,5), size: 90.0, xoff: -5, startalphaf: 0.6 );
	  }
      TNT1 AAA 3 Bright Light("YellowMediumFlicker2");
      Stop;
      }
}

Class PM_BulltubusFastTracer : PM_BulltubusSlowTracer
{ Default { Speed 40; } }

Class PM_BulltubusFlamer : Actor
{
   Default
   {
   Radius 7;
   Height 7;
   Speed 30;
   DamageFunction 3;
   PROJECTILE;
   RENDERSTYLE "Add";
   DamageType "Fire";
   Scale 0.15;
   DeathSound "Fire/Unleash";
   Decal "Scorch";
   +THRUGHOST
   +ROLLSPRITE
   +FORCEXYBILLBOARD
   }
   States
   {
   Spawn:
	  TNT1 A 0 A_SpawnItemEx("OrangeFireEffect",20,4,36,random(2,-2),random(2,-2),random(1,3),0,0,0,0);
      NULL A 1 Bright;
	  TNT1 A 0 A_Jump(255,"Go","Go2");
	  Go:
	  TNT1 A 0 A_SetRoll(random(0,360));
      FLMF ABCDEFEDCBA 2 Bright Light("AvatarFireball2")
	  {
	  A_SetScale(Scale.X+0.06);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF ABCDEF 2 Bright Light("AvatarFireball2");
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBABC 2 Bright Light("AvatarFireball2")
	  {
	  A_SetScale(Scale.X+0.07);
	  A_Fadeout(0.1);
	  }
	  Stop;
	  Go2:
	  TNT1 A 0 A_SetRoll(random(0,360));
	  FLMF FEDCBABCDEF 2 Bright
	  {
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,248);
	  A_SetScale(Scale.X+0.05);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBA 2 Bright;
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF BCDEFED 2 Bright
	  {
	  A_SetScale(Scale.X+0.07);
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,240);
	  A_Fadeout(0.1);
	  }
      stop;
   Death:
      FRFX HIJ 2 Bright;
      FRFX J 0 A_SpawnProjectile ("ZombieDropFire",0,0,0,4);
      FRFX KLM 2 Bright;
      FRFX NO 2 Bright;
      stop;
   }
}