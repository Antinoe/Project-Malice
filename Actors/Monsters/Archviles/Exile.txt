//CREDITS TO DUSTED FOR ATTACK CODE, AND TheDoomLad for the wall fire code!

Class PM_Exile : PMMonsterBase
{
	Default
	{
		Health 700;
		Radius 20;
		Height 56;
		Mass 500;
		Speed 15;
		PainChance 10;
		MaxTargetRange 896;
		Species "Archvile";
		DamageFactor "Vile", 0;
		PainChance "Vile", 0;
		Gibhealth 40;
		Obituary "%o was immolated by an Exile.";
		Tag "Exile";
		SeeSound "Exile/Sight";
		PainSound "Exile/Pain";
		DeathSound "Exile/Death";
		ActiveSound "Exile/Active";
		Bloodtype "PM_NormalBloodBase";
		+NOTARGET
		+QUICKTORETALIATE
		+DONTHARMSPECIES
		+DONTHARMCLASS
		+NOINFIGHTSPECIES
	}

	States
	{
		Spawn:
			EILE A 10 PM_Look();
			Loop;
		Look:
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE AABBCC 2 PM_LookingForPlayer();
			TNT1 A 0 A_JumpifTargetInLOS("Spotted",180,JLOSF_DEADNOJUMP);
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE DDEEFF 2 PM_LookingForPlayer();
			TNT1 A 0 A_CheckSight("See");
			Goto Spotted;
		Spotted:
			TNT1 A 0 PM_AlertSound();
		See2:
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE AABBCC 2 A_VileChase();
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE DDEEFF 2 A_VileChase();
			Loop;
			
		Missile:
			TNT1 A 0 A_Jump(25,"GroundFlames");
			TNT1 A 0 A_Jump(45,"Fireball","GroundFlames","FireLake");
			TNT1 A 0 A_Jump(200,"Fireball","GroundFlames","FirePort","FireWall");
	
		FireLake:
			TNT1 A 0 A_PlaySound("Exile/Attack",CHAN_7);
			EILE G 0 Bright A_PlaySound("vile/start",1);
			TNT1 AAAAAAA 0 A_SpawnItemEx("ExileAttackFire",random(-50,50),random(-50,50),0);
			EILE GGGGG 2 Bright Light("EXILATK1")
			{
				A_FaceTarget();
				A_SpawnItemEx("ExileHandFire",8,-32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
				A_SpawnItemEx("ExileHandFire",8,32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
			}
			TNT1 A 0 A_VileTarget();
			EILE GGGGGGGGGGGGGGGGGGG 2 Bright Light("EXILATK1")
			{
				A_FaceTarget();
				A_SpawnItemEx("ExileHandFire",8,-32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
				A_SpawnItemEx("ExileHandFire",8,32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
			}
			EILE HHHHHIII 2 Bright Light("EXILATK1") 
			{
				A_FaceTarget();
				A_SpawnItemEx("ExileHandFire",8,-32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
				A_SpawnItemEx("ExileHandFire",8,32,78,frandom(-1,1),frandom(-1,1),frandom(0,5));
			}
			EILE I 8 Bright Light("EXILATK2");
			EILE JB 5 Light("EXILATK3");
			Goto See2;
	
		Fireball:
			EILE JK 4 Bright Light("EXILATK2") A_FaceTarget();
			EILE LMNNOO 4 Bright Light("EXILATK2")
			{
				A_FaceTarget();
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,40,random(-4,4),random(-4,4),random(-4,4));
			EILE P 10 Bright Light("EXILATK3") A_SpawnProjectile("PM_ExileFireball",40);
			EILE B 4 A_FaceTarget();
			Goto See2;
	
		GroundFlames:
			EILE B 5 A_FaceTarget();
			EILE LKLMN 3 BRIGHT Light("EXILATK1")
			{
				A_FaceTarget();
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,random(0,40),random(-4,4),random(-4,4),random(-4,4));
			TNT1 A 0 A_PlaySound("Exile/GroundStart",CHAN_6);
			EILE O 3 BRIGHT Light("EXILATK2") A_SpawnProjectile("PM_ExileGroundFlames",20,0,random(-20,20));
			EILE P 4 BRIGHT Light("EXILATK2");
			EILE B 5 A_FaceTarget();
			EILE LKLMN 3 BRIGHT Light("EXILATK1")
			{
				A_FaceTarget();
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,random(0,40),random(-4,4),random(-4,4),random(-4,4));
			TNT1 A 0 A_PlaySound("Exile/GroundStart",CHAN_6);
			EILE O 3 BRIGHT Light("EXILATK2")A_SpawnProjectile("PM_ExileGroundFlames",20,0,random(-20,20));
			EILE P 4 BRIGHT Light("EXILATK2");
			EILE B 5 A_FaceTarget();
			EILE LKLMN 3 BRIGHT Light("EXILATK1")
			{
				A_FaceTarget();
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,random(0,40),random(-4,4),random(-4,4),random(-4,4));
			TNT1 A 0 A_PlaySound("Exile/GroundStart",CHAN_6);
			EILE O 3 BRIGHT Light("EXILATK2") A_SpawnProjectile("PM_ExileGroundFlames",20,0,random(-20,20));
			EILE P 10 BRIGHT Light("EXILATK2");
			Goto See2;
	
		Standing:
			EILA AA 5 A_FaceTarget();
			Loop;
	
		FirePort:
			EILE KKLLMMNN 2 BRIGHT Light("EXILATK2")
			{
				A_SpawnProjectile("PM_FakeExileActor",60,-22,0,CMF_OFFSETPITCH,0);
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 A 0 A_PlaySound("Exile/Teleport",CHAN_6);
			TNT1 A 0 A_SetSpeed(50);
			TNT1 A 0 { bSHOOTABLE = false; }
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
			{
				A_Wander();
				A_SpawnItemEx("PM_ExileFireGlobs",0,0,30,0,0,0,0,128);
				//A_SpawnItemEx("PM_ExileTeleFire",0,0,random(5,56));
			}
			TNT1 A 0 A_SetSpeed(15);
			TNT1 A 0 { bSHOOTABLE = true; }
			TNT1 AAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,40,random(-7,7),random(-7,7),random(-7,7));
			EILE NMLK 1 BRIGHT Light("EXILATK2")
			{
				A_SpawnItemEx("PM_FireGlobs",10,0,40,random(-4,4),random(-4,4),random(-4,4));
			}
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE AABBCC 1
			{
				A_Wander();
				A_SpawnItemEx("PM_HuskRushFire",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
			}
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE DDEEFF 2
			{
				A_Wander();
				A_SpawnItemEx("PM_HuskRushFire",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
			}
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			EILE AABBBCCC 3
			{
				A_Wander();
				A_SpawnItemEx("PM_HuskRushFire",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
			}
			TNT1 A 0 A_PlaySound("Exile/Step",CHAN_5);
			Goto See2;
	
		FireWall:
			TNT1 A 0 A_Jump(100,"Fireball","GroundFlames","FireLake");
			TNT1 A 0 A_FaceTarget();
			TNT1 A 0 A_PlaySound("Exile/ImmolateStart",CHAN_6);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",96,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",88,24,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",88,-24,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",80,48,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",80,-48,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",72,70,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",72,-70,0,0,0,0,0,SXF_NOCHECKPOSITION);
			EILE MMMNNNOOOO 3 BRIGHT Light("EXILATK2") A_FaceTarget();
			TNT1 A 0 A_PlaySound("Exile/ShieldSpawn",CHAN_6);
			TNT1 AAA 0 A_SpawnProjectile ("RainCometNull",28,0,random(-80,80));
			TNT1 A 0 A_SpawnItemEx("PM_ExileWallSpawner",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			EILE PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP 2 Bright Light("EXILATK1")
			{
				A_SpawnItemEx("PM_FireGlobs",10,random(-5,5),random(35,45),random(-1,1),random(-1,1),random(-1,2));
			}
			TNT1 A 0 A_KillChildren("PM_ExileWallSpawner");
			Goto FirePort;
	
		Heal:
			TNT1 A 0 A_PlaySound("Exile/Resurrect",CHAN_6);
			EILR XX 5 Bright Light("SatyrLordRedMissile3") A_SpawnItem("DefilerFire", 55);
			EILR XX 5 Bright Light("SatyrLordRedMissile5");
			EILR XX 5 Bright Light("SatyrLordRedMissile4");
			Goto See2;
		Pain:
			TNT1 A 0 A_KillChildren("PM_ExileWallSpawner");
			TNT1 A 0 A_KillChildren("NewArchvileFire");
			EILE Q 5;
			EILE Q 5 A_Pain();
			TNT1 A 0 A_Jump(20,"FirePort","FireWall");
			Goto See2;
		Death:
			TNT1 A 0 A_KillChildren("PM_ExileWallSpawner");
			TNT1 A 0 A_KillChildren("NewArchvileFire");
			EILE Q 7;
			EILE R 7 A_Scream();
			EILE S 7 A_NoBlocking();
			EILE TUVW 7;
			TNT1 A 0 A_PlaySound("Corpse/FallMed",CHAN_6);
			EILE XY 7;
			EILE Z -1;
			Stop;
	}
}

Class PM_ExileWallSpawner : Actor
{
	Default
	{
		+NOGRAVITY
		+NEVERTARGET
		+SHOOTABLE
		+NOTONAUTOMAP
		-SOLID
		+QUICKTORETALIATE
		+GHOST
		-COUNTKILL
		+NOTARGETSWITCH
		+ALLOWPAIN
		Health 5000;
		Radius 24;
		Height 56;
		RenderStyle "Add";
		DamageFactor "ExileWall", 9000;
	}

	States
	{
		Spawn:
			TNT1 A 1;
			Goto Shield;
		Shield:
			TNT1 A 0 A_StartSound("Exile/ShieldLoop", CHAN_7);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall1",96,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall2",88,24,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall2",88,-24,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall3",80,48,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall3",80,-48,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall4",72,70,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("PM_ExileWall4",72,-70,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			Goto Waiting;
		Waiting:
			TNT1 A 1;
			Loop;
		Death:
			TNT1 A 0 A_StopSound(CHAN_7);
			TNT1 A 0 A_KillChildren();
			Stop;
	}
}

Class PM_ExilePreWallSpawner : Actor
{
	Default
	{
		+NOGRAVITY
		+NEVERTARGET
		-SHOOTABLE
		+NOTONAUTOMAP
		-SOLID
		+QUICKTORETALIATE
		+GHOST
		-COUNTKILL
		+NOTARGETSWITCH
		+ALLOWPAIN
		Health 5000;
		Radius 24;
		Height 56;
		RenderStyle "Add";
		DamageFactor "ExileWall", 9000;
	}

	States
	{
		Spawn:
			TNT1 A 1;
			Goto Shield;
		Shield:
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",96,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",88,24,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",88,-24,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",80,48,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",80,-48,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",72,70,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			TNT1 A 0 A_SpawnItemEx("ExileAttackFire",72,-70,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
		Death:
			Stop;
	}
}

Class PM_ExileWall1 : Actor
{
	Default
	{
		Health 2000;
		XScale 1;
		YScale 2.8;
		Radius 16;
		Height 128;
		Mass 9000;
		Alpha 0.6;
		+Corpse
		-Noinfighting
		+Shootable
		+Solid
		+FORCERADIUSDMG
		+NODAMAGETHRUST
		+DONTSPLASH
		+NOBLOOD
		DamageFactor "archwall", 5000;
		RenderStyle "ADD";
	}
	States
	{
		Spawn:  
			F1RE ABCD 1 Bright;//A_SpawnItemEX("ArchWallPuff",Random(8,-8),Random(32,-32),Random(32,-32),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			F1RE EFGH 2 Bright; //A_SpawnItemEX("ArchWallPuff",Random(8,-8),Random(32,-32),Random(32,-32),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			Loop;
		Death:
			//Idk if there is a puff for Archwall so I just used BulletPuff
			TNT1 A 0 A_Explode (4,64,XF_HURTSOURCE,0,0,0,0,"BulletPuff","Archwall");
			FTRA K 2 Bright;
			FTRA L 2 Bright;
			FTRA MNO 2 Bright;
			Stop;
	}
}

Class PM_ExileWall2 : PM_ExileWall1
{
	Default
	{
		Yscale 2.2;
	}
}

Class PM_ExileWall3 : PM_ExileWall1
{
	Default
	{
		Yscale 1.7;
	}
}

Class PM_ExileWall4 : PM_ExileWall1
{
	Default
	{
		Yscale 1.4;
	}
}

Class PM_ExileGroundFlames : Actor
{
	Default
	{
		Radius 12;
		Height 64;
		Speed 18;
	    DamageFunction (15);
	    XScale 1.7;
	    YScale 1.9;
	    ExplosionDamage 8;
	    ExplosionRadius 8;
	    RenderStyle "ADD";
	    DamageType "fire";
	    Alpha 1;
	    PROJECTILE;
	    Reactiontime 10;
	    +FLOORHUGGER
	    +THRUGHOST
	    -NOGRAVITY
	    +DONTSPLASH
	    +SEEKERMISSILE
		DeathSound "Exile/GroundEnd";
		Decal "BigScorch";
	}

	States
	{
		Spawn: 
			TNT1 A 1;
			TNT1 A 0 A_PlaySound("Exile/GroundLoop",CHAN_7,0.7,1);
		Go:
			FLME ABCDEFGHIJKLM 1 Bright Light("HectebusBallLight") 
			{
				A_SpawnItemEx("PM_LargerRedFlare",0,0,10);
				A_SpawnItemEx("PM_LargerRedFlare",0,0,10);
				A_SeekerMissile (1,1);
				A_SpawnItemEx("PM_ExileGroundFlameTrail",random(-6,6),random(-6,6),0);
			}
			TNT1 A 0 Bright A_CountDown();
			Loop;
		Death:
			TNT1 A 0 A_StopSound(CHAN_7);
			TNT1 A 1 Bright A_RadiusThrust(6000,90);
			TNT1 AAA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			TNT1 A 0 A_SpawnItemEx("OrangeShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_FireGlobs",10,0,random(0,40),random(-4,4),random(-4,4),random(-2,7));
			Stop;
	}
}

Class PM_ExileGroundFlameTrail : Actor
{
	Default
	{
		+NoGravity
		+NoBlockmap
		+FLOORHUGGER
		+NoInteraction
		+CLIENTSIDEONLY
		-Solid
		+Bright
		RENDERSTYLE "ADD";
		Alpha 1;
		Scale 1.8;
		//XScale 0.7
		//YScale 1.0
	}

	States
	{
		Spawn:
			TNT1 A 2;
		Go:
			TNT1 A 0;
			TNT1 A 0 A_Jump(128,"Go2");
			FRTF CDEFGEFC 4 Bright
			{
				A_FadeOut(0.08);
				A_SetScale(scale.x-0.12);
			}
			Stop;
		Go2:
			FRTF FGCEFDEF 4 Bright
			{
				A_FadeOut(0.08);
				A_SetScale(scale.x-0.12);
			}
			Stop;
	}
}

Class PM_ExileFireball : Actor
{
	Default
	{
		Radius 6;
		Height 8;
		Speed 19;
		FastSpeed 30;
		DamageFunction (Random(18,25));
		Scale 1;
		Projectile;
		Alpha 1;
		SeeSound "Husk/Throw";
		DeathSound "GenericCometExp";
		Decal "BigScorch";
	}

	States
	{
		Spawn:
			TNT1 A 1 Light("HectebusBallLight") 
			{
				A_SpawnItemEx("PM_ExileFireGlobs",0,0,0,0,0,0,0,128);
			}
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_PlaySound("BigExplosionDistant", CHAN_7);
			TNT1 A 0 A_Explode(35,140);
			TNT1 A 0 Radius_Quake (2, 24, 0, 5, 0);
			TNT1 A 0 A_PlaySound("baron/attack",CHAN_6);
			TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
			TNT1 AAAAAAA 0 A_SpawnItemEx("PM_GenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-2,3),0,SXF_NOCHECKPOSITION);
			TNT1 AAAAAAAAAA 0 A_SpawnItemEx("PM_ExileFireShrapnel",0,0,0,random(-2,15),random(-2,15),random(-2,15),0,SXF_NOCHECKPOSITION);
			BZR2 CDE 6 Light("EXILATK1");
			Stop;
	}
}

Class PM_ExileSeekingFireball : Actor
{
	Default
	{
		Radius 5;
		Height 5;
		Speed 10;
		FastSpeed 15;
		Damage 5;
		Scale 1;
		Projectile;
		Alpha 1;
		+MISSILE
		+SEEKERMISSILE
		+RIPPER
		Reactiontime 400;
		SeeSound "pyro/flame";
		DeathSound "HectebusBall/Exp";
		Decal "BigScorch";
	}

	States
	{
		Spawn:
			TNT1 AAAAAAA 1 Light("HectebusBallLight") 
			{
				A_SpawnItemEx("PM_ExileSeekingFireGlobs",0,0,0,0,0,0,0,128);
			}
		Go:
			TNT1 A 2 Light("HectebusBallLight") 
			{
				A_SeekerMissile(10,10);
				A_Countdown();
				A_SpawnItemEx("PM_ExileSeekingFireGlobs",0,0,0,0,0,0,0,128);
			}
			Loop;
			
		Death:
			TNT1 A 0 A_SpawnItemEx("OrangeShockwave",0,0,0,0,0,0,0,128,0);
			TNT1 AAAAA 0 A_SpawnProjectile("PM_BigFireGlobs",10,0,random(0,360),2,random(0,360));
			TNT1 A 0 Radius_Quake (2, 24, 0, 5, 0);
			TNT1 AAAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
			TNT1 AAA 0 A_SpawnProjectile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360));
			TABE CDE 3 Bright Light("YellowMediumFlicker2");
			Stop;
	}
}

Class PM_ExileFireShrapnel : Actor
{
	Default
	{
		Radius 4;
		Height 4;
		Speed 9;
		Mass 3;
		BounceFactor 0.6;
		WallBounceFactor 0.6;
		+DOOMBOUNCE
		+NOBLOCKMAP
		+MISSILE
		+NOTELEPORT
		+DONTSPLASH
		//+MOVEWITHSECTOR
		+ROLLSPRITE
		Scale 0.76;
	}

    States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 2 A_SpawnProjectile("PM_BurntSoulFire",0,0,random(0,360),2,random(0,160));
			Loop;
		Death:
			TNT1 A 0;
			TNT1 A 0 A_SpawnItem("ExileDropFire");
			Stop;
    }
}

Class NewArchvileFire : ArchvileFire replaces ArchvileFire
{
	Default
	{
		Scale 0.08;
	}
  
	States
	{
		Spawn:
			TNT1 A 1 NoDelay A_StartFire();
			TNT1 A 0 A_PlaySound("Exile/ImmolateStart",CHAN_6);
			TNT1 AAAAAA 1 Light("EXILATK1")
			{
				A_Fire();
				A_SpawnItemEx("PM_HuskRushFire",frandom(5,30),0,frandom(0,20),frandom(-1,1),frandom(-1,1),random(1,6),random(0,360));
			}
			TNT1 A 0 A_PlaySound("Exile/Immolate",CHAN_5,1.0);
			TNT1 A 0 A_PlaySound("vile/firecrkl",1,1.0,1);
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 1 Light("EXILATK1")
			{
				A_Fire();
				A_SpawnItemEx("PM_HuskRushFire",frandom(5,30),0,frandom(0,20),frandom(-1,1),frandom(-1,1),random(1,6),random(0,360));
			}
			TNT1 A 0 A_SpawnItemEx("NewArchvileFire2",0,0,0,0,0,0,0,SXF_SETMASTER);
			Stop;
	}
}

Class NewArchvileFire2 : Actor
{
	Default
	{
		Scale 0.1;
		+NOBLOCKMAP
		+NOGRAVITY
		+ZDOOMTRANS
		RenderStyle "Add";
		Alpha 1;
	}

	States
	{
		Spawn:
			TNT1 A 0 A_PlaySound("vile/firecrkl",1,1.0,1);
			TNT1 AAAAAAAAAAAAA 1 Light("EXILATK1")
			{
				A_Fire();
				A_SpawnItemEx("PM_FireGlobs",frandom(5,30),0,frandom(0,20),frandom(-1,1),frandom(-1,1),random(1,6),random(0,360));
			}
			TNT1 A 0 A_PlaySound("ExileFireHum");
			TNT1 AAAAAAAAAAAAA 1 Light("EXILATK1")
			{
				A_Fire();
				A_SpawnItemEx("PM_FireGlobs",frandom(5,30),0,frandom(0,20),frandom(-1,1),frandom(-1,1),random(1,6),random(0,360));
			}
		Kaboom:
			TNT1 A 0 A_PlaySound("ExileFireExplode",CHAN_6);
			TNT1 A 0 A_PlaySound("ExileFireReady",CHAN_5);
			TNT1 A 0 A_Explode(55,80);
			TNT1 A 0 Radius_Quake (2, 54, 0, 10, 0);
			TNT1 A 0 A_SpawnItemEx("ExileFireHit");
			TNT1 A 10 Light("EXILATK1");
			TNT1 A 25 Light("EXILATK3");
			Stop;
	}
}


Class ExileFireHit : NewArchvileFire
{
	Default
	{
		+SEEKERMISSILE
	}

	States
	{
		Spawn:
			TNT1 A 0 A_SpawnItemEx("OrangeShockwaveBig",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 AA 0 A_SpawnItemEx("OrangeEmbersSpawn",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			//TNT1 A 0 A_SpawnItemEx("ExileAttackFire")
			TNT1 A 1 A_SpawnItemEx("OrangeExplosionFlareMed",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("OrangeEmbersSpawn",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_SpawnItemEx("OrangeShockwaveBig",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 AAAA 0 A_SpawnProjectile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360));
			TNT1 AAAAA 0 A_SpawnProjectile("PM_BigFireGlobs",10,0,random(0,360),2,random(0,360));
			TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("ExileDropFire",random(-150,150),random(-150,150),0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 AAA 0 A_SpawnProjectile("FiendBallExplode",0,0,random(0,360),2,random(0,360));
			TNT1 A 15 BRIGHT Light("AvatarFireball5");
			Stop;
	}
}

Class ExileAttackFire : Actor
{
	Default
	{
		+NoClip
		+Missile
		+DropOff
		+Randomize
		Scale 1.5;
		Speed 0;
		RenderStyle "Add";
		Alpha 0.67;
	}

	States
	{
		Spawn:
			TNT1 A 8;
			FRTF A 3 Bright;
			FRTF A 0;
			FRTF B 3 Bright;
			FRTF B 0;
			FRTF C 3 Bright;
			FRTF C 0; 
			FRTF C 0; 
			FRTF C 0 A_Jump(191,4);
			FRTF C 0 A_SpawnItemEx("ExileAttackFire",random(-50,50),random(-50,50),0);
			FRTF C 0 A_SpawnItemEx("ExileAttackFire",random(-50,50),random(-50,50),0);
			FRTF C 0 A_SpawnItemEx("ExileAttackFire",random(-50,50),random(-50,50),0);
			FRTF DEFGCDEFHIJKLMNO 3 Bright;
			Stop;
	}
}

Class ExileHandFire : Actor
{
	Default
	{
		+NOINTERACTION
		+CLIENTSIDEONLY
		+ROLLSPRITE
		+FORCEXYBILLBOARD
		Renderstyle "Add";
		Scale 0.43;
		+Bright
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetRoll(random(0,360));
			TNT1 A 0 A_SetScale(Scale.X-0.04);
			F1R1 A 1 A_FadeOut(0.07);
			Loop;
	}
}

Class ExileDropFire : Actor
{
   Default
   {
   Radius 8;
   Height 40;
   PROJECTILE;
   RENDERSTYLE "Add";
   DamageType "Fire";
   ALPHA 1;
   Scale 1.3;
   -NOGRAVITY
   +LOWGRAVITY
   +NOEXPLODEFLOOR
   +NODAMAGETHRUST
   +THRUGHOST
   +DONTSPLASH
   }
   States
   {
   Spawn:
	  TNT1 A 0 NoDelay A_SetScale(frandom(0.4,1.7));
	  FRTF ABC 4 Bright;
	  FRTF A 0 Bright A_Playsound("Fire/Idle",CHAN_5,1,true,2);
	  Go:
      FRTF A 0 Bright A_Explode(1,16,0);
      FRTF CDEFGEF 4 Bright A_Explode(1,16,0);
      FRTF A 0 A_Jump(65,1);
      loop;
      NULL A 0;
	  FRTF GHIJKLMNO 3 Bright;
      goto death;
   Death:
	  TNT1 A 0 A_StopSound(CHAN_5);
      NULL A 1 Bright;
      stop;
   }
}
