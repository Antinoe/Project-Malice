Class XWizard : Actor
{
  Default
  {
    obituary "%o was burned by FleshWizard.";
    health 700;
    radius 15;
    height 60;
    mass 100;
    speed 12;
    painchance 200;
	Species "Archvile";
	+DONTHARMSPECIES
	//DropItem "RagePoint1";
    SEESOUND "wizard/sight";
    painsound "wizard/pain";
    deathsound "wizard/death";
    activesound "wizard/active";
    MONSTER;
    +FLOORCLIP
    -SOLID
  }

  States
  {
  Spawn:
	Idle:
    LMWZ E 10 A_Look();
    loop;
  See:
    LMWZ EEEEEEFFFFFF 2 Light("ShadowBall2")
	{
	A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
	A_VileChase();
	}
    loop;
  Missile:
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ E 0;
    LMWZ EFG 5 Light("ShadowBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
	}
    LMWZ H 20 Light("ShadowBall2") A_SpawnProjectile("WizardBall",65,0);
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ G 5 Light("ShadowBall2");
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ E 5 Light("ShadowBall2");
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ E 1 A_PlaySound("wizard/telenew");
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    Goto See;
  Heal:
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
	LMWZ E 8 BRIGHT Light("ShadowBall2") A_SpawnItem("DefilerFire", 55);
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
	LMWZ FG 8 BRIGHT Light("ShadowBall2") A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
	Goto See;
  Pain:
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ I 3 Light("ShadowBall2");
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ I 5 Light("ShadowBall2") A_Pain();
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3));
    LMWZ I 0 A_PlaySound ("wizard/pain");
    Goto See;
  Death:
    LMWZ J 6 A_Scream();
    LMWZ K 6 A_NoBlocking();
    LMWZ LMNO 6 A_RemoveChildren(1,RMVF_EVERYTHING);
    LMWZ P -1;
    Stop;
  }
}


Class FleshWizard : Actor
{
  Default
  {
    obituary "%o was burned by FleshWizard.";
    health 160;
    radius 20;
    height 80;
    mass 100;
    speed 80;
    painchance 200;
    SEESOUND "wizard/sight";
    painsound "wizard/pain";
    deathsound "wizard/death";
    activesound "wizard/active";
    MONSTER;
    +FLOORCLIP
    +STEALTH
    -SOLID
  }

  States
  {
  Spawn:
    LMWZ E 10 A_Look();
    loop;
  See:
    LMWZ EEFFEEFF 1 A_Chase(null, "Missile", CHF_FASTCHASE);
    loop;
  Missile:
    LMWZ E 50
    { //Original had a 50% chance to jump to states with longer delays at the start
      if(random(0, 1)) //50% chance
        A_SetTics( 50 + (randompick(10, 20, 30, 40, 50, 60, 70)) );
    }
    LMWZ E 0 A_SetShootable();
    LMWZ EFG 5 A_FaceTarget();
    LMWZ H 20 A_SpawnProjectile("WizardBall",65,0);
    LMWZ G 5;
    LMWZ E 5 A_UnSetShootable();
    LMWZ E 0 A_PlaySound ("wizard/tele");
    goto See;
  Pain:
    LMWZ I 3;
    LMWZ I 5 A_Pain();
    LMWZ I 0 A_PlaySound ("wizard/tele");
    Goto See;
  Death:
    LMWZ J 6 A_Scream();
    LMWZ K 6 A_NoBlocking();
    LMWZ LMNO 6;
    LMWZ P -1;
    Stop;
  }
}

Class WizardBall : Actor
{
  Default
  {
    radius 6;
    height 8;
    speed 22;
    damage 5;
    renderstyle "Add";
    seesound "baron/attack";
    deathsound "eltimp/explode";
    PROJECTILE;
    +NOGRAVITY
    +RANDOMIZE
  }
  states
  {
  Spawn:
    LMBW AB 4 bright;
    loop;
  Death:
    LMBW CDE 6 bright;
    stop;
  }
}
