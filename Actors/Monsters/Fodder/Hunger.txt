Class HungerHorde : Actor
{
	States
	{
	Spawn:
	    TNT1 A 1 Nodelay;
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("PM_Afflicted",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_SpawnItemEx("PM_AffScreamer",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION);
		Stop;

	}
}

Class HungryGroup : Actor
{
	States
	{
	Spawn:
	    TNT1 A 1 Nodelay;
		TNT1 AAAAAA 0 A_SpawnItemEx("PM_Afflicted",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION);
		Stop;

	}
}

Class PM_AffScreamer : PMFodderMonsterBase
{
	int called;
    Default
   {
   PMMonsterBase.CodexLore
   "Afflicted are humans that have gone mad from witnessing the sights of Hell - an all too common result of the invasion.
    Their minds were eroded long ago, any hope of convincing them is forever lost, their thoughts already reverted to its base, primal instincts.
	While all Afflicted wield improvised weapons or just their bare fists to hunt, they all eventually become emaciated as time passes, resorting to nothing but their scrawny, long fingers.
	
	Kill on sight lest one of them attracts a horde from their screams and cries.";
   Health 40;
   GibHealth 20;
   Radius 18;
   Height 56;
   Speed 6;
   PainChance 200;
   SeeSound "Afflicted/Sight";
   PainSound "Afflicted2/Pain";
   DeathSound "Afflicted2/Death";
   ActiveSound "Afflicted/Active";
   Obituary "%o was eaten alive by an Afflicted.";
	 DropItem "Clip";
	 Species "Fodder";
	 Tag "Afflicted";
   Monster;
	 Bloodtype "PM_NormalBloodBase";
	 XScale 0.84;
	 YScale 0.91;
     +FLOORCLIP
	+NOINFIGHTING
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+NOBLOCKMONST
	-ACTIVATEMCROSS
	-CANUSEWALLS
	-CANPUSHWALLS
	-COUNTKILL
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_SmallDemonicTeleportFog";
	 }
  States
  {
  Spawn:
	 ZHUN A 0 NoDelay { called = 0; }
	 ZHUN AB 4 A_Wander();
	 ZHUN AB 0 A_Look;
   TNT1 A 0 A_StartSound("Horrorspawn/Step",5,volume:0.3);
	 ZHUN CD 4 A_Wander();
   ZHUN AB 0 A_Look;
   TNT1 A 0 A_StartSound("Horrorspawn/Step",5,volume:0.3);
	 TNT1 A 0 A_Jump(5,"Grumble");
   Loop;
  Grumble:
	TNT1 A 0 A_StartSound("Afflicted2/Idle");
	Goto Spawn;
  See:
    ZHUN AA 1 A_Chase();
	  ZHUN BB 1 A_Chase();
    TNT1 A 0 A_StartSound("Horrorspawn/Step",5,volume:0.3);
	  ZHUN CC 1 A_Chase();
	  ZHUN DD 1 A_Chase();
    TNT1 A 0 A_StartSound("Horrorspawn/Step",5,volume:0.3);
	  TNT1 A 0 A_Jump(3,"Puke");
    Loop;
  Missile:
	TNT1 A 0
	{ if (called) SetStateLabel("See"); }
	ZHUN T 8 A_FaceTarget;
	TNT1 A 0 A_StartSound("Afflicted/ScreechSummon",10);
	ZHUN U 3 A_FaceTarget;
	ZHUN VWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWVWU 1 A_FaceTarget;
	TNT1 A 0 { for(int i=random(16,20);i>0;i--) A_SpawnItemEx("HungrySeekSpawn");  called = 1; }
	ZHUN C 7 A_FaceTarget;
	Goto See;
  Lunge:
	TNT1 A 0 A_Recoil(-10);
	TNT1 A 0 ThrustThingZ(0,20,0,1);
	Goto See;
  Puke:
	TNT1 A 0 A_CheckSight("See");
	ZHUN H 6;
	TNT1 A 0 A_StartSound("GenericBloodPuke",2);
	TNT1 A 0 A_StartSound("Afflicted/Puke");
	ZHUN BBBBBBBBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(10,-20));
	ZHUN B 1 A_SpawnProjectile("HungerMaggotMissile",42,0,random(6,-6),CMF_AIMDIRECTION,random(4,-4));
	ZHUN BBBBBBBBBBBBBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(20,-10));
	ZHUN HC 4;
	Goto See;
  Melee:
    ZHUN EF 10 A_FaceTarget;
    ZHUN G 8 A_CustomMeleeAttack(random(2,16)*1,"ZombieFodder/Melee");
    Goto See;
  Pain:
    ZHUN H 4;
    TNT1 A 0 A_StopSound(10);
    ZHUN H 4 A_Pain;
	Goto See;
  Death:
    ZHUN I 7;
    TNT1 A 0 A_StopSound(10);
    ZHUN J 7 A_Scream;
    ZHUN K 5 A_NoBlocking;
    ZHUN L 5;
	TNT1 A 0 A_StartSound("Corpse/Fall",CHAN_6);
    ZHUN M -1;
    Stop;
  XDeath:
    ZHUN N 5 PM_Gib();
    ZHUN O 5 A_XScream;
    ZHUN P 5 A_NoBlocking;
    ZHUN QR 5;
    ZHUN S -1;
    Stop;
  }
}

Class HungrySeekSpawn : Actor
{
	Default
	{
	Speed 100;
	Radius 20;
  Height 56;
	Mass 40;
	+THRUACTORS
	MaxStepHeight 64;
	MaxDropOffHeight 64;
	+NOTELEPORT
  }
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { bFRIGHTENED = true; }
		TNT1 AAA 1 A_Chase();
		TNT1 A 0 A_Jump(15,"SpawnLoop");
		Loop;
		SpawnLoop:
		TNT1 A 0 { bFRIGHTENED = false; }
		TNT1 A 5 A_Wander();
		TNT1 A 0 A_CheckSight ("Create");
		TNT1 A 5 A_Wander();
		TNT1 A 0 A_CheckSight ("Create");
		TNT1 A 0 A_Jump(5,"Die");
		Loop;
	Create:
		TNT1 A 0 A_SpawnItemEx("PM_Afflicted");
		TNT1 A 0 A_StartSound("Afflicted/Spawn");
		Stop;
	Die:
		Stop;
	}
}

Class PM_Afflicted : PMFodderMonsterBase
{
	int choose;
	int oldspeed;
	bool grouped;
	Default
	{
    Health 20;
    GibHealth 20;
    Radius 18;
    Height 56;
    Speed 10;
    PainChance 200;
    SeeSound "Afflicted/Sight";
    PainSound "Afflicted/Pain";
    DeathSound "Afflicted/Death";
    ActiveSound "Afflicted/Active";
    Obituary "%o was consumed by a Horrorspawn.";
	DropItem "Clip";
	Species "Fodder";
	Tag "Afflicted";
    Monster;
	Bloodtype "PM_NormalBloodBase";
    +FLOORCLIP
	+NOINFIGHTING
	+THRUSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+NOBLOCKMONST
	-Countkill
	-ACTIVATEMCROSS
	-CANUSEWALLS
	-CANPUSHWALLS
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_SmallDemonicTeleportFog";
  }

	void PM_AfflictedAssignSprite()
	{
    switch(choose)
     {
    case 0: sprite = GetSpriteIndex("ZHU1"); break;
    case 1: sprite = GetSpriteIndex("SCZA"); break;
    case 2: sprite = GetSpriteIndex("SCZC"); break;
    case 3: sprite = GetSpriteIndex("ZHUN"); break;
	case 4: sprite = GetSpriteIndex("AF_F"); break;
     }
	}
	
	void PM_AffChase()
	{
	 if(grouped)
	 A_Recoil(-0.65);
	 A_Chase();
	}

	override void PostBeginPlay()
	{ Super.PostBeginPlay();
	  choose = random(0,4);
	  oldspeed = speed;
	  PM_AfflictedAssignSprite();
	  if(choose == 3)
	  {
	  Scale.X = 0.84;
	  Scale.Y = 0.91;
	  PainSound = "Afflicted2/Pain";
	  DeathSound = "Afflicted2/Death";
	  }
	  if(choose == 4)
	  {
	  PainSound = "FAfflicted/Pain";
	  DeathSound = "FAfflicted/Death";
	  }
	}
  States
  {
  LoadSprites:
	ZHU1 ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
	SCZA ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
	SCZC ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
	ZHUN ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
	AF_F ABCDEFGHIJKLMNOPQRSTUVWXYZ 0;
  Spawn:
	"####" AB 4 A_Wander();
	"####" AB 0 A_Look;
  "####" A 0 A_StartSound("Afflicted/WalkStep",5);
	"####" CD 4 A_Wander();
  "####" AB 0 A_Look;
  "####" A 0 A_StartSound("Afflicted/WalkStep",5);
	"####" A 0 A_Jump(5,"Grumble");
    Loop;
  Grumble:
	"####" A 0 { if(choose == 3) 
	A_StartSound("Afflicted2/Idle");
	if(choose == 4)
	A_StartSound("FAfflicted/Idle");
	else
	A_StartSound("Afflicted/Idle"); }
	Goto See;
  See:
  "####" A 0 
  {
  if(CheckProximity("PM_Afflicted",450,5))
  {
   grouped = true;
   speed = random(7,10);
  }
  else
  {
   grouped = false; 
   speed = oldspeed;
  }
   }
  "####" AABB 2 PM_AffChase();
  "####" A 0 A_StartSound("Afflicted/RunStep",5);
  "####" CCDD 2 PM_AffChase();
  "####" A 0 A_StartSound("Afflicted/RunStep",5);
  "####" A 0 A_Jump(8,"Puke","Howl");
  //TNT1 A 0 A_Jump(2,"Lunge")
   Loop;
  Lunge:
	"####" A 0 A_Recoil(-10);
	"####" A 0 ThrustThingZ(0,20,0,1);
	Goto See;
  Puke:
	"####" A 0 A_CheckSight("See");
	"####" G 6;
	"####" A 0 A_StartSound("GenericBloodPuke",2);
	"####" A 0 A_StartSound("Afflicted/Puke",13);
	"####" BBBBBBBBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(10,-20));
	"####" B 1 A_SpawnProjectile("HungerMaggotMissile",42,0,random(6,-6),CMF_AIMDIRECTION,random(4,-4));
	"####" BBBBBBBBBB 1 A_SpawnProjectile("HungerBloodSpray",42,0,random(-25,25),CMF_AIMDIRECTION,random(10,-20));
	"####" GD 4;
	Goto See;
  Howl:
	"####" A 0 A_CheckSight("See");
  "####" D 10 A_FaceTarget;
	"####" A 0 { if(choose == 4)
	A_StartSound("FAfflicted/Howl",15,CHANF_OVERLAP);
	else
	A_StartSound("Afflicted/Howl",15,CHANF_OVERLAP); }
	"####" A 20 A_FaceTarget;
	"####" D 10 A_FaceTarget;
	Goto See;
  Melee:
    "####" EF 10 A_FaceTarget;
    "####" F 8 A_CustomMeleeAttack(random(2,16)*1,"ZombieFodder/Melee");
    Goto See;
  Pain:
    "####" A 0 { if(choose == 3)
	SetStateLabel("CorpsePain"); }
    "####" G 4;
    "####" G 4 A_Pain;
	Goto See;
  CorpsePain:
    ZHUN H 4;
	ZHUN H 4 A_Pain;
	Goto See;
  XDeath:
  Death:
	"####" A 0;
	"####" A 0 { if(choose == 0)
	SetStateLabel("SoldierDeath");
	if(choose == 3)
	SetStateLabel("CorpseDeath"); }
  "####" H 7;
  "####" I 7 A_Scream;
  "####" J 5 A_NoBlocking;
  "####" K 5;
	"####" A 0 A_StartSound("Corpse/Fall",6);
  "####" L 4;
	"####" M 4;
  "####" N -1;
    Stop;
  SoldierDeath:
   ZHU1 F 7;
   ZHU1 G 7 A_Scream;
   ZHU1 H 5 A_NoBlocking;
   TNT1 A 0 A_StartSound("Corpse/Fall",6);
   ZHU1 I 5;
   ZHU1 J -1;
   Stop;
  CorpseDeath:
   ZHUN I 7;
   ZHUN J 7 A_Scream;
   ZHUN K 5 A_NoBlocking;
   TNT1 A 0 A_StartSound("Corpse/Fall",6);
   ZHUN L 5;
   ZHUN M -1;
   Stop;
  }
}

Class HungerBloodPuke : Actor
{
	Default
	{
  DamageFunction 0;
  Height 1;
  Radius 1;
  Speed 6;
  Mass 1;
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  Scale 0.16;
  Gravity 0.8;
  PROJECTILE;
  -NOGRAVITY
  Decal "BloodSplat1";
  }
  States
  {
  Spawn:
  TNT1 A 0 A_SetRoll(random(0,360));
  Go:
	FBLD ABCD 3;
	Loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("BloodPukeSplatter",random(-3,3),random(-3,3));
  Stop;
  }
}

Class HungerBloodSpray : Actor
{
	Default
	{
  DamageFunction 0;
  Height 1;
  Radius 1;
  Speed 6;
  Mass 1;
  +FORCEXYBILLBOARD
  +CLIENTSIDEONLY
  +ROLLSPRITE
  +THRUACTORS
  Scale 0.07;
  Gravity 0.8;
  PROJECTILE;
  -NOGRAVITY
  Species "PukeMonster";
  Decal "BloodSplat1";
  }
  States
  {
  Spawn:
  TNT1 A 0 A_SetRoll(random(0,360));
  Go:
	FBLD AABBCCDD 1
	{
	A_SetScale(Scale.X+0.05);
	A_FadeOut(0.05);
	}
	Stop;
  Death:
	TNT1 A 0 A_SpawnItemEx("BloodPukeSplatter",random(-3,3),random(-3,3));
    Stop;
  }
}

Class HungerMaggotMissile : Actor
{
	Default
	{
  DamageFunction 3;
  Height 1;
  Radius 1;
  Speed 6;
  Mass 1;
  +FORCEXYBILLBOARD
  Gravity 0.8;
  PROJECTILE;
  -NOGRAVITY
  Decal "BloodSplat1";
  }
  States
  {
  Spawn:
	WAGO ABABABABABABABAB 1;
	Loop;
  Death:
    TNT1 A 0 A_SpawnItemEx("LargeWhiteMaggot");
    Stop;
  }
}