ACTOR IsOnCeiling : Inventory
{
Inventory.MaxAmount 1
}

actor Pustule : RandomSpawner 14000
{
    DropItem "Pustule1"
    DropItem "Pustule2"
    DropItem "Pustule3"
	DropItem "Pustule4"
	DropItem "Pustule5"
	DropItem "Pustule6"
	DropItem "Pustule7"
}

actor Pustule1
{
  obituary "%o was popped by a Pustule."
  health 65
  radius 20
  height 24
  mass 100
  speed 3
  scale 0.8
  PainChance 256
  Meleerange 58
  Tag "Pustule"
  seesound "blob/sight"
  painsound "blob/pain"
  activesound "blob/active"
  MONSTER
  Bloodtype "PM_NormalBloodBase"
  +FLOORCLIP
  +LOOKALLAROUND
  -Countkill
  -solid
  +shootable
  +NOINFIGHTING
  +ROLLSPRITE
  +ROLLCENTER
  -CANUSEWALLS
  -CANPUSHWALLS
  states
  {
  Spawn:
    FLOB GGGG 3 A_Wander
	TNT1 A 0 A_Look
	FLOB BBBB 3 A_Wander
	TNT1 A 0 A_Look
	FLOB CCCC 3 A_Wander
	TNT1 A 0 A_Look
	FLOB BBBB 3 A_Wander
	TNT1 A 0 A_Look
    loop
  See:
	TNT1 A 0
    TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
    FLOB GGGGBBBBCCCCBBBB 2 A_Chase
	TNT1 A 0 A_Jump(140,"Yuck")
	TNT1 A 0 A_Jump(50,"CeilingJump")
	TNT1 A 0 A_Jump(90,"Flop","SlideForward","SlideBack","SlideLeft","SlideRight")
    loop
	
  CeilingJump:
	TNT1 A 0 A_JumpIf(CallACS("GetActorZDist") <= 80, 2)
	TNT1 AAA 0
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "See")
    FLOB DE 4
	TNT1 A 0 ThrustThingZ(0,60,0,1) 
	//TNT1 A 0 A_PlaySound("MAMAMIA")	//I will always remember you. F
	FLOB EEEE 2 A_CheckCeiling("CeilingLatch")
	TNT1 A 0 ThrustThingZ(0,45,0,1)
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
	FLOB FFF 2 A_CheckCeiling("CeilingLatch")
	TNT1 A 0 ThrustThingZ(0,45,0,1)
	FLOB EEEE 3 A_CheckCeiling("CeilingLatch")
	FLOB EEEEEEE 3 A_CheckFloor("See")
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
	Goto See
	
	CeilingLatch:
	TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
	TNT1 A 0 A_ChangeFlag("THRUSPECIES", 1)
	//TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 1)
	TNT1 A 0 A_ChangeFlag("NOVERTICALMELEERANGE", 1)
	TNT1 A 0 A_GiveInventory ("IsOnCeiling", 1)
	FLOB EEDDD 3 ThrustThingZ(0,500,0,1) //1
	Goto CeilingCrawl
	
	CeilingCrawl:
		TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce")
		TNT1 A 0 A_JumpIf(velz > 250,"CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		FLOB CCCCCCC 2 A_Chase(0,0,CHF_NOPLAYACTIVE)
		TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-6,6),random(-6,6),0,0,0,0,0,0,125)
		FLOB BBBBBBB 2 A_Chase(0,0,CHF_NOPLAYACTIVE)
		TNT1 A 0 A_Recoil(-0.05)
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		TNT1 A 0 A_JumpIf(velz > 250, "CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce")
		TNT1 A 0 A_JumpIf(velz > 250, "CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 A_JumpIfCloser(50,"CeilingStrike",True)
		TNT1 A 0 A_Jump(5,"CeilingDrop")
		Loop
		
     CeilingStrike:
		TNT1 A 0
		FLOB BBB 5 A_SpawnItemEx("SkullBloodDrip",random(-6,6),random(-6,6),0,0,0,0,0,0)
		TNT1 A 0 A_PlaySound("PUSWAL")
		FLOB BBB 1
		TNT1 A 0 A_TakeInventory ("IsOnCeiling")
		TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 0)
		FLOB G 2 A_FaceTarget
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		Goto CeilingDropping
		
	CeilingDrop:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory ("IsOnCeiling")
		TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 0)
		FLOB G 2 A_FaceTarget
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
	CeilingDropping:
		FLOB DD 1 A_JumpIf (momz == 0, "CeilingLanding")
		loop
	CeilingLanding:
		TNT1 A 0 A_PlaySound("Blob/Slosh")
		FLOB BBBBBB 0 A_SpawnItemEx("MaliceBloodSplash",2,0,0,random(2,6),random(-2,2),random(-2,7))
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		FLOB BBBB 3 A_FaceTarget
		Goto See
		
	CeilingPounce: 
		TNT1 AAA 0
		TNT1 A 0 A_TakeInventory ("IsOnCeiling")
		TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 0)
		TNT1 A 0 A_ChangeFlag("THRUSPECIES", 0)
		TNT1 A 0 A_Recoil (-18)
		TNT1 A 0 A_FaceTarget
		FLOB BBCC 2
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		FLOB GGBB 2
		Goto CeilingDropping		

  Yuck:
	TNT1 A 0 A_PlaySound("PUSWAL")
	Goto See
  Flop:
	FLOB B 15
	FLOB A 0 A_Recoil(-11)
	TNT1 A 0 ThrustThingZ(0,32,0,1)
	FLOB A 17
	TNT1 A 0 A_PlaySound("Blob/Slosh")
	FLOB BBB 1 A_SpawnItemEx("MaliceBloodSplash",2,0,0,random(2,6),random(-2,2),random(-2,7))
	FLOB B 8
	Goto See
  SlideForward:
	TNT1 A 0 ThrustThingZ(0,5,0,1)
	FLOB A 0 A_Recoil(-14)
	TNT1 A 0 A_PlaySound("Blob/Slosh")
	FLOB B 20
	Goto See
  SlideBack:
	TNT1 A 0 ThrustThingZ(0,5,0,1)
	FLOB A 0 A_Recoil(14)
	TNT1 A 0 A_PlaySound("Blob/Slosh")
	FLOB B 20
	Goto See
  SlideLeft:
	TNT1 A 0 ThrustThingZ(0,5,0,1)
	FLOB A 0 ThrustThing(angle*256/360 +64, 12, 0, 0)
	TNT1 A 0 A_PlaySound("Blob/Slosh")
	FLOB B 20
	Goto See
  SlideRight:
	TNT1 A 0 ThrustThingZ(0,5,0,1)
	FLOB A 0 ThrustThing(angle*256/360 +192, 12, 0, 0)
	TNT1 A 0 A_PlaySound("Blob/Slosh")
	FLOB B 20
	Goto See
  Pain:
	TNT1 A 0
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
	TNT1 A 0 A_ChangeFlag("NOGRAVITY",False)
    FLOB A 1 A_Pain
    FLOB A 8
	TNT1 A 0 A_Jump(50,"Flop","SlideForward","SlideBack","SlideLeft","SlideRight","CeilingJump")
    goto See
  Melee:
	TNT1 A 0 A_ChangeFlag("NOPAIN",True)
	FLOB A 0 A_PlaySound("blob/active")
	FLOB GGBBCCBBGGBBCCBBGGBBC 1
	{
	A_SetScale(ScaleX+0.02);
	A_SpawnItemEx("MaliceBloodSplash",random(-5,5),random(-5,5),random(-5,5),random(2,6),random(-2,2),random(-2,7),0,SXF_NOCHECKPOSITION);
	}
    FLOB A 0 A_Die
  Death:
    FLOB F 2 A_PlaySound ("Blob/Explode")
	TNT1 A 0 A_SpawnItemEx("PM_TumorBloodMist",0,0,3,0,0,0,0,SXF_NOCHECKPOSITION)
    FLOB F 0 A_SpawnItem("PustuleDamage")
    FLOB FFFFFFFFFFFFFFFFFFFFFFFF 0 A_SpawnItemEX("PM_TumorBurstBlood", 0, 0, 0, random (4, 15), random(4,15), random (4, 15), random(0, 360),SXF_NOCHECKPOSITION)
    FLOB GH 2
    stop
  }
}

//smaller
actor Pustule2 : Pustule1
{
  scale 0.7
}

//larger
actor Pustule3 : Pustule1
{
  scale 0.6
}

actor Pustule4 : Pustule1
{
  scale 0.4
}

actor Pustule5 : Pustule1
{
  scale 1
}

actor Pustule6 : Pustule1
{
  scale 0.5
}

actor Pustule7 : Pustule1
{
  scale 0.45
}

actor PustuleDamage
{
  Radius 20
  Height 24
  +NOINTERACTION
  obituary "%o was popped by a pustule."
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1 A_Explode (35, 110, 0, 1, 1)
    stop
  }
}

actor BursterBlood
{
  Radius 2
  Height 2
  Speed 1
  Damage 0
  Decal "BloodSplat"
  Projectile
  -NOGRAVITY
  +THRUACTORS
  States
  {
  Spawn:
    TNT1 A 1 A_SpawnItemEX("BursterBloodTrail", random(-2, 2), 0, random(-2, 2), 0, 0, 0, random(0, 360))
    loop
  Death:
    TNT1 A 4
    stop
  }
}

actor BursterBloodTrail
{
  Radius 2
  Height 2
  +NOINTERACTION
  +CLIENTSIDEONLY 
  States
  {
  Spawn:
    BLUD CBA 3
    stop
  }
}
