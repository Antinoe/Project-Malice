Class IsOnCeiling : Inventory {
  default {
    Inventory.MaxAmount 1;
  }
}

Class Pustule : PMFodderMonsterBase {
  bool isOnCeiling;
  default {
	PMMonsterBase.CodexLore
	"Piles of viscera given life from prolonged exposure to ambient evil, these sloshing teratomas serve as Hell's fleshy landmines, creeping up on anything that breathes.
	 Their explosions are the result of sudden pressure buildup until the fragile body can't handle it, releasing a disgusting mix of acidic blood, sinew and bone.
	 
	 Watch the floors and ceiling, otherwise they can be easily avoided by simply moving away from the Pustule before it bursts.";
    Obituary "%o was popped by a Pustule.";
    Health 65;
    Radius 20;
    Height 24;
    Mass 100;
    Speed 3;
	FastSpeed 6;
    Scale 1;
    PainChance 256;
    Meleerange 58;
    Tag "Pustule";
    Species "Fodder";
    SeeSound "blob/sight";
    PainSound "blob/pain";
    ActiveSound "blob/active";
    Monster;
    Bloodtype "PM_NormalBloodBase";
    +FLOORCLIP
    +LOOKALLAROUND
    -Countkill
    -solid
    +shootable
    +NOINFIGHTING
    +ROLLSPRITE
    +ROLLCENTER
    -CANUSEWALLS
    -CANPUSHWALLS
    TeleFogSourceType "PM_SmallDemonicTeleportFog";
    TeleFogDestType "PM_SmallDemonicTeleportFog";
  }

  override void PostBeginPlay() {
    scale.x = frandompick(0.8, 1, 0.7, 0.6, 0.4, 0.5, 0.45);
    scale.y = scale.x;
	super.PostBeginPlay();
  }
  
  void PM_PustuleChase()
	{
	 if(target)
	 {
	  if(CheckSight(target) && Distance3D(target) <= 1200)
	  A_Chase();
	  else
	  A_Wander();
	 }
	}

  states {
    Spawn:
      FLOB GGGG 3 A_Wander();
	  TNT1 A 0 A_Look();
	  FLOB BBBB 3 A_Wander();
	  TNT1 A 0 A_Look();
	  FLOB CCCC 3 A_Wander();
	  TNT1 A 0 A_Look();
	  FLOB BBBB 3 A_Wander();
	  TNT1 A 0 A_Look();
      loop;
    See:
	  TNT1 A 0;
      TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
      FLOB GGGGBBBBCCCCBBBB 2 PM_PustuleChase();
	  TNT1 A 0 A_Jump(140,"Yuck");
	  TNT1 A 0 A_Jump(50,"CeilingJump");
	  TNT1 A 0 A_Jump(90,"Flop","SlideForward","SlideBack","SlideLeft","SlideRight");
      loop;

    CeilingJump:
  	  TNT1 A 0 A_JumpIf(CallACS("GetActorZDist") <= 80, 2);
	  TNT1 AAA 0;
	  TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "See");
      FLOB DE 4;
	  TNT1 A 0 ThrustThingZ(0,60,0,1) ;
	  //TNT1 A 0 A_PlaySound("MAMAMIA")	//I will always remember you. F
	  FLOB EEEE 2 A_CheckCeiling("CeilingLatch");
	  TNT1 A 0 ThrustThingZ(0,45,0,1);
	  TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	  FLOB FFF 2 A_CheckCeiling("CeilingLatch");
	  TNT1 A 0 ThrustThingZ(0,45,0,1);
	  FLOB EEEE 3 A_CheckCeiling("CeilingLatch");
	  FLOB EEEEEEE 3 A_CheckFloor("See");
	  TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	  Goto See;

  	CeilingLatch:
      TNT1 A 0
	  {
	  bNOGRAVITY = true;
	  bTHRUSPECIES = true;
	  bNOVERTICALMELEERANGE = true; 
	  }
	  //TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 1)
	  TNT1 A 0 {isOnCeiling = true;} 
      FLOB EEDDD 3 ThrustThingZ(0,500,0,1); //1;
	  Goto CeilingCrawl;

  	CeilingCrawl:
      TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	  TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
	  TNT1 A 0 A_JumpIf(vel.z > 250,"CeilingPounce");
	  TNT1 A 0 A_CheckFloor("CeilingDrop");
	  TNT1 A 0 ThrustThingZ(0,500,0,1);
	  FLOB CCCCCCC 2 A_Chase(null,null,CHF_NOPLAYACTIVE);
	  TNT1 A 0 A_SpawnItemEx("SkullBloodDrip",random(-6,6),random(-6,6),0,0,0,0,0,0,125);
	  FLOB BBBBBBB 2 A_Chase(null,null,CHF_NOPLAYACTIVE);
	  TNT1 A 0 A_Recoil(-0.05);
	  TNT1 A 0 ThrustThingZ(0,500,0,1);
	  TNT1 A 0 A_JumpIf(vel.z > 250, "CeilingPounce");
      TNT1 A 0 A_CheckFloor("CeilingDrop");
	  TNT1 A 0 ThrustThingZ(0,500,0,1);
	  TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
	  TNT1 A 0 A_JumpIf(vel.z > 250, "CeilingPounce");
	  TNT1 A 0 A_CheckFloor("CeilingDrop");
	  TNT1 A 0 A_JumpIfCloser(50,"CeilingStrike",True);
	  TNT1 A 0 A_Jump(5,"CeilingDrop");
	  Loop;
		
     CeilingStrike:
	  TNT1 A 0;
	  FLOB BBB 5 A_SpawnItemEx("SkullBloodDrip",random(-6,6),random(-6,6),0,0,0,0,0,0);
	  TNT1 A 0 A_StartSound("PUSWAL");
	  FLOB BBB 1;
	  TNT1 A 0 {isOnCeiling=false;}
	  TNT1 A 0 
	  {
	  bNOGRAVITY = false;
	  bCEILINGHUGGER =  false;
	  }
	  FLOB G 2 A_FaceTarget();
	  TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	  Goto CeilingDropping;
		
	CeilingDrop:
	  TNT1 A 0;
	  TNT1 A 0 {isOnCeiling=false;}
	  TNT1 A 0 
	  {
	  bNOGRAVITY = false;
	  bCEILINGHUGGER =  false;
	  }
	  FLOB G 2 A_Face(target);
	  TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	CeilingDropping:
	  FLOB DD 1 A_JumpIf (vel.z == 0, "CeilingLanding");
	  loop;
	CeilingLanding:
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB BBBBBB 0 A_SpawnItemEx("MaliceBloodSplash",2,0,0,random(2,6),random(-2,2),random(-2,7));
	  TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	  FLOB BBBB 3 A_Face(target);
	  Goto See;
		
	CeilingPounce: 
	  TNT1 AAA 0;
	  TNT1 A 0 {isOnCeiling=true;}
	  TNT1 A 0 
	  {
	  bNOGRAVITY = false;
	  bCEILINGHUGGER = false;
	  bTHRUSPECIES = false;
	  }
	  TNT1 A 0 A_Recoil (-18);
	  TNT1 A 0 A_Face(target);
	  FLOB BBCC 2;
	  TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	  FLOB GGBB 2;
	  Goto CeilingDropping;		

    Yuck:
	  TNT1 A 0 A_StartSound("PUSWAL");
	  Goto See;
    Flop:
	  FLOB B 15;
	  FLOB A 0 A_Recoil(-11);
	  TNT1 A 0 ThrustThingZ(0,32,0,1);
	  FLOB A 17;
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB BBB 1 A_SpawnItemEx("MaliceBloodSplash",2,0,0,random(2,6),random(-2,2),random(-2,7));
	  FLOB B 8;
	  Goto See;
    SlideForward:
	  TNT1 A 0 ThrustThingZ(0,5,0,1);
	  FLOB A 0 A_Recoil(-14);
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB B 20;
	  Goto See;
    SlideBack:
	  TNT1 A 0 ThrustThingZ(0,5,0,1);
	  FLOB A 0 A_Recoil(14);
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB B 20;
	  Goto See;
    SlideLeft:
	  TNT1 A 0 ThrustThingZ(0,5,0,1);
	  FLOB A 0 ThrustThing( int(ceil(angle*256/360 +64)), 12, 0, 0);
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB B 20;
	  Goto See;
    SlideRight:
	  TNT1 A 0 ThrustThingZ(0,5,0,1);
	  FLOB A 0 ThrustThing( int(ceil(angle*256/360 +192)) , 12, 0, 0);
	  TNT1 A 0 A_StartSound("Blob/Slosh");
	  FLOB B 20;
	  Goto See;
    Pain:
	  TNT1 A 0;
	  TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	  TNT1 A 0 { bNOGRAVITY = false; }
      FLOB A 1 A_Pain;
      FLOB A 8;
	  TNT1 A 0 A_Jump(50,"Flop","SlideForward","SlideBack","SlideLeft","SlideRight","CeilingJump");
      goto See;
    Melee:
	  TNT1 A 0 { bNOPAIN = true; }
	  FLOB A 0 A_StartSound("blob/active");
	  FLOB GGBBCCBBGGBBCCBBGGBBC 1
	  {
	    A_SetScale(Scale.X+0.02);
	    A_SpawnItemEx("MaliceBloodSplash",random(-5,5),random(-5,5),random(-5,5),random(2,6),random(-2,2),random(-2,7),0,SXF_NOCHECKPOSITION);
	  }
      FLOB A 0 A_Die();
    Death:
      FLOB F 2 A_StartSound("Blob/Explode");
	  TNT1 A 0 A_SpawnItemEx("PM_TumorBloodMist",0,0,3,0,0,0,0,SXF_NOCHECKPOSITION);
      FLOB F 0 A_SpawnItem("PustuleDamage");
      FLOB FFFFFFFFFFFFFFFFFFFFFFFF 0 A_SpawnItemEX("PM_TumorBurstBlood", 0, 0, 0, random (4, 15), random(4,15), random (4, 15), random(0, 360),SXF_NOCHECKPOSITION);
	  FLOB FFFFFF 0 A_SpawnItemEX("PM_NormalMeatChunkLauncher", 0, 0, 0, random (4, 15), random(4,15), random (4, 15), random(0, 360),SXF_NOCHECKPOSITION,70);
      FLOB GH 2;
      stop;
  }
}

Class PustuleDamage : Actor {
  Default {
    Radius 20;
    Height 24;
    +NOINTERACTION
    Obituary "%o was popped by a Pustule!";
  }
  States {
    Spawn:
      TNT1 A 0;
      TNT1 A 1 A_Explode(35, 110, 0, 1, 1);
      stop;
  }
}

Class BursterBlood : Actor {
  Default {
    Radius 2;
    Height 2;
    Speed 1;
    Damage 0;
    Decal "BloodSplat";
    Projectile;
    -NOGRAVITY
    +THRUACTORS
  }
  States {
    Spawn:
      TNT1 A 1 A_SpawnItemEx("BursterBloodTrail", random(-2, 2), 0, random(-2, 2), 0, 0, 0, random(0, 360));
      loop;
    Death:
      TNT1 A 4;
      stop;
  }
}

Class BursterBloodTrail : Actor {
  Default {
    radius 2;
    height 2;
    +NOINTERACTION
    +CLIENTSIDEONLY
  }
  States {
    Spawn:
      BLUD CBA 3;
      stop;
  }
}
