Class PM_Rotfly : PMFodderMonsterBase
{
  Default
  {
  PMMonsterBase.CodexLore
  "Rotflies are huge insects of pestilence that take care of decomposition in a faster way by spewing corrosive acid at carcasses and corpses.
  Hell Energy has, for some reason, taken advantage of pestilence too.
  
  Usually harmless, but disgusting. They usually avoid you by dodging and spits acid to make the rotfly have a delicious feast.";
  Health 20;
  Radius 14;
  Height 13;
  Speed 7;
  Mass 10;
  XScale 1.1;
  YScale 0.95;
  PainChance 256;
  Monster;
  Species "Fodder";
  BloodColor "DarkGreen";
  BloodType "PM_DarkGreenBloodBase";
  ActiveSound "Rotfly/Active";
  SeeSound "Rotfly/See";
  PainSound "Rotfly/Pain";
  DeathSound "Rotfly/Death";
  Tag "Rotfly";
  Obituary "%o was somehow killed by a Rotfly.";
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
  TeleFogDestType "PM_SmallDemonicTeleportFog";
  +FLOAT
  +NOGRAVITY
  -COUNTKILL
  +DONTHARMSPECIES
  -CANUSEWALLS
  -CANPUSHWALLS
  }
  
  States
  {
  Spawn:
	BFLY A 5 PM_Look;
  Look:
    BFLY ABABABAB 2
	{
	A_Wander();
	PM_LookingForPlayer();
	}
	Loop;
  Spotted:
	TNT1 A 0 PM_AlertSound();
	TNT1 A 0 ThrustThingZ(0,6,0,1);
  Go:
	TNT1 A 0 A_SetSpeed(7);
	BFLY ABABABABABABAB 1 A_Chase;
	TNT1 A 0 A_Jump(86, "FlySpawn", "NotScared", "Scared", "FlySpawn");
	TNT1 A 0 A_JumpIfCloser(400,"StandoffStart");
	Loop;
  StandoffCheck:
	TNT1 A 0 A_Jump(50, "StandoffStart");
	Goto Go;
  StandoffStart:
	TNT1 A 0 A_CheckSight("Go");
	TNT1 A 0 A_SetSpeed(0);
	TNT1 A 0 A_JumpIfCloser(100,"Backup");
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	BFLY ABABAB 2
	{
	A_Chase("Scared","StandoffSpit");
	A_FaceTarget();
	}
	TNT1 A 0 A_Jump(130,"RandomDodge");
	TNT1 A 0 A_Jump(6,"Scared");
	Loop;
  Backup:
	TNT1 A 0 A_Recoil(8);
	BFLY ABABABABAB 2;
	TNT1 A 0 A_Jump(40,"Scared");
	Goto StandoffStart;
  RandomDodge:
	TNT1 A 0
	{
		ThrustThing(random(0,360),10);
		ThrustThingZ(0,7,random(0,1),1);
	}
	BFLY ABABAB 2 A_FaceTarget;
	Goto StandoffStart;
  Pain:
	BFLY A 1 A_Pain;
	BFLY BB 2;
	BFLY ABBA 2;
	Goto Scared;
  StandoffSpit:
	TNT1 A 0 A_StartSound("Rotfly/Attack");
	BFLY ABABABAB 1 A_Recoil(1);
	BFLY ABABABAB 1 A_Recoil(-3);
	TNT1 A 0 A_SpawnProjectile("PM_RotflySpit",2);
	Goto StandoffStart;
  Dodge:
	TNT1 A 0 ThrustThing( int(ceil( angle*256/360 + randompick(64, 192) )), 12, 0, 0);
	BFLY ABAB 5 A_FaceTarget;
	Goto StandoffStart;
  FlySpawn:
	BFLY ABABAB 1 A_FaceTarget();
	BFLY A 1 
	{
	A_SpawnItemEx("PM_RotflyMaggot",10,0,5,0,0,0);
	A_SpawnItemEx("PM_RotflySplash",0,0,0,0,0,-1);
	}
	BFLY BABABA 1;
	Goto Go;
  Scared:
	TNT1 A 0
	{
		bFRIGHTENED = true;
		A_ChangeVelocity(random(-1,1),random(-1,1),random(0,1));
	}
	Goto Go;
  NotScared:
	TNT1 A 0 A_Jump(70,"Scared");
	TNT1 A 0 { bFRIGHTENED = false; }
	Goto Go;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_Scream;
	TNT1 A 0 A_StartSound("FlyHit/Hit",0,1);
	TNT1 AAAA 0 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-3,3),random(-3,3),random(-3,3));
	BFLY C 10 A_NoBlocking;
	BFLY D -1;
	Stop;
  }
}

Class PM_RotflyMaggot : PMFodderMonsterBase
{
	Default
	{
	PMMonsterBase.CodexLore
	"Sometimes larvae get cursed with oddities too, making them capable of giving birth to insects.
	Even though it belongs in the fodder section, it might as well have some affiliation to the first apocalypse of Hell, Pestilence.
	
	They have a high chance of dying, so it serves as a lesser fodder threat.";
	Health 1;
	Radius 2;
	Height 2;
	Mass 100;
	Speed 3;
	Scale 0.95;
	PainChance 64;
	MeleeRange 12;
	GibHealth 100;
	Species "Fodder";
	Obituary "%o was nibbled by a Maggot.";
	Tag "Impregnated Maggot";
	MONSTER;
    DamageFactor "Caco", 0;
	+NOBLOOD
	+DONTHARMSPECIES
	+DONTHARMCLASS
	+DONTHARMSPECIES
    +NOINFIGHTING
	+NOTARGET
	+THRUSPECIES
	+LOOKALLAROUND
	-SOLID
	+FRIGHTENED
	-CANUSEWALLS
	-COUNTKILL
	+CANTSEEK
	}
	
	States
	{
	Spawn:
		WAGO B 0;
		Goto Idle;
	Idle:
		WAGO A 40 
		{
		A_Look();
		A_Wander();
		}
		WAGO B 40
		{
		A_Look();
		A_Wander();
		}
	See:
		WAGO AAAAAAA 4;
		WAGO A 0 { bTOUCHY = true; }
		WAGO ABABABABABABABABABABABABABABABABABABAB 5 A_Chase;
		Goto Grow;
	Melee:
		TNT1 A 0 A_Jump(60,"Death");
    	WAGO A 20 A_FaceTarget;
    	WAGO B 3 A_CustomMeleeAttack(1);
    	Goto See;
	Grow:
		WAGO B 1 A_SetScale(1.3);
		WAGO B 40;
		TNT1 AAA 0 A_SpawnItemEx("PM_RotflySplash",zvel:random(0,1),flags:SXF_NOCHECKPOSITION);
		TNT1 A 0 A_SpawnItemEx("PM_RotflyYoungling",zvel:1);
		Stop;
	Pain:
		TNT1 A 0 A_Die();
		Goto Death;
	Death:
		TNT1 A 0 A_StartSound("maggdie");
		WAGO CD 2;
		WAGO E 300;
	Fade:
		WAGO E 5 A_FadeOut(0.05);
		Loop;
	Death2:
		WAGO BBAA 1;
		Stop;

	}
}

Class PM_RotflySpit : DoomImpBall
{
	Default
	{
	DamageFunction 6;
	SeeSound "";
	DeathSound "";
	Speed 12;
	FastSpeed 12;
	Scale 0.93;
	}
	
	States
  	{
	Spawn:
		TNT1 A 2 A_SpawnItemEx("PM_RotflySmallSplash",flags:128);
		Loop;
	Death:
		TNT1 A 0;
		TNT1 AAAAAA 0 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-2,2),random(-2,2),random(0,3));
		Stop;
	}
}

Class PM_RotflyYoungling : PMFodderMonsterBase
{
  Default
  {
  PMMonsterBase.CodexLore
  "Rotflies are capable of having offspring, thus resulting in more pests.
  
  Weaker compared to its adult brethren and doesn't attack, but more agile. Not too much of a threat neither.";
  Health 1;
  Speed 3;
  Mass 10;
  Scale 2.2;
  Monster;
  MeleeRange 30;
  Species "Fodder";
  ActiveSound "Flying/Fly";
  Tag "Rotfly Youngling";
  -CANUSEWALLS
  -SOLID
  -COUNTKILL
  +BOUNCEONWALLS
  +Touchy
  +FLOAT
  +NOGRAVITY
  +DONTHARMSPECIES
  +NOBLOOD
  +CANTSEEK
  Obituary "%o was no match for the Rot Fly's children.";
  }
  
  States
  {
  Spawn:
	FLYA A 0;
	Goto Idle;
  Idle:
    SFLY ABCBAC 2 A_Wander;
	SFLY A 1 A_Look;
	Loop;
  See:
	SFLY ABCBACABCBACABCBACABCBACABCBACABCBACABCBACABCBAC 1 A_FaceTarget;
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	TNT1 A 0 A_ChangeVelocity(random(-1,1),random(-1,1),random(0,1));
  See2:
	SFLY ABCBACABCBACABCBACABCBACABCBACABCBAC 1 A_Chase;
	TNT1 A 0 A_Jump(15,"Leave");
	Loop;
  Melee:
	SFLY ABCBACABCBACABCBAC 1;
	TNT1 A 0 A_StartSound("FlyHit/Hit",11);
	TNT1 A 0 A_Facetarget;
	SFLY B 1 A_CustomMeleeAttack(1);
	SFLY ABCBACABCBACABCBA 1;
	Goto See2;
  Death:
	TNT1 A 0 A_StartSound("FlyHit/Hit",0,1);
	SFLY B 1 A_SpawnItemEx("PM_RotflySplash",0,0,0,random(-4,4),random(-4,4),random(-4,0));
	Stop;
  Leave:
	TNT1 A 1;
	Stop;
  }
}

Class PM_AmbushRotfly : Actor 
{
	Default
	{
	//$Category "PM_Monsters/Fodder/Crickets"
	Monster;
	+FLOAT
	+NOGRAVITY
	+LOOKALLAROUND
	+SHOOTABLE
	-SOLID
	+CANTSEEK
	-COUNTKILL
	+NOBLOOD
	Health 3;
	Mass 10000;
	Radius 24;
	Height 48;
	Speed 0;
	DeathSound "SwarmFlyDeath";
	}
	
	States
	{
	Spawn:
		TNT1 A 5 A_Look;
		Loop;
	See:
		TNT1 A 0 A_JumpIfCloser(256,"SeeGib");
		TNT1 A 4;
		Goto See;
	SeeGib:
		TNT1 A 0
		{
			A_StartSound("MaliceHeadExp",CHAN_6);
			A_StartSound("SwarmFlySight", 50);
			A_SpawnItemEx("PM_TumorBloodMist",0,0,3,0,0,0);
		}
		TNT1 AAAAAAAAAA 2
		{
			A_SpawnProjectile("MaliceSwarmFly", 0, 0, random(0, 360), 2, random(0, -70));
			for (int i = 2; i > 0; i--) A_SpawnItemEX("PM_TumorBurstBlood",0,0,0,random(1,4),random(1,4),random(3,10),random(0, 360),SXF_NOCHECKPOSITION);
		}
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEX("PM_TumorBurstBlood",0,0,0,random(1,6),random(1,6),random(3,20),random(0, 360),SXF_NOCHECKPOSITION);
		TNT1 A 0 A_SpawnItemEx("PM_Rotfly",zvel:3);
		Stop;
	}
}

Class PM_GagRotfly : PM_Rotfly
{
  Default
  {
  PMMonsterBase.CodexLore "";
  Species "Elemental";
  }
  
  States
  {
  Spawn:
	BFLY A 5 PM_Look;
  Look:
    BFLY ABABABAB 2 A_Wander;
	BFLY A 0 PM_LookingForPlayer;
	Loop;
  Spotted:
	TNT1 A 0 PM_AlertSound();
	TNT1 A 0 ThrustThingZ(0,6,0,1);
  Go:
	TNT1 A 0 A_SetSpeed(7);
	BFLY ABABABABABABAB 1 A_Chase;
	TNT1 A 0 A_JumpIfCloser(400,"StandoffStart");
	Loop;
  StandoffCheck:
	TNT1 A 0 A_Jump(50, "StandoffStart");
	Goto Go;
  StandoffStart:
	TNT1 A 0 A_CheckSight("Go");
	TNT1 A 0 A_SetSpeed(0);
	TNT1 A 0 A_JumpIfCloser(100,"Backup");
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	BFLY ABABAB 2
	{
	A_Chase("Kamikaze","Kamikaze");
	A_FaceTarget();
	}
	TNT1 A 0 A_Jump(130,"RandomDodge");
	Loop;
  Backup:
	TNT1 A 0 A_Recoil(8);
	BFLY ABABABABAB 2;
	Goto StandoffStart;
  RandomDodge:
	TNT1 A 0
	{
		ThrustThing(random(0,360),10);
		ThrustThingZ(0,7,random(0,1),1);
	}
	BFLY ABABAB 2 A_FaceTarget;
	Goto StandoffStart;
  Kamikaze:
    TNT1 A 0
	{
		A_ChangeVelocity(0,0,0,CVF_REPLACE);
		A_StartSound("Rotfly/Attack");
	}
    BFLY ABABABABABABABABABA 1
    {
    A_FaceTarget();
    A_SpawnItemEx("PM_PestilenceBlood1",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION,213);
    }
    TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
    TNT1 A 0 A_SpawnProjectile("PM_GagRotflyKamikaze",0,0,0);
	Stop;
   }
}

Class PM_GagRotflyKamikaze : DoomImpBall
{
  Default
  {
  Speed 30;
  Damage (10);
  Radius 10;
  Height 10;
  Health 5;
  Scale 0.76;
  -THRUSPECIES
  -DONTHARMSPECIES
  -DONTHARMSPECIES
  -THRUACTORS
  +SHOOTABLE
  Species "Elemental";
  +MISSILE
  RenderStyle "Normal";
  DamageType "Poison";
  Seesound "Rotfly/See";
  DeathSound "Rotfly/Death";
  }
  
  States
  {
  Spawn:
	BFLY AB 2 Bright;
	Loop;
  Death:
	TNT1 AA 0 A_SpawnItemEx("PM_PestilenceBloodMulti",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_PestilenceBloodEX",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_GagRotflySmokeCloud",0,0,0,random(-1,1),random(-1,1),random(-1,1),0,SXF_NOCHECKPOSITION);
	Stop;
  }
}

Class PM_GagRotflySmokeCloud : Actor
{
  Default
  {
  Radius 2;
  Height 2;
  +NOBLOCKMAP
  +NOGRAVITY
  +DONTTHRUST
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +NOTELEPORT
  +SKYEXPLODE
  +FORCEXYBILLBOARD
  +FOILINVUL
  +CANBLAST
  +RIPPER
  +THRUACTORS
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  +FORCERADIUSDMG
  +DOOMBOUNCE
  +ROLLSPRITE
  BounceFactor 0.3;
  WallBounceFactor 0.3;
  RenderStyle "Translucent";
  Alpha 0.93;
  Scale 1.2;
  DeathSound "RevGhoul/cloud";
  }
  
  states
  {
  Spawn:
	TNT1 A 0 NoDelay A_SetRoll(random(0,360));
    GSM1 A 5 A_SetTranslucent(0.2);
	GSM1 A 5 A_SetTranslucent(0.4);
	GSM1 A 10 A_SetTranslucent(0.8);
	TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
	GSM1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 5
	{
	A_Explode(Random(1,5),35);
	A_FadeOut(0.02);
	}
	Stop;
  }
}