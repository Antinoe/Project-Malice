Actor Trite
{
  HitObituary "%o was nipped by a Trite"
  Health 12
  Radius 16
  Height 18
  Mass 100
  Speed 10
  Scale 0.55
  PainChance 192
  BloodColor DarkGreen
  BloodType "PM_DarkGreenBloodBase"
  PainSound "Trite/Pain"
  SeeSound "Trite/Sight"
  DeathSound "Trite/Death"
  ActiveSound "Trite/Active"
  MeleeSound "Trite/MeleeHit"
  Tag "Trite"
  MeleeDamage 1
  MeleeThreshold 60
  MaxTargetRange 250
  MeleeRange 10
  MaxStepHeight 40
  MaxDropOffHeight 50
  MinMissileChance 10
  Damage 1
  Monster
  Species "PukeMonster"
  +FloorClip
  +DontHurtSpecies
  +NoTarget
  +Jumpdown
  -solid
  +shootable
  -Countkill
  +NOINFIGHTING
  +THRUSPECIES  
  +NOBLOCKMONST
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +ROLLSPRITE
  +ROLLCENTER
  -CANUSEWALLS
  -CANPUSHWALLS
  +CANTSEEK
  States
  {
  Spawn:
    TRIT AA 2 A_Wander
	TNT1 A 0 A_Look
	TRIT BB 2 A_Wander
	TNT1 A 0 A_Look
	TRIT CC 2 A_Wander
	TNT1 A 0 A_Look
	TRIT DD 2 A_Wander
	TNT1 A 0 A_Look
    Goto Spawn
  See:
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
	TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
    TRIT A 2 A_Chase("", "", 0)
    TRIT A 2 A_Chase
    TRIT A 0 A_PlaySoundEx("Trite/Step", "SoundSlot7", 0)
    TRIT BB 2 A_Chase("Melee", "Missile", 2)
    TRIT B 0 A_PlaySoundEx("Trite/Step", "SoundSlot7", 0)
    TRIT CC 2 A_Chase
    TRIT C 0 A_PlaySoundEx("Trite/Step", "SoundSlot7", 0)
    TRIT DD 2 A_Chase("Melee", "Missile", 2)
    TRIT D 0 A_PlaySoundEx("Trite/Step", "SoundSlot7", 0)
	TNT1 A 0 A_Jump(20,"Leap")
	TNT1 A 0 A_Jump(15,"Fight")
	TNT1 A 0 A_Jump(45,"CeilingJump")
	TNT1 A 0 A_JumpIfCloser(235,"BackCheck")
    Loop
	
   CeilingJump:
	TNT1 A 0 A_JumpIf(CallACS("GetActorZDist") <= 80, 2)
	TNT1 AAA 0
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "See")
    TRIT DE 4
	TNT1 A 0 ThrustThingZ(0,60,0,1) 
	//TNT1 A 0 A_PlaySound("MAMAMIA")	//I will always remember you. F
	TRIT FFFF 2 A_CheckCeiling("CeilingLatch")
	TNT1 A 0 ThrustThingZ(0,45,0,1)
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
	TRIT FFF 2 A_CheckCeiling("CeilingLatch")
	TNT1 A 0 ThrustThingZ(0,45,0,1)
	TRIT FFFF 3 A_CheckCeiling("CeilingLatch")
	TRIT FFFFFFF 3 A_CheckFloor("See")
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
	Goto See
	
	CeilingLatch:
	TNT1 A 0 A_ChangeFlag("NOGRAVITY", 1)
	TNT1 A 0 A_ChangeFlag("THRUSPECIES", 1)
	//TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 1)
	TNT1 A 0 A_ChangeFlag("NOVERTICALMELEERANGE", 1)
	TNT1 A 0 A_GiveInventory ("IsOnCeiling", 1)
	TRIT FFFFF 3 ThrustThingZ(0,500,0,1) //1
	Goto CeilingCrawl
	
	CeilingCrawl:
		TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE)
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce")
		TNT1 A 0 A_JumpIf(velz > 250,"CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		TRIT AABBCCDDEE 2 A_Chase
		//TNT1 A 0 A_Recoil(-1)
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		TNT1 A 0 A_JumpIf(velz > 250, "CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 ThrustThingZ(0,500,0,1)
		TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce")
		TNT1 A 0 A_JumpIf(velz > 250, "CeilingPounce")
		TNT1 A 0 A_CheckFloor("CeilingDrop")
		TNT1 A 0 A_JumpIfCloser(350, "CeilingPounce")
		TNT1 A 0 A_Jump(15,"CeilingDrop")
		Loop
		
	CeilingDrop:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory ("IsOnCeiling")
		TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 0)
		TRIT F 2 A_FaceTarget
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
	CeilingDropping:
		TRIT FF 1 A_JumpIf (momz == 0, "CeilingLanding")
		loop
	CeilingLanding:
		TNT1 A 0 A_PlaySoundEx("Trite/Step", "SoundSlot7", 0)
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		TRIT FF 2 A_FaceTarget
		Goto See
		
	CeilingPounce: 
		TNT1 AAA 0
		TNT1 A 0 A_TakeInventory ("IsOnCeiling")
		TNT1 A 0 A_ChangeFlag("NOGRAVITY", 0)
		TNT1 A 0 A_ChangeFlag("CEILINGHUGGER", 0)
		TNT1 A 0 A_ChangeFlag("THRUSPECIES", 0)
		//TNT1 A 0 A_PlaySound("Trite/Attack")
		TNT1 A 0 A_Recoil (-6)
		TNT1 A 0 A_FaceTarget
		TRIT FFFF 2
		TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
		TRIT FFAA 2
		Goto CeilingDropping		
	
  BackCheck:
    TNT1 A 0 A_Jump(35,"Backup")
	TNT1 A 0 A_Jump(25,"Fear")
    Goto See
  Backup:
	TRIT DDCCBBAADDCCBBAADDCCBBAA 2
	{
	A_FaceTarget;
	A_Recoil(1);
	}
    Goto See
  Fear:
    TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
	TNT1 A 0 A_Jump(150,"Scurry")
	Goto See
  Scurry:
	TNT1 A 0 A_SetSpeed(15)
	TRIT A 0 A_CheckSight ("Vanish")
	TRIT AA 1 A_Chase
	TRIT A 0 A_CheckSight ("Vanish")
    TRIT BB 1 A_Chase
	TRIT A 0 A_CheckSight ("Vanish")
    TRIT CC 1 A_Chase
	TRIT A 0 A_CheckSight ("Vanish")
    TRIT DD 1 A_Chase
	TNT1 A 0 A_Jump(4,"Fight")
	Loop
  Vanish:
	TNT1 A 0
	Stop
  Fight:
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
	Goto See
   Leap:
	TNT1 A 0 A_Jump(100,"LeapBack")
	TRIT A 3
	TNT1 A 0 A_Recoil(-9)
	TNT1 A 0 ThrustThingZ(0,28,0,1)
	TRIT F 15
	Goto See
  LeapBack:
	TRIT A 3
	TNT1 A 0 A_Recoil(9)
	TNT1 A 0 ThrustThingZ(0,28,0,1)
	TRIT F 15
	Goto See
  Melee:
    TRIT A 12 A_FaceTarget
    TRIT A 0 A_Jump(192, 2)
    TRIT A 0 A_PlaySound("Trite/Attack")
    TRIT E 6 A_MeleeAttack
    Goto See
  Missile:
	TNT1 A 0 A_JumpIfCloser(100,"See")
    TRIT A 0 A_Jump(128, "See")
    TRIT AAAAA 3 A_FaceTarget
    TRIT F 0 A_PlaySound("Trite/Attack")
	TNT1 A 0 A_Recoil(-20)
    TRIT F 10 ThrustThingZ(0, 25, 0, 1)
    TRIT E 15
    Goto See
  Web:
    TRIT F 0
    TRIT F 0 A_ChangeFlag("LowGravity", 1)
    TRIT F 0 A_ChangeFlag("NoDamage", 1)
    TRIT F 0 A_ChangeFlag("NoPain", 1)
    TRIT F 0 A_PlaySound("Trite/WebStart")
    TRIT F 1 A_CheckFloor(1)
    Goto Web+5
    TRIT A 0 A_ChangeFlag("NoDamage", 0)
    TRIT A 0 A_ChangeFlag("NoPain", 0)
    TRIT A 0 A_ChangeFlag("LowGravity", 0)
    TRIT A 12 A_PlaySound("Trite/WebEnd")
    Goto Spawn
  Pain:
	TNT1 A 0
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE)
	TNT1 A 0 A_ChangeFlag("NOGRAVITY",False)
    TRIT E 3
    TRIT E 3 A_Pain
    Goto See
  Death:
    TRIT G 5 A_Scream
	TNT1 AA 0 A_SpawnItemEx("PM_DarkGreenBloodMist",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION)
	TNT1 A 0 A_SpawnItemEx("PM_DarkGreenBloodMulti",random(-2,2),random(-2,2),random(2,5),random(-2,2),random(-2,2),random(2,5),0,SXF_NOCHECKPOSITION)
	TNT1 AAA 0 A_SpawnItemEx("PM_DarkGreenMeatChunkLauncher",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION,130)
    TRIT H 0 A_PlaySoundEx("Trite/Splat", "SoundSlot7", 0)
    TRIT H 5 A_NoBlocking
    TRIT IJK 5    
    Stop
  }
}

Actor HeadDropper
{
    +ISMONSTER
	States
	{
	Spawn:
	    TNT1 A 1 Nodelay
		TNT1 AAAAAA 0 A_SpawnItemEx("Trite",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION)
		Stop

	}
}

//==========Mapper Notes

//The Web state is activated through the ACS fuction SetActorState
//and is used to lower the Trite to the floor as though it was on a web.
//(Note that this only really helps if it's spawned above the floor.)


//==========Credits
//Decorate: Ghastly_dragon
//Sprites: Monolith, Id Software
//Sprite Edit: Captain Toenail
//Sounds: Id Software
