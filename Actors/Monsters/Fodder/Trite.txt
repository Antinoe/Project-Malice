Class PM_Trite : PMFodderMonsterBase
{
  Default
  {
  PMMonsterBase.CodexLore
  "Were these originally human heads, or just twisted demonic wildlife? ...Don't think about it too much.";
  HitObituary "%o was bitten harshly by a pesky trite.";
  Health 12;
  Radius 16;
  Height 18;
  Mass 100;
  Speed 10;
  Scale 0.55;
  PainChance 192;
  BloodColor "DarkGreen";
  BloodType "PM_DarkGreenBloodBase";
  PainSound "Trite/Pain";
  SeeSound "Trite/Sight";
  DeathSound "Trite/Death";
  ActiveSound "Trite/Active";
  MeleeSound "Trite/MeleeHit";
  Tag "Trite";
  MeleeDamage 1;
  MeleeThreshold 60;
  MaxTargetRange 250;
  MeleeRange 10;
  MaxStepHeight 40;
  MaxDropOffHeight 50;
  MinMissileChance 10;
  Damage 1;
  Monster;
  Species "Fodder";
  +FloorClip
  +DONTHARMSPECIES
  +NoTarget
  +Jumpdown
  -solid
  +shootable
  -Countkill
  +NOINFIGHTING
  +THRUSPECIES  
  +NOBLOCKMONST
  +DONTHARMCLASS
  +DONTHARMSPECIES
  +ROLLSPRITE
  +ROLLCENTER
  -CANUSEWALLS
  -CANPUSHWALLS
  +CANTSEEK
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
  TeleFogDestType "PM_SmallDemonicTeleportFog";
  }
  
  void PM_TriteChase()
	{
	 if(target)
	 {
	  if(CheckSight(target) && Distance3D(target) <= 1200)
	  A_Chase();
	  else
	  A_Wander();
	 }
	}
  
  States
  {
  Spawn:
    TRIT AA 2 A_Wander();
	TNT1 A 0 A_Look();
	TRIT BB 2 A_Wander();
	TNT1 A 0 A_Look();
	TRIT CC 2 A_Wander();
	TNT1 A 0 A_Look();
	TRIT DD 2 A_Wander();
	TNT1 A 0 A_Look();
    Goto Spawn;
  See:
	TNT1 A 0
	{
		A_SetRoll(0,SPF_INTERPOLATE);
		bNOGRAVITY = false;
	}
    TRIT A 2 PM_TriteChase();
    TRIT A 2 PM_TriteChase();
    TRIT A 0 A_StartSound("Trite/Step", CHAN_7);
    TRIT BB 2 PM_TriteChase();
    TRIT B 0 A_StartSound("Trite/Step", CHAN_7);
    TRIT CC 2 PM_TriteChase();
    TRIT C 0 A_StartSound("Trite/Step", CHAN_7);
    TRIT DD 2 PM_TriteChase();
    TRIT D 0 A_StartSound("Trite/Step", CHAN_7);
	TNT1 A 0 A_Jump(20,"Leap");
	TNT1 A 0 A_Jump(15,"Fight");
	TNT1 A 0 A_Jump(45,"CeilingJump");
	TNT1 A 0 A_JumpIfCloser(235,"BackCheck");
    Loop;
   CeilingJump:
	TNT1 A 0 A_JumpIf(CallACS("GetActorZDist") <= 80, 2);
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "See");
    TRIT DE 4;
	TNT1 A 0 ThrustThingZ(0,60,0,1);
	//TNT1 A 0 A_StartSound("MAMAMIA")	//I will always remember you. F
	TRIT FFFF 2 A_CheckCeiling("CeilingLatch");
	TNT1 A 0 { ThrustThingZ(0,45,0,1); A_SetRoll(180,SPF_INTERPOLATE); }
	TRIT FFF 2 A_CheckCeiling("CeilingLatch");
	TNT1 A 0 ThrustThingZ(0,45,0,1);
	TRIT FFFF 3 A_CheckCeiling("CeilingLatch");
	TRIT FFFFFFF 3 A_CheckFloor("See");
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	Goto See;
  CeilingLatch:
	TNT1 A 0
	{
		bNOGRAVITY = true;
		bTHRUSPECIES = true;
		bNOVERTICALMELEERANGE = true;
		A_GiveInventory("IsOnCeiling", 1);
	}
	TRIT FFFFF 3 ThrustThingZ(0,500,0,1);
	Goto CeilingCrawl;
  CeilingCrawl:
	TNT1 A 0 A_SetRoll(180,SPF_INTERPOLATE);
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
	TNT1 A 0 A_JumpIf(vel.z > 250,"CeilingPounce");
	TNT1 A 0 A_CheckFloor("CeilingDrop");
	TNT1 A 0 ThrustThingZ(0,500,0,1);
	TRIT AABBCCDDEE 2 A_Chase;
	TNT1 A 0 ThrustThingZ(0,500,0,1);
	TNT1 A 0 A_JumpIf(vel.z > 250, "CeilingPounce");
	TNT1 A 0 A_CheckFloor("CeilingDrop");
	TNT1 A 0 ThrustThingZ(0,500,0,1);
	TNT1 A 0 A_JumpIf(CallACS("IsThisSky") == 1, "CeilingPounce");
	TNT1 A 0 A_JumpIf(vel.z > 250, "CeilingPounce");
	TNT1 A 0 A_CheckFloor("CeilingDrop");
	TNT1 A 0 A_JumpIfCloser(350, "CeilingPounce");
	TNT1 A 0 A_Jump(15,"CeilingDrop");
	Loop;
  CeilingDrop:
	TNT1 A 0
	{
		A_TakeInventory("IsOnCeiling");
		bNOGRAVITY = false;
		bTHRUSPECIES = false;
		bNOVERTICALMELEERANGE = false;
	}
	TRIT F 2 A_FaceTarget;
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
  CeilingDropping:
	TRIT FF 1 { if (pos.z <= floorz || vel.z == 0) SetStateLabel("CeilingLanding"); }
	Loop;
  CeilingLanding:
	TNT1 A 0
	{
		A_StartSound("Trite/Step", CHAN_7);
		A_SetRoll(0,SPF_INTERPOLATE);
	}
	TRIT FF 2 A_FaceTarget();
	Goto See;
  CeilingPounce: 
	TNT1 A 0
	{
		A_TakeInventory("IsOnCeiling");
		bNOGRAVITY = false;
		bTHRUSPECIES = false;
		bNOVERTICALMELEERANGE = false;
		A_Recoil(-6);
		A_FaceTarget();
	}
	TRIT FFFF 2;
	TNT1 A 0 A_SetRoll(0,SPF_INTERPOLATE);
	TRIT FFAA 2;
	Goto CeilingDropping;		
  BackCheck:
    TNT1 A 0 A_Jump(35,"Backup");
	TNT1 A 0 A_Jump(25,"Fear");
    Goto See;
  Backup:
	TRIT DDCCBBAADDCCBBAADDCCBBAA 2
	{
	A_FaceTarget();
	A_Recoil(1);
	}
    Goto See;
  Fear:
    TNT1 A 0 { bFRIGHTENED = true; }
	TNT1 A 0 A_Jump(150,"Scurry");
	Goto See;
  Scurry:
	TNT1 A 0 A_SetSpeed(15);
	TRIT A 0 A_CheckSight("Vanish");
	TRIT AA 1 A_Chase;
	TRIT A 0 A_CheckSight("Vanish");
    TRIT BB 1 A_Chase;
	TRIT A 0 A_CheckSight("Vanish");
    TRIT CC 1 A_Chase;
	TRIT A 0 A_CheckSight("Vanish");
    TRIT DD 1 A_Chase;
	TNT1 A 0 A_Jump(4,"Fight");
	Loop;
  Vanish:
	TNT1 A 0;
	Stop;
  Fight:
	TNT1 A 0 { bFRIGHTENED = false; }
	Goto See;
  Leap:
	TRIT A 3 A_Jump(100,"LeapBack");
	TNT1 A 0
	{
		A_Recoil(-9);
		ThrustThingZ(0,28,0,1);
	}
	TRIT F 15;
	Goto See;
  LeapBack:
	TRIT A 3;
	TNT1 A 0
	{
		A_Recoil(9);
		ThrustThingZ(0,28,0,1);
	}
	TRIT F 15;
	Goto See;
  Melee:
    TRIT A 12 A_FaceTarget;
    TRIT A 0 A_Jump(192, 2);
    TRIT A 0 A_StartSound("Trite/Attack");
    TRIT E 6 A_CustomMeleeAttack(1);
    Goto See;
  Missile:
	TNT1 A 0 A_JumpIfCloser(100,"See");
    TRIT A 0 A_Jump(128, "See");
    TRIT AAAAA 3 A_FaceTarget;
    TRIT F 10
	{
		A_StartSound("Trite/Attack");
		A_Recoil(-20);
		ThrustThingZ(0, 25, 0, 1);
	}
    TRIT E 15;
    Goto See;
  Web:
    TRIT F 0
	{
		bNOGRAVITY = true;
		bNODAMAGE = true;
		bNOPAIN = true;
		A_StartSound("Trite/WebStart");
	}
    TRIT F 1 A_CheckFloor("WebEnd");
    Goto Web+1;
  WebEnd:
    TRIT A 12
	{
		bNOGRAVITY = true;
		bNODAMAGE = true;
		bNOPAIN = true;
		A_StartSound("Trite/WebEnd");
	}
    Goto Spawn;
  Pain:
	TNT1 A 0
	{
		bNOGRAVITY = false;
		A_SetRoll(0,SPF_INTERPOLATE);
	}
    TRIT E 3;
    TRIT E 3 A_Pain;
    Goto See;
  Death:
    TRIT G 5 A_Scream;
	TNT1 AA 0 A_SpawnItemEx("PM_DarkGreenBloodMist",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_DarkGreenBloodMulti",random(-2,2),random(-2,2),random(2,5),random(-2,2),random(-2,2),random(2,5),0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_DarkGreenMeatChunkLauncher",random(-2,2),random(-2,2),random(2,5),0,0,0,0,SXF_NOCHECKPOSITION,130);
    TRIT H 0 A_StartSound("Trite/Splat", CHAN_7);
    TRIT H 5 A_NoBlocking;
    TRIT IJK 5;
    Stop;
  }
}

Class PM_HeadDropper : Actor
{
    Default { +ISMONSTER }
	
	States
	{
	Spawn:
	    TNT1 A 1 Nodelay;
		TNT1 AAAAAA 0 A_SpawnItemEx("PM_Trite",0,0,0,frandom(-4,4),frandom(-4,4),frandom(1,5),0,SXF_NOCHECKPOSITION);
		Stop;
	}
}

//==========Mapper Notes

//The Web state is activated through the ACS fuction SetActorState
//and is used to lower the Trite to the floor as though it was on a web.
//(Note that this only really helps if it's spawned above the floor.)


//==========Credits
//Decorate: Ghastly_dragon
//Sprites: Monolith, Id Software
//Sprite Edit: Captain Toenail
//Sounds: Id Software (and Dead Space)