Class PM_Grievance : PMEnvyMonsterBase
{
	bool sigil;
	
	Default
	{
	PMMonsterBase.CodexLore
	"Grievances are resentful, floating legless sentinels that only feel hatred towards other demons of sin. It envies those who have rockets that properly home in threats, and yet it feels like it wants to be like the others.
	Their ability to float is unspecified, but research showed that it might be due to blood-powered thrusters that replaced its legs. Even though it can fly, it is not happy.
	
	Can float and dodge, fire poisonous rockets through its cannons, burst out a grenade, and, of course, it is capable of mimicry like every demon of the sixth layer of Hell.";
	DamageFactor "Acid", 0;
	Health 300;
	Radius 20;
	Height 56;
	Mass 500;
	Speed 9;
	PainChance 80;
	Monster;
	+NOGRAVITY
	+FLOAT
	+FLOATBOB
	+SLIDESONWALLS
	+DONTFALL
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+DONTHARMCLASS
	FloatBobStrength 0.4;
	Bloodtype "PM_GreenCyberBloodBase";
	Obituary "A Grievance has shown its resent towards %o.";
	SeeSound "grievance/Sight";
	ActiveSound "grievance/Active";
	PainSound "grievance/Active";
	DeathSound "grievance/Death";
	Species "Revenant";
	Tag "Grievance";
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_EnvyTeleportFog";
	}
  
	override void PostBeginPlay()
	{
	tex[0] = TexMan.CheckForTexture("DTPRA0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GRNLA0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	tex[3] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[4];
	
	override void Tick()
	{
		super.Tick();
		
		if (!level.isFrozen() && health > 0)
		{
			for (int i = -10; i < 11; i += 20)
				A_SpawnParticleEx("279c1a", tex[2], STYLE_ADD, SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT, int(ceil( frandom(55, 85) )), frandom(30, 50), angle, -frandom(4, 6), i + frandom(-2, 2), frandom(-2, 2) + 36, velz: frandom(-1, -2), startalphaf: frandom(0.5, 0.1), startroll: frandom(0, 360), rollvel: frandom(-2, 2));
		}
	}
	
	void GrievanceAimLaser(Vector2 off = (0, 10), double scal = 0.06)
	{
		if (!Target) return;
		double sa = sin(Angle);
		double ca = cos(Angle);
		Vector2 relOff = (off.X * ca + off.Y * sa, off.X * sa - off.Y * ca);
		PM_SpawnBolt(self, Target.Vec3Offset(0,0,Target.Height-22),"24ff24",TexMan.CheckForTexture("RLSSA0"),maxDiff: 0,scale: scal, offset: (relOff.X, relOff.Y, 40),baseTime: 1,fadeTime: 0);
	}
	
	States
	{
	Spawn:
		PSEV A 10 PM_Look();
		Loop;
	Look:
		PSEV AAABBB 4 PM_LookingForPlayer();
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",250,JLOSF_DEADNOJUMP);
		Loop;
	Spotted:
		TNT1 A 0 PM_AlertSound();
	See:
		PSEV AAABBB 4 A_Chase();
		PSEV A 0 A_Jump(50, "Avoid");
		Loop;
	Avoid:
		PSEV A 1
		{
			VelFromAngle(random(10, 20), random(1, 4) * 90);
			A_FaceTarget();
		}
		PSEV AAAAAAAAAAAAAA 1 A_FaceTarget();
		PSEV A 0 A_Stop();
		Goto See;
	Pain:
		TNT1 A 0 
		{ 
			if(sigil)
			A_GiveInventory("PM_EnvySigilGoAway");
			sigil = false;
		}
		PSEV E 3;
		PSEV E 1 A_Pain();
		PSEV E 0 { if (target) A_Jump(54, "Copy"); }
		PSEV E 1 VelFromAngle(random(10, 15), random(1, 4) * 90);
		PSEV EEEEEE 2 A_SpawnItemEx("PM_GenericPhaseGhost",0,0,0,0,0,0,0,SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		PSEV E 0 A_Stop();
		Goto See;
	Missile:
		TNT1 A 0 { if (target && CheckSight(target) && Distance2D(target) < 700 && !random(0, 7)) SetStateLabel("Copy"); }
		TNT1 A 0 { if (target && CheckSight(target) && Distance2D(target) > 500 && !random(0, 7)) SetStateLabel("Copy"); }
		TNT1 A 0 A_Jump(40, "StealthBazooka");
		TNT1 A 0 A_Jump(70, "Grenade");
		TNT1 A 0 A_Jump(128, "DualRocket");
		Goto SingleRocket;
	SingleRocket:
		PSEV CCCCCCCCCCCCCCC 1 Light("Greenball2") A_FaceTarget();
		PSEV D 25 Light("Greenball")
		{
			A_FaceTarget();
			A_StartSound("Draugr/Fire", CHAN_6);
			A_SpawnProjectile("PM_GrievanceRocket", 45, (!random(0, 1) ? 10 : -18));
		}
		PSEV D 0 A_Jump(80, "Avoid");
		Goto See;
	DualRocket:
		PSEV C 0 A_StartSound(ActiveSound, CHAN_VOICE);
		PSEV CCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 Light("Greenball2") A_FaceTarget();
		PSEV D 25 Light("Greenball")
		{
			A_FaceTarget();
			A_StartSound("Draugr/Fire", CHAN_6);
			A_SpawnProjectile("PM_GrievanceRocket", 45, 10);
			A_SpawnProjectile("PM_GrievanceRocket", 45, -18);
		}
		PSEV D 0 A_Jump(128, "Avoid");
		Goto See;
	Grenade:
		PSEV C 0
		{
			A_StartSound("Grievance/Attack", CHAN_VOICE);
			A_StartSound("Grievance/Charge", 11);
		}
		PSEV CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 A_FaceTarget();
		PSEV D 10
		{
			A_Recoil(10);
			A_StartSound("Sybarite/GrenadeLaunch",11);
			A_SpawnProjectile("PM_GrievanceFailedGrenade", 45, (!random(0, 1) ? 10 : -18));
		}
		PSEV D 40 A_Stop();
		Goto See;
	StealthBazooka:
		PSEV C 0
		{
			A_StartSound("Grievance/Attack", CHAN_VOICE);
			A_StartSound("Draugr/GunAim", 11, pitch: 0.65);
			A_StartSound("GenericChargeHeavy",15);
		}
		PSEV CCCCCCCCCCCCCCCCCCCCC 1 A_FaceTarget();
		PSEV CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1
		{
			GrievanceAimLaser((0, -10), 0.2);
			A_FaceTarget();
		}
		PSEV D 10
		{
			A_Recoil(20);
			A_StartSound("Sybarite/GrenadeLaunch",11);
			A_SpawnProjectile("PM_GrievanceStealthBazooka", 45, (!random(0, 1) ? 10 : -18));
		}
		PSEV D 50 A_Stop();
		Goto See;
	Copy:
		TNT1 A 0 { if (!random(0, 1)) A_StartSound("Grievance/Attack", CHAN_VOICE); }
		TNT1 A 0 A_Jump(256, "CopyFiend", "CopyCadaver", "CopyHusk");
	CopyHusk:
		TNT1 A 0 A_Stop();
		TNT1 A 0 A_StartSound("GenericChargeHeavy",15);
		PSEV CCCCCCCCCCCCCCCCCCCCCCC 1 Light("Greenball") A_FaceTarget();
		PSEV C 1 A_StartSound("Husk/Pain",15,volume:0.60,pitch:0.8);
		TNT1 A 0
		{
			let envysigil = Spawn("PM_EnvySigil",pos);
			envysigil.master = self;
			sigil = true;
		}
		PSEV CCCCCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LA0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV CCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LB0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV CCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LC0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV CCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LD0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV CCCCCCCCCCCCCCCCCCCCCCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LE0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV D 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LE0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_StartSound("Husk/Throw");
			A_SpawnProjectile("PM_CopyHuskMeteor", 40);
			A_FaceTarget();
		}
		PSEV DDDDDDDDCCCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LE0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV D 5 PM_LeadTarget(22);
		PSEV D 10 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("GH2LE0"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_StartSound("Husk/Throw");
			A_SpawnProjectile("PM_CopyHuskMeteor", 40, 0, 0, CMF_AIMDIRECTION|CMF_OFFSETPITCH, pitch * 1);
		}
		TNT1 A 0 
		{
		A_GiveInventory("PM_EnvySigilGoAway");
		sigil = false;
		}
		PSEV DDDDD 5;
		Goto See;
	CopyFiend:
		TNT1 A 0 A_Stop();
		TNT1 A 0 A_StartSound("GenericChargeHeavy",15);
		PSEV CCCCCCCCCCCCCCCCCCCCCCC 1 Light("Greenball") A_FaceTarget();
		PSEV C 1 A_StartSound("Fiend/HomingYell",15,volume:0.60,pitch:0.8);
		TNT1 A 0
		{
			let envysigil = Spawn("PM_EnvySigil",pos);
			envysigil.master = self;
			sigil = true;
		}
		PSEV CCCCCCCCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("FIENK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		TNT1 A 0 A_StartSound("Draugr/Arm",23);
		PSEV CCCCCCCCCCCCCCCCCC 1 Light("Greenball2")
		{
			GrievanceAimLaser((0,-17));
			GrievanceAimLaser();
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("FIENK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		TNT1 A 0 A_StartSound("Fiend/Lockon",24);
		PSEV CCCCCCCCCCCCCC 1 Light("YellowMediumFlicker2")
		{
			GrievanceAimLaser((0,-17));
			GrievanceAimLaser();
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("FIENK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV DDDDDDDD 1 Light("Greenball2")
		{
			GrievanceAimLaser((0,-17));
			GrievanceAimLaser();
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("FIENJ1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		Goto CopyFiendLoop;
	CopyFiendLoop:
		TNT1 A 0 { pm_attackloop = random(3,5); }
		PSEV DD 0 Bright Light("Greenball") A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 120.0,xoff: 5,yoff:10,zoff:62,startalphaf: 0.6);
		PSEV D 1 Bright Light("Greenball") A_SpawnProjectile("PM_CopyFiendHomingMissile",62,10,frandom(-40,40),CMF_OFFSETPITCH,frandom(pitch*1+15,pitch*1-18));
		PSEV D 1 A_FaceTarget();
		PSEV DD 0 Bright Light("Greenball") A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 120.0,xoff: 5,yoff:-18,zoff:62,startalphaf: 0.6);
		PSEV D 1 Bright Light("Greenball")
		{
		A_SpawnProjectile("PM_CopyFiendHomingMissile",62,-18,frandom(-40,40),CMF_OFFSETPITCH,frandom(pitch*1+15,pitch*1-18));
		pm_attackloop--;
		}
		PSEV D 1 A_FaceTarget();
		PSEV DD 0 Bright Light("Greenball") A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 120.0,xoff: 5,yoff:10,zoff:62,startalphaf: 0.6);
		TNT1 A 0 { if(pm_attackloop < 0) SetStateLabel("CopyFiendEnd"); }
		Goto CopyFiendLoop+3;
	CopyFiendEnd:
		PSEV DDDDDD 1 Light("Greenball2") A_FaceTarget();
		PSEV D 5 A_FaceTarget();
		TNT1 A 0 
		{
		A_GiveInventory("PM_EnvySigilGoAway");
		sigil = false;
		}
		PSEV AAAAAA 5 A_FaceTarget();
		Goto See;
	CopyCadaver:
		TNT1 A 0 A_Stop();
		TNT1 A 0 A_StartSound("GenericChargeHeavy",15);
		PSEV CCCCCCCCCCCCCCCCCCCCCCC 1 Light("Greenball") A_FaceTarget();
		PSEV C 1 A_StartSound("Cadaver/Sight",15,volume:0.60,pitch:0.8);
		TNT1 A 0
		{
			let envysigil = Spawn("PM_EnvySigil",pos);
			envysigil.master = self;
			sigil = true;
		}
		PSEV CCCCCCCCCC 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVB1E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV CCCCCCCCCCCCCCCCCC 1 Light("Greenball2")
		{
			GrievanceAimLaser();
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVJ1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV D 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_SpawnProjectile("PM_CopyCadaverHRocket", 58, 10);
			A_FaceTarget();
		}
		PSEV DDDDDDDDDDDDD 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV C 0 VelFromAngle(random(15, 25), random(1, 4) * 90);
		PSEV CCCCCCCCCCCCCCCCCC 1 Light("Greenball2")
		{
			GrievanceAimLaser((0, -17));
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVJ1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		PSEV D 1 Light("Greenball2")
		{
			A_Stop();
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_SpawnProjectile("PM_CopyCadaverHRocket", 58, -18);
			A_FaceTarget();
		}
		PSEV DDDDDDDDDDDDD 1 Light("Greenball2")
		{
			A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("CREVK1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 105, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
			A_FaceTarget();
		}
		TNT1 A 0 
		{
		A_GiveInventory("PM_EnvySigilGoAway");
		sigil = false;
		}
		PSEV DDDD 5 A_FaceTarget();
		Goto See;
	Death:
		PSEV E 5;
		PSEV F 10 A_ScreamAndUnblock();
		PSEV G 2
		{
			A_FadeOut(0.025);
			A_SpawnParticleEx("279c1a", tex[2], STYLE_ADD, SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT, int(ceil(frandom(50, 100))), frandom(50, 100), angle, frandom(20, -20), frandom(-20, 20), frandom(-15, 15) + 36, frandom(-3, 3) / 2, frandom(-3, 3) / 2, frandom(1, 3) / 2, frandom(-0.01, 0.01), frandom(-0.01, 0.01), frandom(-0.01, 0.01), startalphaf: frandom(0.5, 1), startroll: frandom(0, 360), rollvel: frandom(-2, 2));
			if (!random(0, 1)) A_SpawnParticleEx("727272", tex[3], STYLE_Normal, SPF_RELATIVE|SPF_ROLL, random(40, 70), frandom(5, 45), angle, frandom(-20, 20), frandom(-20, 20), random(30, 60), velx: frandom(-3, 3), vely: frandom(-3, 3), velz: frandom(-2, 6), accelz: frandom(-0.1, -0.5), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180, 180), rollvel: random(-2, 2));
		}
		Wait;
	}
}

//Normal Attacks
Class PM_GrievanceRocket : RevenantTracer
{
    Default
    {
	Radius 2;
	Height 2;
	DamageFunction 35;
	Speed 8;
	FastSpeed 13;
	Projectile;
	+RANDOMIZE
	RenderStyle "Normal";
	Decal "PM_GreenFireScorch";
	SeeSound "";
	DeathSound "Cadaver/MissileExp";
	Scale 1;
    Translation "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:235=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "250:250=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "48:79=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
    }
	
	int l;
	vector3 l2;
	
	override void PostBeginPlay()
	{
		super.PostBeginPlay();
		l2 = (frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.2, 0.2));
		tex[0] = TexMan.CheckForTexture("DTPRA0",TexMan.Type_Sprite);
		tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
		tex[2] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	}
	
	TextureID tex[3];
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMIS AAAAAAA 1 BRIGHT;
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMIS AAAAAA 1 BRIGHT;
		TNT1 A 0 A_SpawnParticleEx("24ff24",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 76,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	    TNT1 A 0 A_ScaleVelocity(3);
	    TNT1 A 0 A_StartSound("Grievance/Launch",CHAN_7);
	Spawn2:
		CMIS A 1 BRIGHT Light("Greenball3");
		TNT1 A 0
		{
		  if(l > 20)
		  {
			vel.x += l2.x;
			vel.y += l2.y;
			vel.z += l2.z;
		  }
		  
		  l++;
		  
		  A_SpawnItemEx("PM_GrievanceSmokeCloud",frandom(-5, 5),frandom(-5, 5),frandom(-5, 5),frandom(-1, 1),frandom(-1, 1),frandom(-1, 1),flags: SXF_NOCHECKPOSITION);
		  A_SpawnItemEx("PM_CadaverHomingFireGlobs",random(-2,0),random(-3,3),random(-3,3),flags:128|SXF_TRANSFERTRANSLATION);
		  
		  A_SpawnParticleEx("", tex[0], STYLE_ADD, SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, int(ceil(frandom(1, 10))), random(1, 40), angle, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), startalphaf: frandom(.5, 1), startroll: random(-180,180));
		  A_SpawnParticleEx("24ff24",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 76,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	      A_SpawnParticleEx("00ff00",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -10,startalphaf: 0.6);
		}
		Loop;
	Death:
		TNT1 A 0 A_StopSound(CHAN_6);
		TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
		TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode",flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		TNT1 AAA 0 A_SpawnItemEx("PM_SmallerGenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		TNT1 AAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
		TNT1 AAAA 0 A_SpawnItemEx("PM_FiendBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		TNT1 A 5 BRIGHT Light("Greenball2");
		Stop;
	}
}

Class PM_GrievanceSmokeCloud : PM_HostSmokeCloud
{
  Default
  {
    BounceFactor 0.2;
    WallBounceFactor 0.2;
    RenderStyle "Normal";
	DamageType "Acid";
    Alpha 0.3;
    Scale 1.2;
  }
  States
  {
  Spawn:
	TNT1 AAAAAAAAA random(1,8) { A_SpawnParticleEx("004400",TexMan.CheckForTexture("GNSMK0"), STYLE_ADD, SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT, random(80,200), frandom(15,45),velx: frandom(-0.05,0.05),vely: frandom(-0.05,0.05),velz: frandom(-0.05,0.05),startalphaf: frandom(0.1,0.3),fadestepf: -0.001,sizestep: frandom(0.1,1),startroll: random(-180,180),rollvel: random(-2,2)); }
	TNT1 A 0 A_Stop();
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA random(4,10)
	{
	  if (!random(0, 3)) A_SpawnParticleEx("004400",TexMan.CheckForTexture("GNSMK0"), STYLE_ADD, SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT, random(200,400), frandom(25,65),xoff: random(-5,5),yoff: random(-5,5),zoff: random(-5,5),velx: frandom(-0.05,0.05),vely: frandom(-0.05,0.05),velz: frandom(-0.05,0.05),startalphaf: frandom(0.1,0.3),fadestepf: -1,sizestep: frandom(0.1,1),startroll: random(-180,180),rollvel: frandom(-1,1));
	  A_Explode(Random(1,2),36);
	  A_FadeOut(0.02);
	  A_SetScale(scale.x+0.01);
	}
  Death:
	TNT1 A 1;
	Stop;
  }
}

Class PM_GrievanceFailedGrenade : Rocket
{
  int grenadetime;
  Default
  {
  Speed 10;
  Scale 1.1;
  Radius 4;
  Height 10;
  Gravity 0.8;
  Bouncetype "Hexen";
  BounceFactor 0.44;
  WallBounceFactor 0.66;
  BounceCount 3;
  SeeSound "";
  DeathSound "";
  BounceSound "PM/HardMetalBounce";
  Decal "";
  -ROCKETTRAIL
  +THRUACTORS
  +CANBOUNCEWATER
  +BOUNCEONCEILINGS
  StencilColor "24ff24";
  Translation "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:235=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "250:250=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "48:79=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
  }
  
  override void PostBeginPlay()
  {
	super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("DTPRA0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("SPKOG0",TexMan.Type_Sprite);
  }
  
  textureID tex[3];
  
  States
  {
  Spawn:
	GRNP AAAAAAAAAAAAAAAAAA 1 Light("Greenball3") A_SpawnParticleEx("24ff24",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 76,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	GRNP A 1
	{
		A_StartSound("Grievance/RocketFail",CHAN_BODY);
		
		for (int i = random(5, 10); i > 0; i--)
			A_SpawnParticleEx("",tex[2], STYLE_Add, SPF_RELATIVE|SPF_FULLBRIGHT, int(ceil(frandom(30, 50))), frandom(1, 15), angle, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), accelz: -0.1, startalphaf: frandom(0.5, 1));
		
		bNOGRAVITY = false;
		A_ScaleVelocity(0.5);
		vel.z += frandom(2, 4);
		A_SpawnParticleEx("00ff00",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 200.0,xoff: -10,startalphaf: 0.6);
	}
	Goto Malfunction;
  Malfunction:
	GRNP A 1 { if (!random(0, 7)) A_SpawnParticleEx("",tex[2], STYLE_Add, SPF_RELATIVE|SPF_FULLBRIGHT, int(ceil( frandom(30, 50) )) , 
	frandom(1, 15), angle, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), accelz: -0.1, startalphaf: frandom(0.5, 1)); }
	Loop;
  Death:
    GRNP A random(10, 20);
	GRNP A 10 Light("Greenball2") A_StartSound("Grievance/GrenadeOpen",CHAN_7);
	GRNP A 1 Light("Greenball2")
	{
		if (tracer) self.target = tracer;
		A_StartSound("PM/SigilSpawn",20);
		A_SpawnParticleEx("00ff00", TexMan.CheckForTexture("C1TEA0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 35, size: 105, zoff: 25, startalphaf: 1, fadestepf: -1, sizestep: 10, startroll: 180);
		A_SpawnParticleEx("00ff00", TexMan.CheckForTexture("C1TEA0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 2, size: 105, zoff: 25, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	}
	GRNP AAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 { A_SpawnParticleEx("00ff00", TexMan.CheckForTexture("C1TEA0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 2, size: 105, zoff: 25, startalphaf: 1, fadestepf: 0.1, startroll: 180); }
	GRNP A 1 A_StartSound("Grievance/HomingStart",CHAN_6);
  HolySHITitsAlive:
	GRNP A random(4, 15) Light("Greenball2")
	{
		A_SpawnParticleEx("00ff00", TexMan.CheckForTexture("C1TEA0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 35, size: 105, zoff: 25, startalphaf: 1, fadestepf: 0, startroll: 180);
		if (grenadetime > random(10, 20)) SetStateLabel("XDeath");
		A_SpawnParticleEx("00ff00",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 200.0,xoff: -10,startalphaf: 0.6);
		A_StartSound("Grievance/HomingFire",11);
		A_SpawnProjectile("PM_GrievanceSeeker", 5, 0, frandom(0, 360), CMF_AIMDIRECTION|CMF_OFFSETPITCH, frandom(-10, -90));
		grenadetime++;
	}
	Loop;
   XDeath:
	TNT1 A 0 A_SetRenderStyle(1, STYLE_ADDSHADED);
	TNT1 A 0 A_StartSound("PM/MagicFireExplode", 10, attenuation: 0.5, pitch: 0.45);
	TNT1 A 0 A_StartSound("PMInfernal/Explode", 11, pitch: 1.6);
	TNT1 A 0 Radius_Quake (4, 54, 0, 15, 0);
	TNT1 AAAA 0 A_SpawnItemEx("PM_HectebusBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),angle:random(0,360),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AA 0 A_SpawnItemEx("PM_OFireBallExplode",0,0,0,flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AAAAA 0 A_SpawnItemEx("PM_CopyHuskMeteorFire",random(-7,7),random(-7,7),random(1,7),frandom(-8,8),frandom(-8,8),frandom(0,8));
	TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner");
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("PM_NobleFireShrapnel",0,0,0,random(-2,15),random(-2,15),random(-2,15),0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	HUBL EFGHI 1 BRIGHT Light("GreenBall");
	Stop;
	}
}

Class PM_GrievanceSeeker : WickedShot
{
	Default
	{
		+SEEKERMISSILE
		Speed 8;
		FastSpeed 11;
		DamageFunction 8;
		DamageType "Acid";
		DeathSound "Grievance/HomingExp";
	}
	
	States
	{
	Spawn:
		WIBL ABCD 1 Bright Light("Greenball") 
		{
			A_Tracer2(2);
			A_SpawnParticle("80FF38",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
			A_SpawnParticle("80FF38",SPF_FULLBRIGHT|SPF_RELATIVE,95,27,0,random(-22,-15),random(-5,5),random(-5,5),1,0,0,0,0,0,1,-1,-0.8);
			A_SpawnParticleEx("", tex[0], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT, lifetime: 1, size: 66.0, xoff: -10, startalphaf: 0.8);
		}
		Loop;
	}
}

Class PM_GrievanceStealthBazooka : PM_GrievanceRocket
{
    Default
    {
	Radius 3;
	Height 3;
	Scale 1.3;
	DamageFunction (50);
	StencilColor "24ff24";
	Speed 5;
	Alpha 0;
	Projectile;
	+RANDOMIZE
	+SEEKERMISSILE
	Decal "BigScorch";
	RenderStyle "Translucent";
	SeeSound "";
	DeathSound "";
    }
	
	void MissileFX()
	{
		A_SpawnItemEx("PM_DraugrRocketFireGlobs",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,128|SXF_TRANSFERTRANSLATION|SXF_TRANSFERALPHA);
		A_SpawnParticleEx("", tex[0], STYLE_ADD, SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, int(ceil( frandom(1, 10) )), random(1, 40), angle, frandom(-10, 10), frandom(-10, 10), frandom(-10, 10), startalphaf: alpha * frandom(.5, 1), startroll: random(-180,180));
		A_SpawnParticleEx("24ff24",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 76,size: 30.0,xoff: -10,startalphaf: alpha / 2.5,fadestepf: -1,startroll: random(-180,180));
		A_SpawnParticleEx("00ff00",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -10,startalphaf: alpha / 1.5);
	}
	
	States
	{
	Spawn:
		CBRK A 15;
		CBRK A 0 A_StartSound("Grievance/StealthBazookaLoop",CHAN_6,CHANF_LOOPING,1.0);
		CBRK A 0 A_ScaleVelocity(2.15);
	Go:
		CBRK AA 1 BRIGHT
		{
			MissileFX();
			if (alpha > 0) alpha--;
		}
		TNT1 A 0 A_JumpIfTracerCloser(400,"DetectStart");
		Loop;
	DetectStart:
	    TNT1 A 0 A_StartSound("Draugr/RocketLock",CHAN_5);
		TNT1 A 0 A_SetSpeed(20);
	Detect:
		CBRK AAAAAA 1 BRIGHT Light("GreenBall") 
		{
		if (alpha < 1) alpha++;
		MissileFX();
		A_SeekerMissile(1,2,SMF_LOOK);
		}
		Loop;
	Death:
		TNT1 A 0 
		{
		A_SetRenderStyle(1, STYLE_ADDSHADED);
		A_Explode(40,250);
		Radius_Quake (3, 34, 0, 10, 0);
		}
		TNT1 A 0 A_RadiusThrust(9000,90);
		TNT1 A 0 A_StartSound("BigExplosionDistant", CHAN_7);
		TNT1 A 0 A_StartSound("Grievance/StealthBazooka", CHAN_7);
		TNT1 A 0 A_SpawnItemEx("OrangeShockwaveBig",flags:SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		TNT1 A 0 A_SpawnItemEx("OrangeExplosionFlare",flags:SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		TNT1 AAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner",flags:SXF_NOCHECKPOSITION);
		TNT1 AAA 0 A_SpawnItemEx("PM_NobleFireShrapnel",0,0,0,random(-2,15),random(-2,15),random(-2,15),0,SXF_NOCHECKPOSITION,70);
		TNT1 AAAA 0 A_SpawnItemEx("PM_ExplosionLongerSmokeBig",0,0,1,random(-8,8),random(-8,8),random(0,3),0,0,70);
		TNT1 AAAAAAAA 0 A_SpawnItemEx("PM_HectebusBallExplode",0,0,0,random(-5,5),random(-5,5),random(-5,5),random(0,360),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
		TNT1 A 18 BRIGHT Light("Greenball");
		Stop;
	}
}

//Copy Attacks
Class PM_CopyHuskMeteor : PM_HuskMeteor
{
  Default
  {
  RenderStyle "AddShaded";
  StencilColor "5af542";
  }
  
  States
  {
  Spawn:
    HUBL AABBCCDD 1 BRIGHT Light("GreenBall2")
	{
	  A_SeekerMissile(1,1);
	  A_SetRoll(roll+3);
	  A_SpawnItemEx("PM_DraugrRocketFireGlobs",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	  A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 56,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	  A_SpawnParticleEx("",TexMan.CheckForTexture("SPKOG0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -10,startalphaf: 0.6);
	}
	Loop;
  Death:
	TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
	TNT1 AAAA 0 A_SpawnItemEx("PM_HectebusBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),angle:random(0,360),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AA 0 A_SpawnItemEx("PM_OFireBallExplode",0,0,0,flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
    HUBL EFGHI 1 BRIGHT Light("GreenBall");
	TNT1 AAA 0 A_SpawnItemEx("PM_CopyHuskMeteorFire",random(-7,7),random(-7,7),random(1,7),frandom(-8,8),frandom(-8,8),frandom(0,8));
	TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_ExplosionLongerSmokeBig2",0,0,1,random(-3,3),random(-3,3),random(-3,3),0,0,70);
	TNT1 AAAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
	TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner");
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("PM_NobleFireShrapnel",0,0,0,random(-2,15),random(-2,15),random(-2,15),0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	Stop;
	}
}

Class PM_CopyHuskMeteorFire : PM_HuskMeteorFire
{
  Default
  {
  RenderStyle "AddShaded";
  StencilColor "5af542";
  }
  
  States
  {
  Spawn:
    TNT1 A 0;
	TNT1 A 0 { timer = random(4,8); }
  Go:
    SPKO AA 2
	{ A_SpawnParticleEx ( "", TexMan.CheckForTexture("SPKOG0"), style: STYLE_Add,
	 flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: 30, size: 20.0, 
	 velx: frandom(-0.5,0.5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5), startalphaf: 0.75,
	 fadestepf: -0.35, sizestep: -1, startroll: random(-180,180)); }
	SPKO A 0 A_CheckFloor("FirePop");
	Loop;
  FirePop:
    TNT1 A 0;
	TNT1 A 0 {
	A_SetScale(0.85);
	A_ChangeVelocity(0,0,0,CVF_REPLACE);
	A_StartSound("PM/WrathTele2",16);
	A_StartSound("HugeFire/Idle",5,CHANF_LOOPING);
	for(int i=random(3,4);i>0;i--)
	 A_SpawnItemEx("PM_SmallerGenericExplosionFlames",0,0,0,frandom(-2.45,2.45),frandom(-2.45,2.45),frandom(0.45,3),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSTENCILCOL);
	}
  Go:
	FLME A 1 bright A_Explode(8,56);
	FLME BCDEFGHIJKLM 2 Bright A_SpawnParticleEx ( "", TexMan.CheckForTexture("SPKOG0"), style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: random(1,30), size: random(5,15), xoff: frandom(-12,12), yoff: frandom(-12,12), zoff: frandom(0.45,1), velx: frandom(-0.25,0.25), vely: frandom(-0.25,0.25), velz: frandom(3.0,6.5), startalphaf: 0.85, fadestepf: -0.55, sizestep: -0.25, startroll: random(-180,180));
	TNT1 A 0 { if(timer == 0) SetStateLabel("GoEnd"); timer--; }
    Loop;
  }
}

Class PM_CopyFiendHomingMissile : PM_FiendHomingMissile
{
    Default
    {
	Decal "PM_GreenFireScorch";
	Translation "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:235=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "250:250=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "48:79=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
    }
	
	override void PostBeginPlay()
	{
	 RevenantTracer.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("SPKOG0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	}
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0;
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMHO AAAAAAAAAAAAA 1 BRIGHT { A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,70),size: frandom(0.2,9),xoff: random(-10,1),yoff: random(-2,2),zoff: random(-2,2),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0,fadestepf: -0.5,sizestep: -0.2,startroll: random(-180,180)); }
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMHO AAAAAAAAAAAA 1 BRIGHT { A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,70),size: frandom(0.2,9),xoff: random(-10,1),yoff: random(-2,2),zoff: random(-2,2),velx: frandom(-0.6,0.6),vely: frandom(-0.6,0.6),velz: frandom(-0.6,0.6),startalphaf: 1.0,fadestepf: -0.5,sizestep: -0.2,startroll: random(-180,180)); }
		TNT1 A 0 A_StartSound("Fiend/MissileLaunch",24,volume:0.90,pitch:frandom(0.8,1.15));
		TNT1 A 0 A_StartSound("Cadaver/HomingLoop",CHAN_6,CHANF_LOOPING,0.8);
		TNT1 A 0 A_ScaleVelocity(3);
	Go:
		CMHO A 1 BRIGHT Light("Greenball2") 
		{
		A_SpawnItemEx("PM_FiendHFireGlobs",random(-1,1),random(-1,1),random(-1,1),flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
		}
		CMHO A 1 BRIGHT Light("Greenball2") 
		{
		A_SpawnItemEx("PM_FiendHFireGlobs",random(-1,1),random(-1,1),random(-1,1),flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		A_SeekerMissile(1,random(1,5),SMF_PRECISE);
		A_SpawnParticleEx("",tex[1],style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: random(20,80),size: 15.0,xoff: frandom(-2,-15),startalphaf: 0.8,fadestepf: -1,sizestep: 0.8,startroll: random(-180,180),rollvel: frandom(-0.5,0.5));
	    A_SpawnParticleEx("00ff00",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 50.0,xoff: -10,startalphaf: 0.6);
		}
		Loop;
	Death:
		TNT1 A 0 A_StopSound(CHAN_6);
		TNT1 A 0 A_StartSound("SmallMissileExplosion");
		TNT1 A 0 
		{
	     A_SpawnParticleEx
		("22ff00",TexMan.CheckForTexture("EXN1B0"), style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
		lifetime: 30, size: 80.0, startalphaf: 1, fadestepf: -1, sizestep: frandom(0.0,2.5), startroll: random(-180,180));
		}
		TNT1 AAAA 0 A_SpawnItemEx("PM_FiendBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		FSHT BBCCDDEE 1 BRIGHT Light("Greenbal3");
		Stop;
	}
}

Class PM_CopyCadaverHRocket : PM_CadaverHRocket
{
    Default
    {
	Decal "PM_GreenFireScorch";
	Translation "16:47=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "160:235=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "250:250=%[0.00,0.00,0.00]:[1.01,2.00,0.00]", "48:79=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
    }
	
	States
	{
	Spawn:
		TNT1 A 0;
		TNT1 A 0;
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMHO AAAAAAA 1 BRIGHT;
		TNT1 A 0 A_ScaleVelocity(0.6);
		CMHO AAAAAA 1 BRIGHT;
		TNT1 A 0
		{
		  A_SpawnItemEx("PM_CopyHomingFlare",0,0,0);
		  A_SpawnItemEx("PM_DevilBallSmoke",random(-1,1),random(-1,1),random(-1,1),0,0,0,0,flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		}
		TNT1 A 0 A_StartSound("Cadaver/Homing",CHAN_5,CHANF_DEFAULT,1);
		TNT1 A 0 A_StartSound("Cadaver/HomingLoop",CHAN_6,CHANF_LOOPING,0.6);
		TNT1 A 0 A_ScaleVelocity(3);
	Go:
		CMHO A 1 BRIGHT Light("Greenball2") 
		{
		  A_SpawnItemEx("PM_CopyHomingFlare",0,0,0);
		  A_SpawnItemEx("PM_CadaverHomingFireGlobs2",random(-1,1),random(-1,1),random(-1,1),0,0,0,0,flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		  A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
		}
		CMHO A 1 BRIGHT Light("Greenball2") 
		{
		  A_SpawnItemEx("PM_CadaverHomingFireGlobs",random(-1,1),random(-1,1),random(-1,1),0,0,0,0,flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		  A_SeekerMissile(2,3,SMF_PRECISE);
		  A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 56,size: 30.0,xoff: -10,startalphaf: 0.4,fadestepf: -1,startroll: random(-180,180));
	      A_SpawnParticleEx("00ff00",TexMan.CheckForTexture("LENRA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -10,startalphaf: 0.6);
		}
		Loop;
	Death:
		TNT1 A 0 A_StopSound(CHAN_6);
		TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
		TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode",flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		TNT1 AAAA 0 A_SpawnItemEx("PM_SmallerGenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,flags:SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION);
		TNT1 AAAA 0 A_SpawnProjectile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360));
		TNT1 AAAA 0 A_SpawnItemEx("PM_FiendBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
		TNT1 A 5 BRIGHT Light("Greenball2");
		Stop;
	}
}

Class PM_CopyHomingFlare : Actor
{
	Default
	{
		+NOINTERACTION
		+NOGRAVITY
		+FORCEXYBILLBOARD
		+CLIENTSIDEONLY
		Renderstyle "AddShaded";
		StencilColor "5af542";
		Radius 1;
		Height 1;
		Scale 0.25;
		Alpha 0.5;
	}
	
	States
	{
	Spawn:
		LENR A 2 Bright NoDelay;
		Stop;
	}
}