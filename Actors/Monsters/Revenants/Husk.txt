ACTOR PM_Husk : Revenant
{
  Health 300
  Radius 20
  Height 56
  Mass 500
  Speed 7
  PainChance 80
  Monster
  //+MISSILEMORE 
  +FLOORCLIP
  +DONTHURTSPECIES
  +DONTHARMSPECIES
  +DONTHARMCLASS
  //+NOINFIGHTING
  Species "Revenant"
  SeeSound "Husk/Sight"
  PainSound "Husk/Pain"
  DeathSound "Fiend/Death"
  ActiveSound "Fiend/Active"
  MeleeSound "Husk/Melee"
  HitObituary "%o felt the stinging power behind a Husk's fist!"
  Obituary "%o couldn't evade a Husk's smouldering concoction!"
  BloodColor "ff 50 00"
  //DropItem "RagePoint1"
  MeleeThreshold 60
  DamageFactor "Poison",0
  DamageFactor "PoisonCloud",0
  DamageFactor "GhoulCloud",0
  Tag "Husk"
  PoisonDamage 128 //Poison Damage Duration from Melee About 4-5 Seconds
  States
  {
  Spawn:
	GHUL A 10 A_Look
    Loop
  See:
	TNT1 A 0 A_Jump(40,"See2")
	TNT1 A 0 A_SetSpeed(7)
    GHUL AABBCC 3
	{
	A_Chase;
	A_SpawnItemEx("HuskSmoke",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
	A_SpawnItemEx("OrangeEmbersFloat",0,0,random(25,65),FRandom(2,-2),FRandom(2,-2),FRandom(2,-2),0,128,220);
	}
	TNT1 A 0 A_SpawnItemEx("PM_HuskWalkFire",0,5,0,1,0,0,0,128)
	NULL A 0 A_PlaySound("skeleton/step",5)
	GHUL DDEEFF 3
	{
	A_Chase;
	A_SpawnItemEx("HuskSmoke",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
	A_SpawnItemEx("OrangeEmbersFloat",0,0,random(25,65),FRandom(2,-2),FRandom(2,-2),FRandom(2,-2),0,128,220);
	}
	TNT1 A 0 A_SpawnItemEx("PM_HuskWalkFire",0,-5,0,1,0,0,0,128)
	NULL A 0 A_PlaySound("skeleton/step",5)
    Loop
  See2:
	TNT1 A 0 A_SetSpeed(17)
    GHUL AABBCC 2
	{
	A_Chase;
	A_SpawnItemEx("PM_HuskRushFire",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
	A_SpawnItemEx("HuskSmoke",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
	A_SpawnItemEx("OrangeEmbersFloat",0,0,random(25,65),FRandom(2,-2),FRandom(2,-2),FRandom(2,-2),0,128,220);
	}
	TNT1 A 0 A_SpawnItemEx("PM_HuskWalkFire2",0,5,0,1,0,0,0,128)
	NULL A 0 A_PlaySound("skeleton/step",5)
	GHUL DDEEFF 2
	{
	A_Chase;
	A_SpawnItemEx("PM_HuskRushFire",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3));
	A_SpawnItemEx("OrangeEmbersFloat",0,0,random(25,65),FRandom(2,-2),FRandom(2,-2),FRandom(2,-2),0,128,220);
	}
	TNT1 A 0 A_SpawnItemEx("PM_HuskWalkFire2",0,-5,0,1,0,0,0,128)
	NULL A 0 A_PlaySound("skeleton/step",5)
	//TNT1 A 0 A_Jump(20,"See")
    Loop
  Melee:
    GHUL G 1 A_FaceTarget
    GHUL G 6 A_PlaySound ("Husk/MeleeSwing")
    GHUL H 6 A_FaceTarget
    GHUL I 6 A_CustomMeleeAttack(random(1,8)*6,"Husk/Melee","")
    Goto See
  Missile:
	TNT1 A 0 A_Jump(210,"Missile2")
	TNT1 A 0 A_PlaySound("GenericChargeHeavy",CHAN_6)
    GHUL GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 Light("AvatarFireball3") 
	{
	A_FaceTarget;
	A_Spawnitemex("PM_HuskFireGlobs",random(-3,3),random(21,27),30,random(-1,1),random(-1,1),random(0,3));
	}
    GHUL H 5 Light("AvatarFireball3") A_FaceTarget
	GHUL I 10 Light("AvatarFireball4")
	{
	A_PlaySound("Husk/Throw");
	A_CustomMissile("PM_HuskMeteor",43,10,0);
	A_FaceTarget;
	}
	TNT1 A 0 A_StopSound(CHAN_6)
    GHUL I 5 A_FaceTarget
    Goto See
  Missile2:
	TNT1 A 0 A_PlaySound("GenericChargeSmall")
	GHUL GGGGGGGGGGG 1 Light("AvatarFireball3")
	{
	A_FaceTarget;
	A_Spawnitemex("OrangeFireEffect",random(-3,3),random(21,27),30,random(-1,1),random(-1,1),random(0,3));
	}
	GHUL H 3 Light("AvatarFireball3")
	{
	A_FaceTarget;
	A_CustomMissile("PM_HuskSmallFireball",52,10,-40,CMF_OFFSETPITCH,50);
	}
	GHUL H 3 Light("AvatarFireball3")
	{
	A_FaceTarget;
	A_CustomMissile("PM_HuskSmallFireball",48,10,-18,CMF_OFFSETPITCH,28);
	}
	GHUL I 3 Light("AvatarFireball3")
	{
	A_FaceTarget;
	A_CustomMissile("PM_HuskSmallFireball",45,10,0,CMF_OFFSETPITCH,0);
	}
	GHUL I 3 Light("AvatarFireball3")
	{
	A_FaceTarget;
	A_CustomMissile("PM_HuskSmallFireball",40,10,25,CMF_OFFSETPITCH,0);
	}
	GHUL II 5 Light("AvatarFireball4") A_FaceTarget
	GHUL HGA 4 A_FaceTarget
	Goto See
  Pain:
	TNT1 A 0 A_StopSound(CHAN_6)
    GHUL L 5
    GHUL L 5 A_Pain
    Goto See
  Death:
	TNT1 A 0 A_StopSound(CHAN_6)
    GHUL LM 7 A_RemoveChildren(1,RMVF_EVERYTHING)
    GHUL N 7 A_Scream
	TNT1 AAAAAAA 0
		{
		A_SpawnItemEx("MonsterHole",0,0,20,random(-1,1),random(-1,1),random(-1,1),random(1,360));
		}
	TNT1 A 0 bright A_CustomMissile("PyroImpBallFire2", 0, 0, 0, 0)
    GHUL O 7 A_NoBlocking
	TNT1 AAAAA 0 A_SpawnItemEx("PM_HuskSmokeCloud",0,0,2,random(-1,1),random(-1,1),random(0,1))
    GHUL P 7
	TNT1 A 0 A_PlaySound("Corpse/FallMed",CHAN_6)
    GHUL Q -1
    Stop
  Raise:
    GHUL Q 5
    GHUL PONML 5
    Goto See
  }
}

Actor HuskSmokeSpawn
{
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 3 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
	TNT1 A 0 A_SpawnItemEx("HuskSmoke",random(-10,10),random(-16,16),random(25,65),0,0,frandom(1,3))
    Loop
  }
}

Actor HuskEmberSpawn
{
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 30 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
	TNT1 A 0 A_SpawnItemEx("OrangeEmbersFloat",0,0,random(25,65),FRandom(2,-2),FRandom(2,-2),FRandom(2,-2),0,128)
    Loop
  }
}

Actor PM_HuskWalkFire
{
  +NoClip
  +Missile
  +DropOff
  +Randomize
  +CLIENTSIDEONLY
  Scale 0.75
  Speed 0
  RenderStyle Add
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 8
    FRTF A 3 Bright
    FRTF A 0 
    FRTF B 3 Bright
    FRTF B 0 
    FRTF C 3 Bright
    FRTF DEFGHIJKLMNO 3 Bright
    Stop
  }
}

Actor PM_HuskWalkFire2
{
  +NoClip
  +Missile
  +DropOff
  +Randomize
  +CLIENTSIDEONLY
  Scale 2
  Speed 0
  RenderStyle Add
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 8
    FRTF A 3 Bright
    FRTF A 0 
    FRTF B 3 Bright
    FRTF B 0 
    FRTF C 3 Bright
    FRTF DEFGHIJKLMNO 3 Bright
    Stop
  }
}

Actor HuskSmoke
{
  +NOINTERACTION
  +ROLLSPRITE
  Renderstyle Translucent
  Scale 0.4
  Alpha 0.6
  States
  {
  Spawn:
	TNT1 A 1
	TNT1 A 0 A_CheckSight("SightNull")
	TNT1 A 0 A_SetRoll(random(0,360))
	SMK3 ABCDEFGHIJKLM 2 A_FadeOut(0.008)
	Stop
  SightNull:
	TNT1 A 1
	Stop
	}
}

Actor WizardSmokeSpawn
{
  +NOINTERACTION
  States
  {
  Spawn:
    TNT1 A 4 A_Warp(AAPTR_MASTER,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
	TNT1 A 0 A_SpawnItemEx("WizardSmoke",random(-10,10),random(-16,16),random(25,58),0,0,frandom(1,3))
    Loop
  }
}

Actor WizardSmoke
{
  +NOINTERACTION
  Renderstyle Translucent
  Scale 0.4
  Alpha 0.3
  States
  {
  Spawn:
	TNT1 A 0 NoDelay A_SetRoll(random(0,360))
	RSM1 A 1 A_FadeOut(0.008)
	Wait
	}
}

ACTOR PM_HuskSmallFireball : DoomImpBall
{
  Speed 8
  Damage (12)
  Scale 0.8
  Projectile 
  +RANDOMIZE
  +SEEKERMISSILE
  renderstyle Translucent
  Alpha 0.9
  SeeSound "GenericSmallFireball"
  States
 {
  Spawn:
    TNT1 A 1 BRIGHT Light("AvatarFireball2")
	{
	A_SeekerMissile(1,1);
	A_SpawnItemEx("PM_CadaverFireGlobs");
	//A_SpawnItemEx("DevilBallSmoke",random(-1,1),random(-1,1),0,0,0,0,0,128,50);
	//A_SpawnItemEx("PM_CadaverRocketFlare",0,0,0);
	}
	Loop
  Death:
	TNT1 A 0 A_SpawnItemEx("OrangeEmbersSpawn")
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode")
	TNT1 A 0 A_SpawnItemEx("PM_FireballExplosionFlames")
    AFX5 D 2 bright Light("AvatarFireball4")
    AFX5 EFGH 2 Light("AvatarFireball4") bright
	Stop
	}
}

ACTOR PM_HuskMeteor : DoomImpBall
{
  Speed 17
  Damage(45)
  Scale 0.8
  Projectile 
  +RANDOMIZE
  +SEEKERMISSILE
  renderstyle Translucent
  Alpha 0.9
  SeeSound "GenericChargeHeavy"
  DeathSound "GenericCometExp"
  States
 {
  Spawn:
    HUBL AABBCCDD 1 BRIGHT Light("AvatarFireball4")
	{
	A_SeekerMissile(1,1);
	A_SpawnItemEx("PM_DraugrRocketFireGlobs",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,128,0);
	A_SpawnItemEx("DevilBallSmoke",random(-1,1),random(-1,1),0,0,0,0,0,128,50);
	A_SpawnItemEx("PM_CadaverRocketFlare",0,0,0);
	}
	Loop
  Death:
	TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0)
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode",0,0,0)
	TNT1 AAAA 0 A_CustomMissile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360))
	TNT1 A 0 A_SpawnItemEx("OFireBallExplode",0,0,0)
    HUBL EFGHI 1 BRIGHT Light("AvatarFireball4")
	TNT1 A 0 A_CustomMissile("PyroImpBallFire2", 0, 0, 0, 0)
	TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_HuskSmokeCloud",0,0,0,random(-2,2),random(-2,2),random(-2,2),0,SXF_NOCHECKPOSITION)
	TNT1 AAAAA 0 A_CustomMissile("PM_DarkExpSmoke",7,0,random(0,360),2,random(0,360))
	TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION)
	TNT1 AAA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0)
	TNT1 AAAAAAAAAA 0 A_SpawnItemEx("PM_ExileFireShrapnel",0,0,0,random(-2,15),random(-2,15),random(-2,15),0,SXF_NOCHECKPOSITION)
	Stop
	}
}

Actor PM_HuskSmokeCloud
{
  Radius 2
  Height 2
  Mass 0x7fffffff
  +NOBLOCKMAP
  +NOGRAVITY
  +DROPOFF
  +NODAMAGETHRUST
  +DONTSPLASH
  +NOTELEPORT
  +SKYEXPLODE
  +FORCEXYBILLBOARD
  +FOILINVUL
  +CANBLAST
  +RIPPER
  +THRUACTORS
  +BLOODLESSIMPACT
  +BLOCKEDBYSOLIDACTORS
  +FORCERADIUSDMG
  +DOOMBOUNCE
  +ROLLSPRITE
  BounceFactor 0.3
  WallBounceFactor 0.3
 renderstyle Translucent
  Alpha 0.93
  Scale 3.5
  DeathSound "Husk/Cloud"
  states
  {
  Spawn:
	TNT1 A 1
	TNT1 A 0 A_SetRoll(random(0,360))
    X111 AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPPQQRR 10 A_Explode(Random(1,4),68)
  Death:
	TNT1 A 1
	Stop
  }
}