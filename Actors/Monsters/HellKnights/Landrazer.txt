Class PM_Landrazer : PMWrathMonsterBase
{
    int standoff;
	int user_leapstrength;
    Default
	{
	PMMonsterBase.CodexLore
	"Landrazers are dangerously efficient archdemons residing within Wrath. These slightly older nobles have adapted to the harshest conditions within the circle, and have been partially augmented by an unknown force in order to maximize their strength against humans, with the sideeffect of lowering their normally heightened aggression.
	 Their intelligence, then, would be around the same as a Noble, preferring to cast flames from a distance or pick off groups with its flame cannon. If its rage reaches a critical point, it's augmented body prevents it from completely taking over the mind, thus it only empowers its speed and firepower.
	 
	 Fires and casts fireballs, and can snipe you with deadly comets from a distance.";
    Health 500;
    Radius 24;
    Height 64;
    Speed 8;
    PainChance 50;
    Mass 1000;
	Scale 1.15;
	MeleeRange 170;
    SeeSound "Landrazer/Sight";
    ActiveSound "Landrazer/Active";
    PainSound "Landrazer/Pain";
    DeathSound "Landrazer/Death";
	Species "Nobles";
    Obituary "%o was immediately lit ablaze by a Landrazer.";
	Tag "Landrazer";
    BloodType "PM_LavaBloodBase";
    BloodColor "Orange";
    MONSTER;
	+DONTHARMSPECIES
    +FLOORCLIP
	+MissileMore
	+DONTHARMCLASS
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_WrathTeleportFog";
	}
	
	override void PostBeginPlay()
	{
	 pm_irecount = random(200,260);
	 tex[0] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("DTPRO0",TexMan.Type_Sprite);
	 tex[3] = TexMan.CheckForTexture("LENRA0",TexMan.Type_Sprite);
	 tex[4] = TexMan.CheckForTexture("BLHDA0",TexMan.Type_Sprite);
	 Super.PostBeginPlay();
	}
	
	TextureID tex[5];
    
	void PM_LandrazerChase()
	{
	 if(ire)
	  {
	   A_SetTics(2);
	   A_Chase("BlastCharge","IreBarrage");
	  }
	 else
	  { 
	   A_Chase(null,"Missile");
	   pm_irecount--;
	  }
	}
	

  States
  {
  Spawn:
    LRA_ AA 10 PM_Look();
    Loop;
  Look:
    LRA_ AABB 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Landrazer/Step",90,CHANF_OVERLAP,pitch: frandom(0.95,1.2));
	LRA_ BB 3 A_JumpIfTargetInLOS("Spotted",170,JLOSF_DEADNOJUMP);
	LRA_ CCDD 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Landrazer/Step",90,CHANF_OVERLAP,pitch: frandom(0.95,1.2));
	LRA_ DD 3 A_JumpIfTargetInLOS("Spotted",170,JLOSF_DEADNOJUMP);
    Loop;
  Spotted:
    TNT1 A 0 PM_AlertSound();
  See:
	TNT1 A 0 
	{
	 if(ire == true)
	 { A_Jump(256,1); }
	 else if(pm_irecount < 0)
	 { SetStateLabel("Ire"); }
	}
	TNT1 A 0 
	{ 
	 if(ire)
	  { A_Recoil(-0.5); }
	  A_SpawnItemEx("PM_WalkFire",6,10,0,0,0,0,0,SXF_NOCHECKPOSITION);
	  A_StartSound("Landrazer/Step",90,CHANF_OVERLAP,pitch: frandom(0.95,1.2));
	}
	LRA_ A 2;
    LRA_ AAABBB 2 PM_LandrazerChase();
	TNT1 A 0 
	{ 
	 if(ire)
	  { A_Recoil(-0.5); }
	  A_SpawnItemEx("PM_WalkFire",6,-10,0,0,0,0,0,SXF_NOCHECKPOSITION);
	  A_StartSound("Landrazer/Step",90,CHANF_OVERLAP,pitch: frandom(0.95,1.2));
	}
	LRA_ C 2;
	LRA_ CCCDDD 2 PM_LandrazerChase();
	Loop;
  BlastCharge:
	TNT1 A 0 { bNODROPOFF = true; bNOPAIN = true; }
	TNT1 A 0 { A_StartSound("Landrazer/Charge",22); A_StartSound("Bruiser/LeapCharge",23,pitch:1.55); }
	LRA_ JJJJJJJJJJJJJJJJUUUUUUUUUUUUUUUUUU 1 
	{
	  PM_LeadTarget(30);
	  A_SpawnItemEx("PM_HuskFireGlobs",-20,22,56,0,2,-1,flags:SXF_NOCHECKPOSITION);
	  A_SpawnItemEx("PM_HuskFireGlobs",-20,-22,56,0,-2,-1,flags:SXF_NOCHECKPOSITION);
	  A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(30,60), size: frandom(1,300), xoff: random(-30,30), yoff: random(-30,30),zoff: random(-10,70), startalphaf: frandom(0.3,0.7), startroll: random(-180,180) );
	}
	LRA_ U 5
	{
	  A_StartSound("Bruiser/LeapPropel",25);
	  A_StartSound("Noble/BombExpFar",25);
	  A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 8, size: 360, zoff: 40, startalphaf: 1, startroll: random(-180,180) );
	  A_SpawnParticleEx( "", tex[4], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 20, size: 800, zoff: 50, startalphaf: 1, fadestepf: -1, sizestep: -25,startroll: random(-180,180) );
    for(int i=3;i>0;i--)
	  { A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0); }
	}
	TNT1 A 0 A_Recoil(-30);
	LRA_ VVVVVVVVVVVVVVVVVWWWWWWWWWWWWWWWWWW 1
	{
	 A_SpawnItemEx("PM_WalkFire",6,10,0,0,0,0,0,SXF_NOCHECKPOSITION,170);
	 A_SpawnItemEx("PM_WalkFire",6,-10,0,0,0,0,0,SXF_NOCHECKPOSITION,170);
	 A_Recoil(-0.5);
	 A_SpawnProjectile("PM_WeakMeleeHitbox",42,0,0,CMF_AIMDIRECTION);
	 A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(25,40), size: frandom(1,200), xoff: random(-50,50), yoff: random(-50,50),zoff: random(-10,70), startalphaf: frandom(0.3,0.7), startroll: random(-180,180) );
	}
	LRA_ AAAA 1 A_FaceTarget();
	TNT1 A 0 { bNODROPOFF = false; }
	TNT1 A 0 
	{
	 if(ire)
	   A_Jump(256,1);
	 else
	   bNOPAIN = false;
	}
	Goto See;
	Leap:
		TNT1 A 0 A_Jump(55,"MissileReel");
	    TNT1 A 0 
	    { 
	     if(target && Distance3D(target) >= 768)
	      { SetStateLabel("MissileReel"); }
	    }
		TNT1 A 0 A_JumpIfCloser(256,"ChargeAttack"); //if too close, charges towards target
		TNT1 A 0
		{
			A_StartSound("Landrazer/Sight",13);
			A_StartSound("CBrute/Hyd",14);
		}
		LRA_ UUUU 4  A_FaceTarget();
		LRA_ UUU 5 A_FaceTarget();
		LRA_ J 2 PM_LeapTo(target, 968, "MissileReel");
		TNT1 A 0 A_StartSound("Squire/Leap",15);
		TNT1 A 0 A_Recoil(-4);
	LeapLoop:
		LRA_ VVVV 1 
		{
		 if(random(1,15) == 1)
		  { ThrustThing(random(0,360)*256/360,2,0,0); }
		  A_SpawnParticleEx( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(25,40), size: frandom(1,200), xoff: random(-50,50), yoff: random(-50,50),zoff: random(-10,70), startalphaf: frandom(0.3,0.7), startroll: random(-180,180) );
		}
        TNT1 A 0 A_CheckFloor("LeapEnd");
		Loop;
	LeapEnd:
		 TNT1 A 0 A_StartSound("Doom4/Monsters/BaronFloorPound",CHAN_7,CHANF_OVERLAP);
		 TNT1 A 0 A_Quake(4,20,0,512);
		 TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_BulletShrapnelNoCheck",random(-5,5),random(-5,5),0,random(-3,3),random(-5,5),random(5,20),0,SXF_NOCHECKPOSITION);
         TNT1 AAAAA 0 A_SpawnItemEx("PM_BoomSmoke",random(-1,1),random(-1,1),random(2,4),random(-1,1),random(-1,1),random(0,1),0,SXF_NOCHECKPOSITION);
         TNT1 AA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	     TNT1 A 0 { if(target && CheckSight(target) && Distance3D(target) < 145) SetStateLabel("DoDamage"); }
		 Goto ResumeLeapEnd;
	DoDamage:
		 TNT1 A 0 A_BetaSkullAttack;
		 Goto ResumeLeapEnd;
	ResumeLeapEnd:
		 LRA_ W 1 A_Stop;
		 LRA_ W 6;
		 Goto See; 
  IreBarrage:  
    TNT1 A 0 { A_StartSound("Landrazer/Ready",20); A_StartSound("MechaAraneaReady",21,pitch:0.85); }
    LRA_ E 5 A_FaceTarget(360,180);
    LRA_ EEEEEE 5 A_FaceTarget(14,14);
	LRA_ E 25 A_StartSound("Landrazer/Lock",22); 
  IBLoop:
	LRA_ F 2 Light("AvatarFireball2") 
	{
	 A_StartSound("Bulltubus/Gun",23,CHANF_OVERLAP,pitch:0.85);
	 A_StartSound("Sybarite/Autocannon",24,CHANF_OVERLAP,pitch:1.15);
	 A_SpawnProjectile("PM_LandrazerMissile",38,15,frandom(-3.5,3.5),CMF_AIMDIRECTION,pitch+frandom(-5.5,5.5));
	}
	LRA_ EEE 1 A_FaceTarget(14,14);
	LRA_ E 1 A_MonsterRefire(68,"See");
	TNT1 A 0 
	{
	  if(target && Distance3D(target) < 300 && random(0,30) == 1)
	  { SetStateLabel("BlastCharge"); }
	}
	Loop;
  Missile:
	TNT1 A 0 A_Jump(150, "Leap");
	TNT1 A 0 A_JumpIfCloser(550,"MeleeChoice");
  MissileReel:
    TNT1 A 0 A_Jump(90,"FireballThrow");
    TNT1 A 0 A_Jump(65,"Comets","MeteorThrow");
  FireballBurst:
    TNT1 A 0 { if(random(1,2) == 1) { A_StartSound("Landrazer/Attack",20); } A_StartSound("Hectebus/Aim",20,pitch:1.15); }
    LRA_ E 15 A_FaceTarget(360,180);
    LRA_ EEEEEEE 2 A_FaceTarget(15,15);
	LRA_ FFFFF 5 Light("AvatarFireball2") 
	{
     A_FaceTarget(15,15);
	 A_StartSound("Bulltubus/Gun",23,CHANF_OVERLAP,pitch:0.85);
	 A_StartSound("Sybarite/Autocannon",24,CHANF_OVERLAP,pitch:1.15);
	 A_SpawnProjectile("PM_LandrazerMissile",38,15,frandom(-3.5,3.5),CMF_AIMDIRECTION,pitch+frandom(-1.5,1.5));
	}
	LRA_ E 10 A_FaceTarget(15,15);
	Goto MissileEnd;
  MeleeChoice:
	TNT1 A 0;
	TNT1 A 0 A_Jump(80,"MissileReel");
	TNT1 A 0 A_JumpIfTargetInLOS("BlastCharge",170,JLOSF_DEADNOJUMP);
	Goto MissileReel;
  FireballThrow:
    LRA_ U 5 { if(random(1,2) == 1) { A_StartSound("Landrazer/Attack",20); } A_StartSound("GenericChargeSmall",21); A_FaceTarget(360,180); }
    LRA_ UUUUUUUUUUUUU 2 Light("AvatarFireball2") 
	{
      A_FaceTarget(8,8);
      A_SpawnItemEx("PM_HuskRushFire",random(-3,3),random(-25,-32),69,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
	}
    LRA_ VV 3 A_FaceTarget(8,8);
	LRA_ W 8 
	{
	 for(int i=random(3,6);i>0;i--)
	  { A_SpawnProjectile("PM_LandrazerFirebomb",32,0,frandom(-12,12),CMF_AIMDIRECTION,pitch+frandom(0,-15.5)); }
	}
	Goto MissileEnd;
  MeteorThrow:
    LRA_ U 5 { A_StartSound("Landrazer/Ready",20); A_StartSound("GenericChargeHeavy",21); A_FaceTarget(360,180); }
    LRA_ UUUUUUUUUUUUUUUUU 2 Light("AvatarFireball2") 
	{
      A_FaceTarget(12,12);
	  A_SpawnItemEx("PM_HuskFireGlobs",random(-3,3),random(-21,-27),69,frandom(-1,1),frandom(-1,1),frandom(0,3));
	  A_SpawnItemEx("PM_HuskRushFire",random(-3,3),random(-21,-27),69,frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
	}
    LRA_ VV 3 A_FaceTarget(12,12);
	LRA_ W 10 A_SpawnProjectile("PM_LandrazerMeteor",32,0,frandom(-5,5),CMF_AIMDIRECTION,pitch+frandom(-5.5,-10.5));
	Goto MissileEnd;
  Comets:
    TNT1 A 0 { A_StartSound("Landrazer/Ready",20); A_StartSound("CyberZ/Prefire",21,pitch:0.85); A_StartSound("Bruiser/LeapCharge",22); pm_attackloop = 3; }
    LRA_ E 2 A_FaceTarget(360,180);
    LRA_ EEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEEE 2 
	{
	  PM_LeadTarget(36);
	  A_SpawnParticleEx
	  ( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
	  lifetime: random(5,20), size: frandom(150,200), xoff: 20, yoff: random(16,30),
	  zoff: random(30,56), startalphaf: frandom(0.3,0.9), startroll: random(-180,180) );
	}
	LRA_ E 25 
	{
	  A_StartSound("Landrazer/Lock",22);
	  A_SpawnParticleEx
	  ( "", tex[4], style: STYLE_Add, flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,
	   lifetime: 20, size: 500, xoff: 20, yoff: random(16,30), zoff: 50, startalphaf: 1, fadestepf: -1, sizestep: -25,
       startroll: random(-180,180) );
	}
  CometLoop:
    TNT1 A 0 { if(!pm_attackloop) { SetStateLabel("CometEnd"); } pm_attackloop--; }
	LRA_ F 10 Light("AvatarFireball2") 
	{
	 PM_LeadTarget(36);
	 A_StartSound("Draugr/Fire",23,CHANF_OVERLAP);
	 A_SpawnProjectile("PM_LandrazerComet",38,15,frandom(-3.5,3.5),CMF_AIMDIRECTION|CMF_OFFSETPITCH,pitch+frandom(-1.5,1.5));
	}
	LRA_ E 3 A_StartSound("Sybarite/SlotIn",24);
	LRA_ EEEEEEEEEEEEE 2 
	{
	 A_FaceTarget();
	 A_SpawnItemEx("PM_HuskFireGlobs",15,22,40,frandom(0.5,1.5),frandom(0.5,1),frandom(0.1,3),flags:SXF_NOCHECKPOSITION);
	}
	LRA_ EEEE 2 A_SpawnItemEx("PM_HuskFireGlobs",15,22,40,frandom(0.5,1.5),frandom(0.5,1),frandom(0.1,3),flags:SXF_NOCHECKPOSITION);
	Loop;
  CometEnd:
    LRA_ E 10;
	TNT1 A 0 { pm_irecount -= 10; }
	Goto See;
  Ire:
    TNT1 A 0 A_StartSound("GenericChargeSmall",22); 
    LRA_ AAAAAEE 5 { bNOPAIN = true; A_FaceTarget(); }
	TNT1 A 0 { A_SpawnItemEx("PM_WrathIreActivate",0,0,15); A_StartSound("Landrazer/Sight1",20); }
	LRA_ JJJJJ 5 Light("AvatarFireball2") { A_FaceTarget(); A_RadiusThrust(1200,1000,RTF_NOIMPACTDAMAGE); }
	LRA_ JJJJJJJJJJJJ 2 A_FaceTarget();
	TNT1 A 0 A_StartSound("Landrazer/Sight2",21);
	LRA_ EEEEEEEEEEEJ 2 A_FaceTarget();
	LRA_ J 3 
	{ 
	 ire = true;
	 bNOINFIGHTING = true;
	 bNOFEAR = true;
	 A_SpawnItemEx("PM_WrathIreActivate",0,0,15);
	 A_SpawnItemEx("PM_WrathIreEffect",0,0,45,flags:SXF_SETMASTER);
    }
	TNT1 A 0 PM_IreStartSound();
	LRA_ JJJJJJJJJJJJJJJJJJJJJJJJ 4 Light("AvatarFireball3") 
	{
	A_FaceTarget();
	if (!(level.time % 2)) A_SpawnItemEx("PM_OrangeEmbersFloat",0,random(-10,10),15,random(1,12),random(-3,3),random(-3,3));
	}
	Goto See;
  MissileEnd:
    TNT1 A 0 { if(target && Distance3D(target) < 500 && random(0,2) == 1) SetStateLabel("BlastCharge"); }
	Goto See;
  Pain:
    LRA_ J 2;
    LRA_ J 8 A_Pain;
	TNT1 A 0 { pm_irecount -= random(10,15); }
    Goto See;
  Death:
    TNT1 A 0 
	{
	  A_StopSound(19);
	  A_StopSound(20);
      A_RemoveChildren(1,RMVF_EVERYTHING);
	  PM_Scream();
	  bDONTGIB = true;
	}
    LR__ AAAAABBBBBBB 3 
	{
		A_SpawnParticleEx("",TexMan.CheckForTexture("LENRA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(3,6),size: frandom(280.0,450.0),xoff: 5,startalphaf: frandom(0.8,1),startroll: random(-180,180));
		if(random(1,4) == 1)
		  { A_SpawnItemEx("PM_LavaMeatChunkLauncher",random(-20,20),random(-20,20),random(5,60),random(-8,8),random(-8,8),random(0,8),0,SXF_NOCHECKPOSITION); }
		for(int i=random(1,4);i>0;i--)
		  { 
		   A_SpawnItemEx("PM_HuskFireGlobs",random(-20,20),random(-20,20),random(-10,65),frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(-1.5,1.5),random(0,360),SXF_NOCHECKPOSITION);
		   A_SpawnParticleEx("727272",tex[0],style: STYLE_Normal,flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(25,45),size: frandom(2,15),zoff: random(10,60),velx: frandom(-8,8),vely: frandom(-8,8),velz: frandom(-8,12),accelz: frandom(-0.1,-0.5),startalphaf: 1.0,sizestep: -0.2,startroll: random(-180,180),rollvel: random(-4,4));
		  }
	}
	LR__ CCCDDD 4
	{
		A_SpawnItemEx("PM_BigFireGlobs",random(-15,15),random(-15,15),random(-10,65),random(-5,5),random(-5,5),random(-5,5),random(0,360),SXF_NOCHECKPOSITION);
	    A_SpawnItemEx("PM_LavaBloodMulti",random(-15,15),random(-15,15),random(-10,65),random(-8,8),random(-8,8),random(-2,8),0,SXF_NOCHECKPOSITION);
	}
	LR__ E 1 
	{
      A_Fall();
	  Radius_Quake(4, 24, 0, 5, 0);
	 for(int i=random(15,20);i>0;i--)
	  { A_SpawnParticleEx("727272",tex[0],style: STYLE_Normal,flags: SPF_RELATIVE|SPF_ROLL,lifetime: random(40,70),size: frandom(2,35),zoff: random(10,60),velx: frandom(-8,8),vely: frandom(-8,8),velz: frandom(-8,12),accelz: frandom(-0.1,-0.5),startalphaf: 1.0,sizestep: -0.2,startroll: random(-180,180),rollvel: random(-4,4)); }
	  A_Explode(60,200,XF_NOTMISSILE,0,15,0,0,"BulletPuff","Fire");
	  A_StartSound("Noble/BombExpFar", 30);
	  A_StartSound("PMInfernal/Explode", 31);
	  A_StartSound("ArachCrash",32); 
			
	  for (int i=25;i>0;i--) { A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION); }
	  for (int i=5;i>0;i--)
	  {
		A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
		A_SpawnItemEx("PM_GenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-2,3),0,SXF_NOCHECKPOSITION);
	 }
    }
	LR__ FFHH 2;
	LR__ IIIIIIIIIIIIIIIIIIIIIIIIIIII random(1,10)
	{
	 bDONTGIB = false;
	 if(random(1,3) == 1) { A_SpawnItemEx("PM_LavaBloodMulti",random(-15,15),random(-15,15),random(-10,25),random(-8,8),random(-8,8),random(-2,8),0,SXF_NOCHECKPOSITION); }
	}
	LR__ I -1;
	Stop;
   }
}

Class PM_LandrazerMissile : Actor
{
   Default
   {
    Radius 8;
	Height 8;
    DamageFunction 20;
    Speed 28;
    FastSpeed 32;
	Scale 1;
	Decal "PM_OrangeFireScorch";
	DeathSound "Cadaver/MissileExp";
	Projectile;
   }
   States
   {
    Spawn:
	 TNT1 A 0;
	 TNT1 A 0 {
		A_SpawnParticleEx
		( "", TexMan.CheckForTexture("SPKOC0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
		lifetime: random(2,5), size: frandom(100,170), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),
		startroll: random(-180,180));
		
		A_SpawnParticleEx
		( "", TexMan.CheckForTexture("DTPRO0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,7),
		size: frandom(90,155), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
		
		A_SpawnParticleEx
		( "#d2d2d2", TexMan.CheckForTexture("GNSMK0"), style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,300),
		size: frandom(10,90), xoff: -10, velx: frandom(0,6), vely: frandom(-0.8,0.8), velz: frandom(-0.4,0.7), startalphaf: frandom(0.3,0.7),
		fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
	 }
	Go:
      CMIS A 2 { A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 20,size: 40.0, xoff: -4, startalphaf: 0.65, fadestepf: -1, startroll: random(-180,180)); }
	  Loop;
	Death:
	  TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
	  TNT1 A 0 A_SpawnItemEx("PM_OrangeEmbersSpawn");
	  TNT1 A 0 A_SpawnItemEx("PM_OFireBallExplode");
	  TNT1 AAA 0 A_SpawnItemEx("PM_SmallerGenericExplosionFlames",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	  TNT1 AA 0 A_SpawnProjectile("PM_FiendBallExplode",0,0,random(0,360),2,random(0,360));
	  TNT1 A 3 BRIGHT Light("AvatarFireball4");
	  Stop;
   }
}

Class PM_LandrazerMeteor : PM_HuskMeteor
{
   Default
   {
    Speed 20;
    DamageFunction 30;
	SeeSound "Husk/Throw";
	BounceSound "Grievance/HomingExp";
	BounceType "Hexen";
	BounceCount 4;
	-NOGRAVITY
	Gravity 0.25;
	Scale 1.15;
   }
   States
   {
    Spawn:
     HUBL AABBCCDD 1 BRIGHT Light("AvatarFireball4")
	 {
	  A_SetRoll(roll+3);
	  A_SpawnItemEx("PM_DraugrRocketFireGlobs",random(-2,2),random(-2,2),random(-2,2),0,0,0,0,128,0);
	  A_SpawnParticleEx("",TexMan.CheckForTexture("GNSML0"),style: STYLE_None,flags: SPF_ROLL|SPF_RELATIVE,lifetime: 30,size: 30.0,xoff: -10, startalphaf: frandom(0.50,0.95), fadestepf: -1, startroll: random(-180,180));
	  A_SpawnParticleEx("",TexMan.CheckForTexture("LENRA0"),style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 2,size: 80.0,xoff: -10,startalphaf: 0.6);
	 }
	 Loop;
  Death:
	TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
	TNT1 AA 0 A_SpawnItemEx("PM_OFireBallExplode",0,0,0);
	TNT1 AAAA 0 A_SpawnProjectile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360));
    HUBL EFGHI 1 BRIGHT Light("AvatarFireball4");
	TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
	Stop;
    }
}

Class PM_LandrazerFirebomb : PM_HuskFirebomb
{
   Default
   {
    Speed 20;
    DamageFunction 5;
	BounceType "Hexen";
	BounceCount 8;
	-NOGRAVITY
	Gravity 0.25;
   }
}

Class PM_LandrazerComet : Actor
{
   Default
   {
    Radius 13;
	Height 13;
	Projectile;
	DamageFunction 30;
	SeeSound "GenericChargeHeavy";
	DeathSound "GenericCometExp";
	Speed 36;
	Scale 0.85;
	+BRIGHT
	+ROLLSPRITE
   }
   States
   {
   Spawn:
    TNT1 A 0;
	TNT1 A 0 
	{
		A_SpawnParticleEx( "", TexMan.CheckForTexture("SPKOC0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(5,25), size: frandom(200,300), startalphaf: 1.0, fadestepf: -1, sizestep: frandom(0.0,3),startroll: random(-180,180));
		A_SpawnParticleEx( "", TexMan.CheckForTexture("DTPRO0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(5,30), size: frandom(90,290), startalphaf: 1.0, fadestepf: -1, sizestep: frandom(0.0,3), startroll: random(-180,180) );
	}
   Go:
    HUBL AAAABBBBCCCCDDDD 1 Light("AvatarFireball2")
	{
	  A_SetRoll(roll+15);
	  A_SpawnParticleEx( "",  TexMan.CheckForTexture("F1R1A0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,35),  size: frandom(45,85),  xoff: -10,  startalphaf: 1.0, fadestepf: -1,  sizestep: frandom(0.0,-0.50), startroll: random(-180,180),rollvel: random(-5,5));
	  A_SpawnParticleEx( "", TexMan.CheckForTexture("SPKOA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(35,100), size: frandom(5,20), xoff: frandom(-30,30), yoff: frandom(-30,30), zoff: frandom(-20,20), velx: frandom(-0.3,0.3), vely: frandom(-0.3,0.3), velz: frandom(-0.1,0.1), startalphaf: frandom(0.35,1), fadestepf: -1, startroll: random(-180,180));
	}
	Loop;
   Death:
	TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
	TNT1 AA 0 A_SpawnItemEx("PM_OFireBallExplode",0,0,0);
	TNT1 AA 0 A_SpawnProjectile("PM_HectebusBallExplode",0,0,random(0,360),2,random(0,360));
	TNT1 AAA 0 A_SpawnItemEx("PM_HuskMeteorFire",random(-7,7),random(-7,7),random(1,7),frandom(-4,4),frandom(-4,4),frandom(0,8));
	TNT1 AAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_ExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
	Stop;
   }
}