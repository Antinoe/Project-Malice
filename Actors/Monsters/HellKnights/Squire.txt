Class PM_Squire : PMMonsterBase
{
	int user_leapstrength;
	Default
	{
	Health 500;
	Radius 24;
	Height 64;
	Mass 1000;
	Speed 7;
	PainChance 50;
	Monster;
	Bloodtype "PM_BlackBloodBase";
	Bloodcolor "Black";
	+FLOORCLIP
	+DONTHARMSPECIES
	Species "Nobles";
	Tag "Squire";
	SeeSound 	"Squire/Sight";
	ActiveSound "Squire/Active";
	PainSound 	"Squire/Pain";
	DeathSound 	"Squire/Death";
	HitObituary "%o was punched out by a Squire.";
	Obituary "%o was dispersed by a Squire.";
	Scale 1.05;
	Gravity 0.83;
	Damage 3;
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	States
	{
	Spawn:
		BOS6 AB 10 PM_Look();
		Loop;
	Look:
		TNT1 A 0 A_SetSpeed(5);
		BOS6 AAAABBBB 2 PM_LookingForPlayer();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 A 0 A_JumpIfTargetinLOS("Spotted",180,JLOSF_DEADNOJUMP);
		BOS6 CCCCDDDD 2 PM_LookingForPlayer();
		TNT1 A 0 A_StartSound("Squire/Step");
        Loop;
    Spotted:
        Goto Roar;
	See1:
		TNT1 A 0 A_Jump(20,"See2");
		TNT1 A 0 A_SetSpeed(4);
		BOS6 AAAA 2 A_Chase();
		BOS6 BBBB 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCC 2 A_Chase();
		BOS6 DDDD 2 A_Chase();
		TNT1 AA 0 A_Recoil(-0.4);
		TNT1 A 0 A_StartSound("Squire/Step");
		Loop;
	See2:
		TNT1 A 0 A_SetSpeed(12);
		BOS6 AA 3 A_Chase();
		TNT1 AA 0 A_Recoil(-0.8);
		BOS6 BB 3 A_Chase();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 AA 0 A_Recoil(-1.5);
		BOS6 CC 3 A_Chase();
		TNT1 AA 0 A_Recoil(-0.8);
		BOS6 DD 3 A_Chase();
		TNT1 A 0 A_StartSound("Squire/Step");
		TNT1 AA 0 A_Recoil(-1.5);
		TNT1 A 0 A_Jump(5,"See1");
		Loop;
	SeeNoMissile:		
		BOS6 G 20;
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall");
		TNT1 A 0 A_StartSound("Squire/Step");
		Goto See1;	
	Melee:
		BOS6 E 6 A_FaceTarget();
		BOS6 E 0 A_StartSound("Doom4/Other/MeleeSwing",5,1.0,false,1.1);
		BOS6 E 6 A_FaceTarget();
		BOS6 G 6 A_CustomMeleeAttack(random(10,30),"Doom4/Other/MeleeHit");
		BOS6 F 2 A_Recoil(2);
		Goto See1;
	Missile:
		BOS4 M 0 A_JumpIfCloser(290,"DecideToCharge");
		"####" "#" 0 A_Jump(220, "Leap");
		"####" "#" 0 A_Jump(230,"FireBall");
		Goto Leap;
	DecideToCharge:
	    TNT1 A 0 A_JumpIfHigherOrLower("Fireball",null);
		BOS4 M 0 A_Jump(45,"ChargeAttack");
		Goto Missile+1;
	Roar:
		BOS6 PQR 2 A_FaceTarget();
		TNT1 A 0 PM_AlertSound();
		BOS6 STSU 3 A_FaceTarget();
		BOS6 STSU 3 A_FaceTarget();
		BOS6 RQP 2 A_FaceTarget();
		Goto See2;
	PainRoar:
		BOS6 PQR 2 A_FaceTarget();
		TNT1 A 0 A_StartSound("Squire/Sight",CHAN_6);
		BOS6 STSU 3 A_FaceTarget();
		BOS6 STSU 3 A_FaceTarget();
		BOS6 RQP 2 A_FaceTarget();
		Goto See2;
	FireBall:
		TNT1 A 0 A_Jump(30,"Roar");
		BOS6 EEEEEEEEE 1 Light("Greenball4") 
		{
		A_FaceTarget();
		A_Spawnitemex("GreenFireEffect",10,random(30,25),60,0,0,2);
		}
		BOS6 EEEE 1 Light("Greenball4")
		{
		A_FaceTarget();
		A_Spawnitemex("GreenFireEffect",10,random(30,25),57,0,0,2);
		}
		BOS6 FFFF 1 Light("Greenball3") 
		{
		A_FaceTarget();
		A_Spawnitemex("GreenFireEffect",10,random(30,25),45,0,0,2);
		}
		BOS6 G 6 Light("Greenball3")
		{
		A_FaceTarget();
		A_StartSound("HellKnight/Cast",CHAN_7);
		A_SpawnProjectile("PM_SquireFireball",40);
		}
		BOS6 G 5 Light("Greenball3")
		{
		A_FaceTarget();
		}
		Goto See1;
	Leap:
		TNT1 A 0 A_Jump(55,"Roar");
	    TNT1 A 0 
	    { 
	    if(target && Distance3D(target) >= 568)
	    SetStateLabel("Fireball");
	    }
		TNT1 A 0 A_JumpIfCloser(256,"ChargeAttack"); //if too close, charges towards target
		BOS6 EEEEE 4  A_FaceTarget();
		BOS6 EEE 5 A_FaceTarget();
		BOS6 F 0;
		BOS6 A 2 PM_LeapTo(target, 568, "SeeNoMissile");
	LeapLoop:
		BOS6 F 0 A_CheckFloor("LeapEnd");
		BOS6 F 1 A_JumpIf(vel.x==0&&vel.y==0,"DropFast");	
		BOS6 F 0 A_CheckFloor("LeapEnd");
		BOS6 F 1 A_JumpIf(vel.x==0&&vel.y==0,"DropFast");
		BOS6 F 0 A_CheckFloor("LeapEnd");
		BOS6 F 1 A_JumpIf(vel.x==0&&vel.y==0,"DropFast");
		BOS6 F 0 A_SpawnItemEx("KnightLeapTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		Loop;
	DropFast:
		TNT1 A 0 A_ChangeVelocity(0,0,-12,CVF_REPLACE);
		TNT1 A 0 A_CheckFloor("LeapEnd");
		BOS6 F 1 A_JumpIf(vel.x==0&&vel.Z==0,"LeapEnd");
		Goto DropFast+1;
		
		LeapEnd:
			TNT1 A 0 A_StartSound("Doom4/Monsters/BaronFloorPound",CHAN_7,CHANF_OVERLAP);
			TNT1 A 0 A_Quake(4,20,0,512);
			TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_BulletShrapnelNoCheck",random(-5,5),random(-5,5),0,random(-3,3),random(-5,5),random(5,20),0,SXF_NOCHECKPOSITION);
            TNT1 AAAAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareEffectLong",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
            TNT1 AAAAA 0 A_SpawnItemEx("PM_BoomSmoke",random(-1,1),random(-1,1),random(2,4),random(-1,1),random(-1,1),random(0,1),0,SXF_NOCHECKPOSITION);
            TNT1 AA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			TNT1 A 0 A_JumpIfCloser(140,"DoDamage");
			Goto ResumeLeapEnd;
		DoDamage:
			TNT1 A 0 A_BetaSkullAttack;
			Goto ResumeLeapEnd;
		ResumeLeapEnd:
			BOS2 G 1 A_Stop;
			BOS2 G 2;
			Goto SeeNoMissile; 
	ChargeAttack: 
	   TNT1 A 0 A_Jump(60,"Roar");
	   TNT1 A 0 { bNOTARGETSWITCH = true; }
	   TNT1 A 0 A_StartSound("Doom4/Monsters/HellKnight/Charge",5,1.0,false,0.9);
	   BOS6 EFF 4 A_FaceTarget();
	   TNT1 A 0 ThrustThingZ(0,1,0,1);
	   BOS6 A 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee");
	   BOS6 A 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 B 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 B 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee");
	   BOS6 B 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 C 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee");
	   BOS6 C 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 C 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee");
	   BOS6 D 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 D 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee");
	   BOS6 D 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
	   BOS6 D 1 A_Recoil(-4);	TNT1 A 0 A_SpawnProjectile("HKLunge3",18,0,0,CMF_AIMDIRECTION);
    ChargeAttackMelee:
	   TNT1 A 0 A_StartSound("Doom4/Other/MeleeSwing",CHAN_AUTO,1.0,false,1.15);
	   TNT1 A 0 { bSLIDESONWALLS = true; }
	   TNT1 A 0 ThrustThingZ(0,20,0,1);
	   TNT1 A 0 A_Recoil(-1);
	   BOS6 F 2 A_FaceTarget();
	   BOS6 F 3 A_SpawnProjectile("HKLunge2",18,0,0,CMF_AIMDIRECTION);
	   BOS6 G 6;
	   BOS6 G 2 A_ScaleVelocity(0.2);
	   TNT1 A 0 { bSLIDESONWALLS = false; }
	   TNT1 A 0 { bNOTARGETSWITCH = false; }
	   BOS6 G 1 A_ScaleVelocity(0);  
	   Goto See1;
	Pain:
		BOS6 H 2;
		BOS6 H 2 A_Pain;
		TNT1 A 0 A_Jump(20,"PainRoar");
		Goto See1;
	Death:
	    TNT1 A 0 A_StopSounds(0,0);
		BOS6 I 8;
		BOS6 J 8 A_Scream;
		BOS6 K 8;
		BOS6 L 8 A_NoBlocking;
		TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
		BOS6 MN 8;
		BOS6 O -1;
		Stop;
	Raise:
		BOS6 O 8;
		BOS6 NMLKJI  8;
		Goto Look;
		
	}
}

Class KnightLeapTrail : Actor
{
   Default
   {
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle "Translucent";
	Alpha 0.5;
    Scale 1.05;
   }
	States
	{
	Spawn:
		BOZ2 F 2 A_FadeOut(0.05);
		Wait;
	}
}


Class HKLunge : Actor
{
   Default
   {
	Height 16;
	Radius 16;
	Speed 27;
	DamageFunction (5*Random(4,8));
	Species "Fiends";
	SeeSound "";
	DeathSound "Doom4/Other/MeleeHit";
	PROJECTILE;
   }
	States
	{
	Spawn:
		TNT1 A 4;
		Stop;
    }
}

Class HKLunge2 : HKLunge
{
  Default { DamageFunction (3*Random(4,6)); }
}

Class HKLunge3 : HKLunge
{
  Default { DamageFunction (1*Random(0,1)); }
}

Class PM_SquireFireball : DoomImpBall
{
    Default
    {
	+NOGRAVITY
	-LOWGRAVITY
	Speed 15;
	SeeSound "HellKnight/Cast";
	DeathSound "HellKnight/BallImpact";
	Decal "PM_GreenFireScorch";
	DamageFunction (random(30,37));
	DamageType "Fire";
	Scale 0.24;
    }
	states
  	{
  Spawn:
    PBAL C 1 bright Light("GreenFireLight") 
	{
	A_SpawnItemEx("PM_GreenFlameTrails",0,0,0,0,0,0,0,128,0);
	A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
    A_SpawnParticleEx
	(
		"",
		TexMan.CheckForTexture("GNSML0"),
		style: STYLE_None,
		flags: SPF_ROLL|SPF_RELATIVE,
		lifetime: 30,
		size: 30.0,
		angle: 0,
		xoff: -10,
		yoff: 0,
		velx: frandom(-0.3,0.3),
		vely: frandom(-0.3,0.3),
		velz: frandom(-0.3,0.3),
		accelx: 0,
		accely: 0,
		accelz: 0,
		startalphaf: 0.4,
		fadestepf: -1,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: 0,
		rollacc: 0
	);
	A_SpawnParticleEx
	    (
		"",
		TexMan.CheckForTexture("DTPRA0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: 1,
		size: 85.0,
		angle: 0,
		xoff: -10,
		yoff: 0,
		velx: 0,
		vely: 0,
		velz: 0,
		accelx: 0,
		accely: 0,
		accelz: 0,
		startalphaf: 0.5,
		fadestepf: 0,
		sizestep: 0,
		startroll: random(-180,180),
		rollvel: 0,
		rollacc: 0
	    );
	}
	loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_SetTranslation("PM_GreenFusionTint");
	TNT1 A 0 A_SetScale(1.2);
    NDEB D 1 bright Light("GreenFireLight");
	TNT1 A 0 A_SpawnItemEx("GFireBallExplode",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_GreenFireballExpFlames",0,0,0,random(-4,4),random(-4,4),random(-4,4),0,SXF_NOCHECKPOSITION);
    FIR3 CCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 Light("GreenFireLight") bright A_FadeOut(0.05);
    stop;
  }
}

Class GFireBallExplode : Actor
{
   Default
   {
	Scale 1;
	Alpha 0.84;
	Renderstyle "Add";
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	Speed 1;
   }
	States
{
Spawn:
	FXPG AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06);
	Stop;
	}
}

Class GreenFireballTrail : Actor
{
 Default
 {
  Scale 1.04;
  +NOINTERACTION
  +CLIENTSIDEONLY
  +NOBLOCKMAP
  +THRUACTORS
  +DONTSPLASH
  +NOCLIP
  RenderStyle "Add";
 }
  States
  {
  Spawn:
	TNT1 A 1;
	Go:
    NDEB ABC 1
	{
	A_SetScale(Scale.X-0.1);
	A_FadeOut(0.15);
	}
    Loop;
    }
}

Class GreenFireEffect : Actor
{
  Default
  {
  +NOINTERACTION
  +CLIENTSIDEONLY
  +ROLLSPRITE
  Renderstyle "Add";
  Scale 0.3;
  +Bright
  }
  States
  {
  Spawn:
	TNT1 A 0;
	TNT1 A 0 A_SetRoll(random(0,360));
	Go:
	TNT1 A 0 A_SetScale(Scale.X-0.02);
	FIG1 A 1 A_FadeOut(0.08);
	Loop;
	}
}