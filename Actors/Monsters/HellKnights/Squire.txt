ACTOR PM_Squire
{
	var int user_leapstrength;
	Health 500
	Radius 24
	Height 64
	Mass 1000
	Speed 7
	PainChance 50
	Monster
	Bloodcolor "Black"
	+FLOORCLIP
	+DONTHARMSPECIES
	//+MissileMore
	//+NOINFIGHTING
	Species "Nobles"
	Tag "Squire"
	SeeSound 	""//"Squire/Sight"
	ActiveSound "Squire/Active"
	PainSound 	"Squire/Pain"
	DeathSound 	"Squire/Death"
	HitObituary "%o was punched out by a Squire."
	Obituary "%o was dispersed by a Squire."
//	DropItem "RagePoint1"
	Scale 1.05
	Gravity 0.83
	WoundHealth 60
	Damage 3
	States
	{
	Spawn:
		BOS6 AB 10 A_Look 
		Loop
	See:
		TNT1 A 0 A_SetSpeed(5)
		BOS6 AAAA 2 A_Chase("","",CHF_NOPLAYACTIVE)
		BOS6 BBBB 2 A_Chase("","",CHF_NOPLAYACTIVE)
		TNT1 A 0 A_PlaySound("Squire/Step")
        TNT1 A 0 A_JumpifTargetInLOS("Spotted",270,JLOSF_DEADNOJUMP)
		BOS6 CCCC 2 A_Chase("","",CHF_NOPLAYACTIVE)
		BOS6 DDDD 2 A_Chase("","",CHF_NOPLAYACTIVE)
		TNT1 A 0 A_PlaySound("Squire/Step")
        TNT1 A 0 A_CheckSight("See")
		Goto Spotted
    Spotted:
        Goto Roar
	See1:
		TNT1 A 0 A_Jump(20,"See2")
		TNT1 A 0 A_SetSpeed(5)
		BOS6 AAAA 2 A_Chase
		BOS6 BBBB 2 A_Chase
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 CCCC 2 A_Chase
		BOS6 DDDD 2 A_Chase
		TNT1 A 0 A_PlaySound("Squire/Step")
		Loop
		
		See2:
		TNT1 A 0 A_SetSpeed(12)
		BOS6 AA 3 A_Chase
		TNT1 AA 0 A_Recoil(-0.8)
		BOS6 BB 3 A_Chase
		TNT1 A 0 A_PlaySound("Squire/Step")
		TNT1 AA 0 A_Recoil(-0.8)
		BOS6 CC 3 A_Chase
		TNT1 AA 0 A_Recoil(-0.8)
		BOS6 DD 3 A_Chase
		TNT1 A 0 A_PlaySound("Squire/Step")
		TNT1 AA 0 A_Recoil(-0.8)
		TNT1 A 0 A_Jump(5,"See1")
		Loop
		
	SeeNoMissile:		
		BOS6 G 20 
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 AAAABBBB 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		BOS6 CCCCDDDD 2 A_Chase("Melee","FireBall")
		TNT1 A 0 A_PlaySound("Squire/Step")
		Goto See1	
	Melee:
		BOS6 E 6 A_FaceTarget
		BOS6 E 0 A_PlaySound("Doom4/Other/MeleeSwing",5,1.0,false,1.1)
		BOS6 E 6 A_FaceTarget
		BOS6 G 6 A_CustomMeleeAttack(random(10,30),"Doom4/Other/MeleeHit","",0,1)
		BOS6 F 2 A_Recoil(2)
		Goto See1	
	Missile:
		BOS4 M 0 A_JumpIfCloser(290,"DecideToCharge")
		"####" "#" 0 A_Jump(230,"FireBall")
		Goto Leap

	DecideToCharge:
	    TNT1 A 0 A_JumpIfHigherOrLower("Fireball","")
		BOS4 M 0 A_Jump(45,"ChargeAttack")
		Goto Missile+1
	Roar:
		BOS6 PQR 2 A_FaceTarget
		TNT1 A 0 A_PlaySound("Squire/Sight",CHAN_6)
		BOS6 STSU 3 A_FaceTarget
		BOS6 STSU 3 A_FaceTarget
		BOS6 RQP 2 A_FaceTarget
		Goto See1
	PainRoar:
		BOS6 PQR 2 A_FaceTarget
		TNT1 A 0 A_PlaySound("Squire/Sight",CHAN_6)
		BOS6 STSU 3 A_FaceTarget
		BOS6 STSU 3 A_FaceTarget
		BOS6 RQP 2 A_FaceTarget
		Goto See2
	FireBall:
		TNT1 A 0 A_Jump(40,"Roar")
		BOS6 EEEEEEEEE 1 Light("Greenball4") 
		{
		A_FaceTarget;
		A_Spawnitemex("GreenFireEffect",10,random(30,25),60,0,0,2);
		}
		BOS6 EEEEE 1 Light("Greenball4")
		{
		A_FaceTarget;
		A_Spawnitemex("GreenFireEffect",10,random(30,25),57,0,0,2);
		}
		BOS6 FFFFFFFFF 1 Light("Greenball3") 
		{
		A_FaceTarget;
		A_Spawnitemex("GreenFireEffect",10,random(30,25),45,0,0,2);
		}
		BOS6 G 8 Light("Greenball3")
		{
		A_FaceTarget;
		A_PlaySound("HellKnight/Cast",CHAN_7);
		A_CustomMissile("PM_SquireFireball",40);
		}
		BOS6 G 7 Light("Greenball3")
		{
		A_FaceTarget;
		}
		
		Goto See1
	Leap:
		TNT1 A 0 A_Jump(55,"Roar")
		TNT1 A 0 A_JumpIfHigherOrLower("Fireball","")
		TNT1 A 0 A_JumpIfCloser(256,"ChargeAttack") //if too close, charges towards target
		BOS6 EEEEE 4  A_FaceTarget
		TNT1 A 0 A_JumpIfCloser(320,"Prep320")
		TNT1 A 0 A_JumpIfCloser(384,"Prep384")
		TNT1 A 0 A_JumpIfCloser(448,"Prep448")
		TNT1 A 0 A_JumpIfCloser(512,"Prep512")
		TNT1 A 0 A_JumpIfCloser(576,"Prep576")
		TNT1 A 0 A_JumpIfCloser(640,"Prep640")
		TNT1 A 0 A_JumpIfCloser(704,"Prep704")
		TNT1 A 0 A_JumpIfCloser(768,"Prep768")
		Goto FireBall
		
		Prep320:	TNT1 A 0 A_SetUserVar(user_leapstrength,28)	goto ContinueLeap
		Prep384:	TNT1 A 0 A_SetUserVar(user_leapstrength,30)	goto ContinueLeap
		Prep448:	TNT1 A 0 A_SetUserVar(user_leapstrength,33)	goto ContinueLeap
		Prep512:	TNT1 A 0 A_SetUserVar(user_leapstrength,35)	goto ContinueLeap
		Prep576:	TNT1 A 0 A_SetUserVar(user_leapstrength,37)	goto ContinueLeap
		Prep640:	TNT1 A 0 A_SetUserVar(user_leapstrength,39)	goto ContinueLeap
		Prep704:	TNT1 A 0 A_SetUserVar(user_leapstrength,41)	goto ContinueLeap
		Prep768:	TNT1 A 0 A_SetUserVar(user_leapstrength,43)	goto ContinueLeap
		
		ContinueLeap:
			BOS6 ABC 5 A_FaceTarget
			BOS6 F 0 A_Recoil(-(user_leapstrength/1.5))
			BOS6 F 2 ThrustThingZ(0,user_leapstrength,0,0)
			
		LeapLoop:
			BOS6 F 0 A_CheckFloor("LeapEnd")
			BOS6 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")	
			BOS6 F 0 A_CheckFloor("LeapEnd")
			BOS6 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")
			BOS6 F 0 A_CheckFloor("LeapEnd")
			BOS6 F 1 A_JumpIf(velx==0&&vely==0,"DropFast")
			BOS6 F 0 A_SpawnItemEx("KnightLeapTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Loop
			
		DropFast:
			TNT1 A 0 A_ChangeVelocity(0,0,-12,CVF_REPLACE)
			TNT1 A 0 A_CheckFloor("LeapEnd")
			BOS6 F 1 A_JumpIf(velx==0&&velZ==0,"LeapEnd")
			Goto DropFast+1
			
			LeapEnd:
				TNT1 A 0 A_PlaySound("Doom4/Monsters/BaronFloorPound",7,1.0,false,0.9)
				TNT1 A 0 A_Quake(4,20,0,512,"")
				TNT1 A 0 A_SpawnItemEx("KnightShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
				TNT1 A 0 A_JumpIfCloser(140,"DoDamage")
				Goto ResumeLeapEnd
			DoDamage:
				TNT1 A 0 A_BetaSkullAttack
				Goto ResumeLeapEnd
			ResumeLeapEnd:
				BOS2 G 1 A_Stop
				BOS2 G 2
				Goto SeeNoMissile  
		
	ChargeAttack: 
	   TNT1 A 0 A_Jump(60,"Roar")
	   TNT1 A 0 A_ChangeFlag(NOTARGETSWITCH, 1)
	   TNT1 A 0 A_PlaySound("Doom4/Monsters/HellKnight/Charge",5,1.0,false,0.9)
	   BOS6 EFF 4 A_FaceTarget
	   TNT1 A 0 ThrustThingZ(0,1,0,1)
	   
	   BOS6 A 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee")
	   BOS6 A 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 B 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 B 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee")
	   BOS6 B 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 C 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee")
	   BOS6 C 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 C 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee")
	   BOS6 D 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 D 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 A 0 A_JumpIfCloser(64,"ChargeAttackMelee")
	   BOS6 D 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
	   BOS6 D 1 A_Recoil(-4)	TNT1 A 0 A_CustomMissile("HKLunge3",18,0,0,CMF_AIMDIRECTION)
    ChargeAttackMelee:
	   TNT1 A 0 A_PlaySound("Doom4/Other/MeleeSwing",CHAN_AUTO,1.0,false,1.15)
	   TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,1)
	   TNT1 A 0 ThrustThingZ(0,20,0,1)
	   TNT1 A 0 A_Recoil(-1)
	   BOS6 F 2 A_FaceTarget
	   BOS6 F 3 A_CustomMissile("HKLunge2",18,0,0,CMF_AIMDIRECTION)
	   BOS6 G 6
	   BOS6 G 2 A_ScaleVelocity(0.2)
	   TNT1 A 0 A_ChangeFlag(SLIDESONWALLS,0)
	   TNT1 A 0 A_ChangeFlag(NOTARGETSWITCH, 0)
	   BOS6 G 1 A_ScaleVelocity(0)   
	   Goto See1
	
	Pain:
		BOS6 H 2
		BOS6 H 2 A_Pain
		TNT1 A 0 A_Jump(20,"PainRoar")
		Goto See1

	Death:
		BOS6 I 8
		BOS6 J 8 A_Scream 
		BOS6 K 8 
		BOS6 L 8 A_NoBlocking
		TNT1 A 0 A_PlaySound("Corpse/FallMed",CHAN_6)
		BOS6 MN 8
		BOS6 O -1 
		Stop

	Raise:
		BOS6 O 8
		BOS6 NMLKJI  8
		Goto See
		
	}
}

Actor BaronShockwave
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	+FORCEXYBILLBOARD
	RenderStyle Add
	Scale 0.5
	Translation "0:255=%[0,0,0]:[0.5,1.2,0.0]"
	Alpha 0.99
	States
	{
	Spawn:
		NDEB EFGH 2 Light("GreenFireLight") bright 
		{
		A_SetScale(scalex*1.3);
		A_FadeOut(0.1);
		}
		Loop
	}
}

Actor KnightShockwave : BaronShockwave
{
	Translation "0:255=%[0,0,0]:[0.5,1.2,0.0]"
}

Actor KnightLeapTrail
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	Renderstyle Translucent
	Alpha 0.5
        Scale 1.05
	States
	{
	Spawn:
		BOZ2 F 2 A_FadeOut(0.05)
		Wait
	}
}


ACTOR HKLunge
{
	Height 16
	Radius 16
	Speed 27
	Damage (5*Random(4,8))
	Species "Fiends"
	SeeSound ""  
	DeathSound "Doom4/Other/MeleeHit"
	PROJECTILE
	States
	{
	Spawn:
		TNT1 A 4
		Stop
    }
}

ACTOR HKLunge2 : HKLunge
{
	Damage (3*Random(4,6))
}

ACTOR HKLunge3 : HKLunge
{
	Damage (1*Random(0,1))
}

actor PM_SquireFireball : DoomImpBall
{
	+NOGRAVITY
	-LOWGRAVITY
	Speed 15
	SeeSound "HellKnight/Cast"
	DeathSound "HellKnight/BallImpact"
	Damage (random(30,37))
	Scale 0.24
	states
  	{
  Spawn:
    PBAL C 1 bright Light("GreenFireLight") 
	{
	A_SpawnItemEx("PM_GreenFlameTrails",0,0,0,0,0,0,0,128,0);
	A_SpawnItemEx("DevilBallSmoke",random(-2,2),random(-2,2),0,0,0,0,0,0,128,0);
	A_Weave(random(-3,3), random(-3,3), random(0,3), random(0,2));
	}
	loop
  Death:
    NDEB D 2 bright Light("GreenFireLight")
	TNT1 A 0 A_SpawnItemEx("GFireBallExplode")
	TNT1 A 0 A_SpawnItemEx("PM_GreenFireballExpFlames")
    NDEB EFGH 2 Light("GreenFireLight") bright
    stop
  }
}

Actor GFireBallExplode
{
	Scale 1
	Alpha 0.84
	Renderstyle Add
	+CLIENTSIDEONLY
	+NOINTERACTION
	+Bright
	Speed 1
	States
{
Spawn:
	FXPG AABBCDEFGHIJKLM 1 Bright A_FadeOut(0.06)
	Stop
	}
}

ACTOR GreenFireballTrail
{
Scale 1.04
+NOINTERACTION
+CLIENTSIDEONLY
+NOBLOCKMAP
+THRUACTORS
+DONTSPLASH
+NOCLIP
RenderStyle Add
States
{
Spawn:
	TNT1 A 1
	Go:
    NDEB ABC 1
	{
	A_SetScale(ScaleX-0.1);
	A_FadeOut(0.15);
	}
    Loop
    }
}

Actor GreenFireEffect
{
  +NOINTERACTION
  +CLIENTSIDEONLY
  +ROLLSPRITE
  Renderstyle Add
  Scale 0.3
  +Bright
  States
  {
  Spawn:
	TNT1 A 0
	TNT1 A 0 A_SetRoll(random(0,360))
	Go:
	TNT1 A 0 A_SetScale(ScaleX-0.02)
	FIG1 A 1 A_FadeOut(0.08)
	Loop
	}
}