Class PM_HellsFury : PMDeathMonsterBase
{
    int deathwither;
    Default
	{
	//$Category "PM_Monsters/HellKnights"
	Translation "174:191=193:207", "32:47=240:247";
    Health 500;
    Radius 24;
    Height 64;
    Speed 10;
    PainChance 50;
    Mass 1000;
	Scale 1.15;
    SeeSound "HFurySight";
    ActiveSound "HFuryActive";
    PainSound "HFuryPain";
    DeathSound "HFuryDeath";
	Species "Nobles";
    Obituary "%o turned into a sludge of %p former self by a Hell's Fury.";
	Tag "Hell's Fury";
    BloodType "PM_DeathBloodBase";
    BloodColor "Black";
    MONSTER;
	+DONTHARMSPECIES
    +FLOORCLIP
	+MissileMore
	+DONTHARMCLASS
	TeleFogSourceType "PM_SmallDemonicTeleportFog";
	TeleFogDestType "PM_DemonicTeleportFog";
	}
	
	override void PostBeginPlay()
	{
	 deathwither = random(20,35);
	 tex[0] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	}
	
	TextureID tex[1];

    void PM_HellsFuryWither()
	{
	 A_SpawnParticleEx
	 (
	 "545454",
	 tex[0],
	 style: STYLE_Normal,
	 flags: SPF_RELATIVE|SPF_ROLL,
	 lifetime: random(20,120),
	 size: frandom(1,15),
	 xoff: random(-15,15),
	 yoff: random(-25,25),
	 zoff: 5,
	 velx: frandom(-1,1),
	 vely: frandom(-1,1),
	 velz: frandom(1,4),
	 startalphaf: 1.0,
	 fadestepf: -0.3,
	 sizestep: -0.4,
	 startroll: random(-180,180),
	 rollvel: random(0,4)
	 );
	}
  States
  {
  Spawn:
    HFRY AB 10 PM_Look();
    Loop;
  Look:
    HFRY AABB 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Squire/Step");
    TNT1 A 0 A_JumpIfTargetInLOS("Spotted",170,JLOSF_DEADNOJUMP);
	HFRY CCDD 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("Squire/Step");
    Loop;
  Spotted:
    TNT1 A 0 PM_AlertSound();
  See2:
    TNT1 A 0 A_SetSpeed(10);
    HFRY AABB 3 A_Chase();
	TNT1 AA 0 A_Recoil(-1);
	TNT1 A 0 A_StartSound("Squire/Step");
	HFRY CCDD 3 A_Chase();
	TNT1 AA 0 A_Recoil(-1);
	TNT1 A 0 A_StartSound("Squire/Step");
	TNT1 A 0 A_Jump(1,"RushCheck");
    Loop;
  Missile:
		TNT1 A 0 A_JumpIfCloser(270,"FireBreathCheck");
		TNT1 A 0 A_Jump(65,"RushCheck");
		TNT1 A 0 A_Jump(90,"SoulSwarm","ShotgunSoulSwarm");
  NormalMissile:
		HFRY EEEEE 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(30,25),65,0,0,2);
		}
		HFRY EEEE 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(30,25),60,0,0,2);
		}
		HFRY FFFF 1 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_Spawnitemex("PM_HFuryThrowFire",10,random(30,25),50,0,0,2);
		}
		HFRY G 8 Light("HellsFuryBall1")
		{
		A_FaceTarget();
		A_SpawnProjectile("PM_HellsFuryBall",40);
		}
		Goto See2;
   FireBreathCheck:
    TNT1 A 0 A_Jump(70,"FireBreath");
	TNT1 A 0 A_Jump(45,"SoulSwarm","ShotgunSoulSwarm");
	Goto NormalMissile;
   FireBreath:
	TNT1 A 0 A_StartSound("CyberFodder/Flamer",CHAN_5,1,1);
	TNT1 A 0 A_StartSound("HFuryRoar",CHAN_7);
	HFRY PP 5 Light("HellsFuryBall1")
	{
	A_FaceTarget(360,180);
	A_SetAngle(angle+random(-5,5));
	A_SetPitch(pitch+random(-5,5));
	}
	HFRY QQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQQ 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_HFuryFireFlare",5,0,65);
	A_SpawnProjectile("PM_HFuryFirebreath",67,0,random(-1,1));
	}
	TNT1 A 0 A_StopSound(CHAN_5);
	Goto See2;
  RushCheck:
    TNT1 A 0 A_JumpIfHigherOrLower("See2","See2");
	Goto Rush;
  Rush:
    TNT1 A 0 A_SetSpeed(70);
    TNT1 A 0 A_StartSound("HFury/Rush",CHAN_5);
    HFRY A 15 A_FaceTarget();
    TNT1 A 0 A_StartSound("HFuryRushAdd",CHAN_6);
   	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	HFRY A 8 A_FaceTarget();
	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 AAAAAAA 1 Light("HellsFuryBall1")
    {
	A_Chase(null,null);
    A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    A_SpawnProjectile("BlueSoulFlare",32,0,0);
    }
    TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    HFRY A 8 A_FaceTarget();
	TNT1 A 0 A_SetSpeed(10);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 A 0 A_JumpIfCloser(250,"FireBreathCheck");
    Goto See2;
  SoulSwarm:
    TNT1 A 0 A_StartSound("HFury/SoulPrefire");
    TNT1 A 0 A_StartSound("GenericChargeSmall",CHAN_5);
	HFRY PPPPPPPPPPPPPPPPPPPPPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	PM_LeadTarget(15);
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY QQQQQQQQQQQQQQQQQ 2 Light("HellsFuryBall1")
	{
	A_FaceTarget(0.65,180);
	A_SpawnProjectile("PM_HellsFurySoul",65,0,random(-8,8),CMF_AIMDIRECTION,(pitch*1));
	}
	Goto See2;
  ShotgunSoulSwarm:
    TNT1 A 0 A_StartSound("HFury/SoulPrefire");
    TNT1 A 0 A_StartSound("GenericChargeSmall",CHAN_5);
	HFRY PPPPPPPPPPPPPPPPPP 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY PPPP 1 Light("HellsFuryBall1")
	{
	A_FaceTarget();
	A_Spawnitemex("PM_HFuryThrowFire",10,random(-5,5),65,0,0,2);
	}
	HFRY QQQQQ random(25,30) Light("HellsFuryBall1")
	{
	if(target && Distance3D(target) > 200)
	PM_LeadTarget(15);
	else
	A_FaceTarget();
	for(int i=random(5,7);i>0;i--)
	A_SpawnProjectile("PM_HellsFurySoul",65,0,random(-8,8),CMF_AIMDIRECTION,(pitch*1));
	}
	Goto See2;
  Pain:
    HFRY H 2;
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 A_StopSound(CHAN_5);
	TNT1 A 0 A_StopSound(CHAN_7);
    HFRY H 4 A_Pain;
	TNT1 A 0 A_Jump(75,"Evade");
    Goto See2;
  Evade:
    TNT1 A 0 A_SetSpeed(70);
    HFRY H 5 A_FaceTarget();
    TNT1 A 0 A_StartSound("HFuryRushAdd",CHAN_6);
   	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	HFRY A 4 A_FaceTarget();
	TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    TNT1 AAAAAAA 1 Light("HellsFuryBall1")
    {
	A_Wander();
    A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    A_SpawnProjectile("BlueSoulFlare",32,0,0);
    }
    TNT1 A 0 A_ChangeVelocity(0,0,0,CVF_REPLACE);
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
	TNT1 A 0 A_SetSpeed(10);
    HFRY A 8 A_FaceTarget();
    TNT1 AAA 0 A_SpawnProjectile("AbhorBallExplode",random(10,45),0,random(0,360),2,random(0,360));
    Goto See2;
  Death:
    TNT1 A 0 A_StopSounds(0,0);
    HFRY I 1 A_Scream;
	HFRY IIIIIII 1 A_SpawnItemEx("HuskSmoke",xofs:random(-10,10),yofs:random(-12,12),zofs:random(1,30),zvel:frandom(0.2,2));
    HFRY J 0 
	{
	 for(int i=random(2,4);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:25,zvel:3,flags:SXF_NOCHECKPOSITION);
	}
    HFRY J 1 A_SpawnItemEx("HellsFurySkull", 0, 0, 65, 5, 0, 0, RandomPick(20, 10, 0, -10, -20), SXF_CLIENTSIDE);
	HFRY JJJJJJJ 1 
	{
	A_SpawnItemEx("HuskSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(10,30),zvel:frandom(0.2,2));
    PM_HellsFuryWither();
	}
    HFRY J 0 
	{
	 for(int i=random(1,2);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:15,zvel:3,flags:SXF_NOCHECKPOSITION);
	 for(int i=random(1,2);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodSoul",xofs:random(-10,10),yofs:random(-15,15),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),flags:SXF_NOCHECKPOSITION);
	}
    HFRY KKKKKK 1 A_SpawnItemEx("PM_DeathBloodSoul",yofs:random(-10,10),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),flags:SXF_NOCHECKPOSITION);
    HFRY J 0 
	{
	 for(int i=random(2,4);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodMulti",zofs:10,zvel:3,flags:SXF_NOCHECKPOSITION);
	 for(int i=random(1,3);i>0;i--)
	 A_SpawnItemEx("PM_DeathBloodSoul",xofs:random(-10,10),yofs:random(-15,15),zofs:random(15,20),yvel:random(-3,3),zvel:random(3,4),flags:SXF_NOCHECKPOSITION);
	}
    HFRY L 3 A_Fall;
	HFRY LLLLLLLLLL 1 
	{
	A_SpawnItemEx("HuskSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
	TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
    HFRY MMMMMMMNNNNNNNN 1 
	{
	A_SpawnItemEx("HuskSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
  DeathLoop:
    HFRY O 1
	{
	if(deathwither < 0)
	SetStateLabel("DeathEnd");
	A_SpawnItemEx("HuskSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	deathwither--;
	}
    Loop;
  DeathEnd:
    HFRY OOOOOOOOOOOOOOOO 1
	{
	A_FadeOut(0.05);
	A_SpawnItemEx("HuskSmoke",xofs:random(-15,15),yofs:random(-25,25),zofs:random(5,15),zvel:frandom(0.2,2));
	PM_HellsFuryWither();
	}
	Stop;
   }
}

Class PM_HellsFuryBall : Actor
{
    Default
    {
    Radius 6;
    Height 8;
    Speed 15;
    DamageFunction (random(30,40));
    RenderStyle "Add";
    Alpha 1;
	Scale 1;
	SeeSound "HFuryProj";
    DeathSound "HFuryProjExp";
	Decal "PM_DeathSoulScorchBig";
	DamageType "Fire";
	+SEEKERMISSILE
    Projectile;
    }
  States
  {
  Spawn:
    PSMM DDEEFF 1 Bright Light("HellsFuryBall1")
    {
	A_SpawnItemEx("PM_HFuryFlameTrails",0,0,0,0,0,0,0,128,0);
	A_SeekerMissile(1,1);
	A_SpawnParticleEx
	   (
		"",
		TexMan.CheckForTexture("DTPRB0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(1,2),
		size: frandom(40.0,200.0),
		angle: 0,
		xoff: -8,
		startalphaf: frandom(0.2,0.95),
		startroll: random(-180,180)
	    );
	A_SpawnParticleEx
			(
			"",
			TexMan.CheckForTexture("BLULA0"),
			style: STYLE_Add,
			flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
			lifetime: random(1,5),
			size: random(1,120),
			velx: random(-15,1),
			vely: random(-1,1),
			velz: random(-1,1),
			startalphaf: frandom(0,1),
			startroll: random(-180,180)
			);
	A_SpawnParticleEx
	    (
		"61A9FF",
		TexMan.CheckForTexture("SPKOS0"),
		style: STYLE_Add,
		flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,
		lifetime: random(10,70),
		size: frandom(0.2,15),
		xoff: random(-7,0),
		yoff: random(-5,5),
		zoff: random(-5,5),
		velx: frandom(-0.6,0.6),
		vely: frandom(-0.6,0.6),
		velz: frandom(-0.6,1),
		startalphaf: 1.0,
		fadestepf: -0.5,
		sizestep: -0.2,
		startroll: random(-180,180)
		);
	A_SpawnParticleEx
	(
		"",
		TexMan.CheckForTexture("GNSML0"),
		style: STYLE_None,
		flags: SPF_ROLL|SPF_RELATIVE,
		lifetime: 56,
		size: 30.0,
		xoff: -10,
		startalphaf: 0.4,
		fadestepf: -1,
		startroll: random(-180,180)
	);
	}
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(5,100),0,3,0,"PM_ConduitArcs");
    Loop;
  Death:
	TNT1 A 0;
	TNT1 A 0 A_StartSound("imp/shotx",CHAN_7);
	TNT1 AAAAA 0 A_SpawnItemEx("PM_BlueShockBig",0,0,0,random(-3,3),random(-3,3),random(-3,3),0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("RFireBallExplode",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("PM_BlueLongFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 A 0 A_SpawnItemEx("NetherShockFlare",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TNT1 AAA 0 A_SpawnItemEx("PM_BlueFireShrapnel",0,0,0,random(-10,10),random(10,10),random(1,10),0,SXF_NOCHECKPOSITION);
	//TNT1 AAA 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	//TNT1 A 0 A_CustomRailgun(5,0,"","White",RGF_SILENT|RGF_CENTERZ|RGF_FULLBRIGHT,0,9000,"PM_BlueLongFlare2",180,180,random(50,800),0,3,0,"PM_ConduitArcs");
	TNT1 AAAA 0 A_SpawnItemEx("PM_HFuryBlueBallExplode",0,0,0,random(-6,6),random(-6,6),random(-6,6),0,SXF_NOCHECKPOSITION);
	TNT1 AAAAAAAAAAAA 0 A_SpawnProjectile("BlueShock",2,0,random(0,360),CMF_OFFSETPITCH,random(0,360));
    HFBB CDE 5 Bright Light("HellsFuryBall2");
    Stop;
  }
}

Class PM_HFuryThrowFire : PM_RedFireEffect
{
  Default
  {
  Scale 0.3;
  Translation "0:255=%[0,0,0]:[0.4,0.7,2.0]";
  }
  States
{
  Spawn:
	FIR1 R 1
	{
	A_FadeOut(0.05);
	A_SetRoll(random(0,360));
	A_SetScale(Scale.X-0.015);
	}
	Loop;
	}
}

Class HellsFurySkull : Actor
{
    int timer;
    Default
	{
	Translation "174:191=193:207", "32:47=240:247";
    Radius 5;
    Height 5;
    Speed 5;
    Mass 1500;
	Scale 1.15;
    +DropOff
    +DoomBounce
    +Missile
	}
  States
  {
  Spawn:
    TNT1 A 0 NoDelay { timer = random(30,50); }
  Go:
    FSKL ABCDEFGHIJK 2
	{
	 if(timer < 0)
	 SetStateLabel("Death");
	 if(random(1,5) == 1)
	 A_SpawnItemEx("HuskSmoke",xofs:random(-5,5),yofs:random(-5,5),zofs:random(1,2),zvel:frandom(0.2,2));
	 timer--;
	}
    Loop;
  Death:
    FSKL L 1
	{
	 A_FadeOut(0.015);
	 A_SpawnItemEx("HuskSmoke",xofs:random(-5,5),yofs:random(-5,5),zofs:random(1,2),zvel:frandom(0.2,2));
	}
    Loop;
  }
}

Class PM_BlueDropFire : ZombieDropFire
{
 Default
 { Translation "0:255=%[0,0,0]:[0.6,0.9,2.0]"; }
}

Class PM_HellsFurySoul : Actor
{
	int soulhspeed;
	int soulvspeed;
	int soulhdist;
	int soulvdist;
   Default
   {
	DamageFunction 8;
	Projectile;
	+SEEKERMISSILE
	DeathSound "Abhor/SoulExp";
	Decal " ";
	RenderStyle "Add";
	Alpha 1.0;
	Height 5;
	Radius 5;
	Speed 15;
	FastSpeed 10;
	Scale 0.35;
   }
   
   override void PostBeginPlay()
   {
    tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
   }
   
   TextureID tex[1];

	states
  	{
  Spawn:
    TNT1 A 0 NoDelay
	{
	 soulhspeed = random(-2,2);
	 soulvspeed = random(-2,2);
	 soulhdist = random(-3,3);
	 soulvdist = random(-3,3);
	}
	TNT1 A 0 A_StartSound("HFury/SoulScreech",pitch:frandom(0.6,1.2));
	TNT1 A 0 
	{
	A_SeekerMissile(1,5);
	A_ScaleVelocity(frandom(0.85,1));
	}
  Go:
    SHBA AAABBB 1 BRIGHT//Light("HellsFuryBall1")
	{
	 if(random(1,20) == 1)
	 SetStateLabel("WeaveReset");
	 A_SpawnParticleEx
	 (
	 "0B3D94",
	 tex[0],
	 style: STYLE_Add,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(15,20),
	 size: 20.0,
	 xoff: -5,
	 startalphaf: frandom(0.25,0.65),
	 fadestepf: -1,
	 startroll: random(-180,180)
	 );
	 A_SpawnParticleEx
	 (
	 "143B76",
	 tex[0],
	 style: STYLE_Add,
	 flags: SPF_ROLL|SPF_RELATIVE,
	 lifetime: random(6,10),
	 size: 35.0,
	 xoff: -5,
	 startalphaf: frandom(0.5,0.8),
	 fadestepf: -1,
	 startroll: random(-180,180)
	 );
	 A_Weave(soulhspeed,soulvspeed,soulhdist,soulvdist);
	}
	loop;
  WeaveReset:
    TNT1 A 0
	{
	 soulhspeed = random(-2,2);
	 soulvspeed = random(-2,2);
	 soulhdist = random(-3,3);
	 soulvdist = random(-3,3);
	}
	Goto Go;
  Death:
    SHBA CDEFGHIKL 2 Bright Light("HellsFurySoul1");
    TNT1 A 5 Bright Light("HellsFurySoul2");
	TNT1 A 3 Bright Light("HellsFurySoul3");
    stop;
  }
}

Class PM_HellsFurySpreadFire : Actor
{
   Default
   {
	DamageFunction 18;
	+NOGRAVITY
	-LOWGRAVITY
	+MISSILE
	seesound "Abhor/SoulExp";
	Decal " ";
	RenderStyle "Add";
	Alpha 1.0;
	Speed 15;
	FastSpeed 15;
	Scale 0.50;
   }
	states
  	{
  Spawn:
    PSMM DDEEFF 1
	{
	A_SpawnItemEx("PM_WhiteFlameTrails",0,0,0,0,0,0,0,128,0);
	}
	loop;
  Death:
	TNT1 A 0 A_SpawnItemEx("PM_WhiteFireballExplode");
	TNT1 A 0 A_SpawnItemEx("PM_WhiteFireballExplosionFlames");
    AFX5 D 2 Bright Light("PM_SmallWhiteExpLight");
    AFX5 EFGH 2 Bright Light("PM_SmallWhiteExpLight");
    stop;
  }
}

Class PM_HFuryFirebreath : Actor
{
   Default
  {
   Radius 2;
   Height 4;
   Speed 12;
   Damage 1;
   PROJECTILE;
   RENDERSTYLE "Add";
   DamageType "Fire";
   Translation "0:255=%[0,0,0]:[0.6,0.9,2.0]";
   Scale 0.02;
   DeathSound "Fire/Unleash";
   Decal "PM_DeathSoulScorchMed";
   +THRUGHOST
   +ROLLSPRITE
   +FORCEXYBILLBOARD
  }
   States
   {
   Spawn:
	  TNT1 A 0;
      NULL A 1 Bright;
	  TNT1 A 0 A_Jump(255,"Go","Go2");
	  Go:
	  TNT1 A 0 A_SetRoll(random(0,360));
      FLMF ABCDEFEDCBA 2 Bright Light("HellsFuryBall1")
	  {
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,248);
	  A_SetScale(Scale.X+0.04);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF ABCDEF 2 Bright Light("HellsFuryBall1");
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBABC 1 Bright Light("HellsFuryBall1")
	  {
	  A_SetScale(Scale.X+0.05);
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,240);
	  A_Fadeout(0.1);
	  }
	  Stop;
	  Go2:
	  TNT1 A 0 A_SetRoll(random(0,360));
	  FLMF FEDCBABCDEF 2 Bright
	  {
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,248);
	  A_SetScale(Scale.X+0.03);
	  }
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF EDCBA 2 Bright;
	  TNT1 A 0 A_ChangeVelocity(random(-3,-6),random(-2,2),random(0,4),CVF_RELATIVE);
	  FLMF BCDEFED 1 Bright
	  {
	  A_SetScale(Scale.X+0.05);
	  A_SpawnItemEx("HuskSmoke",random(-5,5),random(-5,5),random(0,3),0,0,random(1,3),0,0,240);
	  A_Fadeout(0.1);
	  }
      stop;
   Death:
      FRFX HIJ 2 Bright;
      FRFX J 0 A_SpawnProjectile ("PM_BlueDropFire",0,0,0,4);
      FRFX KLM 2 Bright;
      FRFX NO 2 Bright;
      stop;
   }
}