Actor PM_Vore
{
  Health 500
  Radius 25
  Height 64
  Mass 1000
  Speed 8
  Scale 1.2
  PainChance 30
  RadiusDamageFactor 0.5
  MaxStepHeight 32
  MaxDropOffHeight 35
  Monster
  +FloorClip
  +BOSSDEATH
  //+NOINFIGHTING
  +DONTHARMSPECIES
  +Donthurtspecies
  +MISSILEMORE
  +MissileEvenMore
  Obituary "%o did not keep %p eyes on the Vore's cursed magic." 
  SeeSound "Vore/Sight"
  ActiveSound "Vore/Active"
  PainSound "Vore/Pain"
  DeathSound "Vore/Death"
  Species "Nobles"
  Tag "Vore"
  States
  {
  Spawn:
	  VORE A 5 A_Look
	  Loop
  See:
	TNT1 A 0 A_SetSpeed(8)
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
	//TNT1 A 0 A_ChangeFlag("MISSILEevenMORE",0)
	VORE A 0 A_ChangeFlag("NOPAIN",0)
    VORE AA 3 A_Chase
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE B 6 A_Chase
    VORE CC 3 A_Chase
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE D 6 A_Chase
    VORE EE 3 A_Chase
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE F 6 A_Chase
	Loop
  Melee:
  Missile:
	TNT1 A 0 A_Jump(130,"StandoffCheck")
	TNT1 A 0 A_jump(150,"Missile2")
    //TNT1 A 0 A_jump(80,"Shield")
    VORE G 0 Bright A_PlaySoundEx("Vore/Attack", "Body")
	VORE GGGGHHHHHHHHHHHHHHHH 1 Bright Light("Greenball") 
	{
	A_FaceTarget;
	A_Spawnitemex("GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE II 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Vore/Throw")
	VORE J 8 Bright Light("Greenball3") A_CustomMissile("PM_VoreShadowBall", 32, 0, 0)
    VORE II 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Vore/Throw")
	VORE J 8 Bright Light("Greenball3") A_CustomMissile("PM_VoreShadowBall", 32, 0, 0)
    VORE II 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Vore/Throw")
	VORE J 8 Bright Light("Greenball3") A_CustomMissile("PM_VoreShadowBall", 32, 0, 0)
	VORE JJ 4 Bright Light("Greenball2") A_facetarget
	Goto See
  Missile2:
    VORE G 0 Bright A_FaceTarget
	VORE GGGGGHHHHH 1 Bright Light("Greenball") 
	{
	A_FaceTarget;
	A_Spawnitemex("GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Vore/Throw")
	VORE J 8 Bright Light("Greenball3") A_CustomMissile("PM_VoreShadowBall", 32, 0, 0)
    VORE J 4 Bright Light("Greenball2") A_FaceTarget
	Goto See
  StandoffCheck:
	TNT1 A 0 A_JumpIfCloser(150,"Missile2")
  Standoff:
	//TNT1 A 0 A_ChangeFlag("MISSILEevenMORE",1)
	TNT1 A 0 A_Jump(1,"See")
	TNT1 A 0 A_JumpIfCloser(150,"Missile2")
	TNT1 A 0 A_SetSpeed(0)
	VORE BBBRRRBBBSSS 3 
	{
	A_Chase("","StandoffMissile");
	A_FaceTarget;
	}
	TNT1 A 0 A_CheckSight("See")
	TNT1 A 0 A_Jump(120,"StandoffRepos")
	Loop
  RetreatCheck:
	TNT1 A 0 A_JumpIfCloser(150,"Missile2")
	TNT1 A 0 A_Jump(100,"Standoff")
  Retreat:
    TNT1 A 0 A_SetSpeed(9)
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
	VORE F 4
	{
	A_Chase;
	A_FaceTarget;
	}
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE EE 2
	{
	A_Chase;
	A_FaceTarget;
	}
    VORE D 4
	{
	A_Chase;
	A_FaceTarget;
	}
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE CC 2
	{
	A_Chase;
	A_FaceTarget;
	}
    VORE B 4
	{
	A_Chase;
	A_FaceTarget;
	}
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE AA 2
	{
	A_Chase;
	A_FaceTarget;
	}
	TNT1 A 0 A_Jump(150,"Standoff")
	Loop
  StandoffMissile:
    VORE G 0 Bright A_FaceTarget
	VORE GGGGGHHHHH 1 Bright Light("Greenball") 
	{
	A_FaceTarget;
	A_Spawnitemex("GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_PlaySound("Vore/Throw")
	VORE J 8 Bright Light("Greenball3") A_CustomMissile("PM_VoreShadowBall", 32, 0, 0)
    VORE J 4 Bright Light("Greenball2") A_FaceTarget
	TNT1 A 0 A_JumpIfCloser(400,"RetreatCheck")
	Goto Standoff
  StandoffRepos:
	TNT1 A 0 A_SetSpeed(10)
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",1)
	VORE A 1 A_Chase
  StandoffReposLoop:
    VORE AA 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE B 5 A_Wander
    VORE CC 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE D 5 A_Wander
    VORE EE 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE F 5 A_Wander
	VORE AA 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE B 5 A_Wander
    VORE CC 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE D 5 A_Wander
    VORE EE 2 A_Wander
	TNT1 A 0 A_PlaySound("FUSWALK")
    VORE F 5 A_Wander
	TNT1 A 0 A_ChangeFlag("FRIGHTENED",0)
	TNT1 A 0 A_Jump(100,"Standoff")
	Loop
  Pain:
	VORE K 4
	VORE K 4 A_Pain
	TNT1 A 0 A_Jump(150,"RetreatCheck")
	Goto See
  Death:
	VORE L 12 A_Scream
	VORE M 8
	VORE N 5
	VORE O 4 A_NoBlocking
	TNT1 A 0 A_PlaySound("Corpse/FallMed",CHAN_6)
	VORE P 4
	VORE Q -1 A_bossdeath
	Stop
  }
}

Actor PM_VoreShadowBall
{
    Radius 6
    Height 8
    Speed 28
    Damage 4
    RenderStyle "Add"
	Scale 1.4
    Alpha 0.90
    SeeSound "Shadow/attack"
    DeathSound "imp/shotx"
    Projectile
    +RANDOMIZE

  States
  {
  Spawn:
    SBAL A 1 BRIGHT Light("GreenBall5") A_SpawnItemEx("PM_VoreShadowTrail", -10,random(-2,2),random(-2,2), 0,0,0, 0, SXF_CLIENTSIDE)
	SBAL B 1 BRIGHT Light("GreenBall5") A_SpawnItemEx("PM_VoreShadowTrail", -10,random(-2,2),random(-2,2), 0,0,0, 0, SXF_CLIENTSIDE)
	SBAL C 1 BRIGHT Light("GreenBall5") A_SpawnItemEx("PM_VoreShadowTrail", -10,random(-2,2),random(-2,2), 0,0,0, 0, SXF_CLIENTSIDE)
    Loop
  Death:
    SBAL C 5 Bright Light("GreenBall2")
    SBAL DEFGH 4 BRIGHT
    Stop
  }
}

Actor PM_VoreShadowTrail 
{
    Radius 1
    Height 1
    Speed 0
    RENDERSTYLE "ADD"
    ALPHA 0.6
    PROJECTILE
	+NOINTERACTION
	+CLIENTSIDEONLY
	+NOBLOCKMAP
	+THRUACTORS
	+DONTSPLASH
    +NOCLIP

  States
  {
  Spawn:
    2STR ABCDEF 3 Bright
    Stop
  }
}