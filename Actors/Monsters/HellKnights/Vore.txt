Class PM_Vore : PMEnvyMonsterBase
{
  bool sigil;
  Default
  {
  PMMonsterBase.CodexLore
  "Vores are not known for their eldritch presence or their cowardly deportment, but their ability to create pasquinades and travesties of other Knights.
  The Hell's hand has created them specifically for being kept as paladins of the circle of Envy. They might not as be as tough on the outside, but when a threat comes nearby its solitude, they'll see its true colors.
  
  Will spawn hell-powered knives to throw at the player, but alongside that, more spells are being put to use.";
  Health 500;
  Radius 25;
  Height 64;
  Mass 1000;
  Speed 8;
  Scale 1.2;
  PainChance 30;
  RadiusDamageFactor 0.5;
  MaxStepHeight 32;
  MaxDropOffHeight 35;
  Monster;
  +FloorClip
  //+NOINFIGHTING
  +DONTHARMSPECIES
  +DONTHARMCLASS
  +MISSILEMORE
  +MissileEvenMore
  Bloodtype "PM_NormalBloodBase";
  Obituary "%o did not keep %p eyes on the Vore's cursed magic.";
  SeeSound "Vore/Sight";
  ActiveSound "Vore/Active";
  PainSound "Vore/Pain";
  DeathSound "Vore/Death";
  Species "Nobles";
  Tag "Vore";
  TeleFogSourceType "PM_SmallDemonicTeleportFog";
  TeleFogDestType "PM_EnvyTeleportFog";
  }
  
  void PM_EnvyRuneGlow()
  {
	A_SpawnParticleEx("47ae1a",TexMan.CheckForTexture("DTPRA0"),style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(2,10),size: frandom(80,300),xoff: 5,zoff: 40,startalphaf: 0.5,fadestepf: -1,startroll: random(-180,180));
  }
  
  States
  {
  Spawn:
	  VORE A 5 PM_Look();
	  Loop;
  Look:
	TNT1 A 0 A_SetSpeed(8);
	TNT1 A 0 { bFRIGHTENED = false; }
	VORE A 0 { bNOPAIN = false; }
    VORE AA 3 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE BCC 6 PM_LookingForPlayer();
    TNT1 A 0 A_JumpIfTargetinLOS("Spotted",170,JLOSF_DEADNOJUMP);
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE DEE 6 PM_LookingForPlayer();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE F 6 PM_LookingForPlayer();
    Loop;
  Spotted:
    TNT1 A 0 PM_AlertSound();
  See:
	TNT1 A 0 A_SetSpeed(8);
	TNT1 A 0 { bFRIGHTENED = false; bNOPAIN = false; }
    VORE AA 2 A_Chase();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE B 4 A_Chase();
    VORE CC 2 A_Chase();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE D 4 A_Chase();
    VORE EE 2 A_Chase();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE F 4 A_Chase();
	Loop;
  Melee:
  Missile:
	TNT1 A 0 A_Jump(65,"StandoffCheck");
	TNT1 A 0 A_Jump(78,"SmallDaggers");
  NormalMissile:
    VORE G 0 Bright A_FaceTarget();
	VORE GGGGGHHHHH 1 Bright Light("Greenball") 
	{
	  if(random(1,10) == 1 && target && CheckSight(target) && Distance3D(target) < 350)
	   { SetStateLabel("CopyAttack"); }
	  A_FaceTarget(360,180);
	  A_Spawnitemex("PM_GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	TNT1 A 0 A_StartSound("Vore/Throw",25,CHANF_OVERLAP);
	VORE J 8 Bright Light("Greenball3") A_SpawnProjectile("PM_VoreDagger",flags:CMF_AIMDIRECTION,pitch);
    VORE J 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	Goto See;
  SmallDaggers:
    TNT1 A 0 { if(random(0,2) == 1) { A_StartSound("Vore/Attack",30); } }
	VORE GGGGGGGGGGHHHH 1 Bright Light("GreenBall")
	{
	  if(random(1,10) == 1 && target && CheckSight(target) && Distance3D(target) < 350)
	   { SetStateLabel("CopyAttack"); }
	  A_FaceTarget(360,180);
	  A_SpawnItemEx("PM_GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	TNT1 A 0 A_StartSound("Vore/Throw",25,CHANF_OVERLAP);
	VORE JJJ 1 Bright Light("Greenball3") A_SpawnProjectile("PM_VoreSmallDagger",32,0,frandom(-4,4),CMF_AIMDIRECTION,pitch+frandom(-1.4,1.4));
    VORE J 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	Goto See;
  //[NERU] only does copy attacks if threatened during standoff state
  //or if player is too close during attack states
  CopyAttack:
    TNT1 A 0 A_StartSound("GenericChargeSmall",18);
    VORE GGGGGGGGG 1 Bright Light("Greenball2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_NobleBigFlameTrails",10,random(12,8),70,0,0,2);
	}
	VORE HHHHHHHHHHHHHHH 1 Bright Light("GreenBall2")
	{
	A_FaceTarget();
	A_SpawnItemEx("PM_NobleBigFlameTrails",10,random(12,8),70,0,0,2);
	}
	TNT1 A 0 A_Jump(85,"CopyNoble");
    TNT1 A 0 A_Jump(75,"CopyWhiplash");
	TNT1 A 0 A_Jump(65,"CopyLandrazer");
  CopySquire:
    TNT1 A 0
	{
        A_StartSound("Draugr/BuffStart",10,volume:0.35,pitch:0.65);
		A_StartSound("Envy/SigilSummon",11,volume:0.5,pitch:frandom(0.7,1.2));
		for(int a=0;a<20;a++)
		 { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(75,80),xoff: random(-15,15),yoff: random(-30,30),zoff: random(20,50),velx: random(-5,5),vely: random(-5,5),velz: random(-5,5)); }
	}
    BOS6 EEEEE 0 A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
    BOS6 EEEEEEEEEEEE 1 Light("GreenBall2") 
	{
		 for(int a=0;a<random(1,5);a++)
		 { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(15,20),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,45),velz: frandom(0.5,3)); }
		A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
	TNT1 A 0 A_StartSound("Squire/LeapReady",18);
	BOS6 EEEEEEE 4  A_FaceTarget();
	BOS6 EEE 5 A_FaceTarget();
	TNT1 A 0 A_StartSound("Squire/Leap",18);
	BOS6 A 2 PM_LeapTo(target, 568, "CopyLeapPause");
   CopyLeapLoop:
	BOS6 FFFF 1 
	{
     if(random(1,15) == 1)
	   { ThrustThing(random(0,360)*256/360,2,0,0); }
	   A_SpawnItemEx("KnightLeapTrail",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	}
    TNT1 A 0 A_CheckFloor("CopyLeapEnd");
	Loop;
   CopyLeapEnd:
	TNT1 A 0 A_StartSound("Doom4/Monsters/BaronFloorPound",CHAN_7,CHANF_OVERLAP);
	TNT1 A 0 A_Quake(4,20,0,512);
    TNT1 AAAAAAAAA 0 A_SpawnItemEx("PM_BulletShrapnelNoCheck",random(-5,5),random(-5,5),0,random(-3,3),random(-5,5),random(5,20),0,SXF_NOCHECKPOSITION);
    TNT1 AAAAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareEffectLong",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
    TNT1 AAAAA 0 A_SpawnItemEx("PM_BoomSmoke",random(-1,1),random(-1,1),random(2,4),random(-1,1),random(-1,1),random(0,1),0,SXF_NOCHECKPOSITION);
    TNT1 AA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	BOS6 G 25;
    TNT1 A 0
	{
     for(int a=0;a<20;a++)
	  { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(75,80),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,50),velx: random(-5,5),vely: random(-5,5),velz: random(-5,5)); }
	}
	BOS6 GGGGGGGGGG 1 Light("GreenBall2") 
	{
     for(int a=0;a<random(1,5);a++)
	  { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(15,20),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,45),velz: frandom(0.5,3)); }
      A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
	VORE JJJJJJJ 1 Light("GreenBall2") 
	{
	 for(int a=0;a<random(1,5);a++)
      { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(15,20),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,45),velz: frandom(0.5,3)); }
      A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
		Goto See;
  CopyLeapPause:
	BOS6 EEEEEEEEE 1 Light("Greenball4") 
	{
      A_FaceTarget();
	  A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),60,0,0,2);
	}
    BOS6 EEEE 1 Light("Greenball4")
	{
	  A_FaceTarget();
	  A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),57,0,0,2);
	}
	BOS6 FFFF 1 Light("Greenball3") 
	{
	  A_FaceTarget();
	  A_Spawnitemex("PM_GreenFireEffect",10,random(30,25),45,0,0,2);
	}
	BOS6 G 6 Light("Greenball3")
	{
	  A_FaceTarget();
	  A_StartSound("HellKnight/Cast",CHAN_7);
	  A_SpawnProjectile("PM_SquireFireball",40);
	}
	BOS6 G 5 Light("Greenball3") A_FaceTarget();
    BOS6 GGGGGGGG 1 Light("GreenBall2")
	{
     for(int a=0;a<random(1,5);a++)
     { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(15,20),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,45),velz: frandom(0.5,3)); }
      A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
    VORE JJJJJJJ 1 Light("GreenBall2")
	{
	 for(int a=0;a<random(1,5);a++)
     { A_SpawnParticle("80FF38",flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,40),size: random(15,20),xoff: random(-20,20),yoff: random(-30,30),zoff: random(20,45),velz: frandom(0.5,3)); }
	 A_SpawnItemEx("PM_EnvyMorphGhost",zvel:1,flags:SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERSCALE);
	}
	Goto See;
  CopyNoble:
	TNT1 A 0
	{
	  A_StartSound("Noble/PreBomb",20,pitch:0.85);
	  let envysigil = Spawn("PM_EnvySigil",pos);
	  envysigil.master = self;
      sigil = true;
	}
	VORE HHHHHHHHHHHHHHHHHHIIIIIIIIIIIII 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("NO1SE1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	VORE IIII 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("NO1SF1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	TNT1 A 0 A_SpawnProjectile("PM_NobleBomb",40,0,0,CMF_OFFSETPITCH,7);
	VORE JJJJJ 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("NO1SG1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	TNT1 A 0 
	{ 
	 if(sigil)
	 A_GiveInventory("PM_EnvySigilGoAway");
	 sigil = false;
	}
	VORE JJJJJJ 1 A_FaceTarget;
	Goto See;
  CopyWhiplash:
	TNT1 A 0
	{
	A_StartSound("Whiplash/Flank",20,pitch:0.85);
	let envysigil = Spawn("PM_EnvySigil",pos);
	envysigil.master = self;
    sigil = true;
	}
	VORE HHHHHHHHHHHHHHHHHHIIIIIIIIIIIII 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("WHIPE1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	VORE IIII 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("WHIPF1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	TNT1 A 0 A_SpawnProjectile("PM_CopyWhiplashGroundSeeker");
	VORE JJJJJ 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33",TexMan.CheckForTexture("WHIPG1"),style: STYLE_ADD,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: 30,size: 110,xoff: 20,zoff: 60,startalphaf: 1,fadestepf: 0.1,startroll: 180);
	  A_FaceTarget();
	}
	VORE JJJJJJJJJJJJJJJJ 1 Bright Light("GreenBall2") A_FaceTarget();
	VORE J 10 Bright Light("GreenBall2") A_SpawnProjectile("PM_CopyWhiplashGroundSeeker");
    VORE JJJJJJJJJJJJJJJJ 1 Bright Light("GreenBall2") A_FaceTarget();
	VORE J 6 Bright Light("GreenBall2") A_SpawnProjectile("PM_CopyWhiplashGroundSeeker");
	TNT1 A 0 
	{ 
	 if(sigil)
	 A_GiveInventory("PM_EnvySigilGoAway");
	 sigil = false;
	}
	VORE JJJJJJ 1 A_FaceTarget;
	Goto See;
  CopyLandrazer:
	TNT1 A 0
	{
	  A_StartSound("Landrazer/Ready",20,pitch:0.85);
	  let envysigil = Spawn("PM_EnvySigil",pos);
	  envysigil.master = self;
      sigil = true;
	  pm_attackloop = 3;
	}
	VORE HHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHHH 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	  A_FaceTarget(7,7);
	}
	VORE H 0 A_StartSound("Landrazer/Lock");
	VORE IIIIIIII 1 Bright Light("GreenBall2")
	{
	  A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	  A_FaceTarget(7,7);
	}
	Goto CopyLRLoop;
  CopyLRLoop:
	TNT1 A 0 { if(!pm_attackloop) { SetStateLabel("CometEnd"); } pm_attackloop--; }
	VORE J 1 Light("Greenball2") 
	{
	 A_StartSound("Draugr/Fire",23,CHANF_OVERLAP);
	 A_SpawnProjectile("PM_CopyLandrazerComet",38,15,frandom(-3.5,3.5),CMF_AIMDIRECTION,pitch+frandom(-1.5,1.5));
	 A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	}
	VORE JJJJJJ 1 Light("Greenball2") A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	VORE J 0 A_StartSound("Sybarite/SlotIn",24);
	VORE JJJ 1 Light("Greenball2") A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	VORE JJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJJ 1 
	{
	  A_SpawnParticleEx("4EFF33", TexMan.CheckForTexture("LRA_E1"), style: STYLE_ADD, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: 30, size: 110, xoff: 20, zoff: 60, startalphaf: 1, fadestepf: 0.1, startroll: 180);
	  A_FaceTarget(6,6);
	}
	Loop;
  CometEnd:
	TNT1 A 0 
	{ 
	 if(sigil)
	 A_GiveInventory("PM_EnvySigilGoAway");
	 sigil = false;
	}
    VORE J 10;
	Goto See;
  StandoffCheck:
	TNT1 A 0 A_JumpIfCloser(150,"CopyAttack");
  Standoff:
	//TNT1 A 0 A_ChangeFlag("MISSILEevenMORE",1)
	TNT1 A 0 A_Jump(1,"See");
	TNT1 A 0 A_JumpIfCloser(150,"CopyAttack");
	TNT1 A 0 A_SetSpeed(0);
	VORE BBBRRRBBBSSS 3 
	{
	  A_Chase(null,"StandoffMissile");
	  A_FaceTarget();
	}
	TNT1 A 0 A_CheckSight("See");
	TNT1 A 0 A_Jump(120,"StandoffRepos");
	Loop;
  RetreatCheck:
	TNT1 A 0 A_JumpIfCloser(250,"CopyAttack");
	TNT1 A 0 A_Jump(100,"Standoff");
  Retreat:
    TNT1 A 0 A_SetSpeed(9);
	TNT1 A 0 { bFRIGHTENED = true; }
	VORE F 4
	{
	  A_Chase();
	  A_FaceTarget();
	}
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE EE 2
	{
	  A_Chase();
	  A_FaceTarget();
	}
    VORE D 4
	{
	  A_Chase();
	  A_FaceTarget();
	}
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE CC 2
	{
	  A_Chase();
	  A_FaceTarget();
	}
    VORE B 4
	{
	  A_Chase();
	  A_FaceTarget();
	}
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE AA 2
	{
	  A_Chase();
	  A_FaceTarget();
	}
	TNT1 A 0 A_Jump(150,"Standoff");
	Loop;
  StandoffMissile:
    TNT1 A 0 A_Jump(65,"StandoffSmallDaggers");
  StandoffNMissile:
    VORE G 0 Bright A_FaceTarget();
	VORE GGGGGHHHHH 1 Bright Light("Greenball") 
	{
	A_FaceTarget(360,180);
	A_SpawnItemEx("PM_GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	TNT1 A 0 A_StartSound("Vore/Throw");
	VORE J 8 Bright Light("Greenball3") A_SpawnProjectile("PM_VoreDagger",flags:CMF_AIMDIRECTION,pitch);
    VORE J 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	TNT1 A 0 A_JumpIfCloser(400,"RetreatCheck");
	Goto Standoff;
  StandoffSmallDaggers:
    TNT1 A 0 { if(random(0,2) == 1) { A_StartSound("Vore/Attack",30); } }
	VORE GGGGGGGGGGHHHH 1 Bright Light("GreenBall")
	{
	  if(random(1,10) == 1 && target && CheckSight(target) && Distance3D(target) < 350)
	   { SetStateLabel("CopyAttack"); }
	  A_FaceTarget(360,180);
	  A_SpawnItemEx("PM_GreenFireEffect",10,random(12,8),70,0,0,2);
	}
	VORE I 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	TNT1 A 0 A_StartSound("Vore/Throw",25,CHANF_OVERLAP);
	VORE JJJ 1 Bright Light("Greenball3") A_SpawnProjectile("PM_VoreSmallDagger",32,0,frandom(-4,4),CMF_AIMDIRECTION,pitch+frandom(-1.4,1.4));
    VORE J 4 Bright Light("Greenball2") A_FaceTarget(14,14);
	Goto Standoff;
  StandoffRepos:
	TNT1 A 0 A_SetSpeed(10);
	TNT1 A 0 { bFRIGHTENED = true; }
	VORE A 1 A_Chase();
  StandoffReposLoop:
    VORE AA 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE B 5 A_Wander();
    VORE CC 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE D 5 A_Wander();
    VORE EE 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE F 5 A_Wander();
	VORE AA 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE B 5 A_Wander();
    VORE CC 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE D 5 A_Wander();
    VORE EE 2 A_Wander();
	TNT1 A 0 A_StartSound("GenericArachWalk",8);
    VORE F 5 A_Wander();
	TNT1 A 0 { bFRIGHTENED = false; }
	TNT1 A 0 A_Jump(100,"Standoff");
	Loop;
  Pain:
    TNT1 A 0 
	{ 
	 if(sigil)
	 A_GiveInventory("PM_EnvySigilGoAway");
	 sigil = false;
	}
	VORE K 4;
	VORE K 4 A_Pain;
	TNT1 A 0 A_Jump(150,"RetreatCheck");
	Goto See;
  Death:
	TNT1 A 0 
	{
	 A_RemoveChildren(1,RMVF_EVERYTHING);
	 if(sigil)
	 A_GiveInventory("PM_EnvySigilGoAway");
	 sigil = false;
	}
	VORE L 12 PM_Scream();
	VORE M 8;
	VORE N 5;
	VORE O 4 A_NoBlocking;
	TNT1 A 0 A_StartSound("Corpse/FallMed",CHAN_6);
	VORE P 4;
	VORE Q -1;
	Stop;
  Raise:
    VORE QPONML 6;
	Goto Look;
  }
}

Class PM_VoreDagger : Actor
{
   Default
   {
    Radius 5;
    Height 8;
    Speed 30;
    DamageFunction 32;
    RenderStyle "Add";
	Scale 0.85;
    Alpha 0.90;
    SeeSound "Shadow/attack";
    DeathSound "HellKnight/BallImpact";
	Decal "PM_GreenFireScorch";
    Projectile;
    +RANDOMIZE
   }

	override void PostBeginPlay()
	{
	 Super.PostBeginPlay();
	 tex[0] = TexMan.CheckForTexture("DTPRA0",TexMan.Type_Sprite);
	 tex[1] = TexMan.CheckForTexture("SPKOG0",TexMan.Type_Sprite);
	 tex[2] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
	}
	
	TextureID tex[3];

  States
  {
  Spawn:
    TRCR AA 1 BRIGHT Light("GreenBall5")
	{
	 A_SpawnParticle("80FF38",SPF_FULLBRIGHT|SPF_RELATIVE,95,8,0,random(20,-20),random(-5,5),random(-5,5),10,0,0,0,0,0,1,-1,-0.3);
	 A_SpawnParticle("80FF38",SPF_FULLBRIGHT|SPF_RELATIVE,95,8,0,random(20,-20),random(-5,5),random(-5,5),10,0,0,0,0,0,1,-1,-0.3);
	 A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(5,35),size: random(1,15),xoff: random(-15,15),yoff: random(-9,9),zoff: random(-7,7),velx: frandom(-0.2,0.2),vely: frandom(-0.5,0.5),velz: frandom(-0.2,-2),startalphaf: frandom(0.65,0.95),startroll: random(-180,180));
	}
    Loop;
  Death:
    SBAL C 5 Bright Light("GreenBall2")
	{
	A_SetScale(1.1);
	for(int i=random(7,10);i>0;i--)
	 { A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(20,30),size: random(1,20),velx: random(-4,4),vely: random(-4,4),velz: random(-4,4),startalphaf: 0.9,startroll: random(-180,180)); }
	for(int i=5;i>0;i--)
	 { A_SpawnParticleEx("80FF38",tex[2],style: STYLE_Add,flags: SPF_ROLL|SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(20,45),size: random(34,46),xoff: -10,startalphaf: 0.75,fadestepf: -0.65,sizestep: -0.25,startroll: random(-180,180)); }
	for(int i=2;i>0;i--)
	 { A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(5,10),size: 130.0,startalphaf: 0.8,sizestep: 0.75); }
	}
    SBAL DEFGH 4 BRIGHT A_FadeOut(0.5);
    Stop;
  }
}


Class PM_VoreSmallDagger : PM_VoreDagger
{
   Default
   {
    Radius 6;
    Height 8;
    Speed 27;
    DamageFunction 15;
    RenderStyle "Add";
	Scale 0.80;
    Alpha 0.50;
	SeeSound "";
    DeathSound "GenericPlasmaExp";
	Decal "PM_GreenFireScorch";
    Projectile;
    +RANDOMIZE
	+SEEKERMISSILE
   }

    void PM_VoreOrbParticles()
	{
	  A_SpawnParticleEx("",tex[0],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,5),size: 15.0,xoff: 30,startalphaf: frandom(0.15,0.45),startroll: random(-180,180));
	  A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(1,80),size: random(1,8),xoff: random(-3,3),yoff: random(-2,2),zoff: random(-2,2),velx: frandom(-0.1,0.1),vely: frandom(-0.1,0.1),velz: frandom(-0.1,0.1),startalphaf: frandom(0.65,0.95),startroll: random(-180,180));
	}
	
  States
  {
  Spawn:
    TNT1 A 0;
    TNT1 A 0 
	{ 
	  A_StartSound("Shadow/attack",8,volume:0.65,pitch:frandom(1.10,1.20));
	  A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL,lifetime: random(5,10),size: random(5,40),startalphaf: 0.8,sizestep: 0.75,rollvel: random(-10,10));
    }
  Go:
    TRCR AA 1 Bright PM_VoreOrbParticles();
	Loop;
  Death:
    SBAL C 5 Bright Light("GreenBall2")
	{
	 for(int i=2;i>0;i--)
	   { A_SpawnParticleEx("",tex[1],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: random(5,10),size: 35.0,startalphaf: 0.8,sizestep: 3.75); }
	}
    Stop;
  }
}

Class PM_VoreOrbSummon : Actor
{
  Default
  {
  Scale 0.25;
  Alpha 0.15;
  RenderStyle "Add";
  Species "Nobles";
  +MISSILE
  +SEEKERMISSILE
  +ACTIVATEPCROSS
  +NOTELEPORT
  +NOGRAVITY
  +NOBLOCKMAP
  +THRUSPECIES
  +MTHRUSPECIES
  +DONTSPLASH
  +BRIGHT
  }
  States
  {
  Spawn:
   TNT1 A 0 NoDelay A_StartSound("PM/RuneFill",pitch:0.65);
  Death:
   SPKO GGGGGGGGG 2 A_FadeIn(0.25);
  DeathLoop:
   SPKO GGGG 2 Light("GreenBall5")
   {
    if(CountInv("PM_VoreReleaseToken")) SetStateLabel("Release");
	A_FaceTracer();
    A_SpawnItemEx("PM_NobleFlameTrails",0,0,0,0,0,0,0,128,0);
   }
  Loop;
  Release:
   TNT1 A 1 
   {
    A_FaceTracer();
    A_StartSound("Vore/Throw");
	A_SpawnProjectile("PM_VoreDagger",0,0,0,CMF_AIMDIRECTION|CMF_TRACKOWNER);
	for(int i=2;i>0;i--)
	A_SpawnParticleEx
	(
	"",
	TexMan.CheckForTexture("SPKOG0"),
	style: STYLE_Add,
    flags: SPF_RELATIVE|SPF_FULLBRIGHT,
	lifetime: random(5,10),
	size: 120.0,
	startalphaf: 0.8,
	sizestep: 0.75
    );
   }
  Stop;
  }
}

Class PM_VoreReleaseToken : Inventory { Default { Inventory.MaxAmount 1; } }

Class PM_CopyWhiplashGroundSeeker : PM_WhiplashGroundSeeker
{
 Default
 {
 Translation "0:255=%[0,0,0]:[0.0,0.8,0.0]";
 }
   States
   {
   Spawn: 
	TNT1 A 1;
	TNT1 A 0 A_StartSound("Whiplash/GroundLoop",CHAN_7,CHANF_LOOPING,0.7);
	Go:
		FLME ABCDEFGHIJKLM 1 Bright Light("GreenBall3") 
		{
		A_SeekerMissile(1,1);
		A_SpawnItemEx("PM_CopyWhiplashGroundFlameTrail",flags:SXF_NOCHECKPOSITION);
		}
		TNT1 A 0 Bright A_CountDown;
		Loop;
   Death:
	  TNT1 A 0 A_StopSound(CHAN_7);
	  FLME ABCD 1 Bright Light("GreenBall4") ;
	  TNT1 A 0 A_SpawnItemEx("PM_BruiserExplosionFlareEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	  TNT1 A 0 A_StartSound("Whiplash/KnifeStab",CHAN_5);
      stop;
   }
}

Class PM_CopyWhiplashGroundFlameTrail : Actor
{
 Default
 {
  +FLOORHUGGER
  +NOINTERACTION
  +CLIENTSIDEONLY
  +BRIGHT
  RenderStyle "Add";
  Alpha 0.8;
  XScale 0.7;
  YScale 0.8;
  Translation "0:255=%[0,0,0]:[0.0,0.8,0.0]";
 }
States
 {
 Spawn:
	TNT1 A 2;
 Go:
	FLME BABCDEFGHIJKLM 1 Bright
	{
	A_FadeOut(0.07);
	A_SetScale(Scale.X-0.06);
	}
    Loop;
 }
}

Class PM_CopyLandrazerComet : PM_LandrazerComet
{
   Default
   {
    RenderStyle "Add";
	StencilColor "24ff24";
	Translation "0:255=%[0.00,0.00,0.00]:[1.01,2.00,0.00]";
   }
   
   States
   {
   Spawn:
    TNT1 A 0;
	TNT1 A 0 
	{
		A_SpawnParticleEx("", TexMan.CheckForTexture("SPKOG0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,lifetime: random(2,10), size: frandom(100,170), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3),startroll: random(-180,180));
		A_SpawnParticleEx("", TexMan.CheckForTexture("DTPRA0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,15), size: frandom(90,290), startalphaf: 1.0, fadestepf: 0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
	}
   Go:
    HUBL AAAABBBBCCCCDDDD 1 Light("GreenBall2")
	{
	  A_SetRoll(roll+15);
	  A_SpawnParticleEx("",  TexMan.CheckForTexture("FRPGC0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,35),  size: frandom(45,85),  xoff: -10,  startalphaf: 1.0, fadestepf: -1,  sizestep: frandom(0.0,-0.50), startroll: random(-180,180),rollvel: random(-5,5));
	  A_SpawnParticleEx("", TexMan.CheckForTexture("SPKOG0"), style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(35,100), size: frandom(5,20), xoff: frandom(-30,30), yoff: frandom(-30,30), zoff: frandom(-20,20), velx: frandom(-0.3,0.3), vely: frandom(-0.3,0.3), velz: frandom(-0.1,0.1), startalphaf: frandom(0.35,1), fadestepf: -0.35, startroll: random(-180,180));
	}
	Loop;
   Death:
	TNT1 A 0 Radius_Quake (2, 54, 0, 15, 0);
	TNT1 A 0 A_SetRenderStyle(1, STYLE_ADDSHADED);
	TNT1 AA 0 A_SpawnItemEx("PM_OFireBallExplode", flags: SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AA 0 A_SpawnItemEx("PM_HectebusBallExplode",0,0,0,frandom(0, 2),0,frandom(-2, 2),angle:random(0,360),flags:SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AAA 0 A_SpawnItemEx("PM_CopyHuskMeteorFire",random(-7,7),random(-7,7),random(1,7),frandom(-4,4),frandom(-4,4),frandom(0,8));
	TNT1 AAAAA 0 A_SpawnItemEx("PM_BigFireGlobs",0,0,0,random(-11,11),random(-11,11),random(-9,11),0,SXF_NOCHECKPOSITION|SXF_TRANSFERSTENCILCOL|SXF_TRANSFERRENDERSTYLE);
	TNT1 AAA 0 A_SpawnItemEx("PM_BruiserExplosionFlareSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,0);
	Stop;
   }
}