//Zombies

Class PM_BulletTracer : Actor
{
Default
{
Projectile;
+BLOODSPLATTER 
+THRUGHOST
+SEEKERMISSILE
decal "PM_BulletHoleMedium";
height 2;
radius 2;
speed 36;
DamageFunction 9;
scale 0.85;
RenderStyle "Add";
Alpha 1.0;
}

 override void PostBeginPlay()
 {
  Super.PostBeginPlay();
  tex[0] = TexMan.CheckForTexture("GNSMK0",TexMan.Type_Sprite);
  tex[1] = TexMan.CheckForTexture("LEYSO0",TexMan.Type_Sprite);
  tex[2] = TexMan.CheckForTexture("SPKOB0",TexMan.Type_Sprite);
  tex[3] = TexMan.CheckForTexture("SPKOC0",TexMan.Type_Sprite);
  tex[4] = TexMan.CheckForTexture("SPKOA0",TexMan.Type_Sprite);
 }
 
 TextureID tex[5];

States
{
	Spawn:
        TNT1 A 0;
		TNT1 A 0
		{
		A_SpawnParticleEx
			( "#d2d2d2", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,250),
			size: frandom(10,50), xoff: -25, velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),
			startalphaf: frandom(0.3,0.5), fadestepf: -0.5, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		A_SpawnParticleEx
			( "", tex[2], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(1,4), size: frandom(40,100), xoff: -22, startalphaf: frandom(0.75,1), sizestep: frandom(0.0,3),
	        startroll: random(-180,180) );
		A_SpawnParticleEx
			( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(1,4), size: frandom(40,190), xoff: -22, startalphaf: 1.0, sizestep: frandom(0.0,3),
			startroll: random(-180,180) );
		}
	Go:
		TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        TNT1 A 0 A_JumpIfTracerCloser(150,"Noise");
        Loop;
    Noise:
        TNT1 A 0 A_StartSound("PM/BulletWhiz",3,CHANF_DEFAULT,0.50);
    Cont:
        TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        Loop;
	Death:
		TNT1 A 0 A_Jump(256,"Death1","Death2");
  Death1:
	TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TRCR A 0 A_StartSound("PM/BulletHit");
	TNT1 A 0 A_SpawnItemEx("PM_BulletHitDust",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,170);
    stop;
  Death2:
	TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	TRCR A 0 A_StartSound("PM/BulletHit");
    TNT1 AAAAAA 0 A_SpawnItemEx("PM_BulletShrapnel",0,0,0,random(-10,10),random(-10,10),random(-10,10),random(-180,180),SXF_NOCHECKPOSITION,170);
	TNT1 A 0 A_SpawnItemEx("PM_BulletHitFog",random(-10,10),random(-10,10),random(-10,10),0,0,0,0,SXF_NOCHECKPOSITION,150);
    stop;
  XDeath:
     TNT1 A 0;
     TRCR A 0 A_StartSound("PM/BulletHitFlesh");
     stop;
  Crash:
    TNT1 A 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
    TRCR A 0 A_StartSound("PM_BulletHit");
    Stop;
	}
}

Class PM_RevolverTracer : PM_BulletTracer
{
Default
 {
  DamageFunction 10;
  Scale 0.75;
 }
}

Class PM_DirectorTracer : PM_BulletTracer
{
Default
 {
  Speed 45;
  DamageFunction 10;
  Scale 1;
	}
	States
	{
	Spawn:
        TNT1 A 0;
		TNT1 A 0
		{
			A_SpawnParticleEx
			( "#d2d2d2", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,300),
			size: frandom(10,150), xoff: -10, velx: frandom(0,8), vely: frandom(-2,2), velz: frandom(-1,1), startalphaf: frandom(0.0,0.8),
			fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
			A_SpawnParticleEx
			( "", tex[3], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT,
			lifetime: random(2,5), size: frandom(80,250), startalphaf: 1.0, sizestep: frandom(0.0,3),
			startroll: random(-180,180));
			A_SpawnParticleEx
			( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,4),
			size: frandom(40,190), xoff: -22, startalphaf: 1.0, sizestep: frandom(0.0,3), startroll: random(-180,180) );
		}
	Go:
		TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        TNT1 A 0 A_JumpIfTracerCloser(150,"Noise");
        Loop;
    Noise:
        TNT1 A 0 A_StartSound("PM/BulletWhiz",3,CHANF_DEFAULT,0.50);
    Cont:
        TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        Loop;
	Death:
	    TNT1 AA 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
	    TRCR A 0 A_StartSound("PM/BulletHit");
	    TNT1 A 0
	    {
		 for(int i=3;i>0;i--)
		 A_SpawnParticleEx
		 ( "#d2d2d2", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(50,120),
		 size: frandom(65,150), velx: frandom(-2,2), vely: frandom(-2,2), velz: frandom(-1,1), startalphaf: frandom(0.4,0.9),
		 fadestepf: -1, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		 for(int i=random(2,6);i>0;i--)
		 A_SpawnParticleEx
		 ( "", tex[4], style: STYLE_Add, flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, lifetime: random(20,85),
		 size: frandom(3,15), velx: frandom(-5,5), vely: frandom(-5,5), velz: frandom(-1,5), accelz: -0.3, startalphaf: frandom(0.4,1.0),
		 fadestepf: -1, sizestep: -0.2, startroll: random(-180,180), rollvel: frandom(-1,1));
	    }
        Stop;
  XDeath:
        TNT1 A 0;
		TNT1 A 0 A_RadiusThrust(150,200,RTF_NOIMPACTDAMAGE);
        TRCR A 0 A_StartSound("PM/BulletHitFlesh");
        Stop;
  Crash:
        TNT1 AA 0 A_SpawnItemEx("PM_HitPuff",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
        TRCR A 0 A_StartSound("PM_BulletHit");
        Stop;
	}
}

Class PM_PelletTracer : PM_BulletTracer
{
Default
{
 Projectile;
 +BLOODSPLATTER 
 +THRUGHOST
 +SEEKERMISSILE
 decal "PM_BulletHoleSmall";
 height 2;
 radius 2;
 speed 36;
 DamageFunction 7;
 scale 0.85;
 RenderStyle "Add";
 Alpha 1.0;
}
States
{
	Spawn:
        TNT1 A 0;
		TNT1 A 0
		{
		A_SpawnParticleEx
			( "#d2d2d2", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(10,250),
			size: frandom(10,50), xoff: -25, velx: frandom(0,2), vely: frandom(-0.4,0.4), velz: frandom(-0.4,0.7),
			startalphaf: frandom(0.1,0.5), fadestepf: -0.5, sizestep: frandom(0.0,1.4), startroll: random(-180,180), rollvel: frandom(-1,1));
		A_SpawnParticleEx
			( "", tex[1], style: STYLE_Add, flags: SPF_RELATIVE|SPF_ROLL|SPF_FULLBRIGHT, lifetime: random(1,4), size: frandom(6,180),
			xoff: -22, startalphaf: 0.9, sizestep: frandom(0.0,3), startroll: random(-180,180));
		for(int i=random(1,2);i>0;i--)
		A_SpawnParticleEx
			( "", tex[4], style: STYLE_Add, flags: SPF_RELATIVE|SPF_FULLBRIGHT|SPF_ROLL, lifetime: random(1,40),
			size: frandom(0.1,7), xoff: -22, velx: frandom(1,5), vely: frandom(-0.5,0.5), velz: frandom(-0.5,0.5),
			accelz: frandom(-0.0,-0.3), startalphaf: 1.0, fadestepf: -0.5, sizestep: -0.2, startroll: random(-180,180) );
		}
	Go:
        TNT1 A 0 A_Jump(56,"NoNoiseSpawn");
		TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        TNT1 A 0 A_JumpIfTracerCloser(150,"Noise");
        Loop;
    Noise:
        TNT1 A 0 A_StartSound("PM/BulletWhiz",3,CHANF_DEFAULT,0.50);
    Cont:
        TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        Loop;
    NoNoiseSpawn:
        TRCR A 1 BRIGHT A_Quake(1,4,0,100,0);
        Loop;
	}
}

//Melee Attacks

Class PM_FastMeleeHitbox : Actor //[NERU] primarily used for lunging monsters
{
  Default
  {
    Height 15;
    Radius 15;
    Speed 22;
    DamageFunction 4;
    +RIPPER
    +THRUGHOST
    +NODAMAGETHRUST
    DeathSound "";
    SeeSound "";
    DamageType "Melee";
    Projectile;
  }
  States
  {
  Spawn:
	TNT1 A 2;
    Stop;
    }
}

Class PM_MeleeHitbox : PM_FastMeleeHitbox //[NERU] used for pinkies
{
  Default
  {
   Speed 12;
   FastSpeed 12;
  }
  States
  {
  Spawn:
    TNT1 A 6;
	Stop;
  }
}